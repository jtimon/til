mode lib

import("src/core/constants")

Dynamic := struct {} // For dynamic arguments not checked at type checking phase

// DivideByZero := struct {} // REM Apparently Roc tried this and rolled it back

I64_OverflowError := struct {
    mut msg: Str = ""

    new := func(msg: Str) returns I64_OverflowError {
        mut err := I64_OverflowError()
        err.msg = msg
        return err
    }
}

U8_OverflowError := struct {
    mut msg: Str = ""

    new := func(msg: Str) returns U8_OverflowError {
        mut err := U8_OverflowError()
        err.msg = msg
        return err
    }
}

IndexOutOfBoundsError := struct {
    mut msg: Str = ""

    new := func(msg: Str) returns IndexOutOfBoundsError {
        mut err := IndexOutOfBoundsError()
        err.msg = msg
        return err
    }
}

FullError := struct {
    mut msg: Str = ""

    new := func(msg: Str) returns FullError {
        mut err := FullError()
        err.msg = msg
        return err
    }
}

// TODO check for undefined types in catch
AllocError := struct {
    mut msg: Str = ""

    new := func(msg: Str) returns AllocError {
        mut err := AllocError()
        err.msg = msg
        return err
    }
}

DuplicatedKeyError := struct {
    mut msg: Str = ""

    new := func(msg: Str) returns DuplicatedKeyError {
        mut err := DuplicatedKeyError()
        err.msg = msg
        return err
    }
}

KeyNotFoundError := struct {
    mut msg: Str = ""

    new := func(msg: Str) returns KeyNotFoundError {
        mut err := KeyNotFoundError()
        err.msg = msg
        return err
    }
}

// FIX not properly working for errors in imports, says the right pos in the importing file instead of the source one
loc := ext_func() returns Str; // Returns the current source location as "<file>:<line>:<col>:"

// Comparisons are hateful, plus two of them are probably enough for now:
lt        := ext_func(a: I64, b: I64) returns Bool;
gt        := ext_func(a: I64, b: I64) returns Bool;

// Minimal arithmehics are fair and necessary
add       := ext_func(a: I64, b: I64) returns I64;
sub       := ext_func(a: I64, b: I64) returns I64;
mul       := ext_func(a: I64, b: I64) returns I64;
div       := ext_func(a: I64, b: I64) returns I64;
mod       := ext_func(a: I64, b: I64) returns I64;

single_print := ext_proc(args: ..Str);
// TODO temporary optimization since things are really slow since we have so many Array implementations here (loaded and checked with each test)
print_flush  := ext_proc();
exit         := ext_func(code: I64);
// TODO parse with dots like other languages, then change the direction of the slash depending on the os
// TODO use imports in declarations:
// example_cil: my_matrix_def := import(matrix.matrix_def)
// example_cil: my_matrix : struct = import(matrix)
// example_python: from matrix import matrix_def as my_matrix_def
import       := ext_proc(path: Str);

// Memory operations
NULL   := 0
size_of := ext_func(type: Dynamic) returns I64;
to_ptr := ext_func(var_identifier: Dynamic) returns I64;
type_as_str := ext_func(type: Dynamic) returns Str;
// TODO Use throws for memory operations, don't return 0 from malloc
malloc := ext_func(size: I64) returns I64 throws AllocError;
free   := ext_func(ptr: I64);
memcpy := ext_func(mut dest: I64, src: I64, size: I64);
memcmp := ext_func(ptr1: I64, ptr2: I64, size: I64) returns I64;
memset := ext_func(mut dest: I64, value: U8, size: I64);

print := proc(args: ..Str) {
    for i in 0..args.len {
        mut val := ""
        args.get(i, val)
        single_print(val)
    }
    print_flush()

    catch(err: IndexOutOfBoundsError) {
        panic(loc(), "print: IndexOutOfBoundsError should never happen")
    }
}

println := proc(args: ..Str) {
    for i in 0..args.len {
        mut val := ""
        args.get(i, val)
        single_print(val)
    }
    single_print("\n")
    print_flush()

    catch(err: IndexOutOfBoundsError) {
        panic(loc(), "println: IndexOutOfBoundsError should never happen")
    }
}

panic := proc(loc_str: Str, msg: Str) {
    println(loc_str.format(msg))
    exit(1)
}

Ptr := struct {
    mut data: I64 = NULL

    // TODO FIX should type error because size is already declared
    size_of := func() returns I64 { return size_of(I64) }

    new_by_size := proc(size: I64) returns Ptr throws AllocError {
        if size.lteq(0) {
            throw AllocError.new(format(loc(), "Ptr.new_by_size: invalid size ", size.to_str()))
        }
        mut p := Ptr()
        p.data = malloc(size)
        if NULL.eq(p.data) {
            throw AllocError.new(format(loc(), "Ptr.new: failed to allocate ", size.to_str(), " bytes"))
        }
        return p
    }

    new := proc(T: Dynamic) returns Ptr throws AllocError {
        return Ptr.new_by_size(size_of(T))
    }

    delete := proc(self: Ptr) throws AllocError {
        if NULL.eq(self.data) {
            throw AllocError.new(format(loc(), "Ptr.free: trying to free a NULL pointer"))
        }
        free(self.data)
    }

    /** Ptr.offset is danger-ous, also known as "pointer arythmetics" */
    offset := proc(self: Ptr, offset: I64) returns Ptr {
        mut p := Ptr()
        p.data = add(self.data, offset)
        return p
    }

    // cast := macro(self: Ptr, type_name: Str) returns Dynamic {
    //     // TODO: This cast is purely cosmetic for now.
    //     // Proper type-changing macros are not implemented yet.
    //     return self
    // }

    copy_from := proc(mut self: Ptr, src: Ptr, size: I64) {
        // TODO this should fail if self arg is not declared as mut
        memcpy(self.data, src.data, size)
    }

    copy_to := proc(self: Ptr, mut dest: Ptr, size: I64) {
        memcpy(dest.data, self.data, size)
    }

    set_zero := proc(mut self: Ptr, size: I64) {
        memset(self.data, U8.from_i64(0), size)
    }
}

not := func(b: Bool) returns Bool {
    if b {
        return false
    }
    return true
}

or := func(args: ..Bool) returns Bool {
    for i in 0..args.len {
        mut val := false
        args.get(i, val)
        if val {
            return true
        }
    }
    return false

    catch(err: IndexOutOfBoundsError) {
        panic(loc(), "or: IndexOutOfBoundsError should never happen")
    }
}

and := func(args: ..Bool) returns Bool {
    for i in 0..args.len {
        mut val := false
        args.get(i, val)
        if not(val) {
            return false
        }
    }
    return true

    catch(err: IndexOutOfBoundsError) {
        panic(loc(), "and: IndexOutOfBoundsError should never happen")
    }
}

Bool := struct {
    mut data: U8 = 0

    len := func(self: Bool) returns I64 {
        return 1
    }

    eq := func(a: Bool, b: Bool) returns Bool {
        return or(and(a, b), and(not(b), not(a)))
    }

    to_str := func(self: Bool) returns Str {
        if self {
            return "true"
        }
        return "false"
    }

    // necessary for branchless arithmetics (but it must be implemented as a cast for it to make sense that way)
    to_i64 := func(self: Bool) returns I64 {
        if self {
            return 1
        }
        return 0
    }

    to_u8 := func(self: Bool) returns U8 {
        return self.data
    }

    from_i64 := func(i: I64) returns Bool { // TODO support regular struct returns to be able to this as a regular struct internally (except for if/while conditions)
        mut b := Bool()
        if gt(i, 0) {
            b.data = 1
        }
        return b
    }
}
false := Bool.from_i64(0)
// true := Bool.from_i64(1) // TODO stop using "true" as literal

lteq := func(a: I64, b: I64) returns Bool {
    if lt(a, b) {
        return true
    }
    if a.eq(b) {
        return true
    }
    return false
}

gteq := func(a: I64, b: I64) returns Bool {
    if gt(a, b) {
        return true
    }
    if a.eq(b) {
        return true
    }
    return false
}

// Note that I64 and U8 don't have fields because the're the "real core types"
// think of them as "half-external types" for now
U8 := struct {
    // TODO FIX can't use itself as base data, can it?
    // TODO This is the base of data storage, not I64
    // mut data: I8 = 0

    len := func(self: I64) returns I64 {
        return 1
    }

    eq := func(self: U8, other: U8) returns Bool {
        return U8.to_i64(self).eq(U8.to_i64(other))
    }

    to_i64 := func(self: U8) returns I64 {
        // return cast("I64", self) // TODO implement cast, start easy with U8 to I64
        u8_to_i64 := ext_func(u8: U8) returns I64;
        return u8_to_i64(self)
    }

    // TODO do the rest of the operations, first fake them with i64, until real compilation
    // TODO rename add into i64_add or something
    // TODO rename add into I64.add directly in the rust/interpreter side, afterwards
    u8_add := func(a: U8, b: U8) returns U8 throws U8_OverflowError {
        return U8.from_i64(add(a.to_i64(), b.to_i64()))
    }

    from_i64 := func(self: I64) returns U8 throws U8_OverflowError {
        if lt(self, 0) {
            throw U8_OverflowError.new(format(loc(), "Negative values cannot be cast into 'U8'"))
        }
        if gt(self, MAX_U8) {
            throw U8_OverflowError.new(format(loc(), "U8: cannot be casted from an I64 greater than: ", I64.to_str(MAX_U8)))
        }
        i64_to_u8 := ext_func(i64: I64) returns U8;
        return i64_to_u8(self)
    }

    to_str := func(self: U8) returns Str {
        return I64.to_str(U8.to_i64(self))
    }
}

I64 := struct {
    // mut data : Array = Array.new("U8", size_of(U8), 8) // TODO Use [8]U8 for this one

    eq := func(a: I64, b: I64) returns Bool {
        // REM We can't use the variadic or from here without mutual recusrion and a stack overflow
        if gt(a, b) {
            return false
        }
        if gt(b, a) {
            return false
        }
        if lt(a, b) {
            return false
        }
        if lt(b, a) {
            return false
        }
        return true
    }

    to_str := func(self: I64) returns Str {
        i64_to_str := ext_func(a: I64) returns Str;
        return i64_to_str(self)
    }

    from_str := func(str: Str) returns I64 {
        str_to_i64 := ext_func(a: Str) returns I64;
        return str_to_i64(str) // TODO: this can fail and panic from rust
    }

    inc := proc(mut self: I64) {
        self = add(self, 1)
    }

    dec := proc(mut self: I64) {
        self = sub(self, 1)
    }

    // TODO: implement operators and allow operator overload
    __add__ := func(self: I64, other: I64) returns I64 {
        return add(self, other)
    }
}

// TODO get rid of special cases for Str besides literals
Str := struct {
    mut c_string : I64 = 0
    mut cap      : I64 = 0
    // mut len : I64 = 0 // TODO make sure double declarations inside the same struct give type errors

    len := func(self: Str) returns I64 {
        return self.cap
    }

    eq := func(self: Str, other: Str) returns Bool {
        if not(self.cap.eq(other.cap)) {
            return false
        }

        for i in 0..self.cap {
            mut self_byte  : U8 = 0
            mut other_byte : U8 = 0
            memcpy(to_ptr(self_byte), add(self.c_string, i), 1)
            memcpy(to_ptr(other_byte), add(other.c_string, i), 1)

            if not(U8.eq(self_byte, other_byte)) {
                return false
            }
        }

        return true
    }

    to_i64 := func(self: Str) returns I64 {
        return I64.from_str(self)
    }
}

get_substr := func(s: Str, start: I64, end: I64) returns Str throws IndexOutOfBoundsError, AllocError {

    if start.lt(0) {
        throw IndexOutOfBoundsError.new(format(loc(), "get_substr: start index ", start.to_str(), " cannot be negative"))
    }
    if end.lt(0) {
        throw IndexOutOfBoundsError.new(format(loc(), "get_substr: end index ", end.to_str(), " cannot be negative"))
    }
    if start.gt(end) {
        throw IndexOutOfBoundsError.new(format(loc(), "get_substr: start index ", start.to_str(), " is greater than end index ", end.to_str()))
    }
    if end.gt(s.len()) {
        throw IndexOutOfBoundsError.new(format(loc(), "get_substr: end index ", end.to_str(), " is greater than string length ", I64.to_str(s.len())))
    }
    if start.eq(end) {
        throw IndexOutOfBoundsError.new(format(loc(), "get_substr: start and end are the same (", start.to_str(), "), no substring"))
    }

    mut substr := Str()
    substr.cap = end.sub(start)
    substr.c_string = malloc(substr.cap)
    if NULL.eq(substr.c_string) {
        throw AllocError.new(format(loc(), "Str.get_substr: failed to allocate ", I64.to_str(substr.cap), " bytes"))
    }
    memcpy(substr.c_string, add(s.c_string, start), substr.cap)

    return substr
}

get_char := func(s: Str, pos: I64) returns Str throws IndexOutOfBoundsError, AllocError {
    return s.get_substr(pos, add(pos, 1))
}

format := func(prefix: Str, args: ..Str) returns Str throws I64_OverflowError, IndexOutOfBoundsError, AllocError {
    mut result := Str()
    result.cap = prefix.cap

    for i in 0..args.len {
        mut arg := ""
        args.get(i, arg)
        result.cap = add(result.cap, arg.cap)
        if lt(result.cap, arg.cap) {
            throw I64_OverflowError.new(format(loc(), "format: integer overflow while summing cap for arg ", i.to_str()))
        }
    }

    result.c_string = malloc(result.cap)
    if NULL.eq(result.c_string) {
        throw AllocError.new(format(loc(), "format: malloc failed for cap = ", I64.to_str(result.cap)))
    }

    mut offset := 0
    memcpy(result.c_string, prefix.c_string, prefix.cap)
    offset = prefix.cap

    for i in 0..args.len {
        mut s := ""
        args.get(i, s)
        memcpy(add(result.c_string, offset), s.c_string, s.cap)
        offset = add(offset, s.cap)
    }

    return result
}

Array := struct {
    mut type_name : Str  = ""
    mut type_size : I64  = 0
    mut ptr       : I64  = 0
    mut len       : I64  = 0

    new := proc(T_str: Str, type_size: I64, capacity: I64) returns Array throws AllocError {
        mut arr := Array()
        arr.type_name = T_str
        arr.type_size = type_size
        // TODO: Simplify to Array.new(T, capacity) when Dynamic type parameters work
        // arr.type_name = type_as_str(T)
        // arr.type_size = size_of(T)
        // Currently blocked: passing type identifiers as Dynamic parameters causes
        // "Expr index 0 out of bounds" error in interpreter
        size_bytes := mul(capacity, arr.type_size)
        arr.ptr = malloc(size_bytes)
        memset(arr.ptr, U8.from_i64(0), size_bytes)
        arr.len = capacity
        return arr
    }

    get := proc(self: Array, index: I64, mut T: Dynamic) throws IndexOutOfBoundsError {
        if gteq(index, self.len) {
            throw IndexOutOfBoundsError.new(format(loc(), "Array.get: index out of bounds"))
        }

        // if not(Str.eq(type_as_str(T), self.type_name)) { // TODO FIX
        //     throw format(loc(), format("Array.get: destination type mismatch: expected '", self.type_name, "' found '", type_as_str(T), "'"))
        // }

        src := add(self.ptr, mul(index, self.type_size))
        mut dest := to_ptr(T)
        memcpy(dest, src, self.type_size)
    }

    set := proc(mut self: Array, index: I64, value: Dynamic) throws IndexOutOfBoundsError {
        if gteq(index, self.len) {
            throw IndexOutOfBoundsError.new(format(loc(), "Array.set: index out of bounds"))
        }

        // if not(Str.eq(type_as_str(type_of(value)), self.type_name)) { // TODO FIX
        //     throw format(loc(), "Array.set: value type mismatch")
        // }

        mut dest := add(self.ptr, mul(index, self.type_size))
        memcpy(dest, to_ptr(value), self.type_size)
    }

    delete := proc(mut self: Array) {
        free(self.ptr)
        self.ptr = 0
        self.len = 0
    }
}

// Vec (dynamic/growable array) - replaces Array.new_dyn()
Vec := struct {
    mut type_name : Str  = ""
    mut type_size : I64  = 0
    mut ptr       : I64  = 0
    mut len       : I64  = 0
    mut cap       : I64  = 0

    INIT_CAP : I64 = 16
    MAX_CAP  : I64 = 1024

    new := proc(T_str: Str, type_size: I64) returns Vec throws AllocError {
        mut vec := Vec()
        vec.type_name = T_str
        vec.type_size = type_size
        size_bytes := mul(Vec.INIT_CAP, vec.type_size)
        vec.ptr = malloc(size_bytes)
        vec.len = 0
        vec.cap = Vec.INIT_CAP
        return vec
    }

    push := proc(mut self: Vec, value: Dynamic) throws FullError, AllocError {
        // if not(Str.eq(type_as_str(type_of(value)), self.type_name)) { // TODO FIX
        //     throw format(loc(), "Vec.push: value type mismatch")
        // }

        if self.len.eq(self.cap) {
            new_cap := mul(self.cap, 2)
            if gt(new_cap, Vec.MAX_CAP) {
                throw FullError.new(format(loc(), "Vec.push: capacity exceeded Vec.MAX_CAP"))
            }

            mut new_ptr := malloc(mul(new_cap, self.type_size))
            memcpy(new_ptr, self.ptr, mul(self.len, self.type_size))
            free(self.ptr)
            self.ptr = new_ptr
            self.cap = new_cap
        }

        mut dest := add(self.ptr, mul(self.len, self.type_size))
        memcpy(dest, to_ptr(value), self.type_size)
        self.len = add(self.len, 1)
    }

    get := proc(self: Vec, index: I64, mut T: Dynamic) throws IndexOutOfBoundsError {
        if gteq(index, self.len) {
            throw IndexOutOfBoundsError.new(format(loc(), "Vec.get: index out of bounds"))
        }

        // if not(Str.eq(type_as_str(T), self.type_name)) { // TODO FIX
        //     throw format(loc(), format("Vec.get: destination type mismatch: expected '", self.type_name, "' found '", type_as_str(T), "'"))
        // }

        src := add(self.ptr, mul(index, self.type_size))
        mut dest := to_ptr(T)
        memcpy(dest, src, self.type_size)
    }

    set := proc(mut self: Vec, index: I64, value: Dynamic) throws IndexOutOfBoundsError {
        if gteq(index, self.len) {
            throw IndexOutOfBoundsError.new(format(loc(), "Vec.set: index out of bounds"))
        }

        // if not(Str.eq(type_as_str(type_of(value)), self.type_name)) { // TODO FIX
        //     throw format(loc(), "Vec.set: value type mismatch")
        // }

        mut dest := add(self.ptr, mul(index, self.type_size))
        memcpy(dest, to_ptr(value), self.type_size)
    }

    delete := proc(mut self: Vec) {
        free(self.ptr)
        self.ptr = 0
        self.len = 0
        self.cap = 0
    }
}

// Generic Map implementation - ordered, not hashed
// Uses parallel arrays for keys and values (maintains insertion order)
// Type parameters passed as strings (key_type_name, value_type_name)
// No generics syntax needed - uses dynamic typing like Array
//
// Key type support:
//   - Str: Fully supported (uses Str.eq() for content comparison)
//   - I64, U8, Bool: Fully supported (uses memcmp for byte comparison)
//   - Other primitives: Should work with memcmp
//   - Structs: NOT recommended as keys (memcmp compares raw bytes including padding)
//
// Value type support:
//   - Fully dynamic: Any type works (I64, Str, Bool, U8, Vec2, Array, etc.)
//   - Uses memcpy for storage and retrieval
Map := struct {
    mut key_type_name: Str = ""       // e.g., "Str", "I64"
    mut key_type_size: I64 = 0        // from size_of(KeyType)
    mut value_type_name: Str = ""     // e.g., "Str", "Bool", "Expr"
    mut value_type_size: I64 = 0      // from size_of(ValueType)
    mut keys: Vec = Vec()         // Dynamic array of keys
    mut values: Vec = Vec()       // Dynamic array of values (parallel)
    mut size: I64 = 0                 // Number of entries

    // Create new empty map with specified key and value types
    // Example: Map.new("Str", size_of(Str), "I64", size_of(I64))
    new := proc(key_type: Str, key_size: I64, value_type: Str, value_size: I64) returns Map throws AllocError {
        mut m := Map()
        m.key_type_name = key_type
        m.key_type_size = key_size
        m.value_type_name = value_type
        m.value_type_size = value_size
        m.keys = Vec.new(key_type, key_size)
        m.values = Vec.new(value_type, value_size)
        m.size = 0
        return m
    }

    // Insert new key-value pair
    // Throws DuplicatedKeyError if key already exists
    // Use set() instead if you want to overwrite existing keys
    insert := proc(mut self: Map, key: Dynamic, value: Dynamic) throws AllocError, DuplicatedKeyError {
        // Check if key already exists
        mut i := 0
        mut keys_arr := self.keys
        while lt(i, self.size) {
            // Special case for Str keys: compare content, not struct bytes
            mut keys_match := 0
            if Str.eq(self.key_type_name, "Str") {
                // For Str keys, use Str.eq() to compare content
                mut existing_key := ""
                mut existing_key_ptr := add(keys_arr.ptr, mul(i, self.key_type_size))
                memcpy(to_ptr(existing_key), existing_key_ptr, self.key_type_size)
                mut key_str := ""
                memcpy(to_ptr(key_str), to_ptr(key), self.key_type_size)
                if key_str.eq(existing_key) {
                    keys_match = 0
                } else {
                    keys_match = 1
                }
            } else {
                // For other types, use memcmp
                mut existing_key_ptr := add(keys_arr.ptr, mul(i, self.key_type_size))
                keys_match = memcmp(to_ptr(key), existing_key_ptr, self.key_type_size)
            }

            if keys_match.eq(0) {
                // Key already exists - throw error
                throw DuplicatedKeyError.new("Key already exists in map")
            }
            i = add(i, 1)
        }

        // Key doesn't exist - add it
        // Workaround for bug: pushing Dynamic parameters doesn't work
        // Instead, manually grow the array and memcpy directly
        if keys_arr.len.eq(keys_arr.cap) {
            // Need to grow
            mut new_cap := mul(keys_arr.cap, 2)
            mut new_ptr := malloc(mul(new_cap, keys_arr.type_size))
            memcpy(new_ptr, keys_arr.ptr, mul(keys_arr.len, keys_arr.type_size))
            free(keys_arr.ptr)
            keys_arr.ptr = new_ptr
            keys_arr.cap = new_cap
        }
        // Copy key directly into array memory
        mut dest_ptr := add(keys_arr.ptr, mul(keys_arr.len, keys_arr.type_size))
        memcpy(dest_ptr, to_ptr(key), keys_arr.type_size)
        keys_arr.len = add(keys_arr.len, 1)
        self.keys = keys_arr

        // Same workaround for values
        mut values_arr := self.values
        if values_arr.len.eq(values_arr.cap) {
            mut new_cap := mul(values_arr.cap, 2)
            mut new_ptr := malloc(mul(new_cap, values_arr.type_size))
            memcpy(new_ptr, values_arr.ptr, mul(values_arr.len, values_arr.type_size))
            free(values_arr.ptr)
            values_arr.ptr = new_ptr
            values_arr.cap = new_cap
        }
        mut dest_ptr := add(values_arr.ptr, mul(values_arr.len, values_arr.type_size))
        memcpy(dest_ptr, to_ptr(value), values_arr.type_size)
        values_arr.len = add(values_arr.len, 1)
        self.values = values_arr

        self.size = add(self.size, 1)
    }

    // Set key-value pair
    // Overwrites value if key already exists
    // Creates new entry if key doesn't exist
    set := proc(mut self: Map, key: Dynamic, value: Dynamic) throws AllocError {
        // Try to find existing key
        mut i := 0
        mut keys_arr := self.keys
        while lt(i, self.size) {
            // Special case for Str keys: compare content, not struct bytes
            mut keys_match := 0
            if Str.eq(self.key_type_name, "Str") {
                // For Str keys, use Str.eq() to compare content
                mut existing_key := ""
                mut existing_key_ptr := add(keys_arr.ptr, mul(i, self.key_type_size))
                memcpy(to_ptr(existing_key), existing_key_ptr, self.key_type_size)
                mut key_str := ""
                memcpy(to_ptr(key_str), to_ptr(key), self.key_type_size)
                if key_str.eq(existing_key) {
                    keys_match = 0
                } else {
                    keys_match = 1
                }
            } else {
                mut existing_key_ptr := add(keys_arr.ptr, mul(i, self.key_type_size))
                keys_match = memcmp(to_ptr(key), existing_key_ptr, self.key_type_size)
            }

            if keys_match.eq(0) {
                // Key found - update value
                mut values_arr := self.values
                mut value_ptr := add(values_arr.ptr, mul(i, self.value_type_size))
                memcpy(value_ptr, to_ptr(value), self.value_type_size)
                return
            }
            i = add(i, 1)
        }

        // Key not found - insert new entry (use same workaround as insert())
        if keys_arr.len.eq(keys_arr.cap) {
            mut new_cap := mul(keys_arr.cap, 2)
            mut new_ptr := malloc(mul(new_cap, keys_arr.type_size))
            memcpy(new_ptr, keys_arr.ptr, mul(keys_arr.len, keys_arr.type_size))
            free(keys_arr.ptr)
            keys_arr.ptr = new_ptr
            keys_arr.cap = new_cap
        }
        mut dest_ptr := add(keys_arr.ptr, mul(keys_arr.len, keys_arr.type_size))
        memcpy(dest_ptr, to_ptr(key), keys_arr.type_size)
        keys_arr.len = add(keys_arr.len, 1)
        self.keys = keys_arr

        mut values_arr := self.values
        if values_arr.len.eq(values_arr.cap) {
            mut new_cap := mul(values_arr.cap, 2)
            mut new_ptr := malloc(mul(new_cap, values_arr.type_size))
            memcpy(new_ptr, values_arr.ptr, mul(values_arr.len, values_arr.type_size))
            free(values_arr.ptr)
            values_arr.ptr = new_ptr
            values_arr.cap = new_cap
        }
        mut dest_ptr2 := add(values_arr.ptr, mul(values_arr.len, values_arr.type_size))
        memcpy(dest_ptr2, to_ptr(value), values_arr.type_size)
        values_arr.len = add(values_arr.len, 1)
        self.values = values_arr

        self.size = add(self.size, 1)
    }

    // Get value for given key
    // Copies value into dest parameter
    // Throws KeyNotFoundError if key doesn't exist
    get := proc(self: Map, key: Dynamic, mut dest: Dynamic) throws KeyNotFoundError {
        mut i := 0
        mut keys_arr := self.keys
        while lt(i, self.size) {
            // Special case for Str keys: compare content, not struct bytes
            mut keys_match := 0
            if Str.eq(self.key_type_name, "Str") {
                // For Str keys, use Str.eq() to compare content
                mut existing_key := ""
                mut existing_key_ptr := add(keys_arr.ptr, mul(i, self.key_type_size))
                memcpy(to_ptr(existing_key), existing_key_ptr, self.key_type_size)
                mut key_str := ""
                memcpy(to_ptr(key_str), to_ptr(key), self.key_type_size)
                if key_str.eq(existing_key) {
                    keys_match = 0
                } else {
                    keys_match = 1
                }
            } else {
                mut existing_key_ptr := add(keys_arr.ptr, mul(i, self.key_type_size))
                keys_match = memcmp(to_ptr(key), existing_key_ptr, self.key_type_size)
            }

            if keys_match.eq(0) {
                // Key found - get value
                mut values_arr := self.values
                mut value_ptr := add(values_arr.ptr, mul(i, self.value_type_size))
                memcpy(to_ptr(dest), value_ptr, self.value_type_size)
                return
            }
            i = add(i, 1)
        }

        // Key not found
        throw KeyNotFoundError.new("Key not found in map")
    }

    // Check if key exists in map
    contains_key := proc(self: Map, key: Dynamic) returns Bool {
        mut i := 0
        mut keys_arr := self.keys
        while lt(i, self.size) {
            // Special case for Str keys: compare content, not struct bytes
            mut keys_match := 0
            if Str.eq(self.key_type_name, "Str") {
                // For Str keys, use Str.eq() to compare content
                mut existing_key := ""
                mut existing_key_ptr := add(keys_arr.ptr, mul(i, self.key_type_size))
                memcpy(to_ptr(existing_key), existing_key_ptr, self.key_type_size)
                mut key_str := ""
                memcpy(to_ptr(key_str), to_ptr(key), self.key_type_size)
                if key_str.eq(existing_key) {
                    keys_match = 0
                } else {
                    keys_match = 1
                }
            } else {
                mut existing_key_ptr := add(keys_arr.ptr, mul(i, self.key_type_size))
                keys_match = memcmp(to_ptr(key), existing_key_ptr, self.key_type_size)
            }

            if keys_match.eq(0) {
                return true
            }
            i = add(i, 1)
        }
        return false
    }

    // Get position (index) of key in insertion order
    // Returns -1 if key not found
    // Useful for enum variant tags which depend on insertion order
    position := proc(self: Map, key: Dynamic) returns I64 {
        mut i := 0
        mut keys_arr := self.keys
        while lt(i, self.size) {
            // Special case for Str keys: compare content, not struct bytes
            mut keys_match := 0
            if Str.eq(self.key_type_name, "Str") {
                // For Str keys, use Str.eq() to compare content
                mut existing_key := ""
                mut existing_key_ptr := add(keys_arr.ptr, mul(i, self.key_type_size))
                memcpy(to_ptr(existing_key), existing_key_ptr, self.key_type_size)
                mut key_str := ""
                memcpy(to_ptr(key_str), to_ptr(key), self.key_type_size)
                if key_str.eq(existing_key) {
                    keys_match = 0
                } else {
                    keys_match = 1
                }
            } else {
                mut existing_key_ptr := add(keys_arr.ptr, mul(i, self.key_type_size))
                keys_match = memcmp(to_ptr(key), existing_key_ptr, self.key_type_size)
            }

            if keys_match.eq(0) {
                return i
            }
            i = add(i, 1)
        }
        return sub(0, 1)  // -1 for not found
    }

    // Get number of entries in map
    len := proc(self: Map) returns I64 {
        return self.size
    }

    // Free map memory and reset
    delete := proc(mut self: Map) {
        mut keys_arr := self.keys
        keys_arr.delete()

        mut values_arr := self.values
        values_arr.delete()

        self.size = 0
    }
}

// List - Dynamic heterogeneous collection (also known as Tuple)
// Stores elements of different types in contiguous memory
// Type information tracked via parallel metadata vectors
List := struct {
    mut data : I64  = 0     // Pointer to raw memory blob
    mut len : I64 = 0       // Number of elements stored
    mut type_names : Vec = Vec()  // Vec<Str> - type name per element
    mut type_sizes : Vec = Vec()  // Vec<I64> - size per element

    // Create new empty list
    new := proc() returns List throws AllocError {
        mut list := List()
        // Allocate enough for reasonable number of elements
        list.data = malloc(1024)

        if I64.eq(list.data, 0) {
            throw AllocError("List.new: malloc failed")
        }

        list.len = 0
        list.type_names = Vec.new("Str", size_of(Str))
        list.type_sizes = Vec.new("I64", size_of(I64))

        return list
    }

    // Calculate byte offset for element at index
    calc_offset := proc(self: List, index: I64) returns I64 throws IndexOutOfBoundsError {
        mut offset := 0
        mut i := 0
        while lt(i, index) {
            mut size := 0
            self.type_sizes.get(i, size)
            offset = add(offset, size)
            i = add(i, 1)
        }
        return offset
    }

    // Calculate total bytes used
    calc_used := proc(self: List) returns I64 throws IndexOutOfBoundsError {
        return self.calc_offset(self.len)
    }

    // Append element to list
    push := proc(mut self: List, value: Dynamic) throws AllocError, FullError, IndexOutOfBoundsError {
        mut type_name := type_as_str(value)
        mut type_size := size_of(value)

        // Copy value to data blob at current end
        mut offset := self.calc_used()
        mut value_ptr := to_ptr(value)
        memcpy(add(self.data, offset), value_ptr, type_size)

        // Record metadata
        self.type_names.push(type_name)
        self.type_sizes.push(type_size)
        self.len = add(self.len, 1)
    }

    // Get element at index
    get := proc(self: List, index: I64, mut dest: Dynamic) throws IndexOutOfBoundsError {
        if gteq(index, self.len) {
            throw IndexOutOfBoundsError(format("List.get: index ",
                format(index.to_str(), format(" >= len ", self.len.to_str()))))
        }

        if lt(index, 0) {
            throw IndexOutOfBoundsError(format("List.get: negative index ", index.to_str()))
        }

        // Get type info for validation
        mut stored_type := ""
        self.type_names.get(index, stored_type)

        mut dest_type := type_as_str(dest)

        // TODO FIX type checking when Dynamic type checking is enabled
        // if not(Str.eq(stored_type, dest_type)) {
        //     throw TypeMismatchError(...)
        // }

        // Calculate offset and size
        mut offset := self.calc_offset(index)
        mut size := 0
        self.type_sizes.get(index, size)

        // Copy data to destination
        mut dest_ptr := to_ptr(dest)
        memcpy(dest_ptr, add(self.data, offset), size)
    }

    // Set element at index
    set := proc(mut self: List, index: I64, value: Dynamic) throws IndexOutOfBoundsError {
        if gteq(index, self.len) {
            throw IndexOutOfBoundsError(format("List.set: index ",
                format(index.to_str(), format(" >= len ", self.len.to_str()))))
        }

        if lt(index, 0) {
            throw IndexOutOfBoundsError(format("List.set: negative index ", index.to_str()))
        }

        mut new_type := type_as_str(value)
        mut new_size := size_of(value)

        mut old_type := ""
        self.type_names.get(index, old_type)

        mut old_size := 0
        self.type_sizes.get(index, old_size)

        // Case 1: Same size - simple overwrite
        if I64.eq(new_size, old_size) {
            mut offset := self.calc_offset(index)
            mut value_ptr := to_ptr(value)
            memcpy(add(self.data, offset), value_ptr, new_size)

            // Update type name if different
            if not(Str.eq(new_type, old_type)) {
                self.type_names.set(index, new_type)
            }
            return
        }

        // Case 2: Different size - need to shift elements
        mut old_offset := self.calc_offset(index)
        mut after_offset := add(old_offset, old_size)
        mut used := self.calc_used()
        mut bytes_after := sub(used, after_offset)

        // Shift elements after index
        if gt(bytes_after, 0) {
            mut new_after_offset := add(old_offset, new_size)
            memcpy(add(self.data, new_after_offset), add(self.data, after_offset), bytes_after)
        }

        // Write new value
        mut value_ptr := to_ptr(value)
        memcpy(add(self.data, old_offset), value_ptr, new_size)

        // Update metadata
        self.type_names.set(index, new_type)
        self.type_sizes.set(index, new_size)
    }

    // Free list memory
    delete := proc(mut self: List) {
        free(self.data)
        self.data = 0
        self.len = 0

        mut names := self.type_names
        names.delete()

        mut sizes := self.type_sizes
        sizes.delete()
    }
}

