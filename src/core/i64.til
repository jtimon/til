mode lib

I64_SIZE  := 8

/** Error thrown by safe_add when overflow occurs. */
I64_Overflow := struct {
    mut msg: Str = ""
}

/** Error thrown by safe_div() when dividing by zero. */
DivideByZero := struct {}

/** I64 - Signed 64-bit integer type. */
// Note that I64 and U8 don't have fields because they're the "real core types"
// think of them as "half-external types" for now
I64 := struct {
    // mut data : Array = Array.new("U8", size_of(U8), 8) // TODO Use [8]U8 for this one

    /** Division that throws DivideByZero when dividing by zero.
     *  Use div() if you want silent behavior (returns 0). */
    safe_div := func(a: I64, b: I64) returns I64 throws DivideByZero {
        if I64.eq(b, 0) {
            throw DivideByZero()
        }
        return a.div(b)
    }

    /** Addition that throws I64_Overflow when overflow occurs.
     *  Use add() if you want silent wrapping behavior. */
    safe_add := func(a: I64, b: I64) returns I64 throws I64_Overflow {
        result := a.add(b)
        // Overflow detection: adding positive gave smaller, or adding negative gave larger
        if b.gt(0) {
            if result.lt(a) {
                throw I64_Overflow(msg=format(loc(), "I64 overflow: ", a.to_str(), " + ", b.to_str()))
            }
        }
        if b.lt(0) {
            if result.gt(a) {
                throw I64_Overflow(msg=format(loc(), "I64 overflow: ", a.to_str(), " + ", b.to_str()))
            }
        }
        return result
    }

    /** Subtraction that throws I64_Overflow when underflow occurs.
     *  Use sub() if you want silent wrapping behavior. */
    safe_sub := func(a: I64, b: I64) returns I64 throws I64_Overflow {
        result := a.sub(b)
        // Underflow detection: subtracting positive gave larger, or subtracting negative gave smaller
        if b.gt(0) {
            if result.gt(a) {
                throw I64_Overflow(msg=format(loc(), "I64 underflow: ", a.to_str(), " - ", b.to_str()))
            }
        }
        if b.lt(0) {
            if result.lt(a) {
                throw I64_Overflow(msg=format(loc(), "I64 underflow: ", a.to_str(), " - ", b.to_str()))
            }
        }
        return result
    }

    /** Multiplication that throws I64_Overflow when overflow occurs.
     *  Use mul() if you want silent wrapping behavior. */
    safe_mul := func(a: I64, b: I64) returns I64 throws I64_Overflow {
        result := a.mul(b)
        // Overflow detection: if a != 0 and result / a != b, overflow occurred
        if not(a.eq(0)) {
            if not(result.div(a).eq(b)) {
                throw I64_Overflow(msg=format(loc(), "I64 overflow: ", a.to_str(), " * ", b.to_str()))
            }
        }
        return result
    }

    // Bitwise operations
    xor := func(a: I64, b: I64) returns I64 {
        i64_xor := ext_func(a: I64, b: I64) returns I64 {}
        return i64_xor(a, b)
    }

    and := func(a: I64, b: I64) returns I64 {
        i64_and := ext_func(a: I64, b: I64) returns I64 {}
        return i64_and(a, b)
    }

    or := func(a: I64, b: I64) returns I64 {
        i64_or := ext_func(a: I64, b: I64) returns I64 {}
        return i64_or(a, b)
    }

    /** Modulo operation. Returns 0 when dividing by zero. */
    mod := func(a: I64, b: I64) returns I64 {
        i64_mod := ext_func(a: I64, b: I64) returns I64 {}
        return i64_mod(a, b)
    }

    /** Subtraction. */
    sub := func(a: I64, b: I64) returns I64 {
        i64_sub := ext_func(a: I64, b: I64) returns I64 {}
        return i64_sub(a, b)
    }

    /** Multiplication. */
    mul := func(a: I64, b: I64) returns I64 {
        i64_mul := ext_func(a: I64, b: I64) returns I64 {}
        return i64_mul(a, b)
    }

    /** Division that returns 0 when dividing by zero.
     *  This is the default, silent behavior - no error, no throw.
     *  Use safe_div() if you want to catch division by zero errors. */
    div := func(a: I64, b: I64) returns I64 {
        i64_div := ext_func(a: I64, b: I64) returns I64 {}
        return i64_div(a, b)
    }

    /** Addition. */
    add := func(a: I64, b: I64) returns I64 {
        i64_add := ext_func(a: I64, b: I64) returns I64 {}
        return i64_add(a, b)
    }

    eq := func(a: I64, b: I64) returns Bool {
        // REM We can't use the variadic or from here without mutual recusrion and a stack overflow
        if a.gt(b) {
            return false
        }
        if b.gt(a) {
            return false
        }
        if a.lt(b) {
            return false
        }
        if b.lt(a) {
            return false
        }
        return true
    }

    lt := func(a: I64, b: I64) returns Bool {
        i64_lt := ext_func(a: I64, b: I64) returns Bool {}
        return i64_lt(a, b)
    }

    gt := func(a: I64, b: I64) returns Bool {
        i64_gt := ext_func(a: I64, b: I64) returns Bool {}
        return i64_gt(a, b)
    }

    to_str := func(self: I64) returns Str {
        i64_to_str := ext_func(a: I64) returns Str {}
        return i64_to_str(self)
    }

    from_str := func(str: Str) returns I64 {
        str_to_i64 := ext_func(a: Str) returns I64 {}
        return str_to_i64(str) // TODO: this can fail and panic from rust
    }

    inc := proc(mut self: I64) {
        self = self.add(1)
    }

    dec := proc(mut self: I64) {
        self = self.sub(1)
    }

    gteq := func(a: I64, b: I64) returns Bool {
        if a.gt(b) {
            return true
        }
        if a.lt(b) {
            return false
        }
        return true  // equal
    }

    lteq := func(a: I64, b: I64) returns Bool {
        if a.lt(b) {
            return true
        }
        if a.gt(b) {
            return false
        }
        return true  // equal
    }

    size := func() returns I64 {
        return 8
    }
}

// Constants defined after struct so I64.sub is available
MIN_I64   := I64.sub(0, 922337203)
MAX_I64   := 9223372036854775807
