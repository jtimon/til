// C code generator for TIL
// Translates TIL AST to C source code

mode liba

import("self.parser")
import("self.init")
import("self.typer")
import("self.eval_heap")  // For EvalHeap.extract_vec_contents, g_heap

// Prefix for all TIL-generated names in C code (structs, functions, types)
TIL_PREFIX := "til_"

// Helper: Sort Vec<EnumVariant> by name field in-place (simple bubble sort)
sort_enum_variants := proc(mut v: Vec) {
    mut n := v.len()
    while gt(n, 1) {
        mut new_n := 0
        for i in 1..n {
            mut prev := EnumVariant()
            mut curr := EnumVariant()
            v.get(i.sub(1), prev)?
            v.get(i, curr)?
            if gt(prev.name.cmp(curr.name), 0) {
                v.set(i.sub(1), curr)?
                v.set(i, prev)?
                new_n = i
            }
            catch (err: IndexOutOfBoundsError) {
                panic(loc(), "sort_enum_variants: ", err.msg)
            }
        }
        n = new_n
    }
}

// Bug #133: Info about a precomputed Vec's static array serialization
PrecomputedStaticInfo := struct {
    mut data_array_name: Str = ""       // Name of the static array (e.g., "_precomp_chromatic_data")
}

// Codegen context for tracking function info
CodegenContext := struct {
    // Counter for generating unique temp variable names (deterministic per-compilation)
    mut mangling_counter: I64 = 0
    // Map function name -> variadic arg info
    mut func_variadic_args: Map = Map.new(Str, VariadicParamInfo)
    // Currently generating function's throw types (if any)
    mut current_throw_types: Vec = Vec.new(ValueType)
    // Currently generating function's return types (if any)
    mut current_return_types: Vec = Vec.new(ValueType)
    // Set of declared variable names in current function (to avoid redefinition)
    mut declared_vars: Set = Set.new(Str)
    // Set of by-ref param names in current function - for using -> instead of . for field access
    mut current_ref_params: Set = Set.new(Str)
    // Map of variadic param names to their element type (e.g., "args" -> "Bool")
    // Passed as til_Array* so need dereference, and need type info for Array.get casting
    mut current_variadic_params: Map = Map.new(Str, Str)
    // All known type names for generating til_size_of function
    mut known_types: Vec = Vec.new(Str)
    // Map of locally-caught error types to catch label info
    // For explicit throw statements that have a local catch block
    mut local_catch_labels: Map = Map.new(Str, CatchLabelInfo)
    // Current function name for nested function name mangling (empty at top-level)
    mut current_function_name: Str = ""
    // C code for hoisted nested function definitions
    mut hoisted_functions: Vec = Vec.new(Str)
    // C code for hoisted nested function prototypes
    mut hoisted_prototypes: Vec = Vec.new(Str)
    // Map original function name -> mangled name for nested functions
    mut nested_func_names: Map = Map.new(Str, Str)
    // Bug #133 fix: Map var_name -> static array name for precomputed heap values
    // When emitting assignments in main(), Ptr fields will use these static pointers
    mut precomputed_static_arrays: Map = Map.new(Str, PrecomputedStaticInfo)
    // Bug #168: variable name aliased to _ret for placement construction
    mut ret_var_alias: Str = ""

namespace:
    new := func() returns CodegenContext {
        return CodegenContext()
    }
}

// Generate unique mangled name using context counter (deterministic)
// Includes function name prefix for uniqueness across functions
next_mangled := func(mut ctx: CodegenContext) returns Str {
    n := ctx.mangling_counter
    ctx.mangling_counter = ctx.mangling_counter.add(1)
    func_name := ctx.current_function_name
    mut result := ""
    if not(func_name.is_empty()) {
        result = format("_tmp_", func_name, "_", n.to_str())
    } else {
        result = format("_tmp_", n.to_str())
    }

    return result
}

// Returns the C name for a TIL identifier - adds TIL_PREFIX
// Exceptions: C keywords (true, false), pointer deref (*name), and generated names (_ prefix)
til_name := func(name: Str) returns Str {
    mut result := name
    switch name {
    case "true":
    case "false":
    case:
        if name.starts_with("*").or(name.starts_with("_")) {
        } else {
            result = format(TIL_PREFIX, name)
        }
    }

    return result
}

// Bug #97: Returns the type-mangled C name for a variable
// e.g., til_var_name("x", "Str") -> "til_Str_x"
// Exceptions: C keywords (true, false), pointer deref (*name), and generated names (_ prefix)
til_var_name := func(name: Str, type_name: Str) returns Str {
    switch name {
    case "true":
        return name
    case "false":
        return name
    case:
        if name.starts_with("*").or(name.starts_with("_")) {
            return name
        } else {
            return format(TIL_PREFIX, type_name, "_", name)
        }
    }
}

// Bug #97: Convert a ValueType to a short string for use in variable name mangling
value_type_to_c_prefix := func(vt: ValueType) returns Str {
    switch vt {
    case ValueType.TCustom(name):
        return name
    case ValueType.TFunction(_):
        return "Func"
    case ValueType.TType(_):
        return "Type"
    case ValueType.TMulti(elem_type):
        return format("Multi_", elem_type)
    }
}

// Bug #97: Get the type-mangled C name for a variable by looking up its type
til_var_name_from_context := func(name: Str, context: Context) returns Str {
    if name.eq("true").or(name.eq("false")).or(name.starts_with("*")).or(name.starts_with("_")) {
        return name
    }
    sym := context.scope_stack.lookup_symbol(name)?
    catch (err: KeyNotFoundError) {
        // Fallback to old behavior if not a known variable (could be a type name, etc.)
        return til_name(name)
    }
    type_str := value_type_to_c_prefix(sym.value_type)
    return format(TIL_PREFIX, type_str, "_", name)
}

// Bug #168: Resolve a variable name, mapping ret_var_alias to "_ret"
resolve_var_name := func(name: Str, ctx: CodegenContext, context: Context) returns Str {
    if not(ctx.ret_var_alias.is_empty()) {
        if name.eq(ctx.ret_var_alias) {
            return "_ret"
        }
    }
    return til_var_name_from_context(name, context)
}

// Bug #168: Recursively collect Return nodes from AST, skipping nested FuncDefs
collect_return_exprs := proc(stmts: Vec, mut out: Vec) {
    for expr: Expr in stmts {
        switch expr.node_type {
        case NodeType.Return:
            out.push(expr.clone())
        case NodeType.FuncDef(_):
            // Skip nested function bodies - their returns belong to them
        case:
            // Recurse into children
            collect_return_exprs(expr.params, out)
        }
    }
}

// Bug #168: Check if a variable is reassigned (full assignment, not field write)
// anywhere in the function body. Skips nested FuncDefs.
has_reassignment_of := proc(stmts: Vec, var_name: Str) returns Bool {
    for expr: Expr in stmts {
        switch expr.node_type {
        case NodeType.Assignment(name):
            // Full reassignment (no dot = no field access)
            if name.eq(var_name).and(name.find(".").lt(0)) {
                return true
            }
        case NodeType.FuncDef(_):
            // Skip nested function bodies
        case:
            if has_reassignment_of(expr.params, var_name) {
                return true
            }
        }
    }
    return false
}

// Bug #168: Pre-scan function body to find a single return variable that can be
// placed directly into _ret. Returns the var name or "" if not applicable.
find_ret_var_for_placement := proc(func_def: SFuncDef) returns Str throws Str {
    // Must be a throwing function with struct/enum return type
    if func_def.throw_types.is_empty() {
        return ""
    }
    if func_def.return_types.is_empty() {
        return ""
    }
    first_ret_type_ref := func_def.return_types.get_by_ref(0)?
    create_alias(first_ret_type, ValueType, first_ret_type_ref.data)
    catch (err: IndexOutOfBoundsError) {
        return ""
    }
    switch first_ret_type {
    case ValueType.TCustom(_):
        // good - struct/enum return type
    case:
        return ""
    }

    // Collect all return expressions
    mut ret_exprs := Vec.new(Expr)
    collect_return_exprs(func_def.body, ret_exprs)

    if ret_exprs.is_empty() {
        return ""
    }

    // Check that all returns use the same simple Identifier
    mut ret_var_name := ""
    mut has_ret_var := false
    for ret_expr: Expr in ret_exprs {
        if ret_expr.params.is_empty() {
            return ""  // bare return with no value
        }
        value := ret_expr.get(0)?
        dont_delete(value)
        switch value.node_type {
        case NodeType.Identifier(name):
            if not(value.params.is_empty()) {
                return ""  // field access like result.field
            }
            if not(has_ret_var) {
                ret_var_name = name.clone()
                has_ret_var = true
            } else if not(ret_var_name.eq(name)) {
                return ""  // different return variables
            }
        case:
            return ""  // not a simple identifier (e.g. FCall)
        }
    }

    if not(has_ret_var) {
        return ""
    }

    // Must not be a function parameter (params have their own storage)
    for arg: Declaration in func_def.args {
        if arg.name.eq(ret_var_name) {
            return ""
        }
    }

    // Must not be reassigned after initialization (RVO aliasing makes
    // *_ret = func(_ret, ...) unsafe - the C compiler may construct the
    // return value directly in *_ret, destroying the input argument)
    if has_reassignment_of(func_def.body, ret_var_name) {
        return ""
    }

    // Must not have any parameter with the same type as the return type.
    // Callers can pass the same variable as both _ret and an input param
    // (e.g. new_body = f(new_body, ...)). The optimization writes to *_ret
    // immediately, destroying the aliased input before it's read.
    ret_type_str := value_type_to_str(first_ret_type)
    for arg: Declaration in func_def.args {
        if value_type_to_str(arg.value_type).eq(ret_type_str) {
            return ""
        }
    }

    return ret_var_name
}

// Emit a struct literal, using compound literal syntax if already_declared
// e.g., " = {0}" vs " = (til_TypeName){0}"
emit_struct_literal_assign := proc(mut output: Str, type_name: Str, already_declared: Bool, literal_content: Str) {
    output.push_str(" = ")
    if already_declared {
        output.push_str("(")
        output.push_str(til_name(type_name))
        output.push_str(")")
    }
    output.push_str(literal_content)
    output.push_str(";\n")
}

// Start a struct literal assignment - caller will emit content and closing brace
// e.g., " = " vs " = (til_TypeName)"
emit_struct_literal_start := proc(mut output: Str, type_name: Str, already_declared: Bool) {
    output.push_str(" = ")
    if already_declared {
        output.push_str("(")
        output.push_str(til_name(type_name))
        output.push_str(")")
    }
}

// Returns the C name for a TIL function - adds TIL_PREFIX and converts dots to underscores
// Used for function names like Array.len -> til_Array_len
til_func_name := func(name: Str) returns Str {
    result := format(TIL_PREFIX, name.replace(".", "_"))

    return result
}

// Get function name from FCall's first param, handling both AST patterns:
// - Identifier("func") with params = [] -> "func"
// - Identifier("I64.inc") with params = [] -> "I64_inc" (from precomp)
// - Identifier("Vec") with params = [Identifier("new")] -> "Vec_new" (from parser)
// Returns the C-ready name with underscores (not dots)
get_func_name_string := func(first_param: Expr) returns Str throws Str {
    switch first_param.node_type {
    case NodeType.Identifier(name):
        if first_param.params.is_empty() {
            return name.replace(".", "_")
        } else {
            mut parts := Vec.new(Str)
            parts.push(name)
            for p: Expr in first_param.params {
                switch p.node_type {
                case NodeType.Identifier(part_name):
                    parts.push(part_name)
                case:
                }
            }
            return parts.join("_")
        }
    case:
        throw "get_func_name_string: not an Identifier"
    }

}

// Get TIL function name from FCall's first param (for scope lookups)
// Returns the dot-separated name like "Vec.new" or "I64.inc"
// This avoids the fragile .replacen('_', ".", 1) hack which breaks on type names with underscores
get_til_func_name_string := func(first_param: Expr) returns Str throws Str {
    switch first_param.node_type {
    case NodeType.Identifier(name):
        if first_param.params.is_empty() {
            // Already has dots (from precomp) or is a simple name
            return name
        } else {
            // Build dotted name from nested identifiers
            mut parts := Vec.new(Str)
            parts.push(name)
            for p: Expr in first_param.params {
                switch p.node_type {
                case NodeType.Identifier(part_name):
                    parts.push(part_name)
                case:
                }
            }
            return parts.join(".")
        }
    case:
        throw "get_til_func_name_string: not an Identifier"
    }

}

/// Check if an expression is a type identifier (a Type parameter at call site)
/// Returns the type name if it is, so it can be emitted as a string literal
/// Matches interpreter.rs behavior (line 1703-1713)
/// Only matches STANDALONE identifiers - not field access like Vec.INIT_CAP
get_type_arg_name := func(expr: Expr, context: Context) returns Str throws KeyNotFoundError {
    switch expr.node_type {
    case NodeType.Identifier(name):
        // Only match standalone identifiers (no field access)
        if not(expr.params.is_empty()) {
            throw KeyNotFoundError(msg="field access, not type arg")
        }
        // Check if this identifier is a type name (struct, enum, or builtin type)
        sym := context.scope_stack.lookup_symbol(name)?
        switch sym.value_type {
        case ValueType.TType:
            return name
        case:
            throw KeyNotFoundError(msg="not a type")
        }
    case:
        throw KeyNotFoundError(msg="not an identifier")
    }
}

// Get function definition for an FCall expression
// Uses get_func_def_for_fcall_with_expr from typer.rs which handles all cases correctly
// (regular functions, struct methods, UFCS - though UFCS is already resolved by precomp)
// Returns None for struct/enum constructors
get_fcall_func_def := proc(context: Context, fcall_expr: Expr) returns SFuncDef throws KeyNotFoundError {
    mut expr_clone := fcall_expr.clone()
    result := get_func_def_for_fcall_with_expr(context, expr_clone)?

    catch (err: Str) { throw KeyNotFoundError(msg=err) }

    return result
}

/// Bug #60: Determine if a parameter should be passed by reference (pointer)
/// - copy params: pass by value (copy is made)
/// - Type params: already pointers (const char*), pass by value
/// - All other const/own/mut params (including Dynamic): pass by pointer
param_needs_by_ref := func(param: Declaration) returns Bool {
    if param.is_copy {
        return false
    }
    // Type is already a pointer type in C (const char*), no extra indirection needed
    switch param.value_type {
    case ValueType.TCustom(type_name):
        if type_name.eq("Type") {
            return false
        }
        return true
    case:
        return true
    }
}

/// Extract struct field type dependencies for topological sorting
/// Returns the type name if it's a custom type that needs to be defined first
get_field_type_dependency := func(value_type: ValueType) returns Str throws KeyNotFoundError {
    switch value_type {
    case ValueType.TCustom(name):
        // I64 and U8 are primitives, not struct dependencies
        switch name {
        case "I64":
            throw KeyNotFoundError(msg="primitive")
        case "U8":
            throw KeyNotFoundError(msg="primitive")
        case:
            if name.eq(INFER_TYPE) {
                panic(loc(), "INFER_TYPE in ccodegen - should have been resolved by typer")
            }
            return name
        }
    case:
        throw KeyNotFoundError(msg="not custom type")
    }
}

/// Get struct dependencies (other struct types used as fields)
get_struct_dependencies := func(expr: Expr) returns Vec throws Str {
    mut deps := Vec.new(Str)
    switch expr.node_type {
    case NodeType.Declaration:
        if not(expr.params.is_empty()) {
            first_param := expr.get(0)?
            dont_delete(first_param)
            switch first_param.node_type {
            case NodeType.StructDef(struct_def):
                for member: Declaration in struct_def.members {
                    if member.is_mut {
                        dep := get_field_type_dependency(member.value_type)?
                        deps.push(dep)
                        catch (err: KeyNotFoundError) {
                            // REM: Not a custom type dependency, skip
                        }
                    }
                }
            case:
            }
        }
    case:
    }


    return deps
}

/// Get struct name from a struct declaration expression
get_struct_name := func(expr: Expr) returns Str throws KeyNotFoundError, Str {
    switch expr.node_type {
    case NodeType.Declaration(decl):
        if not(expr.params.is_empty()) {
            first_param := expr.get(0)?
            dont_delete(first_param)
            switch first_param.node_type {
            case NodeType.StructDef:
                return decl.name
            case:
            }
        }
    case:
    }


    throw KeyNotFoundError(msg="not a struct declaration")
}

/// Get enum dependencies (types used in payloads) as a Vec
get_enum_dependencies := func(expr: Expr) returns Vec throws Str {
    mut deps := Vec.new(Str)
    switch expr.node_type {
    case NodeType.Declaration:
        if not(expr.params.is_empty()) {
            first_param := expr.get(0)?
            dont_delete(first_param)
            switch first_param.node_type {
            case NodeType.EnumDef(enum_def):
                for variant: EnumVariant in enum_def.variants {
                    if not(variant.payload_type.is_null()) {
                        mut pt := ValueType.TCustom("")
                        memcpy(to_ptr(pt), variant.payload_type.data, size_of(ValueType))
                        switch pt {
                        case ValueType.TCustom(type_name):
                            // Skip primitives (but NOT Str or Bool - they're structs)
                            if not(type_name.eq("I64")).and(not(type_name.eq("U8"))).and(not(type_name.eq("Dynamic"))).and(not(type_name.eq("Type"))) {
                                deps.push(type_name.clone())
                            }
                        case:
                        }
                    }
                }
            case:
            }
        }
    case:
    }


    return deps
}

/// Get enum name from an enum-with-payloads declaration expression
get_enum_name := func(expr: Expr) returns Str throws KeyNotFoundError, Str {
    switch expr.node_type {
    case NodeType.Declaration(decl):
        if not(expr.params.is_empty()) {
            first_param := expr.get(0)?
            dont_delete(first_param)
            switch first_param.node_type {
            case NodeType.EnumDef:
                return decl.name
            case:
            }
        }
    case:
    }


    throw KeyNotFoundError(msg="not an enum declaration")
}

/// Get dependencies for a type (struct or enum-with-payloads)
get_type_dependencies := func(expr: Expr) returns Vec throws Str {
    if is_struct_declaration(expr)? {
        return get_struct_dependencies(expr)?
    } else if is_enum_declaration(expr)?.and(is_enum_with_payloads(expr)?) {
        return get_enum_dependencies(expr)?
    } else {
        return Vec.new(Str)
    }
}

/// Get name for a type (struct or enum-with-payloads)
get_type_name := func(expr: Expr) returns Str throws KeyNotFoundError, Str {
    if is_struct_declaration(expr)? {
        return get_struct_name(expr)?
    } else if is_enum_declaration(expr)?.and(is_enum_with_payloads(expr)?) {
        return get_enum_name(expr)?
    } else {
        throw KeyNotFoundError(msg="not a type declaration")
    }
}

/// Topologically sort type declarations (structs and enums-with-payloads) by their dependencies
/// Returns indices into the original vector in sorted order
topological_sort_types := func(types: Vec) returns Vec throws Str {
    // Build name -> index map
    mut name_to_idx := Map.new(Str, I64)
    mut idx := 0
    while idx.lt(types.len()) {
        expr_ref := types.get_by_ref(idx)?
        create_alias(expr, Expr, expr_ref.data)
        name := get_type_name(expr)?
        name_to_idx.set(name, idx.clone())
        catch (err: KeyNotFoundError) {
            // REM: Not a type declaration, skip
        }
        idx.inc()
    }

    // Build adjacency list (dependencies)
    mut deps := Vec.new(Vec)
    mut i := 0
    while i.lt(types.len()) {
        deps.push(Vec.new(I64))
        i.inc()
    }
    idx = 0
    while idx.lt(types.len()) {
        dep_expr_ref := types.get_by_ref(idx)?
        create_alias(dep_expr, Expr, dep_expr_ref.data)
        dep_names := get_type_dependencies(dep_expr)?
        for dep_name: Str in dep_names {
            if name_to_idx.contains_key(dep_name) {
                mut dep_idx := 0
                name_to_idx.get(dep_name, dep_idx)?
                if not(dep_idx.eq(idx)) {
                    mut dep_vec := Vec.new(I64)
                    deps.get(idx, dep_vec)?
                    dep_vec.push(dep_idx)
                    deps.set(idx, dep_vec)?
                }
            }
        }
        idx.inc()
    }

    // Kahn's algorithm for topological sort
    // If A depends on B, B must come first, so reverse the edges
    mut reverse_deps := Vec.new(Vec)
    i = 0
    while i.lt(types.len()) {
        reverse_deps.push(Vec.new(I64))
        i.inc()
    }
    idx = 0
    while idx.lt(deps.len()) {
        dep_list_ref := deps.get_by_ref(idx)?
        create_alias(dep_list, Vec, dep_list_ref.data)
        for dep: I64 in dep_list {
            mut rev_dep_vec := Vec.new(I64)
            reverse_deps.get(dep, rev_dep_vec)?
            rev_dep_vec.push(idx)
            reverse_deps.set(dep, rev_dep_vec)?
        }
        idx.inc()
    }

    // Calculate in-degree for reversed graph
    mut in_degree := Vec.new(I64)
    i = 0
    while i.lt(types.len()) {
        in_degree.push(0)
        i.inc()
    }
    for dep_list: Vec in reverse_deps {
        for dep: I64 in dep_list {
            mut degree := 0
            in_degree.get(dep, degree)?
            in_degree.set(dep, degree.add(1))?
        }
    }

    mut queue := Vec.new(I64)
    idx = 0
    while idx.lt(in_degree.len()) {
        mut queue_degree := 0
        in_degree.get(idx, queue_degree)?
        if queue_degree.eq(0) {
            queue.push(idx)
        }
        idx.inc()
    }

    mut result := Vec.new(I64)
    while not(queue.is_empty()) {
        mut pop_idx := 0
        queue.pop(pop_idx)?
        result.push(pop_idx.clone())
        rev_dep_list_ref := reverse_deps.get_by_ref(pop_idx)?
        create_alias(rev_dep_list, Vec, rev_dep_list_ref.data)
        for next: I64 in rev_dep_list {
            mut next_degree := 0
            in_degree.get(next, next_degree)?
            next_degree = next_degree.sub(1)
            in_degree.set(next, next_degree.clone())?
            if next_degree.eq(0) {
                queue.push(next)
            }
        }
    }

    // If we couldn't sort all (cycle), just append remaining in original order
    if result.len().lt(types.len()) {
        idx = 0
        while idx.lt(types.len()) {
            mut found := false
            for r: I64 in result {
                if r.eq(idx) {
                    found = true
                    break
                }
            }
            if not(found) {
                result.push(idx)
            }
            idx.inc()
        }
    }

    catch (err: IndexOutOfBoundsError) { throw err.msg }
    catch (err: KeyNotFoundError) { throw err.msg }

    return result
}

/// Hoist variadic arguments into a til_Array
/// Returns the array variable name, or None if no variadic args
/// Also returns the element type for use in Array.delete
hoist_variadic_args := proc(elem_type: Str, variadic_args: Vec, already_hoisted: Map, regular_count: I64, mut output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) returns Str throws Str {
    indent_str := repeat("    ", indent)
    arr_var := next_mangled(ctx)
    err_suffix := next_mangled(ctx)
    variadic_count := variadic_args.len()

    // Declare the array variable
    output.push_str(indent_str)
    output.push_str(TIL_PREFIX)
    output.push_str("Array ")
    output.push_str(arr_var)
    output.push_str(";\n")

    // Declare error var for Array.set (IndexOutOfBoundsError)
    output.push_str(indent_str)
    output.push_str(TIL_PREFIX)
    output.push_str("IndexOutOfBoundsError __attribute__((unused)) _err_idx_")
    output.push_str(err_suffix)
    output.push_str(";\n")

    // Hoist variadic args into temp vars (needed to pass address to Array.set)
    mut arg_temps := Vec.new(Str)
    c_elem_type := format(TIL_PREFIX, elem_type)
    mut i := 0
    while i.lt(variadic_args.len()) {
        arg_ref := variadic_args.get_by_ref(i)?
        create_alias(arg, Expr, arg_ref.data)
        hoisted_idx := add(regular_count, i)
        if already_hoisted.contains_key(hoisted_idx) {
            // Already hoisted, use that temp
            mut temp := ""
            already_hoisted.get(hoisted_idx, temp)?
            arg_temps.push(temp)
        } else {
            // Need to hoist into a temp
            // Bug #143: Use emit_arg_string to properly handle nested FCalls that need by-ref
            arg_str := emit_arg_string(arg, Ptr(), false, output, indent, ctx, context)?
            temp_var := next_mangled(ctx)
            output.push_str(indent_str)
            output.push_str(c_elem_type)
            output.push_str(" ")
            output.push_str(temp_var)
            output.push_str(" = ")
            output.push_str(arg_str)
            output.push_str(";\n")
            arg_temps.push(temp_var)
        }
        i.inc()
    }

    // Bug #60: Hoist Type and I64 literals for Array.new call
    // Type param needs hoisting because we can't take address of string literal for const char* const*
    type_temp := next_mangled(ctx)
    output.push_str(indent_str)
    output.push_str(TIL_PREFIX)
    output.push_str("Type ")
    output.push_str(type_temp)
    output.push_str(" = \"")
    output.push_str(elem_type)
    output.push_str("\";\n")

    // Capacity param needs hoisting because we can't take address of integer literal
    count_temp := next_mangled(ctx)
    output.push_str(indent_str)
    output.push_str(TIL_PREFIX)
    output.push_str("I64 ")
    output.push_str(count_temp)
    output.push_str(" = ")
    output.push_str(variadic_count.to_str())
    output.push_str(";\n")

    // Emit Array.new call (non-throwing, panics internally on malloc failure)
    // til_Array arr = til_Array_new(type_temp, &count_temp);
    output.push_str(indent_str)
    output.push_str(arr_var)
    output.push_str(" = ")
    output.push_str(TIL_PREFIX)
    output.push_str("Array_new(")
    output.push_str(type_temp)
    output.push_str(", &")
    output.push_str(count_temp)
    output.push_str(");\n")

    // Declare status variable for Array.set calls
    output.push_str(indent_str)
    output.push_str("int __attribute__((unused)) _arr_status_")
    output.push_str(err_suffix)
    output.push_str(";\n")

    // Emit Array.set for each variadic arg
    mut ti := 0
    while ti.lt(arg_temps.len()) {
        temp_item_ref := arg_temps.get_by_ref(ti)?
        create_alias(temp_item, Str, temp_item_ref.data)
        // Bug #60: Hoist index literal because we can't take address of integer literal
        idx_temp := next_mangled(ctx)
        output.push_str(indent_str)
        output.push_str(TIL_PREFIX)
        output.push_str("I64 ")
        output.push_str(idx_temp)
        output.push_str(" = ")
        output.push_str(ti.to_str())
        output.push_str(";\n")

        // int _status = til_Array_set(&_err_idx, &arr, &idx_temp, (til_Dynamic*)&temp);
        output.push_str(indent_str)
        output.push_str("_arr_status_")
        output.push_str(err_suffix)
        output.push_str(" = ")
        output.push_str(TIL_PREFIX)
        output.push_str("Array_set(&_err_idx_")
        output.push_str(err_suffix)
        output.push_str(", &")
        output.push_str(arr_var)
        output.push_str(", &")
        output.push_str(idx_temp)
        output.push_str(", (")
        output.push_str(TIL_PREFIX)
        output.push_str("Dynamic*)&")
        output.push_str(temp_item)
        output.push_str(");\n")

        // Error check for Array.set (IndexOutOfBoundsError - shouldn't happen but propagate if thrown)
        output.push_str(indent_str)
        output.push_str("if (_arr_status_")
        output.push_str(err_suffix)
        output.push_str(" != 0) {\n")
        mut curr_idx := 0
        for curr_throw: ValueType in ctx.current_throw_types {
            switch curr_throw {
            case ValueType.TCustom(curr_type_name):
                if curr_type_name.eq("IndexOutOfBoundsError") {
                    output.push_str(indent_str)
                    output.push_str("    *_err")
                    output.push_str(add(curr_idx, 1).to_str())
                    output.push_str(" = _err_idx_")
                    output.push_str(err_suffix)
                    output.push_str("; return ")
                    output.push_str(add(curr_idx, 1).to_str())
                    output.push_str(";\n")
                    break
                }
            case:
            }
            curr_idx.inc()
        }
        output.push_str(indent_str)
        output.push_str("}\n")
        ti.inc()
    }

    catch (err: IndexOutOfBoundsError) { throw err.msg }
    catch (err: KeyNotFoundError) { throw format("KeyNotFoundError: ", err.msg) }

    return arr_var
}

/// Detect if an expression is a variadic function call
/// Returns VariadicFCallInfo if it's a variadic call
detect_variadic_fcall := func(expr: Expr, ctx: CodegenContext) returns VariadicFCallInfo throws KeyNotFoundError {
    if expr.params.is_empty() {
        throw KeyNotFoundError(msg="Not variadic")
    }

    // Get original TIL function name (with dots) for variadic map lookup
    first_param := expr.get(0)?
    dont_delete(first_param)
    orig_func_name := get_til_func_name_string(first_param)?

    mut info := VariadicFCallInfo()
    ctx.func_variadic_args.get(orig_func_name, info)?
    catch (err: Str) {
        throw KeyNotFoundError(msg="Not variadic: name error")
    }

    return VariadicFCallInfo(elem_type=info.elem_type.clone(), regular_count=info.regular_count)
}

/// Check if an expression is a pure lvalue (simple identifier or field access chain)
/// A pure lvalue can use & directly without hoisting.
/// Returns false for FCall results, enum constructors, and struct constructors.
is_pure_lvalue := func(arg: Expr, context: Context) returns Bool throws Str {
    switch arg.node_type {
    case NodeType.Identifier(name):
        if arg.params.is_empty() {
            // Simple identifier is always an lvalue
            return true
        } else {
            // Could be field access OR enum/struct constructor
            // Check if name.first_param is an enum constructor
            if not(arg.params.is_empty()) {
                first_param := arg.get(0)?
                dont_delete(first_param)
                switch first_param.node_type {
                case NodeType.Identifier(variant):
                    combined := format(name, ".", variant)
                    is_enum := context.scope_stack.is_enum_constructor(combined)
                    if is_enum {
                        return false  // Enum constructor is NOT an lvalue
                    }
                    // Check if it's a struct type (struct constructor like Type.CONSTANT)
                    // Rust: if context.scope_stack.lookup_struct(name).is_some() { return false; }
                    // TIL: use flag pattern since lookup_struct throws on not found
                    mut is_struct := false
                    _ := context.scope_stack.lookup_struct(name)?
                    is_struct = true
                    catch (err: KeyNotFoundError) {
                        // REM: Not a struct, continue checking
                    }
                    if is_struct {
                        return false  // Struct constant access is NOT an lvalue (it's a global)
                    }
                case:
                }
            }
            // Field access chain: all params must also be pure identifiers (field names)
            for p: Expr in arg.params {
                switch p.node_type {
                case NodeType.Identifier(id):
                    if not(p.params.is_empty()) {
                        return false
                    }
                case:
                    return false
                }
            }
            return true
        }
    case:
        return false
    }
}

/// This is the core of the single-pass hoist+emit approach (Bug #143 fix).
/// By processing each arg once and returning the string directly, we avoid
/// the need for expression identity tracking.
emit_arg_string := proc(arg: Expr, param_type: Ptr, param_by_ref: Bool, mut hoist_output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) returns Str throws Str {
    indent_str := repeat("    ", indent)

    // Check if arg is a type identifier - emit as string literal
    mut type_name_result := ""
    mut is_type_arg := false
    type_name_result = get_type_arg_name(arg, context)?
    is_type_arg = true
    catch (err: KeyNotFoundError) {
        // REM: Not a type arg, continue
    }
    if is_type_arg {
        return format("\"", type_name_result, "\"")
    }

    // Check if this is a throwing FCall - needs hoisting
    switch arg.node_type {
    case NodeType.FCall(_):
        mut fd_throwing := SFuncDef()
        mut is_throwing := false
        fd_throwing = get_fcall_func_def(context, arg)?
        if not(fd_throwing.throw_types.is_empty()) {
            is_throwing = true
        }
        catch (err: KeyNotFoundError) {
            // REM: Not a function call we can look up, continue
        }
        if is_throwing {
            return emit_throwing_arg_string(arg, fd_throwing, param_type, param_by_ref, hoist_output, indent, ctx, context)?
        }
    case:
    }

    // Check if this is a variadic FCall (even if not throwing) - needs hoisting
    switch arg.node_type {
    case NodeType.FCall(_):
        mut vi_check := VariadicFCallInfo()
        mut is_variadic := false
        vi_check = detect_variadic_fcall(arg, ctx)?
        is_variadic = true
        catch (err: KeyNotFoundError) {
            // REM: Not variadic, continue
        }
        if is_variadic {
            return emit_variadic_arg_string(arg, vi_check, param_type, param_by_ref, hoist_output, indent, ctx, context)?
        }
    case:
    }

    // Check if param type is Dynamic - need special handling
    mut is_dynamic := false
    if not(param_type.is_null()) {
        mut pt := ValueType.TCustom(INFER_TYPE)
        param_type.dereference(ValueType, pt)
        switch pt {
        case ValueType.TCustom(pt_name):
            if pt_name.eq("Dynamic") {
                is_dynamic = true
            }
        case:
        }
    }

    if is_dynamic {
        return emit_arg_string_dynamic(arg, hoist_output, indent, indent_str, ctx, context)?
    }

    // Check if param is by-ref (mut) - need & prefix
    if param_by_ref {
        return emit_arg_string_by_ref(arg, hoist_output, indent, indent_str, ctx, context)?
    }

    // For non-throwing, non-variadic FCalls, recursively process nested args
    switch arg.node_type {
    case NodeType.FCall(_):
        return emit_fcall_arg_string(arg, hoist_output, indent, ctx, context)?
    case:
    }

    // Handle UFCS result chains: Identifier("_")[FCall, field1, field2, ...]
    switch arg.node_type {
    case NodeType.Identifier(ufcs_name):
        if ufcs_name.eq("_").and(not(arg.params.is_empty())) {
            ufcs_first := arg.get(0)?
            dont_delete(ufcs_first)
            switch ufcs_first.node_type {
            case NodeType.FCall(_):
                base_str := emit_arg_string(ufcs_first, Ptr(), false, hoist_output, indent, ctx, context)?
                mut ufcs_result := base_str.clone()
                mut ufcs_i := 1
                while ufcs_i.lt(arg.params.len()) {
                    ufcs_field := arg.get(ufcs_i)?
                    dont_delete(ufcs_field)
                    switch ufcs_field.node_type {
                    case NodeType.Identifier(field_name):
                        ufcs_result.push_str(".")
                        ufcs_result.push_str(field_name)
                    case:
                    }
                    ufcs_i.inc()
                }
                return ufcs_result
            case:
            }
        }
    case:
    }

    // For other expressions, emit to a temp string and return
    mut final_expr_str := ""
    emit_expr(arg, final_expr_str, 0, ctx, context)?

    return final_expr_str
}

/// Helper for emit_arg_string: Handle Dynamic params
emit_arg_string_dynamic := proc(arg: Expr, mut hoist_output: Str, indent: I64, indent_str: Str, mut ctx: CodegenContext, mut context: Context) returns Str throws Str {
    // Check if it's a pure lvalue
    if is_pure_lvalue(arg, context)? {
        switch arg.node_type {
        case NodeType.Identifier(name):
            if arg.params.is_empty() {
                mut is_ptr := false
                if ctx.current_ref_params.contains(name) {
                    is_ptr = true
                } else if ctx.current_variadic_params.contains_key(name) {
                    is_ptr = true
                } else {
                    sym := context.scope_stack.lookup_symbol(name)?
                    switch sym.value_type {
                    case ValueType.TCustom(t):
                        if t.eq("Dynamic") {
                            is_ptr = true
                        }
                    case:
                    }
                    catch (err: KeyNotFoundError) {
                        // REM: Symbol not found
                    }
                }
                if is_ptr {
                    // Bug #97: Use type-mangled name
                    // Bug #168: resolve_var_name maps ret_var_alias to "_ret"
                    return format("(", TIL_PREFIX, "Dynamic*)", resolve_var_name(name, ctx, context))
                }
            }
        case:
        }
        mut lv_str := ""
        emit_expr(arg, lv_str, 0, ctx, context)?
        return format("(", TIL_PREFIX, "Dynamic*)&", lv_str)
    }

    // String literals
    switch arg.node_type {
    case NodeType.LLiteral(lit):
        switch lit {
        case Literal.Str(s):
            mut lit_str := ""
            emit_expr(arg, lit_str, 0, ctx, context)?
            return format("(", TIL_PREFIX, "Dynamic*)&", lit_str)
        case:
            // REM: not a string literal
        }
    case:
    }

    // Non-lvalue - need to hoist
    mut dyn_expr_str := ""
    switch arg.node_type {
    case NodeType.FCall(_):
        dyn_expr_str = emit_fcall_arg_string(arg, hoist_output, indent, ctx, context)?
    case:
        emit_expr(arg, dyn_expr_str, 0, ctx, context)?
    }

    dyn_c_type := get_c_type_for_expr(arg, context)?

    dyn_temp := next_mangled(ctx)
    hoist_output.push_str(indent_str)
    hoist_output.push_str(dyn_c_type)
    hoist_output.push_str(" ")
    hoist_output.push_str(dyn_temp)
    hoist_output.push_str(" = ")
    hoist_output.push_str(dyn_expr_str)
    hoist_output.push_str(";\n")
    return format("(", TIL_PREFIX, "Dynamic*)&", dyn_temp)
}

/// Helper for emit_arg_string: Handle by-ref params
emit_arg_string_by_ref := proc(arg: Expr, mut hoist_output: Str, indent: I64, indent_str: Str, mut ctx: CodegenContext, mut context: Context) returns Str throws Str {
    // Check if it's a pure lvalue
    if is_pure_lvalue(arg, context)? {
        switch arg.node_type {
        case NodeType.Identifier(name):
            if arg.params.is_empty() {
                mut is_ptr := false
                if ctx.current_ref_params.contains(name) {
                    is_ptr = true
                } else if ctx.current_variadic_params.contains_key(name) {
                    is_ptr = true
                }
                if is_ptr {
                    // Bug #97: Use type-mangled name
                    // Bug #168: resolve_var_name maps ret_var_alias to "_ret"
                    return resolve_var_name(name, ctx, context)
                }
            }
        case:
        }
        mut lv_str := ""
        emit_expr(arg, lv_str, 0, ctx, context)?
        return format("&", lv_str)
    }

    // String literals
    switch arg.node_type {
    case NodeType.LLiteral(lit):
        switch lit {
        case Literal.Str(s):
            mut str_lit := ""
            emit_expr(arg, str_lit, 0, ctx, context)?
            return format("&", str_lit)
        case:
            // REM: not a string literal
        }
    case:
    }

    // Non-lvalue - need to hoist
    mut ref_expr_str := ""
    switch arg.node_type {
    case NodeType.FCall(_):
        ref_expr_str = emit_fcall_arg_string(arg, hoist_output, indent, ctx, context)?
    case NodeType.Identifier(id_name):
        // Handle UFCS chain: _[FCall, field...]
        if id_name.eq("_").and(not(arg.params.is_empty())) {
            ufcs_first := arg.get(0)?
            dont_delete(ufcs_first)
            switch ufcs_first.node_type {
            case NodeType.FCall(_):
                base_str := emit_arg_string(ufcs_first, Ptr(), false, hoist_output, indent, ctx, context)?
                mut chain_result := base_str.clone()
                mut chain_i := 1
                while chain_i.lt(arg.params.len()) {
                    chain_field := arg.get(chain_i)?
                    dont_delete(chain_field)
                    switch chain_field.node_type {
                    case NodeType.Identifier(field_name):
                        chain_result.push_str(".")
                        chain_result.push_str(field_name)
                    case:
                    }
                    chain_i.inc()
                }
                ref_expr_str = chain_result
            case:
                emit_expr(arg, ref_expr_str, 0, ctx, context)?
            }
        } else {
            emit_expr(arg, ref_expr_str, 0, ctx, context)?
        }
    case:
        emit_expr(arg, ref_expr_str, 0, ctx, context)?
    }

    ref_c_type := get_c_type_for_expr(arg, context)?

    ref_temp := next_mangled(ctx)
    hoist_output.push_str(indent_str)
    hoist_output.push_str(ref_c_type)
    hoist_output.push_str(" ")
    hoist_output.push_str(ref_temp)
    hoist_output.push_str(" = ")
    hoist_output.push_str(ref_expr_str)
    hoist_output.push_str(";\n")

    return format("&", ref_temp)
}

/// Helper: Emit a throwing FCall arg, hoisting it and returning temp var string
emit_throwing_arg_string := proc(arg: Expr, fd: SFuncDef, param_type: Ptr, param_by_ref: Bool, mut hoist_output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) returns Str throws Str {
    indent_str := repeat("    ", indent)

    // Check for variadic EARLY so we can use by_ref=false for variadic args
    mut variadic_info := VariadicFCallInfo()
    mut has_variadic := false
    variadic_info = detect_variadic_fcall(arg, ctx)?
    has_variadic = true
    catch (err: KeyNotFoundError) {
        // REM: Not variadic
    }
    mut variadic_regular_count := 999999999
    if has_variadic {
        variadic_regular_count = variadic_info.regular_count
    }

    // First, recursively process this call's arguments
    // For variadic args (i >= regular_count), use by_ref=false because they get stored in temp vars
    mut nested_arg_strings := Vec.new(Str)
    if arg.params.len().gt(1) {
        mut i := 0
        mut arg_idx := 1
        while arg_idx.lt(arg.params.len()) {
            nested_arg := arg.get(arg_idx)?
            dont_delete(nested_arg)
            is_variadic_arg := not(i.lt(variadic_regular_count))
            mut nested_param_type := Ptr()
            mut nested_by_ref := false
            if i.lt(fd.args.len()) {
                param_decl_ref := fd.args.get_by_ref(i)?
                create_alias(param_decl, Declaration, param_decl_ref.data)
                nested_param_type.data = to_ptr(param_decl.value_type)
                if not(is_variadic_arg) {
                    nested_by_ref = param_needs_by_ref(param_decl)
                }
            }
            nested_str := emit_arg_string(nested_arg, nested_param_type, nested_by_ref, hoist_output, indent, ctx, context)?
            nested_arg_strings.push(nested_str)
            i.inc()
            arg_idx.inc()
        }
    }

    // Generate temp variable for result
    temp_var := next_mangled(ctx)
    temp_suffix := next_mangled(ctx)

    // Get return type
    if fd.return_types.is_empty() {
        throw arg.lang_error(context.path, "ccodegen", "Throwing call has no return type")
    }
    first_ret_ref := fd.return_types.get_by_ref(0)?
    create_alias(first_ret, ValueType, first_ret_ref.data)
    c_type := til_type_to_c(first_ret)?

    // Declare temp variable
    hoist_output.push_str(indent_str)
    hoist_output.push_str(c_type)
    hoist_output.push_str(" ")
    hoist_output.push_str(temp_var)
    hoist_output.push_str(";\n")

    // Declare error variables for each throw type (skip empty struct errors)
    mut err_idx := 0
    for throw_type: ValueType in fd.throw_types {
        if is_empty_error_struct(context, throw_type) {
            err_idx.inc()
            continue
        }
        switch throw_type {
        case ValueType.TCustom(type_name):
            hoist_output.push_str(indent_str)
            hoist_output.push_str(til_name(type_name))
            hoist_output.push_str(" _err")
            hoist_output.push_str(err_idx.to_str())
            hoist_output.push_str("_")
            hoist_output.push_str(temp_suffix)
            hoist_output.push_str(";\n")
        case:
        }
        err_idx.inc()
    }

    // Build variadic array if needed (variadic_info was computed earlier)
    mut variadic_arr_var := ""
    if has_variadic {
        // Build variadic array using the nested arg strings
        mut variadic_arg_strings := Vec.new(Str)
        mut vi := variadic_info.regular_count
        while vi.lt(nested_arg_strings.len()) {
            vas_ref := nested_arg_strings.get_by_ref(vi)?
            create_alias(vas, Str, vas_ref.data)
            variadic_arg_strings.push(vas)
            vi.inc()
        }
        variadic_arr_var = emit_variadic_array_with_strings(variadic_info.elem_type, variadic_arg_strings, hoist_output, indent, ctx)?
    }

    // Emit the function call with output pointers
    hoist_output.push_str(indent_str)
    hoist_output.push_str("int __attribute__((unused)) _status_")
    hoist_output.push_str(temp_suffix)
    hoist_output.push_str(" = ")

    // Get function name
    if arg.params.is_empty() {
        throw arg.lang_error(context.path, "ccodegen", "Cannot determine function name")
    }
    ta_first_param := arg.get(0)?
    dont_delete(ta_first_param)
    func_name := get_func_name_string(ta_first_param)?
    mut orig_func_name := func_name.clone()
    orig_func_name = get_til_func_name_string(ta_first_param)?
    catch (err: Str) {
        // REM: Use func_name as fallback
    }
    mut mangled_name := func_name.clone()
    if ctx.nested_func_names.contains_key(orig_func_name) {
        ctx.nested_func_names.get(orig_func_name, mangled_name)?
    }

    // Emit function name
    hoist_output.push_str(TIL_PREFIX)
    hoist_output.push_str(mangled_name)
    hoist_output.push_str("(")

    // First arg: output pointer for return value
    hoist_output.push_str("&")
    hoist_output.push_str(temp_var)

    // Error output pointers (skip empty error structs - they don't have a param slot)
    err_idx = 0
    for throw_type: ValueType in fd.throw_types {
        if is_empty_error_struct(context, throw_type) {
            err_idx.inc()
            continue
        }
        hoist_output.push_str(", ")
        hoist_output.push_str("&_err")
        hoist_output.push_str(err_idx.to_str())
        hoist_output.push_str("_")
        hoist_output.push_str(temp_suffix)
        err_idx.inc()
    }

    // Regular arguments (using pre-computed strings)
    mut regular_count := nested_arg_strings.len()
    if has_variadic {
        regular_count = variadic_info.regular_count
    }
    mut ri := 0
    while ri.lt(regular_count) {
        arg_str_ref := nested_arg_strings.get_by_ref(ri)?
        create_alias(arg_str, Str, arg_str_ref.data)
        hoist_output.push_str(", ")
        hoist_output.push_str(arg_str)
        ri.inc()
    }

    // Variadic array pointer if present
    if not(variadic_arr_var.is_empty()) {
        hoist_output.push_str(", &")
        hoist_output.push_str(variadic_arr_var)
    }

    hoist_output.push_str(");\n")

    // Emit error handling - check for local catches first, then propagate
    emit_error_handling(fd.throw_types, temp_suffix, hoist_output, indent, ctx, context)?

    // Delete variadic array if constructed
    if not(variadic_arr_var.is_empty()) {
        hoist_output.push_str(indent_str)
        hoist_output.push_str(TIL_PREFIX)
        hoist_output.push_str("Array_delete(&")
        hoist_output.push_str(variadic_arr_var)
        hoist_output.push_str(");\n")
    }

    catch (err: IndexOutOfBoundsError) { throw err.msg }
    catch (err: KeyNotFoundError) { throw format("KeyNotFoundError: ", err.msg) }

    // Return the temp var name with appropriate prefix
    return format_hoisted_result(temp_var, param_type, param_by_ref)
}

/// Helper: Emit error propagation code
emit_error_propagation := proc(throw_types: Vec, temp_suffix: Str, mut output: Str, indent: I64, ctx: CodegenContext, context: Context) {
    indent_str := repeat("    ", indent)

    output.push_str(indent_str)
    output.push_str("if (_status_")
    output.push_str(temp_suffix)
    output.push_str(" != 0) {\n")

    mut err_idx := 0
    for throw_type: ValueType in throw_types {
        switch throw_type {
        case ValueType.TCustom(type_name):
            // Find matching throw type in current function
            mut curr_idx := 0
            for curr_throw: ValueType in ctx.current_throw_types {
                switch curr_throw {
                case ValueType.TCustom(curr_type_name):
                    if curr_type_name.eq(type_name) {
                        output.push_str(indent_str)
                        output.push_str("    if (_status_")
                        output.push_str(temp_suffix)
                        output.push_str(" == ")
                        output.push_str(add(err_idx, 1).to_str())
                        output.push_str(") { ")
                        if not(is_empty_error_struct(context, throw_type)) {
                            output.push_str("*_err")
                            output.push_str(add(curr_idx, 1).to_str())
                            output.push_str(" = _err")
                            output.push_str(err_idx.to_str())
                            output.push_str("_")
                            output.push_str(temp_suffix)
                            output.push_str("; ")
                        }
                        output.push_str("return ")
                        output.push_str(add(curr_idx, 1).to_str())
                        output.push_str("; }\n")
                        break
                    }
                case:
                }
                curr_idx.inc()
            }
        case:
        }
        err_idx.inc()
    }

    output.push_str(indent_str)
    output.push_str("}\n")
}

/// Helper: Emit error handling - checks for local catches first, then propagates
/// This replaces emit_error_propagation for throwing calls that need catch support
emit_error_handling := proc(throw_types: Vec, temp_suffix: Str, mut output: Str, indent: I64, ctx: CodegenContext, context: Context) throws Str {
    indent_str := repeat("    ", indent)

    // Check if there are local catch labels for any of these error types
    mut has_local_catch := false
    for throw_type: ValueType in throw_types {
        switch throw_type {
        case ValueType.TCustom(type_name):
            if ctx.local_catch_labels.contains_key(type_name) {
                has_local_catch = true
                break
            }
        case:
        }
    }

    if has_local_catch {
        // Emit goto for each error type that has a local catch
        mut err_idx := 0
        for throw_type: ValueType in throw_types {
            switch throw_type {
            case ValueType.TCustom(type_name):
                if ctx.local_catch_labels.contains_key(type_name) {
                    ci_ref := ctx.local_catch_labels.get_by_ref(type_name)?
                    create_alias(catch_info, CatchLabelInfo, ci_ref.data)
                    output.push_str(indent_str)
                    output.push_str("if (_status_")
                    output.push_str(temp_suffix)
                    output.push_str(" == ")
                    output.push_str(add(err_idx, 1).to_str())
                    output.push_str(") { ")
                    // Issue #119: Skip copying empty struct errors
                    if not(is_empty_error_struct(context, throw_type)) {
                        output.push_str(catch_info.temp_var)
                        output.push_str(" = _err")
                        output.push_str(err_idx.to_str())
                        output.push_str("_")
                        output.push_str(temp_suffix)
                        output.push_str("; ")
                    }
                    output.push_str("goto ")
                    output.push_str(catch_info.label)
                    output.push_str("; }\n")
                }
            case:
            }
            err_idx.inc()
        }
    } else {
        // No local catch - propagate errors
        emit_error_propagation(throw_types, temp_suffix, output, indent, ctx, context)
    }

    catch (err: KeyNotFoundError) { throw format("KeyNotFoundError: ", err.msg) }
}

/// Helper: Format a hoisted temp var with appropriate prefix for param type
format_hoisted_result := func(temp_var: Str, param_type: Ptr, param_by_ref: Bool) returns Str {
    mut is_dynamic := false
    if not(param_type.is_null()) {
        mut pt := ValueType.TCustom(INFER_TYPE)
        param_type.dereference(ValueType, pt)
        switch pt {
        case ValueType.TCustom(pt_name):
            if pt_name.eq("Dynamic") {
                is_dynamic = true
            }
        case:
        }
    }
    if is_dynamic {
        return format("(", TIL_PREFIX, "Dynamic*)&", temp_var)
    }
    if param_by_ref {
        return format("&", temp_var)
    }
    return temp_var.clone()
}

/// Helper: Emit a non-throwing variadic FCall arg
emit_variadic_arg_string := proc(arg: Expr, vi: VariadicFCallInfo, param_type: Ptr, param_by_ref: Bool, mut hoist_output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) returns Str throws Str {
    indent_str := repeat("    ", indent)

    // Get function definition
    fd := get_fcall_func_def(context, arg)?
    catch (err: KeyNotFoundError) {
        throw arg.lang_error(context.path, "ccodegen", "Cannot find function definition for variadic call")
    }

    // First, recursively process this call's arguments
    // For variadic args (i >= regular_count), use by_ref=false because they get stored in temp vars
    mut nested_arg_strings := Vec.new(Str)
    if arg.params.len().gt(1) {
        mut i := 0
        mut arg_idx := 1
        while arg_idx.lt(arg.params.len()) {
            nested_arg := arg.get(arg_idx)?
            dont_delete(nested_arg)
            is_variadic_arg := not(i.lt(vi.regular_count))
            mut nested_param_type := Ptr()
            mut nested_by_ref := false
            if i.lt(fd.args.len()) {
                param_decl_ref := fd.args.get_by_ref(i)?
                create_alias(param_decl, Declaration, param_decl_ref.data)
                nested_param_type.data = to_ptr(param_decl.value_type)
                if not(is_variadic_arg) {
                    nested_by_ref = param_needs_by_ref(param_decl)
                }
            }
            nested_str := emit_arg_string(nested_arg, nested_param_type, nested_by_ref, hoist_output, indent, ctx, context)?
            nested_arg_strings.push(nested_str)
            i.inc()
            arg_idx.inc()
        }
    }

    // Generate temp variable for result
    temp_var := next_mangled(ctx)

    // Get return type
    if fd.return_types.is_empty() {
        throw arg.lang_error(context.path, "ccodegen", "Function has no return type")
    }
    first_ret_ref := fd.return_types.get_by_ref(0)?
    create_alias(first_ret, ValueType, first_ret_ref.data)
    c_type := til_type_to_c(first_ret)?

    // Declare temp variable
    hoist_output.push_str(indent_str)
    hoist_output.push_str(c_type)
    hoist_output.push_str(" ")
    hoist_output.push_str(temp_var)
    hoist_output.push_str(";\n")

    // Build variadic array using the nested arg strings
    mut variadic_arg_strings := Vec.new(Str)
    mut vai := vi.regular_count
    while vai.lt(nested_arg_strings.len()) {
        vas_ref := nested_arg_strings.get_by_ref(vai)?
        create_alias(vas, Str, vas_ref.data)
        variadic_arg_strings.push(vas)
        vai.inc()
    }
    variadic_arr_var := emit_variadic_array_with_strings(vi.elem_type, variadic_arg_strings, hoist_output, indent, ctx)?

    // Emit the function call
    hoist_output.push_str(indent_str)
    hoist_output.push_str(temp_var)
    hoist_output.push_str(" = ")

    // Get function name
    if arg.params.is_empty() {
        throw arg.lang_error(context.path, "ccodegen", "Cannot determine function name")
    }
    va_first_param := arg.get(0)?
    dont_delete(va_first_param)
    func_name := get_func_name_string(va_first_param)?
    mut orig_func_name := func_name.clone()
    orig_func_name = get_til_func_name_string(va_first_param)?
    catch (err: Str) {
        // REM: Use func_name as fallback
    }
    mut mangled_name := func_name.clone()
    if ctx.nested_func_names.contains_key(orig_func_name) {
        ctx.nested_func_names.get(orig_func_name, mangled_name)?
    }

    hoist_output.push_str(til_func_name(mangled_name))
    hoist_output.push_str("(")

    // Regular arguments
    mut first := true
    mut ri := 0
    while ri.lt(vi.regular_count) {
        arg_str_ref := nested_arg_strings.get_by_ref(ri)?
        create_alias(arg_str, Str, arg_str_ref.data)
        if not(first) {
            hoist_output.push_str(", ")
        }
        first = false
        hoist_output.push_str(arg_str)
        ri.inc()
    }

    // Variadic array pointer
    if not(first) {
        hoist_output.push_str(", ")
    }
    hoist_output.push_str("&")
    hoist_output.push_str(variadic_arr_var)
    hoist_output.push_str(");\n")

    // Delete variadic array
    hoist_output.push_str(indent_str)
    hoist_output.push_str(TIL_PREFIX)
    hoist_output.push_str("Array_delete(&")
    hoist_output.push_str(variadic_arr_var)
    hoist_output.push_str(");\n")

    catch (err: IndexOutOfBoundsError) { throw err.msg }
    catch (err: KeyNotFoundError) { throw format("KeyNotFoundError: ", err.msg) }

    // Return the temp var name with appropriate prefix
    return format_hoisted_result(temp_var, param_type, param_by_ref)
}

/// Helper: Build variadic array using pre-computed arg strings
/// Mirrors hoist_variadic_args but uses pre-computed arg_strings instead of emit_expr
emit_variadic_array_with_strings := proc(elem_type: Str, arg_strings: Vec, mut output: Str, indent: I64, mut ctx: CodegenContext) returns Str throws Str {
    indent_str := repeat("    ", indent)
    arr_var := next_mangled(ctx)
    err_suffix := next_mangled(ctx)

    elem_count := arg_strings.len()
    mut c_elem_type := format(TIL_PREFIX, "I64")
    c_elem_type = til_type_to_c(ValueType.TCustom(elem_type))?
    catch (err: Str) {
        // REM: Use default I64
    }

    // Declare the array variable
    output.push_str(indent_str)
    output.push_str(TIL_PREFIX)
    output.push_str("Array ")
    output.push_str(arr_var)
    output.push_str(";\n")

    // Declare error var for Array.set (IndexOutOfBoundsError)
    output.push_str(indent_str)
    output.push_str(TIL_PREFIX)
    output.push_str("IndexOutOfBoundsError __attribute__((unused)) _err_idx_")
    output.push_str(err_suffix)
    output.push_str(";\n")

    // Hoist variadic args into temp vars (needed to pass address to Array.set)
    mut arg_temps := Vec.new(Str)
    mut asi := 0
    while asi.lt(arg_strings.len()) {
        arg_str_ref := arg_strings.get_by_ref(asi)?
        create_alias(arg_str, Str, arg_str_ref.data)
        temp_var := next_mangled(ctx)
        output.push_str(indent_str)
        output.push_str(c_elem_type)
        output.push_str(" ")
        output.push_str(temp_var)
        output.push_str(" = ")
        output.push_str(arg_str)
        output.push_str(";\n")
        arg_temps.push(temp_var)
        asi.inc()
    }

    // Hoist Type param for Array.new
    type_temp := next_mangled(ctx)
    output.push_str(indent_str)
    output.push_str(TIL_PREFIX)
    output.push_str("Type ")
    output.push_str(type_temp)
    output.push_str(" = \"")
    output.push_str(elem_type)
    output.push_str("\";\n")

    // Hoist capacity param
    count_temp := next_mangled(ctx)
    output.push_str(indent_str)
    output.push_str(TIL_PREFIX)
    output.push_str("I64 ")
    output.push_str(count_temp)
    output.push_str(" = ")
    output.push_str(elem_count.to_str())
    output.push_str(";\n")

    // Call Array.new (non-throwing)
    output.push_str(indent_str)
    output.push_str(arr_var)
    output.push_str(" = ")
    output.push_str(TIL_PREFIX)
    output.push_str("Array_new(")
    output.push_str(type_temp)
    output.push_str(", &")
    output.push_str(count_temp)
    output.push_str(");\n")

    // Declare status variable for Array.set calls
    output.push_str(indent_str)
    output.push_str("int __attribute__((unused)) _arr_status_")
    output.push_str(err_suffix)
    output.push_str(";\n")

    // Emit Array.set for each variadic arg
    mut ti := 0
    while ti.lt(arg_temps.len()) {
        temp_item_ref := arg_temps.get_by_ref(ti)?
        create_alias(temp_item, Str, temp_item_ref.data)
        // Hoist index literal
        idx_temp := next_mangled(ctx)
        output.push_str(indent_str)
        output.push_str(TIL_PREFIX)
        output.push_str("I64 ")
        output.push_str(idx_temp)
        output.push_str(" = ")
        output.push_str(ti.to_str())
        output.push_str(";\n")

        // int _status = til_Array_set(&_err_idx, &arr, &idx_temp, (til_Dynamic*)&temp);
        output.push_str(indent_str)
        output.push_str("_arr_status_")
        output.push_str(err_suffix)
        output.push_str(" = ")
        output.push_str(TIL_PREFIX)
        output.push_str("Array_set(&_err_idx_")
        output.push_str(err_suffix)
        output.push_str(", &")
        output.push_str(arr_var)
        output.push_str(", &")
        output.push_str(idx_temp)
        output.push_str(", (")
        output.push_str(TIL_PREFIX)
        output.push_str("Dynamic*)&")
        output.push_str(temp_item)
        output.push_str(");\n")
        ti.inc()
    }

    catch (err: IndexOutOfBoundsError) { throw err.msg }

    return arr_var
}

/// Helper: Emit a non-throwing, non-variadic FCall, recursively processing args
emit_fcall_arg_string := proc(arg: Expr, mut hoist_output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) returns Str throws Str {
    // Check if this is a struct constructor - emit directly via emit_expr
    // (emit_expr handles struct constructors properly as compound literals)
    if arg.params.is_empty() {
        throw arg.lang_error(context.path, "ccodegen", "Cannot determine function name")
    }
    first_param := arg.get(0)?
    dont_delete(first_param)
    func_name := get_func_name_string(first_param)?

    // Struct constructors should be emitted as compound literals
    // But we need to recursively process named args in case they contain variadic/throwing calls
    struct_def := context.scope_stack.lookup_struct(func_name)?
    // Process named args (field values) with emit_arg_string
    mut field_values := Map.new(Str, Str)
    mut ai := 1
    while ai.lt(arg.params.len()) {
        named_arg := arg.get(ai)?
        dont_delete(named_arg)
        switch named_arg.node_type {
        case NodeType.NamedArg(field_name):
            if not(named_arg.params.is_empty()) {
                value_expr := named_arg.get(0)?
                dont_delete(value_expr)
                // Find the field's expected type
                mut field_type := Ptr()
                for m: Declaration in struct_def.members {
                    if m.name.eq(field_name) {
                        field_type.data = to_ptr(m.value_type)
                        break
                    }
                }
                // Process the value with emit_arg_string
                value_str := emit_arg_string(value_expr, field_type, false, hoist_output, indent, ctx, context)?
                field_values.set(field_name, value_str)
            }
        case:
        }
        ai.inc()
    }

    // Build the compound literal
    mut result := format("(", TIL_PREFIX, func_name, ")")
    if struct_def.members.is_empty().or(struct_def.default_values.is_empty()) {
        result.push_str("{}")
    } else {
        result.push_str("{")
        mut first := true
        for member: Declaration in struct_def.members {
            if not(member.is_mut) {
                continue  // Skip static members
            }
            if not(first) {
                result.push_str(", ")
            }
            first = false
            result.push_str(".")
            result.push_str(member.name)
            result.push_str(" = ")
            if field_values.contains_key(member.name) {
                fv_ref := field_values.get_by_ref(member.name)?
                create_alias(field_val, Str, fv_ref.data)
                result.push_str(field_val)
            } else if struct_def.default_values.contains_key(member.name) {
                dv_ref := struct_def.default_values.get_by_ref(member.name)?
                create_alias(default_expr, Expr, dv_ref.data)
                // Use emit_arg_string for defaults too (may contain variadic/throwing calls)
                mut member_type := Ptr()
                member_type.data = to_ptr(member.value_type)
                default_str := emit_arg_string(default_expr, member_type, false, hoist_output, indent, ctx, context)?
                result.push_str(default_str)
            } else {
                // No value and no default - use zero
                result.push_str("0")
            }
        }
        result.push_str("}")
    }
    return result
    catch (err: KeyNotFoundError) {
        // Not a struct constructor, continue with regular function call
    }

    mut orig_func_name := func_name.clone()
    orig_func_name = get_til_func_name_string(first_param)?
    catch (err: Str) {
        // REM: Use func_name as fallback
    }

    // Handle builtins that have inline codegen (don't use til_func_name prefix)
    if orig_func_name.eq("to_ptr") {
        // to_ptr: generates (til_I64)&arg, not a function call
        if arg.params.len().lt(2) {
            throw arg.lang_error(context.path, "ccodegen", "to_ptr requires 1 argument")
        }
        inner_arg := arg.get(1)?
        dont_delete(inner_arg)
        // Check if arg is a Dynamic parameter (already void*)
        mut is_dynamic_param := false
        switch inner_arg.node_type {
        case NodeType.Identifier(name):
            if inner_arg.params.is_empty() {
                sym := context.scope_stack.lookup_symbol(name)?
                switch sym.value_type {
                case ValueType.TCustom(t):
                    if t.eq("Dynamic") {
                        is_dynamic_param = true
                    }
                case:
                }
                catch (err: KeyNotFoundError) {
                    // REM: Symbol not found
                }
            }
        case:
        }
        if is_dynamic_param {
            // Bug #97: Use type-mangled name
            switch inner_arg.node_type {
            case NodeType.Identifier(name):
                return format("(", TIL_PREFIX, "I64)", til_var_name_from_context(name, context))
            case:
            }
        }
        // Process the inner arg (may need hoisting if nested)
        inner_str := emit_arg_string(inner_arg, Ptr(), false, hoist_output, indent, ctx, context)?
        return format("(", TIL_PREFIX, "I64)&", inner_str)
    }

    // Bug #144: create_alias is handled in emit_fcall only (standalone statement, never an expression)

    if orig_func_name.eq("size_of") {
        // size_of: generates til_size_of(&Str), with special Str handling
        if arg.params.len().lt(2) {
            throw arg.lang_error(context.path, "ccodegen", "size_of requires 1 argument")
        }
        sizeof_arg := arg.get(1)?
        dont_delete(sizeof_arg)
        mut sizeof_result := ""
        sizeof_result.push_str(TIL_PREFIX)
        sizeof_result.push_str("size_of(&")
        switch sizeof_arg.node_type {
        case NodeType.Identifier(type_name):
            // Check if this is a Type variable or a literal type name
            mut sizeof_is_type_var := false
            sizeof_sym := context.scope_stack.lookup_symbol(type_name)?
            switch sizeof_sym.value_type {
            case ValueType.TCustom(t):
                if t.eq("Type") {
                    sizeof_is_type_var = true
                }
            case:
            }
            catch (err: KeyNotFoundError) {
                // REM: Not a Type variable
            }
            if sizeof_is_type_var {
                // Bug #97: Use type-mangled name for Type variable reference
                c_var_name := til_var_name_from_context(type_name, context)
                sizeof_result.push_str(format("((", TIL_PREFIX, "Str){((", TIL_PREFIX, "Ptr){(", TIL_PREFIX, "I64)", c_var_name, ", 1, 0, 0, 0}), strlen(", c_var_name, "), 0})"))
            } else {
                // Literal type name
                sizeof_result.push_str(format("((", TIL_PREFIX, "Str){((", TIL_PREFIX, "Ptr){(", TIL_PREFIX, "I64)\"", type_name, "\", 1, 0, 0, 0}), ", type_name.len().to_str(), ", 0})"))
            }
        case:
            throw arg.lang_error(context.path, "ccodegen", "size_of requires type name argument")
        }
        sizeof_result.push_str(")")
        return sizeof_result
    }

    if orig_func_name.eq("enum_to_str") {
        // enum_to_str: generates EnumType_to_str(&arg)
        if arg.params.len().lt(2) {
            throw arg.lang_error(context.path, "ccodegen", "enum_to_str requires 1 argument")
        }
        ets_inner_arg := arg.get(1)?
        dont_delete(ets_inner_arg)
        value_type := get_value_type(context, ets_inner_arg)?
        switch value_type {
        case ValueType.TCustom(enum_type_name):
            _ := context.scope_stack.lookup_enum(enum_type_name)?
            // Pass Dynamic param type so enum constructors get hoisted properly
            dynamic_type := ValueType.TCustom("Dynamic")
            mut dynamic_ptr := Ptr()
            dynamic_ptr.data = to_ptr(dynamic_type)
            ets_inner_str := emit_arg_string(ets_inner_arg, dynamic_ptr, false, hoist_output, indent, ctx, context)?
            // ets_inner_str will be "(til_Dynamic*)&x" for lvalues or "(til_Dynamic*)&_tmpX" for hoisted
            // Strip the Dynamic prefix and keep just the &...
            dynamic_prefix := format("(", TIL_PREFIX, "Dynamic*)")
            mut ref_str := ""
            if ets_inner_str.starts_with(dynamic_prefix) {
                ref_str = ets_inner_str.get_substr(dynamic_prefix.len(), ets_inner_str.len())?
            } else {
                ref_str = format("&", ets_inner_str)
            }
            return format(til_name(enum_type_name), "_to_str(", ref_str, ")")
            catch (err: KeyNotFoundError) {
                // REM: Not an enum, fall through
            }
        case:
        }
        // Fall through to emit_expr for non-enum case
        mut expr_str := ""
        emit_expr(arg, expr_str, 0, ctx, context)?
        return expr_str
    }

    if orig_func_name.eq("type_as_str") {
        // type_as_str: generates Str literal or Str from Type variable
        if arg.params.len().lt(2) {
            throw arg.lang_error(context.path, "ccodegen", "type_as_str requires 1 argument")
        }
        tas_arg := arg.get(1)?
        dont_delete(tas_arg)
        switch tas_arg.node_type {
        case NodeType.Identifier(type_name):
            // Check if this is a Type variable or a literal type name
            mut tas_is_type_var := false
            tas_sym := context.scope_stack.lookup_symbol(type_name)?
            switch tas_sym.value_type {
            case ValueType.TCustom(t):
                if t.eq("Type") {
                    tas_is_type_var = true
                }
            case:
            }
            catch (err: KeyNotFoundError) {
                // REM: Not a Type variable
            }
            if tas_is_type_var {
                // Type variable - already a const char*, wrap in Str struct literal
                // Bug #97: Use type-mangled name for Type variable reference
                tas_c_var_name := til_var_name_from_context(type_name, context)
                return format("((", TIL_PREFIX, "Str){((", TIL_PREFIX, "Ptr){(", TIL_PREFIX, "I64)", tas_c_var_name, ", 1, 0, 0, 0}), strlen(", tas_c_var_name, "), 0})")
            } else {
                // Literal type name - create Str compound literal
                return format("((", TIL_PREFIX, "Str){((", TIL_PREFIX, "Ptr){(", TIL_PREFIX, "I64)\"", type_name, "\", 1, 0, 0, 0}), ", type_name.len().to_str(), ", 0})")
            }
        case:
            return format("((", TIL_PREFIX, "Str){((", TIL_PREFIX, "Ptr){(", TIL_PREFIX, "I64)\"unknown\", 1, 0, 0, 0}), 7, 0})")
        }
    }

    // For non-throwing, non-variadic FCalls, we still need to recursively process args
    // to hoist any nested throwing/variadic calls

    mut fd_opt := SFuncDef()
    mut fd_found := false
    fd_opt = get_fcall_func_def(context, arg)?
    fd_found = true
    catch (err: KeyNotFoundError) {
        // REM: No function definition found
    }

    // Process nested args
    mut nested_arg_strings := Vec.new(Str)
    if arg.params.len().gt(1) {
        mut i := 0
        mut arg_idx := 1
        while arg_idx.lt(arg.params.len()) {
            nested_arg := arg.get(arg_idx)?
            dont_delete(nested_arg)
            mut nested_param_type := Ptr()
            mut nested_by_ref := false
            if fd_found.and(i.lt(fd_opt.args.len())) {
                param_decl_ref := fd_opt.args.get_by_ref(i)?
                create_alias(param_decl, Declaration, param_decl_ref.data)
                nested_param_type.data = to_ptr(param_decl.value_type)
                nested_by_ref = param_needs_by_ref(param_decl)
            }
            nested_str := emit_arg_string(nested_arg, nested_param_type, nested_by_ref, hoist_output, indent, ctx, context)?
            nested_arg_strings.push(nested_str)
            i.inc()
            arg_idx.inc()
        }
    }

    // Build the call string
    mut mangled_name := func_name.clone()
    if ctx.nested_func_names.contains_key(orig_func_name) {
        ctx.nested_func_names.get(orig_func_name, mangled_name)?
    }

    // Check if this is an enum constructor (Type_Variant) - need til_Type_make_Variant
    mut is_enum_constructor := false
    mut enum_ctor_name := ""
    mut variant_ctor_name := ""
    underscore_pos := func_name.find("_")
    if underscore_pos.gt(0) {
        enum_ctor_name = func_name.get_substr(0, underscore_pos)?
        variant_ctor_name = func_name.get_substr(add(underscore_pos, 1), func_name.len())?
        enum_def := context.scope_stack.lookup_enum(enum_ctor_name)?
        if enum_def.contains_key(variant_ctor_name) {
            is_enum_constructor = true
        }
        catch (err: KeyNotFoundError) {
            // REM: Not an enum
        }
    }

    mut call_str := ""
    if is_enum_constructor {
        call_str.push_str(TIL_PREFIX)
        call_str.push_str(enum_ctor_name)
        call_str.push_str("_make_")
        call_str.push_str(variant_ctor_name)
    } else {
        call_str.push_str(til_func_name(mangled_name))
    }
    call_str.push_str("(")

    mut ci := 0
    while ci.lt(nested_arg_strings.len()) {
        arg_str_ref := nested_arg_strings.get_by_ref(ci)?
        create_alias(arg_str, Str, arg_str_ref.data)
        if ci.gt(0) {
            call_str.push_str(", ")
        }
        call_str.push_str(arg_str)
        ci.inc()
    }

    call_str.push_str(")")

    catch (err: IndexOutOfBoundsError) { throw err.msg }
    catch (err: KeyNotFoundError) { throw format("KeyNotFoundError: ", err.msg) }

    return call_str
}

/// Helper: Get C type for an expression (for hoisting declarations)
get_c_type_for_expr := proc(arg: Expr, context: Context) returns Str throws Str {
    switch arg.node_type {
    case NodeType.LLiteral(lit):
        switch lit {
        case Literal.Str(s):
            return format(TIL_PREFIX, "Str")
        case Literal.Number(n):
            return format(TIL_PREFIX, "I64")
        case:
            return format(TIL_PREFIX, "I64")  // Default for other literals
        }
    case NodeType.FCall(_):
        fd := get_fcall_func_def(context, arg)?
        if not(fd.return_types.is_empty()) {
            ret_type_ref := fd.return_types.get_by_ref(0)?
            create_alias(ret_type, ValueType, ret_type_ref.data)
            return til_type_to_c(ret_type)?
        }
        catch (err: KeyNotFoundError) {
            // REM: No function definition, check for struct/enum constructor
        }
        // Check for struct/enum constructor
        func_name := get_fcall_func_name(arg)?
        _ := context.scope_stack.lookup_struct(func_name)?
        return til_name(func_name)
        catch (err: KeyNotFoundError) {
            // REM: Not a struct, check enum
        }
        // Check enum constructor
        underscore_pos := func_name.find("_")
        if underscore_pos.gt(0) {
            enum_name := func_name.get_substr(0, underscore_pos)?
            _ := context.scope_stack.lookup_enum(enum_name)?
            return til_name(enum_name)
            catch (err: KeyNotFoundError) {
                // REM: Not an enum
            }
        }
        throw arg.lang_error(context.path, "ccodegen", "Cannot determine type for FCall")
    case NodeType.Identifier(name):
        if not(arg.params.is_empty()) {
            // Bug #143: Handle UFCS chain with field access: _[FCall, field1, field2, ...]
            // After UFCS desugaring, x.method().field1.field2 becomes _[FCall, field1, field2]
            if name.eq("_") {
                first_param := arg.get(0)?
                dont_delete(first_param)
                switch first_param.node_type {
                case NodeType.FCall(_):
                    // Get the return type of the FCall
                    fcall_type := get_c_type_for_expr(first_param, context)?
                    // Navigate through field access chain
                    if arg.params.len().eq(1) {
                        return fcall_type
                    }
                    // Get the struct name from the C type (strip til_ prefix)
                    mut struct_name := fcall_type.clone()
                    if fcall_type.starts_with(TIL_PREFIX) {
                        struct_name = fcall_type.get_substr(TIL_PREFIX.len(), fcall_type.len())?
                    }
                    mut current_type := struct_name.clone()
                    mut field_idx := 1
                    while field_idx.lt(arg.params.len()) {
                        field_param := arg.get(field_idx)?
                        dont_delete(field_param)
                        switch field_param.node_type {
                        case NodeType.Identifier(field_name):
                            chain_struct_def := context.scope_stack.lookup_struct(current_type)?
                            mut found := false
                            for m: Declaration in chain_struct_def.members {
                                if m.name.eq(field_name) {
                                    switch m.value_type {
                                    case ValueType.TCustom(next_struct):
                                        current_type = next_struct.clone()
                                    case:
                                        // Final type is not a struct - convert and return
                                        return til_type_to_c(m.value_type)?
                                    }
                                    found = true
                                    break
                                }
                            }
                            if not(found) {
                                throw arg.lang_error(context.path, "ccodegen", format("Field '", field_name, "' not found on struct '", current_type, "'"))
                            }
                            catch (err: KeyNotFoundError) {
                                throw arg.lang_error(context.path, "ccodegen", format("Expected struct type for field access, got '", current_type, "'"))
                            }
                        case:
                        }
                        field_idx.inc()
                    }
                    // If we ended on a struct type, return it
                    return til_name(current_type)
                case:
                }
            }
            // Type-qualified call or field access
            typeq_first_param := arg.get(0)?
            dont_delete(typeq_first_param)
            switch typeq_first_param.node_type {
            case NodeType.Identifier(field_or_variant):
                // Check enum constructor
                enum_def := context.scope_stack.lookup_enum(name)?
                if enum_def.contains_key(field_or_variant) {
                    return til_name(name)
                }
                catch (err: KeyNotFoundError) {
                    // REM: Not an enum
                }
                // Check instance field access (variable.field)
                sym := context.scope_stack.lookup_symbol(name)?
                switch sym.value_type {
                case ValueType.TCustom(struct_name):
                    instance_struct_def := context.scope_stack.lookup_struct(struct_name)?
                    for m: Declaration in instance_struct_def.members {
                        if m.name.eq(field_or_variant) {
                            return til_type_to_c(m.value_type)?
                        }
                    }
                    catch (err: KeyNotFoundError) {
                        // REM: Not a struct
                    }
                case:
                }
                catch (err: KeyNotFoundError) {
                    // REM: Symbol not found
                }
                // Check static field access on struct/namespace (StructName.field)
                static_struct_def := context.scope_stack.lookup_struct(name)?
                for m: Declaration in static_struct_def.members {
                    if m.name.eq(field_or_variant) {
                        return til_type_to_c(m.value_type)?
                    }
                }
                catch (err: KeyNotFoundError) {
                    // REM: Not a struct
                }
            case:
            }
            throw arg.lang_error(context.path, "ccodegen", "Cannot determine type")
        }
    case:
    }
    catch (err: IndexOutOfBoundsError) { throw err.msg }

    throw arg.lang_error(context.path, "ccodegen", "Cannot determine type for expression")
}


// Emit C code from AST (multi-pass architecture)
emit := proc(ast: Expr, mut context: Context) returns Str throws Str {
    mut output := ""
    mut ctx := CodegenContext.new()

    // C boilerplate
    output.push_str("#include <stdio.h>\n")
    output.push_str("#include <stdlib.h>\n")
    output.push_str("#include <string.h>\n\n")
    output.push_str(format("typedef unsigned char ", TIL_PREFIX, "U8;\n"))
    output.push_str(format("typedef long long ", TIL_PREFIX, "I64;\n"))
    output.push_str(format("typedef struct ", TIL_PREFIX, "Bool { ", TIL_PREFIX, "U8 data; } ", TIL_PREFIX, "Bool;\n"))
    // Dynamic and Type are special placeholder types
    output.push_str(format("typedef void* ", TIL_PREFIX, "Dynamic;\n"))
    output.push_str(format("typedef const char* ", TIL_PREFIX, "Type;\n\n"))

    // Pass 0: collect function info (throw types, return types) for call-site generation
    switch ast.node_type {
    case NodeType.Body:
        for child: Expr in ast.params {
            collect_func_info(child, ctx)?
        }
    case:
    }

    // Pass 0a: collect nested function info for hoisting (populates hoisted_prototypes and nested_func_names)
    switch ast.node_type {
    case NodeType.Body:
        for child: Expr in ast.params {
            collect_nested_func_info(child, ctx, context, "")?
        }
    case:
    }

    // Pass 0b: emit forward declarations for all structs and enums-with-payloads
    // (enums with payloads are implemented as structs in C, so they need forward declarations too)
    // Skip I64, U8, Bool, Dynamic, Type - these are primitive typedefs defined in boilerplate
    switch ast.node_type {
    case NodeType.Body:
        for child: Expr in ast.params {
            if is_struct_declaration(child)? {
                switch child.node_type {
                case NodeType.Declaration(decl):
                    if decl.name.eq("I64").or(decl.name.eq("U8")).or(decl.name.eq("Bool")).or(decl.name.eq("Dynamic")).or(decl.name.eq("Type")) {
                        continue  // Skip - these are primitive typedefs
                    }
                    struct_name := til_name(decl.name)
                    output.push_str("typedef struct ")
                    output.push_str(struct_name)
                    output.push_str(" ")
                    output.push_str(struct_name)
                    output.push_str(";\n")
                case:
                }
            }
            // Also forward-declare enums with payloads (they're implemented as structs)
            if is_enum_declaration(child)?.and(is_enum_with_payloads(child)?) {
                switch child.node_type {
                case NodeType.Declaration(decl):
                    enum_name := til_name(decl.name)
                    output.push_str("typedef struct ")
                    output.push_str(enum_name)
                    output.push_str(" ")
                    output.push_str(enum_name)
                    output.push_str(";\n")
                case:
                }
            }
        }
        output.push_str("\n")
    case:
    }

    // Pass 1a: emit simple enums (no payloads) - safe to emit early, structs may use them as fields
    switch ast.node_type {
    case NodeType.Body:
        for child: Expr in ast.params {
            if is_enum_declaration(child)?.and(not(is_enum_with_payloads(child)?)) {
                emit_enum_declaration(child, output)?
            }
        }
    case:
    }

    // Pass 1b: emit all structs and enums-with-payloads in topologically sorted order
    // Both are "complex types" that can depend on each other
    switch ast.node_type {
    case NodeType.Body:
        mut type_decls := Vec.new(Expr)
        for child: Expr in ast.params {
            if is_struct_declaration(child)?.or(is_enum_declaration(child)?.and(is_enum_with_payloads(child)?)) {
                type_decls.push(child)
            }
        }
        sorted_indices := topological_sort_types(type_decls)?
        for idx: I64 in sorted_indices {
            child_ref := type_decls.get_by_ref(idx)?
            create_alias(child, Expr, child_ref.data)
            if is_struct_declaration(child)? {
                emit_struct_declaration(child, output)?
            } else {
                emit_enum_declaration(child, output)?
            }
        }
    case:
    }

    // Pass 2: emit function prototypes (forward declarations)
    // 2a: top-level functions
    switch ast.node_type {
    case NodeType.Body:
        for child: Expr in ast.params {
            if is_func_declaration(child)? {
                emit_func_prototype(child, context, output)?
            }
        }
    case:
    }
    // 2b1: Issue #108 - enum methods (delete/clone)
    switch ast.node_type {
    case NodeType.Body:
        for child: Expr in ast.params {
            if is_enum_declaration(child)? {
                emit_enum_method_prototypes(child, context, output)?
            }
        }
    case:
    }
    // 2b2: Issue #108 - namespace functions (with mangled names)
    switch ast.node_type {
    case NodeType.Body:
        for child: Expr in ast.params {
            if is_namespace_definition(child) {
                emit_namespace_func_prototypes(child, context, output)?
            }
        }
    case:
    }
    // 2c: hoisted nested function prototypes (collected in Pass 0a)
    if not(ctx.hoisted_prototypes.is_empty()) {
        output.push_str("\n// Nested function prototypes (hoisted)\n")
        for proto: Str in ctx.hoisted_prototypes {
            output.push_str(proto)
        }
    }
    output.push_str("\n")

    // Pass 3: include external C interface (after structs and forward decls)
    // Use angle brackets to search only -I paths (src/), not the current directory
    // This avoids including generated c/self/ext.c instead of the hand-written src/ext.c
    output.push_str("#include <ext.c>\n\n")

    // Pass 4: emit struct constants (non-mut, non-function fields with mangled names)
    // Also emits size_of constants for each struct
    switch ast.node_type {
    case NodeType.Body:
        for child: Expr in ast.params {
            if is_struct_declaration(child)? {
                emit_struct_constants(child, output, ctx, context)?
            }
        }
    case:
    }

    // Pass 4a: emit size_of constants for enums
    switch ast.node_type {
    case NodeType.Body:
        for child: Expr in ast.params {
            if is_enum_declaration(child)? {
                emit_enum_size_of_constant(child, output, ctx)?
            }
        }
    case:
    }

    // Pass 4a2: emit enum_to_str functions for all enums
    // This must come after structs are defined (Str is needed for return type)
    switch ast.node_type {
    case NodeType.Body:
        for child: Expr in ast.params {
            if is_enum_declaration(child)? {
                emit_enum_to_str_for_declaration(child, output, context)?
            }
        }
    case:
    }

    // Pass 4b: emit top-level constants (non-mut declarations with literal values)
    switch ast.node_type {
    case NodeType.Body:
        for child: Expr in ast.params {
            if is_constant_declaration(child)? {
                emit_constant_declaration(child, output, ctx, context)?
            }
        }
    case:
    }

    // Pass 4c: emit global declarations (non-constant, non-func/struct/enum declarations)
    // These need to be file-scope statics so functions can access them
    switch ast.node_type {
    case NodeType.Body:
        for child: Expr in ast.params {
            if is_global_declaration(child)? {
                emit_global_declaration(child, output, ctx, context)
            }
        }
    case:
    }

    // Pass 4c2 (Bug #133 fix): emit static arrays for precomputed heap values
    // This serializes Vec/List contents to static arrays so Ptr fields can reference them
    emit_precomputed_static_arrays(output, ctx, context)?

    // Pass 4d: emit til_size_of function (runtime type size lookup)
    emit_size_of_function(output, ctx)

    output.push_str("\n")

    // Pass 5: emit function definitions
    // 5a: top-level functions
    switch ast.node_type {
    case NodeType.Body:
        for child: Expr in ast.params {
            if is_func_declaration(child)? {
                emit_func_declaration(child, output, ctx, context)?
            }
        }
    case:
    }
    // 5b1: Issue #108 - enum methods (delete/clone)
    switch ast.node_type {
    case NodeType.Body:
        for child: Expr in ast.params {
            if is_enum_declaration(child)? {
                emit_enum_method_bodies(child, output, ctx, context)?
            }
        }
    case:
    }
    // 5b2: Issue #108 - namespace functions (with mangled names)
    switch ast.node_type {
    case NodeType.Body:
        for child: Expr in ast.params {
            if is_namespace_definition(child) {
                emit_namespace_func_bodies(child, output, ctx, context)?
            }
        }
    case:
    }

    // 5c: hoisted nested function definitions
    // These were collected during emit_func_declaration when encountering nested functions
    // (prototypes were already emitted in Pass 2c)
    if not(ctx.hoisted_functions.is_empty()) {
        output.push_str("\n// Hoisted nested function definitions\n")
        for func_code: Str in ctx.hoisted_functions {
            output.push_str(func_code)
        }
    }

    // Check if main has variadic args (needs argc/argv)
    mut main_has_variadic := false
    if context.mode_def.needs_main_proc {
        fd := context.scope_stack.lookup_func("main")?
        catch (err: KeyNotFoundError) { }
        for fd_arg: Declaration in fd.args {
            switch fd_arg.value_type {
            case ValueType.TMulti:
                main_has_variadic = true
                break
            case:
            }
        }
    }

    // Main function - always name params (unnamed triggers -Wc23-extensions on clang)
    output.push_str("int main(int argc, char** argv) {\n")
    if not(main_has_variadic) {
        // Suppress -Wunused-parameter when argc/argv not used
        output.push_str("    (void)argc; (void)argv;\n")
    }

    // Re-populate declared_vars with global declarations (functions clear declared_vars)
    // This ensures global declarations emit only assignments in main(), not redeclarations
    switch ast.node_type {
    case NodeType.Body:
        for child: Expr in ast.params {
            if is_global_declaration(child)? {
                switch child.node_type {
                case NodeType.Declaration(decl):
                    // Bug #97: Use type-mangled name
                    // Note: decl.value_type should be resolved by typer (not INFER_TYPE)
                    type_prefix := value_type_to_c_prefix(decl.value_type)
                    _ := ctx.declared_vars.insert(til_var_name(decl.name, type_prefix))
                case:
                }
            }
        }
    case:
    }

    // Pass 6: emit non-struct, non-function, non-enum, non-constant statements
    // Collect them into a Vec and use emit_stmts for proper variadic/throwing call handling
    // Note: global declarations are still included but will emit only assignments since they're already declared
    switch ast.node_type {
    case NodeType.Body:
        mut main_stmts := Vec.new(Expr)
        for child: Expr in ast.params {
            // Skip true/false declarations - they're now #defines
            mut skip := false
            switch child.node_type {
            case NodeType.Declaration(decl):
                if decl.name.eq("true").or(decl.name.eq("false")) {
                    skip = true
                }
            case:
            }
            if not(skip).and(not(is_func_declaration(child)?)).and(not(is_struct_declaration(child)?)).and(not(is_enum_declaration(child)?)).and(not(is_constant_declaration(child)?)) {
                main_stmts.push(child)
            }
        }

        emit_stmts(main_stmts, output, 1, ctx, context)?
    case:
    }

    // Check argv for --mem-report flag, enable heap tracking if found
    output.push_str("    for (int _i = 1; _i < argc; _i++) {\n")
    output.push_str("        if (strcmp(argv[_i], \"--mem-report\") == 0) {\n")
    output.push_str("            til_HeapState_enable();\n")
    output.push_str("            for (int _j = _i; _j < argc - 1; _j++) argv[_j] = argv[_j + 1];\n")
    output.push_str("            argc--; _i--;\n")
    output.push_str("        }\n")
    output.push_str("    }\n")

    // Call til_main() for modes that require a main proc (like cli)
    if context.mode_def.needs_main_proc {
        if main_has_variadic {
            // Convert argc/argv to til_Array and pass to til_main
            // Skip argv[0] (exe path) to match interpreter behavior
            output.push_str("    til_Array _main_args = til_Array_new(\"Str\", &(til_I64){argc - 1});\n")
            output.push_str("    for (int i = 1; i < argc; i++) {\n")
            output.push_str("        til_Str _arg = {((til_Ptr){(til_I64)argv[i], 1, 0, 0, 0}), strlen(argv[i]), 0};\n")
            output.push_str("        til_IndexOutOfBoundsError _set_err;\n")
            output.push_str("        til_Array_set(&_set_err, &_main_args, &(til_I64){i - 1}, (til_Dynamic*)&_arg);\n")
            output.push_str("    }\n")
            output.push_str("    til_main(&_main_args);\n")
        } else {
            output.push_str("    til_main();\n")
        }
    }

    output.push_str("    til_HeapState_report();\n")
    output.push_str("    return 0;\n")
    output.push_str("}\n")

    catch (err: IndexOutOfBoundsError) { throw err.msg }
    return output
}

// Check if an expression is a struct declaration (Name := struct {...})
is_struct_declaration := func(expr: Expr) returns Bool throws Str {
    switch expr.node_type {
    case NodeType.Declaration:
        if not(expr.params.is_empty()) {
            first_param := expr.get(0)?
            dont_delete(first_param)
            switch first_param.node_type {
            case NodeType.StructDef:
                return true
            case:
            }
        }
    case:
    }

    return false
}

// Issue #108: Check if expression is a namespace definition
is_namespace_definition := func(expr: Expr) returns Bool {
    switch expr.node_type {
    case NodeType.NamespaceDef(_ns_def):
        return true
    case:
    }
    return false
}

// Check if an expression is an enum declaration (Name := enum {...})
is_enum_declaration := func(expr: Expr) returns Bool throws Str {
    switch expr.node_type {
    case NodeType.Declaration:
        if not(expr.params.is_empty()) {
            first_param := expr.get(0)?
            dont_delete(first_param)
            switch first_param.node_type {
            case NodeType.EnumDef:
                return true
            case:
            }
        }
    case:
    }

    return false
}

// Check if an expression is a top-level constant declaration (name := literal)
// Constants are non-mut declarations with literal values (numbers, strings, bools)
is_constant_declaration := func(expr: Expr) returns Bool throws Str {
    switch expr.node_type {
    case NodeType.Declaration(decl):
        // Must not be mutable
        if decl.is_mut {
            return false
        }
        if not(expr.params.is_empty()) {
            first_param := expr.get(0)?
            dont_delete(first_param)
            switch first_param.node_type {
            case NodeType.LLiteral:
                // Literal values are constants
                return true
            case NodeType.Identifier(name):
                // Bool identifiers (true/false) are constants
                if name.eq("true").or(name.eq("false")) {
                    return true
                }
            case NodeType.StructDef:
                // Skip struct definitions
                return false
            case NodeType.EnumDef:
                // Skip enum definitions
                return false
            case NodeType.FuncDef:
                // Skip function definitions
                return false
            case:
                return false
            }
        }
    case:
    }

    return false
}

// Check if an expression is a function call that uses out-params (i.e., the function throws)
// Such calls cannot be used inline in struct initializers - they need separate statements
// Emit a top-level constant declaration at file scope
emit_constant_declaration := proc(expr: Expr, mut output: Str, mut ctx: CodegenContext, context: Context) throws Str {
    switch expr.node_type {
    case NodeType.Declaration(decl):
        if not(expr.params.is_empty()) {
            // Bug #97: Use type-mangled name for constants
            mut var_name := ""
            if decl.name.eq("_") {
                var_name = next_mangled(ctx)
            } else {
                type_prefix := value_type_to_c_prefix(decl.value_type)
                var_name = til_var_name(decl.name, type_prefix)
            }

            // Handle literal constants (numbers, strings)
            first_param := expr.get(0)?
            dont_delete(first_param)
            switch first_param.node_type {
            case NodeType.LLiteral(lit):
                _has_str := context.scope_stack.lookup_struct("Str")?
                mut has_str_bool := true
                catch (err: KeyNotFoundError) {
                    has_str_bool = false
                }
                // Use the declaration's explicit type if available, otherwise infer from literal
                mut c_type := ""
                switch lit {
                case Literal.Number(n):
                    // Check if declaration has explicit type annotation
                    switch decl.value_type {
                    case ValueType.TCustom(const_type_name):
                        if const_type_name.eq(INFER_TYPE) {
                            throw expr.lang_error(context.path, "ccodegen", format("Declaration '", decl.name, "' has INFER_TYPE - should have been resolved by typer"))
                        }
                        c_type = TIL_PREFIX.concat(const_type_name)
                    case:
                        c_type = TIL_PREFIX.concat("I64")
                    }
                case Literal.Str(s):
                    if has_str_bool {
                        c_type = TIL_PREFIX.concat("Str")
                    } else {
                        c_type = "const char*"
                    }
                case Literal.List(l):
                    return // Skip list literals for now
                case:
                }
                // Non-mut declarations are constants
                if not(decl.is_mut) {
                    output.push_str("const ")
                }
                output.push_str(c_type)
                output.push_str(" ")
                output.push_str(var_name)
                output.push_str(" = ")
                emit_literal(lit, output, context)?
                output.push_str(";\n")
                // Handle Bool constants (true/false identifiers)
            case NodeType.Identifier(name):
                if name.eq("true").or(name.eq("false")) {
                    if not(decl.is_mut) {
                        output.push_str("const ")
                    }
                    output.push_str(TIL_PREFIX)
                    output.push_str("Bool ")
                    output.push_str(var_name)
                    output.push_str(" = ")
                    // Use struct literal instead of macro (valid constant initializer)
                    if name.eq("true") {
                        output.push_str("((")
                        output.push_str(TIL_PREFIX)
                        output.push_str("Bool){1})")
                    } else {
                        output.push_str("((")
                        output.push_str(TIL_PREFIX)
                        output.push_str("Bool){0})")
                    }
                    output.push_str(";\n")
                }
            case:
            }
        }
    case:
    }
}

// Check if an expression is a non-constant top-level declaration that needs to be a global
// These are declarations that are NOT: functions, structs, enums, or literal constants
// but are still non-mut and could be referenced from function bodies
is_global_declaration := func(expr: Expr) returns Bool throws Str {
    switch expr.node_type {
    case NodeType.Declaration(decl):
        // Skip true/false declarations - they're handled specially
        if decl.name.eq("true").or(decl.name.eq("false")) {
            return false
        }
        if not(expr.params.is_empty()) {
            first_param := expr.get(0)?
            dont_delete(first_param)
            switch first_param.node_type {
                // Skip these - they have their own handling
            case NodeType.StructDef(sd):
                return false
            case NodeType.EnumDef(ed):
                return false
            case NodeType.FuncDef(fd):
                return false
                // Skip literal constants - they're handled by is_constant_declaration
                // (only non-mut literals are constants)
            case NodeType.LLiteral(lit):
                if not(decl.is_mut) {
                    return false
                }
                return true
                // Skip true/false RHS - they're handled by is_constant_declaration
                // (only non-mut booleans are constants)
            case NodeType.Identifier(name):
                if name.eq("true").or(name.eq("false")).and(not(decl.is_mut)) {
                    return false
                }
                return true
                // Everything else (function calls like EvalHeap.new(), etc.) is a global
            case:
                return true
            }
        }
    case:
    }
    return false
}

// Emit a global declaration as a static variable at file scope (type only, no initializer)
// The initializer will be emitted in main()
emit_global_declaration := proc(expr: Expr, mut output: Str, mut ctx: CodegenContext, context: Context) {
    switch expr.node_type {
    case NodeType.Declaration(decl):
        if not(expr.params.is_empty()) {
            // Skip precomputed globals - they get initialized declarations in emit_precomputed_vec_static
            mut is_precomputed := false
            for phv: PrecomputedHeapValue in context.precomputed_heap_values {
                if phv.var_name.eq(decl.name) {
                    is_precomputed = true
                }
            }
            if is_precomputed {
                return
            }

            // Bug #97: Use type-mangled name for global declarations
            // Note: decl.value_type should be resolved by typer (not INFER_TYPE)
            mut var_name := ""
            if decl.name.eq("_") {
                var_name = next_mangled(ctx)
            } else {
                type_prefix := value_type_to_c_prefix(decl.value_type)
                var_name = til_var_name(decl.name, type_prefix)
            }

            // Determine the C type from the initializer expression using get_value_type
            first_param := expr.get(0)?
            dont_delete(first_param)
            mut have_vt := true
            vt := get_value_type(context, first_param)?
            catch (err: Str) {
                have_vt = false
            }
            mut c_type := "int"
            if have_vt {
                c_type = til_type_to_c(vt)?
                catch (err: Str) {
                    c_type = til_type_to_c(decl.value_type)?
                    catch (err2: Str) {
                        c_type = "int"
                    }
                }
            } else {
                c_type = til_type_to_c(decl.value_type)?
                catch (err: Str) {
                    c_type = "int"
                }
            }

            // Emit static declaration (no initializer - will be set in main)
            output.push_str("static ")
            output.push_str(c_type)
            output.push_str(" ")
            output.push_str(var_name)
            output.push_str(";\n")

            // Track that this variable has been declared globally
            _ := ctx.declared_vars.insert(var_name)
        }
    case:
    }
}

// Collect function info (throw types, return types, names) from AST into context
// Handles both top-level functions and struct methods
collect_func_info := proc(expr: Expr, mut ctx: CodegenContext) throws Str {
    switch expr.node_type {
    case NodeType.Declaration(decl):
        if not(expr.params.is_empty()) {
            first_param := expr.get(0)?
            dont_delete(first_param)
            switch first_param.node_type {
            case NodeType.FuncDef(func_def):
                // Top-level function - check for variadic args (TMulti)
                mut idx := 0
                for func_def_arg: Declaration in func_def.args {
                    switch func_def_arg.value_type {
                    case ValueType.TMulti(elem_type):
                        mut info := VariadicParamInfo()
                        info.elem_type = elem_type.clone()
                        info.regular_count = idx
                        ctx.func_variadic_args.set(decl.name.clone(), info)
                        break // Only one variadic arg per function
                    case:
                    }
                    idx.inc()
                }
            case:
            }
        }
    case:
    }
}

// Collect nested function info (for hoisting): scan function bodies for nested FuncDef declarations
// This populates ctx.nested_func_names and ctx.hoisted_prototypes before we emit function bodies
collect_nested_func_info := proc(expr: Expr, mut ctx: CodegenContext, context: Context, parent_func_name: Str) throws Str {
    switch expr.node_type {
    case NodeType.Declaration(decl):
        if not(expr.params.is_empty()) {
            first_param := expr.get(0)?
            dont_delete(first_param)
            switch first_param.node_type {
            case NodeType.FuncDef(func_def):
                if not(parent_func_name.eq("")) {
                    // This is a nested function - record it for hoisting
                    mangled_name := parent_func_name.concat("_").concat(decl.name)
                    ctx.nested_func_names.set(decl.name.clone(), mangled_name.clone())

                    // Generate prototype using emit_func_signature (handles throwing functions properly)
                    mut proto := ""
                    emit_func_signature(til_name(mangled_name), func_def, context, proto)?
                    proto.push_str(";\n")
                    ctx.hoisted_prototypes.push(proto)

                    // Recursively check for nested functions within this nested function
                    new_parent := mangled_name.clone()
                    for body_expr: Expr in func_def.body {
                        collect_nested_func_info(body_expr, ctx, context, new_parent)?
                    }
                } else {
                    // Top-level function - scan its body for nested functions
                    for body_expr: Expr in func_def.body {
                        collect_nested_func_info(body_expr, ctx, context, decl.name)?
                    }
                }
            case:
            }
        }
    case NodeType.Body:
        for child: Expr in expr.params {
            collect_nested_func_info(child, ctx, context, parent_func_name)?
        }
    case NodeType.If:
        // Check then and else branches (params[0] = condition, params[1] = then, params[2] = else)
        if expr.params.len().gt(1) {
            then_branch := expr.get(1)?
            dont_delete(then_branch)
            collect_nested_func_info(then_branch, ctx, context, parent_func_name)?
        }
        if expr.params.len().gt(2) {
            else_branch := expr.get(2)?
            dont_delete(else_branch)
            collect_nested_func_info(else_branch, ctx, context, parent_func_name)?
        }
    case NodeType.While:
        // Check loop body (params[0] = condition, params[1] = body)
        if expr.params.len().gt(1) {
            body := expr.get(1)?
            dont_delete(body)
            collect_nested_func_info(body, ctx, context, parent_func_name)?
        }
    case:
        // Recursively check all params for other node types
        for child: Expr in expr.params {
            collect_nested_func_info(child, ctx, context, parent_func_name)?
        }
    }
}

// Emit a struct declaration as a C struct (only mut fields become struct fields)
// Forward declarations are emitted separately, so we use "struct Name { ... };" here
emit_struct_declaration := proc(expr: Expr, mut output: Str) throws Str {
    switch expr.node_type {
    case NodeType.Declaration(decl):
        // Skip I64, U8, Bool, Dynamic, Type - these are primitive typedefs defined in boilerplate
        if decl.name.eq("I64").or(decl.name.eq("U8")).or(decl.name.eq("Bool")).or(decl.name.eq("Dynamic")).or(decl.name.eq("Type")) {
            return
        }
        if not(expr.params.is_empty()) {
            first_param := expr.get(0)?
            dont_delete(first_param)
            switch first_param.node_type {
            case NodeType.StructDef(struct_def):
                output.push_str("struct ")
                output.push_str(til_name(decl.name))
                output.push_str(" {\n")
                for member: Declaration in struct_def.members {
                    // Only emit mut fields as struct members
                    // Skip functions and non-mut fields (constants)
                    if member.is_mut {
                        c_type := til_type_to_c(member.value_type)?
                        output.push_str("    ")
                        output.push_str(c_type)
                        output.push_str(" ")
                        output.push_str(member.name)
                        output.push_str(";\n")
                    }
                }
                output.push_str("};\n\n")
                return
            case:
            }
        }
    case:
    }


    throw "emit_struct_declaration: not a struct declaration"
}

// Emit struct constants (non-mut, non-function fields) with mangled names: StructName_constant
// Also emits size_of constant: til_size_of_StructName = sizeof(til_StructName)
emit_struct_constants := proc(expr: Expr, mut output: Str, mut ctx: CodegenContext, mut context: Context) throws Str {
    switch expr.node_type {
    case NodeType.Declaration(decl):
        if not(expr.params.is_empty()) {
            first_param := expr.get(0)?
            dont_delete(first_param)
            switch first_param.node_type {
            case NodeType.StructDef(struct_def):
                struct_name := til_name(decl.name)
                for member: Declaration in struct_def.members {
                    // Only emit non-mut, non-function fields as constants
                    if not(member.is_mut) {
                        mut c_type_valid := false
                        mut c_type := ""
                        c_type = til_type_to_c(member.value_type)?
                        c_type_valid = true
                        catch (err: Str) {
                            // REM: Rust's Err case - til_type_to_c failed, skip this member
                        }

                        if c_type_valid {
                            mut default_val_valid := false
                            val_ref := struct_def.default_values.get_by_ref(member.name)?
                            create_alias(default_val, Expr, val_ref.data)
                            default_val_valid = true
                            catch (err: KeyNotFoundError) {
                                // REM: Rust's None case - member has no default value, skip
                            }

                            if default_val_valid {
                                output.push_str("const ")
                                output.push_str(c_type)
                                output.push_str(" ")
                                output.push_str(struct_name)
                                output.push_str("_")
                                output.push_str(member.name)
                                output.push_str(" = ")
                                emit_expr(default_val, output, 0, ctx, context)?
                                output.push_str(";\n")
                            }
                        }
                    }
                }
                // Emit size_of constant for this struct
                output.push_str("const ")
                output.push_str(TIL_PREFIX)
                output.push_str("I64 ")
                output.push_str(TIL_PREFIX)
                output.push_str("size_of_")
                output.push_str(decl.name)
                output.push_str(" = sizeof(")
                output.push_str(struct_name)
                output.push_str(");\n")
                // Track this type for til_size_of function generation
                ctx.known_types.push(decl.name.clone())
                return
            case:
            }
        }
    case:
    }
    // Not a struct, nothing to emit
}

// Emit size_of constant for an enum: til_size_of_EnumName = sizeof(til_EnumName)
emit_enum_size_of_constant := proc(expr: Expr, mut output: Str, mut ctx: CodegenContext) throws Str {
    switch expr.node_type {
    case NodeType.Declaration(decl):
        if not(expr.params.is_empty()) {
            first_param := expr.get(0)?
            dont_delete(first_param)
            switch first_param.node_type {
            case NodeType.EnumDef(enum_def):
                enum_name := til_name(decl.name)
                output.push_str("const ")
                output.push_str(TIL_PREFIX)
                output.push_str("I64 ")
                output.push_str(TIL_PREFIX)
                output.push_str("size_of_")
                output.push_str(decl.name)
                output.push_str(" = sizeof(")
                output.push_str(enum_name)
                output.push_str(");\n")
                // Track this type for til_size_of function generation
                ctx.known_types.push(decl.name.clone())
            case:
            }
        }
    case:
    }
}

// Emit til_size_of function for runtime type size lookup
emit_size_of_function := proc(mut output: Str, ctx: CodegenContext) {
    output.push_str("\n")
    output.push_str("static inline ")
    output.push_str(TIL_PREFIX)
    output.push_str("I64 ")
    output.push_str(TIL_PREFIX)
    output.push_str("size_of(const ")
    output.push_str(TIL_PREFIX)
    output.push_str("Str* type_name) {\n")

    // All known types from structs and enums
    for type_name: Str in ctx.known_types {
        output.push_str("    if (strcmp((char*)type_name->c_string.data, \"")
        output.push_str(type_name)
        output.push_str("\") == 0) return ")
        output.push_str(TIL_PREFIX)
        output.push_str("size_of_")
        output.push_str(type_name)
        output.push_str(";\n")
    }

    output.push_str("    fprintf(stderr, \"size_of: unknown type %s\\n\", (char*)type_name->c_string.data);\n")
    output.push_str("    exit(1);\n")
    output.push_str("}\n")
}

// Bug #133 fix: Emit static arrays for precomputed heap values (Vec, List, etc.)
// This serializes interpreter heap contents to static C arrays so Ptr fields can reference them
emit_precomputed_static_arrays := proc(mut output: Str, mut ctx: CodegenContext, context: Context) throws Str {
    if context.precomputed_heap_values.is_empty() {
        return
    }

    output.push_str("\n// Bug #133: Static arrays for precomputed heap values\n")

    for phv: PrecomputedHeapValue in context.precomputed_heap_values {
        if phv.type_name.eq("Vec") {
            emit_precomputed_vec_static(output, ctx, context, phv)?
        }
        // TODO: Add List, Map support as needed
    }
}

// Emit static arrays for a single precomputed Vec
emit_precomputed_vec_static := proc(mut output: Str, mut ctx: CodegenContext, context: Context, phv: PrecomputedHeapValue) throws Str {
    // Extract Vec contents from EvalHeap
    contents := EvalHeap.extract_vec_contents(context, phv.instance_name)?

    var_name := phv.var_name

    // Use recursive helper to emit static data arrays
    emit_precomputed_vec_recursive(output, ctx, context, var_name, contents)?

    // Emit initialized static declaration (instead of bare declaration + assignment in main)
    c_var_name := til_var_name(var_name, "Vec")
    elem_type := contents.element_type_name
    dont_delete(elem_type) // alias of contents.element_type_name
    elem_count := contents.element_bytes.len()
    type_size := contents.type_size
    mut static_info := PrecomputedStaticInfo()
    ctx.precomputed_static_arrays.get(var_name, static_info)?
    catch (err: KeyNotFoundError) {
        panic(loc(), "emit_precomputed_vec_static: no static info for '", var_name, "'")
    }
    data_array_name := static_info.data_array_name

    output.push_str("static ")
    output.push_str(TIL_PREFIX)
    output.push_str("Vec ")
    output.push_str(c_var_name)
    output.push_str(" = {\n")

    // type_name field
    output.push_str("    .type_name = (")
    output.push_str(TIL_PREFIX)
    output.push_str("Str){(")
    output.push_str(TIL_PREFIX)
    output.push_str("Ptr){(")
    output.push_str(TIL_PREFIX)
    output.push_str("I64)\"")
    output.push_str(elem_type)
    output.push_str("\", 1, 0, 0, 0}, ")
    output.push_str(elem_type.len().to_str())
    output.push_str(", 0},\n")

    // type_size field
    output.push_str("    .type_size = ")
    output.push_str(type_size.to_str())
    output.push_str(",\n")

    // ptr field - point to static data array
    output.push_str("    .ptr = (")
    output.push_str(TIL_PREFIX)
    output.push_str("Ptr){(")
    output.push_str(TIL_PREFIX)
    output.push_str("I64)")
    output.push_str(data_array_name)
    output.push_str(", 1, 0, 0, 0},\n")

    // _len field
    output.push_str("    ._len = ")
    output.push_str(elem_count.to_str())
    output.push_str(",\n")

    // cap field
    output.push_str("    .cap = ")
    output.push_str(elem_count.to_str())
    output.push_str("\n")

    output.push_str("};\n")

    _ := ctx.declared_vars.insert(c_var_name)
}

// Recursive helper to emit static arrays for Vec contents
// Handles nested types by recursively emitting inner elements first
emit_precomputed_vec_recursive := proc(mut output: Str, mut ctx: CodegenContext, context: Context, var_name: Str, contents: VecContents) throws Str {
    elem_type := contents.element_type_name
    elem_count := contents.element_bytes.len()

    // Generate static array name
    data_array_name := format("_precomp_", var_name, "_data")

    // Handle different element types
    if elem_type.eq("Str") {
        // Vec<Str> needs two-level serialization: string data + Str structs
        emit_precomputed_vec_str_static(output, ctx, context, var_name, contents)?
    } else if elem_type.eq("I64") {
        // Vec<I64> - emit array of I64 values
        output.push_str("static ")
        output.push_str(TIL_PREFIX)
        output.push_str("I64 ")
        output.push_str(data_array_name)
        output.push_str("[")
        output.push_str(elem_count.to_str())
        output.push_str("] = {")
        for i in 0..elem_count {
            if i.gt(0) {
                output.push_str(", ")
            }
            bytes_ref := contents.element_bytes.get_by_ref(i)?
            create_alias(bytes, Vec, bytes_ref.data)
            // Read I64 from 8 bytes (little endian)
            val := bytes_to_i64(bytes)
            output.push_str(val.to_str())
        }
        output.push_str("};\n")
    } else if elem_type.eq("U8") {
        // Vec<U8> - emit array of U8 values
        output.push_str("static ")
        output.push_str(TIL_PREFIX)
        output.push_str("U8 ")
        output.push_str(data_array_name)
        output.push_str("[")
        output.push_str(elem_count.to_str())
        output.push_str("] = {")
        for u8_i in 0..elem_count {
            if u8_i.gt(0) {
                output.push_str(", ")
            }
            u8_bytes_ref := contents.element_bytes.get_by_ref(u8_i)?
            create_alias(u8_bytes, Vec, u8_bytes_ref.data)
            byte_val_ref := u8_bytes.get_by_ref(0)?
            create_alias(byte_val, U8, byte_val_ref.data)
            output.push_str(byte_val.to_str())
        }
        output.push_str("};\n")
    } else if EvalHeap.type_needs_heap_serialization(context, elem_type) {
        // Nested type that needs heap serialization (Vec, List, etc.)
        // Recursively emit inner elements first, then emit outer array
        emit_precomputed_nested_vec_static(output, ctx, context, var_name, contents)?
    } else {
        // Other struct types - emit raw bytes as array (fallback)
        mut total_bytes := 0
        for bytes_vec: Vec in contents.element_bytes {
            total_bytes = total_bytes.add(bytes_vec.len())
        }
        output.push_str("static unsigned char ")
        output.push_str(data_array_name)
        output.push_str("[")
        output.push_str(total_bytes.to_str())
        output.push_str("] = {")
        mut first := true
        for bytes_vec: Vec in contents.element_bytes {
            for b: U8 in bytes_vec {
                if not(first) {
                    output.push_str(", ")
                }
                first = false
                output.push_str(b.to_str())
            }
        }
        output.push_str("};\n")
    }

    // Store info for later use when emitting assignments in main()
    ctx.precomputed_static_arrays.set(var_name.clone(), PrecomputedStaticInfo(
        data_array_name=data_array_name
    ))

    catch (err: IndexOutOfBoundsError) {
        throw format("emit_precomputed_vec_recursive: ", err.msg)
    }
}

// Emit static arrays for Vec containing nested types (Vec<Vec<T>>, Vec<List<T>>, etc.)
// First recursively emits inner element data, then emits outer array with patched pointers
emit_precomputed_nested_vec_static := proc(mut output: Str, mut ctx: CodegenContext, context: Context, var_name: Str, contents: VecContents) throws Str {
    elem_type := contents.element_type_name
    elem_count := contents.element_bytes.len()

    // First pass: recursively emit static arrays for each inner element
    mut inner_data_names := Vec.new(Str)

    for idx in 0..elem_count {
        elem_bytes_ref := contents.element_bytes.get_by_ref(idx)?
        create_alias(elem_bytes, Vec, elem_bytes_ref.data)

        inner_var_name := format(var_name, "_", idx.to_str())

        if elem_type.eq("Vec") {
            // Get the ptr field from the inner Vec struct bytes
            // Vec layout: type_name (Str), type_size (I64), ptr (Ptr), _len (I64), cap (I64)
            str_size := context.get_type_size("Str")?
            ptr_offset := str_size.add(8)  // after type_name and type_size
            inner_data_ptr := bytes_to_i64_from_offset(elem_bytes, ptr_offset)

            // Extract inner Vec contents from its memory location
            inner_type_name := EvalHeap.extract_str_from_bytes(context, elem_bytes)?
            inner_type_size := bytes_to_i64_from_offset(elem_bytes, str_size)

            ptr_size := context.get_type_size("Ptr")?
            len_offset := ptr_offset.add(ptr_size)
            inner_len := bytes_to_i64_from_offset(elem_bytes, len_offset)

            // Extract inner element bytes
            mut inner_element_bytes := Vec.new(Vec)
            for i in 0..inner_len {
                elem_offset := inner_data_ptr.add(i.mul(inner_type_size))
                mut bytes := Vec.new(U8)
                for j in 0..inner_type_size {
                    byte_ptr := g_heap.get(elem_offset.add(j), 1)
                    mut byte_val : U8 = 0
                    Ptr(data=byte_ptr).dereference(U8, byte_val)
                    bytes.push(byte_val)
                }
                inner_element_bytes.push(bytes)
            }

            inner_contents := VecContents(
                element_type_name=inner_type_name,
                type_size=inner_type_size,
                element_bytes=inner_element_bytes
            )

            // Recursively emit static arrays for inner Vec
            emit_precomputed_vec_recursive(output, ctx, context, inner_var_name, inner_contents)?
        }
        // TODO: Handle List and other nested types

        inner_data_names.push(format("_precomp_", inner_var_name, "_data"))
    }

    // Second pass: emit the outer array of structs with patched pointers
    data_array_name := format("_precomp_", var_name, "_data")
    output.push_str("static ")
    output.push_str(TIL_PREFIX)
    output.push_str(elem_type)
    output.push_str(" ")
    output.push_str(data_array_name)
    output.push_str("[")
    output.push_str(elem_count.to_str())
    output.push_str("] = {\n")

    for outer_idx in 0..elem_count {
        outer_elem_bytes_ref := contents.element_bytes.get_by_ref(outer_idx)?
        create_alias(outer_elem_bytes, Vec, outer_elem_bytes_ref.data)

        if elem_type.eq("Vec") {
            // Emit Vec struct with patched pointers
            // Vec layout: type_name (Str), type_size (I64), ptr (Ptr), _len (I64), cap (I64)
            outer_str_size := context.get_type_size("Str")?
            outer_ptr_size := context.get_type_size("Ptr")?

            // Extract type_size
            type_size := bytes_to_i64_from_offset(outer_elem_bytes, outer_str_size)

            // Extract _len
            outer_len_offset := outer_str_size.add(8).add(outer_ptr_size)
            len := bytes_to_i64_from_offset(outer_elem_bytes, outer_len_offset)

            // Extract cap
            cap_offset := outer_len_offset.add(8)
            cap := bytes_to_i64_from_offset(outer_elem_bytes, cap_offset)

            // Get inner type name
            outer_inner_type_name := EvalHeap.extract_str_from_bytes(context, outer_elem_bytes)?

            inner_data_name_str_ref := inner_data_names.get_by_ref(outer_idx)?
            create_alias(inner_data_name_str, Str, inner_data_name_str_ref.data)

            output.push_str("    {.type_name = (")
            output.push_str(TIL_PREFIX)
            output.push_str("Str){.c_string = (")
            output.push_str(TIL_PREFIX)
            output.push_str("Ptr){(")
            output.push_str(TIL_PREFIX)
            output.push_str("I64)\"")
            output.push_str(outer_inner_type_name)
            output.push_str("\", 1, 0, 0, 0}, ._len =")
            output.push_str(outer_inner_type_name.len().to_str())
            output.push_str(", .cap = 0}, .type_size = ")
            output.push_str(type_size.to_str())
            output.push_str(", .ptr = (")
            output.push_str(TIL_PREFIX)
            output.push_str("Ptr){(")
            output.push_str(TIL_PREFIX)
            output.push_str("I64)")
            output.push_str(inner_data_name_str)
            output.push_str(", 1, 0, 0, 0}, ._len =")
            output.push_str(len.to_str())
            output.push_str(", .cap = ")
            output.push_str(cap.to_str())
            output.push_str("}")
        }
        // TODO: Handle List and other nested types

        if outer_idx.add(1).lt(elem_count) {
            output.push_str(",")
        }
        output.push_str("\n")
    }

    output.push_str("};\n")

    catch (err: IndexOutOfBoundsError) {
        throw format("emit_precomputed_nested_vec_static: ", err.msg)
    }
}

// Helper to convert 8 bytes to I64 (little endian)
bytes_to_i64 := func(bytes: Vec) returns I64 {
    mut result := 0
    mut multiplier := 1  // 256^0 = 1
    for i in 0..8 {
        if i.lt(bytes.len()) {
            b_ref := bytes.get_by_ref(i)?
            create_alias(b, U8, b_ref.data)
            // Add byte value * position multiplier (256^i)
            result = result.add(b.to_i64().mul(multiplier))
            multiplier = multiplier.mul(256)
        }
        catch (err: IndexOutOfBoundsError) { }
    }
    return result
}

// Emit static arrays for Vec<Str> - two-level: string literals + Str structs
emit_precomputed_vec_str_static := proc(mut output: Str, mut _ctx: CodegenContext, context: Context, var_name: Str, contents: VecContents) throws Str {
    elem_count := contents.element_bytes.len()
    ptr_size := context.get_type_size("Ptr")?

    // Emit Str struct array with inline string literals
    data_array_name := format("_precomp_", var_name, "_data")
    output.push_str("static ")
    output.push_str(TIL_PREFIX)
    output.push_str("Str ")
    output.push_str(data_array_name)
    output.push_str("[")
    output.push_str(elem_count.to_str())
    output.push_str("] = {\n")

    for idx in 0..elem_count {
        bytes_ref := contents.element_bytes.get_by_ref(idx)?
        create_alias(bytes, Vec, bytes_ref.data)

        // Extract string data from Str struct bytes
        // Str layout: c_string (Ptr), _len (I64), cap (I64)
        c_string_ptr := bytes_to_i64_from_offset(bytes, 0)
        len := bytes_to_i64_from_offset(bytes, ptr_size)

        // Read the actual string bytes from heap
        mut string_data := ""
        if c_string_ptr.gt(0).and(len.gt(0)) {
            for si in 0..len {
                byte_ptr := g_heap.get(c_string_ptr.add(si), 1)
                mut byte_val : U8 = 0
                Ptr(data=byte_ptr).dereference(U8, byte_val)
                string_data = concat(string_data, Str.from_byte(byte_val))
            }
        }

        output.push_str("    {.c_string = (")
        output.push_str(TIL_PREFIX)
        output.push_str("Ptr){(")
        output.push_str(TIL_PREFIX)
        output.push_str("I64)\"")
        // Escape special characters
        for ci in 0..string_data.len() {
            ch := string_data.char_at(ci)
            ch_i64 := ch.to_i64()
            if ch_i64.eq(34) {  // '"'
                output.push_str("\\\"")
            } else if ch_i64.eq(92) {  // '\\'
                output.push_str("\\\\")
            } else if ch_i64.eq(10) {  // '\n'
                output.push_str("\\n")
            } else if ch_i64.eq(13) {  // '\r'
                output.push_str("\\r")
            } else if ch_i64.eq(9) {  // '\t'
                output.push_str("\\t")
            } else if ch_i64.lt(32) {  // control character
                output.push_str("\\x")
                output.push_str(hex_byte(ch))
            } else {
                output = concat(output, Str.from_byte(ch))
            }
        }
        output.push_str("\", 1, 0, 0, 0}, ._len =")
        output.push_str(len.to_str())
        output.push_str(", .cap = 0}")
        if idx.add(1).lt(elem_count) {
            output.push_str(",")
        }
        output.push_str("\n")
    }

    output.push_str("};\n")

    catch (err: IndexOutOfBoundsError) {
        throw format("emit_precomputed_vec_str_static: ", err.msg)
    }
}

// Helper to read I64 from bytes at an offset
bytes_to_i64_from_offset := func(bytes: Vec, offset: I64) returns I64 {
    mut result := 0
    mut multiplier := 1  // 256^0 = 1
    for i in 0..8 {
        idx := offset.add(i)
        if idx.lt(bytes.len()) {
            b_ref := bytes.get_by_ref(idx)?
            create_alias(b, U8, b_ref.data)
            result = result.add(b.to_i64().mul(multiplier))
            multiplier = multiplier.mul(256)
        }
        catch (err: IndexOutOfBoundsError) { }
    }
    return result
}

// Convert a byte to a 2-digit hex string
hex_byte := func(b: U8) returns Str {
    hex_chars := "0123456789abcdef"
    high := b.to_i64().div(16)  // equivalent to >> 4
    low := I64.and(b.to_i64(), 15)  // mask low 4 bits
    mut result := ""
    result = concat(result, Str.from_byte(hex_chars.char_at(high)))
    result = concat(result, Str.from_byte(hex_chars.char_at(low)))
    return result
}

// Bug #133 fix: Emit assignment for a precomputed Vec with patched Ptr fields
// Check if an enum has any payloads
enum_has_payloads := func(enum_def: SEnumDef) returns Bool {
    for v: EnumVariant in enum_def.variants {
        if not(v.payload_type.is_null()) {
            return true
        }
    }
    return false
}

// Check if an expression is an enum declaration with payloads
is_enum_with_payloads := func(expr: Expr) returns Bool throws Str {
    switch expr.node_type {
    case NodeType.Declaration(decl):
        if not(expr.params.is_empty()) {
            first_param := expr.get(0)?
            dont_delete(first_param)
            switch first_param.node_type {
            case NodeType.EnumDef(enum_def):
                return enum_has_payloads(enum_def)
            case:
            }
        }
    case:
    }
    return false
}

// Emit an enum with payloads as a tagged union
emit_enum_with_payloads := proc(enum_name: Str, enum_def: SEnumDef, mut output: Str) throws Str {
    // Sort variants by name for deterministic output
    mut variants := Vec.new(EnumVariant)
    for v: EnumVariant in enum_def.variants {
        variants.push(v.clone())
    }
    sort_enum_variants(variants)

    // 1. Emit tag enum: typedef enum { Color_Unknown = 0, ... } Color_Tag;
    output.push_str("typedef enum {\n")
    mut index := 0
    for v: EnumVariant in variants {
        output.push_str("    ")
        output.push_str(enum_name)
        output.push_str("_")
        output.push_str(v.name)
        output.push_str(" = ")
        output.push_str(I64.to_str(index))
        output.push_str(",\n")
        index.inc()
    }
    output.push_str("} ")
    output.push_str(enum_name)
    output.push_str("_Tag;\n\n")

    // 2. Emit payload union (only for variants that have payloads)
    // typedef union { unsigned char Green; long long Number; } Color_Payload;
    mut has_any_payload := false
    for v: EnumVariant in variants {
        if not(v.payload_type.is_null()) {
            has_any_payload = true
            break
        }
    }
    if has_any_payload {
        output.push_str("typedef union {\n")
        for v: EnumVariant in variants {
            if not(v.payload_type.is_null()) {
                // Read payload_type from Ptr
                mut payload_vt := ValueType.TCustom("")
                memcpy(to_ptr(payload_vt), v.payload_type.data, size_of(ValueType))
                c_type := til_type_to_c(payload_vt)?
                output.push_str("    ")
                output.push_str(c_type)
                output.push_str(" ")
                output.push_str(v.name)
                output.push_str(";\n")
            }
        }
        output.push_str("} ")
        output.push_str(enum_name)
        output.push_str("_Payload;\n\n")
    }

    // 3. Emit wrapper struct: struct Color { til_I64 tag; Color_Payload payload; };
    // Note: typedef is already forward-declared, so we just define the struct body
    // Bug #137: Use til_I64 for tag to ensure consistent 8-byte offset for payload
    output.push_str("struct ")
    output.push_str(enum_name)
    output.push_str(" {\n")
    output.push_str("    ")
    output.push_str(TIL_PREFIX)
    output.push_str("I64 tag;\n")
    if has_any_payload {
        output.push_str("    ")
        output.push_str(enum_name)
        output.push_str("_Payload payload;\n")
    }
    output.push_str("};\n\n")

    // 4. Emit constructor functions for ALL variants (including no-payload ones)
    // This ensures consistent calling convention: Color_make_Red(42), Color_make_Unknown()
    for v: EnumVariant in variants {
        output.push_str("static inline ")
        output.push_str(enum_name)
        output.push_str(" ")
        output.push_str(enum_name)
        output.push_str("_make_")
        output.push_str(v.name)
        output.push_str("(")

        // Parameter for payload (if any)
        mut has_payload := false
        if not(v.payload_type.is_null()) {
            mut ctor_payload_vt := ValueType.TCustom("")
            memcpy(to_ptr(ctor_payload_vt), v.payload_type.data, size_of(ValueType))
            ctor_c_type := til_type_to_c(ctor_payload_vt)?
            output.push_str(ctor_c_type)
            output.push_str(" value")
            has_payload = true
        } else {
            output.push_str("void")
        }
        output.push_str(") {\n")

        // Constructor body
        output.push_str("    ")
        output.push_str(enum_name)
        output.push_str(" result = { .tag = ")
        output.push_str(enum_name)
        output.push_str("_")
        output.push_str(v.name)
        output.push_str(" };\n")

        // Set payload if present
        if has_payload {
            output.push_str("    result.payload.")
            output.push_str(v.name)
            output.push_str(" = value;\n")
        }

        output.push_str("    return result;\n")
        output.push_str("}\n\n")
    }

}

// Emit an enum declaration as a C typedef enum (for simple enums without payloads)
// or as a tagged union struct (for enums with payloads)
emit_enum_declaration := proc(expr: Expr, mut output: Str) throws Str {
    switch expr.node_type {
    case NodeType.Declaration(decl):
        if not(expr.params.is_empty()) {
            first_param := expr.get(0)?
            dont_delete(first_param)
            switch first_param.node_type {
            case NodeType.EnumDef(enum_def):
                enum_name := til_name(decl.name)

                if enum_has_payloads(enum_def) {
                    // Phase 2: Enums with payloads - tagged union
                    emit_enum_with_payloads(enum_name, enum_def, output)?
                    return
                }

                // Phase 1: Simple enum without payloads
                // typedef enum { EnumName_Variant1 = 0, ... } EnumName;
                output.push_str("typedef enum {\n")

                // Sort variants by name for deterministic output
                mut variants := Vec.new(Str)
                for v: EnumVariant in enum_def.variants {
                    variants.push(v.name)
                }
                sort_str_vec(variants)

                mut index := 0
                for variant_name: Str in variants {
                    output.push_str("    ")
                    output.push_str(enum_name)
                    output.push_str("_")
                    output.push_str(variant_name)
                    output.push_str(" = ")
                    output.push_str(I64.to_str(index))
                    output.push_str(",\n")
                    index.inc()
                }

                output.push_str("} ")
                output.push_str(enum_name)
                output.push_str(";\n\n")

                // Generate constructor functions for consistency with payload enums
                // static inline EnumName EnumName_make_Variant(void) { return EnumName_Variant; }
                for variant_name: Str in variants {
                    output.push_str("static inline ")
                    output.push_str(enum_name)
                    output.push_str(" ")
                    output.push_str(enum_name)
                    output.push_str("_make_")
                    output.push_str(variant_name)
                    output.push_str("(void) { return ")
                    output.push_str(enum_name)
                    output.push_str("_")
                    output.push_str(variant_name)
                    output.push_str("; }\n")
                }
                output.push_str("\n")

                return
            case:
            }
        }
    case:
    }


    throw "emit_enum_declaration: not an enum declaration"
}

// Emit a _to_str function for an enum type
// Takes a const pointer since we only read the enum
// For simple enums: til_Str til_EnumName_to_str(const til_EnumName* e)
// For enums with payloads: til_Str til_EnumName_to_str(const til_EnumName* e)
emit_enum_to_str_function := proc(enum_name: Str, enum_def: SEnumDef, mut output: Str) throws Str {
    has_payloads := enum_has_payloads(enum_def)
    mut variants := Vec.new(Str)
    for v: EnumVariant in enum_def.variants {
        variants.push(v.name)
    }
    sort_str_vec(variants)

    // Function signature - takes const pointer since we only read the enum
    output.push_str("static inline til_Str ")
    output.push_str(enum_name)
    output.push_str("_to_str(const ")
    output.push_str(enum_name)
    output.push_str("* e) {\n    switch(")
    if has_payloads {
        output.push_str("e->tag")
    } else {
        output.push_str("*e")
    }
    output.push_str(") {\n")

    // Cases - need the original (non-prefixed) enum name for the string
    mut original_name := enum_name
    if enum_name.starts_with(TIL_PREFIX) {
        original_name = enum_name.get_substr(TIL_PREFIX.len(), enum_name.len())?
    }

    for variant: Str in variants {
        full_name := original_name.concat(".").concat(variant)
        output.push_str("        case ")
        output.push_str(enum_name)
        output.push_str("_")
        output.push_str(variant)
        // Use new Str format with Ptr { data, is_borrowed, alloc_size, elem_type, elem_size }, len, cap=0
        output.push_str(": return ((til_Str){((til_Ptr){(til_I64)\"")
        output.push_str(full_name)
        output.push_str("\", 1, 0, 0, 0}), ")
        output.push_str(I64.to_str(full_name.len()))
        output.push_str(", 0});\n")
    }

    // Default case (shouldn't happen but good for safety)
    unknown_name := original_name.concat(".?")
    // Use new Str format with Ptr { data, is_borrowed, alloc_size, elem_type, elem_size }, len, cap=0
    output.push_str("    }\n    return ((til_Str){((til_Ptr){(til_I64)\"")
    output.push_str(unknown_name)
    output.push_str("\", 1, 0, 0, 0}), ")
    output.push_str(I64.to_str(unknown_name.len()))
    output.push_str(", 0});\n}\n\n")

    catch (err: IndexOutOfBoundsError) { throw err.msg }
}

// Emit _to_str function for an enum declaration node
emit_enum_to_str_for_declaration := proc(expr: Expr, mut output: Str, context: Context) throws Str {
    switch expr.node_type {
    case NodeType.Declaration(decl):
        enum_name := decl.name
        mut have_enum_def := true
        enum_def := context.scope_stack.lookup_enum(enum_name)?
        catch (err: KeyNotFoundError) {
            have_enum_def = false
        }
        if have_enum_def {
            c_enum_name := til_name(enum_name)
            emit_enum_to_str_function(c_enum_name, enum_def, output)?
            return
        }
    case:
    }
    throw "emit_enum_to_str_for_declaration: not an enum declaration"
}

// Issue #108: Emit enum method prototypes (delete/clone)
emit_enum_method_prototypes := proc(expr: Expr, context: Context, mut output: Str) throws Str {
    switch expr.node_type {
    case NodeType.Declaration(decl):
        if not(expr.params.is_empty()) {
            first_param := expr.get(0)?
            dont_delete(first_param)
            switch first_param.node_type {
            case NodeType.EnumDef(enum_def):
                enum_name := til_name(decl.name)
                for method_name: Str in enum_def.methods.keys {
                    mut method_expr := Expr()
                    enum_def.methods.get(method_name, method_expr)?
                    catch (err: KeyNotFoundError) {}
                    switch method_expr.node_type {
                    case NodeType.FuncDef(func_def):
                        mangled_name := enum_name.concat("_").concat(method_name)
                        emit_func_signature(mangled_name, func_def, context, output)?
                        output.push_str(";\n")
                    case:
                    }
                }
                return
            case:
            }
        }
    case:
    }

}

// Issue #108: Emit enum method bodies (delete/clone)
emit_enum_method_bodies := proc(expr: Expr, mut output: Str, mut ctx: CodegenContext, mut context: Context) throws Str {
    switch expr.node_type {
    case NodeType.Declaration(decl):
        if not(expr.params.is_empty()) {
            first_param := expr.get(0)?
            dont_delete(first_param)
            switch first_param.node_type {
            case NodeType.EnumDef(enum_def):
                enum_name := til_name(decl.name)
                for method_name: Str in enum_def.methods.keys {
                    mut method_expr := Expr()
                    enum_def.methods.get(method_name, method_expr)?
                    catch (err: KeyNotFoundError) {}
                    switch method_expr.node_type {
                    case NodeType.FuncDef(func_def):
                        // Create a fake Declaration for emit_struct_func_body
                        method_decl := Declaration(
                            name=method_name,
                            value_type=ValueType.TFunction(func_def.function_type),
                            is_mut=false,
                            is_copy=false,
                            is_own=false,
                            default_value=Ptr()
                        )
                        emit_struct_func_body(enum_name, method_decl, func_def, output, ctx, context)?
                    case:
                    }
                }
                return
            case:
            }
        }
    case:
    }

}

// Emit a struct/namespace/enum function body with mangled name: TypeName_funcname
emit_struct_func_body := proc(struct_name: Str, member: Declaration, func_def: SFuncDef, mut output: Str, mut ctx: CodegenContext, mut context: Context) throws Str {
    // Skip external functions
    if func_def.is_ext() {
        return
    }

    // Set current function context
    ctx.current_throw_types = func_def.throw_types.clone()
    ctx.current_return_types = func_def.return_types.clone()
    // Track mut params for pointer dereference (-> vs .)
    ctx.current_ref_params.clear()
    // Track variadic params - they're passed as til_Array* so need dereference
    ctx.current_variadic_params.clear()
    for func_def_arg: Declaration in func_def.args {
        // Bug #60: All non-copy args are passed by pointer (mut, own, and const/default)
        if not(func_def_arg.is_copy) {
            _ := ctx.current_ref_params.insert(func_def_arg.name.clone())
        }
        switch func_def_arg.value_type {
        case ValueType.TMulti(elem_type):
            // elem_type is the type name string like "Bool"
            ctx.current_variadic_params.set(func_def_arg.name.clone(), til_name(elem_type))
        case:
        }
    }

    // Push a new scope frame for this function (like interpreter does)
    mut function_frame := ScopeFrame()
    function_frame.scope_type = ScopeType.Function
    // Register function parameters in the frame
    for func_def_arg: Declaration in func_def.args {
        mut info := SymbolInfo()
        info.value_type = func_def_arg.value_type
        info.is_mut = func_def_arg.is_mut
        info.is_copy = func_def_arg.is_copy
        info.is_own = func_def_arg.is_own
        info.is_comptime_const = false
        function_frame.symbols.set(func_def_arg.name.clone(), info)
    }
    context.scope_stack.frames.push(function_frame)

    // Clear declared_vars for new function scope
    ctx.declared_vars.clear()

    mangled_name := struct_name.concat("_").concat(member.name)

    // Save and set current function name for deterministic temp naming (Bug #42 fix)
    prev_function_name := ctx.current_function_name.clone()
    prev_mangling_counter := ctx.mangling_counter
    ctx.current_function_name = mangled_name.clone()
    ctx.mangling_counter = 0

    emit_func_signature(mangled_name, func_def, context, output)?
    output.push_str(" {\n")

    // Bug #99: Suppress -Wunused-parameter for intentionally unused params
    // _-prefixed params are always unused; all params unused in empty-body functions
    for arg: Declaration in func_def.args {
        if arg.name.starts_with("_").or(func_def.body.is_empty()) {
            c_name := til_var_name(arg.name, value_type_to_c_prefix(arg.value_type))
            output.push_str("    (void)")
            output.push_str(c_name)
            output.push_str(";\n")
        }
    }

    // Bug #168: Pre-scan for return variable placement optimization
    ctx.ret_var_alias = find_ret_var_for_placement(func_def)?
    if not(ctx.ret_var_alias.is_empty()) {
        _ := ctx.current_ref_params.insert(ctx.ret_var_alias.clone())
    }

    // Emit function body with catch pattern detection
    emit_stmts(func_def.body, output, 1, ctx, context)?

    // Add implicit return at end to silence gcc -Wreturn-type warnings
    // when all paths return inside branches but gcc can't prove exhaustiveness
    if not(func_def.throw_types.is_empty()) {
        // Throwing functions return int (0=success, 1+=error)
        output.push_str("    return 0;\n")
    } else if not(func_def.return_types.is_empty()) {
        // Non-throwing functions with return type - add zero-initialized fallback
        ret_type_vt_ref := func_def.return_types.get_by_ref(0)?
        create_alias(ret_type_vt, ValueType, ret_type_vt_ref.data)
        ret_type := til_type_to_c(ret_type_vt)?
        output.push_str("    return (")
        output.push_str(ret_type)
        output.push_str("){0};\n")
    }

    output.push_str("}\n\n")

    // Restore previous function name and counter
    ctx.current_function_name = prev_function_name
    ctx.mangling_counter = prev_mangling_counter

    // Pop the function scope frame
    mut _popped_frame := ScopeFrame()
    context.scope_stack.frames.pop(_popped_frame)?

    // Clear current function context
    ctx.current_throw_types.clear()
    ctx.current_return_types.clear()
    ctx.ret_var_alias = ""

    catch (err: IndexOutOfBoundsError) {
        throw err.msg
    }
}

// Issue #108: Emit namespace function prototypes (forward declarations)
emit_namespace_func_prototypes := proc(expr: Expr, context: Context, mut output: Str) throws Str {
    switch expr.node_type {
    case NodeType.NamespaceDef(ns_def):
        type_name := til_name(ns_def.type_name)
        for member: Declaration in ns_def.members {
            val_ref := ns_def.default_values.get_by_ref(member.name)?
            create_alias(func_expr, Expr, val_ref.data)
            catch (err: KeyNotFoundError) {
                // member has no default value, skip
            }
            switch func_expr.node_type {
            case NodeType.FuncDef(func_def):
                mangled_name := type_name.concat("_").concat(member.name)
                emit_func_signature(mangled_name, func_def, context, output)?
                output.push_str(";\n")
            case:
            }
        }
    case:
    }
}

// Issue #108: Emit namespace function bodies
emit_namespace_func_bodies := proc(expr: Expr, mut output: Str, mut ctx: CodegenContext, mut context: Context) throws Str {
    switch expr.node_type {
    case NodeType.NamespaceDef(ns_def):
        type_name := til_name(ns_def.type_name)
        for member: Declaration in ns_def.members {
            val_ref := ns_def.default_values.get_by_ref(member.name)?
            create_alias(func_expr, Expr, val_ref.data)
            catch (err: KeyNotFoundError) {
                // member has no default value, skip
            }
            switch func_expr.node_type {
            case NodeType.FuncDef(func_def):
                emit_struct_func_body(type_name, member, func_def, output, ctx, context)?
            case:
            }
        }
    case:
    }
}

// Convert TIL type to C type. Returns Err if the type can't be represented in C (e.g., functions, infer)
til_type_to_c := func(til_type: ValueType) returns Str throws Str {
    switch til_type {
    case ValueType.TCustom(name):
        if name.eq(INFER_TYPE) {
            throw "Cannot convert inferred type to C"
        } else {
            // All types get TIL_PREFIX
            return TIL_PREFIX.concat(name)
        }
    case ValueType.TFunction(ft):
        throw "Cannot convert function type to C"
    case ValueType.TType(tt):
        throw "Cannot convert Type to C"
    case ValueType.TMulti(mt):
        throw "Cannot convert multi-type to C"
    }
}

// Helper to get C type name for a ValueType (for error struct definitions)
value_type_to_c_name := func(vt: ValueType) returns Str throws Str {
    switch vt {
    case ValueType.TCustom(name):
        // All types get TIL_PREFIX
        return TIL_PREFIX.concat(name)
    case:
        throw "Cannot convert to C type name"
    }
}

// Issue #119: Check if a throw type refers to an empty struct (no fields)
// For empty structs, we don't need to pass error parameters - just the status code
is_empty_error_struct := func(context: Context, throw_type: ValueType) returns Bool {
    switch throw_type {
    case ValueType.TCustom(type_name):
        struct_def_opt := context.scope_stack.lookup_struct(type_name)?
        catch (err: KeyNotFoundError) {
            return false
        }
        return struct_def_opt.members.is_empty()
    case:
        return false
    }
}

// Emit function signature (used by both prototype and definition)
// For throwing functions:
//   int func_name(RetType* _ret, Error1* _err1, Error2* _err2, args...)
// For non-throwing:
//   RetType func_name(args...)
// Issue #119: Empty struct errors don't get parameters - only status code matters
emit_func_signature := proc(func_name: Str, func_def: SFuncDef, context: Context, mut output: Str) throws Str {
    is_throwing := not(func_def.throw_types.is_empty())

    if is_throwing {
        // Throwing function returns int status code
        output.push_str("int ")
    } else {
        // Non-throwing function returns its actual type
        if func_def.return_types.is_empty() {
            output.push_str("void ")
        } else {
            ret_type_vt_ref := func_def.return_types.get_by_ref(0)?
            create_alias(ret_type_vt, ValueType, ret_type_vt_ref.data)
            ret_type := til_type_to_c(ret_type_vt)?
            output.push_str(ret_type)
            output.push_str(" ")
        }
    }

    output.push_str(func_name)
    output.push_str("(")

    mut param_count := 0

    if is_throwing {
        // Output params first: return value pointer, then error pointers
        if not(func_def.return_types.is_empty()) {
            thr_ret_type_vt_ref := func_def.return_types.get_by_ref(0)?
            create_alias(thr_ret_type_vt, ValueType, thr_ret_type_vt_ref.data)
            thr_ret_type := til_type_to_c(thr_ret_type_vt)?
            output.push_str(thr_ret_type)
            output.push_str("* _ret")
            param_count.inc()
        }

        // Issue #119: Skip empty struct errors - only status code matters
        for i in 0..func_def.throw_types.len() {
            throw_type_ref := func_def.throw_types.get_by_ref(i)?
            create_alias(throw_type, ValueType, throw_type_ref.data)
            if is_empty_error_struct(context, throw_type) {
                // Skip this error parameter for empty struct
            } else {
                if param_count.gt(0) {
                    output.push_str(", ")
                }
                err_type := value_type_to_c_name(throw_type)?
                output.push_str(err_type)
                output.push_str("* _err")
                output.push_str(I64.to_str(i.add(1)))
                param_count.inc()
            }
        }
    }

    // Input parameters
    for func_def_arg: Declaration in func_def.args {
        if param_count.gt(0) {
            output.push_str(", ")
        }
        // Check for variadic arg (TMulti)
        switch func_def_arg.value_type {
        case ValueType.TMulti(elem_type):
            // Variadic args are passed as til_Array* - Bug #97: use type-mangled name
            output.push_str(TIL_PREFIX)
            output.push_str("Array* ")
            // Variadic params have effective type Array in scope_stack
            output.push_str(til_var_name(func_def_arg.name, "Array"))
            param_count.inc()
            break // Variadic must be last
        case:
            // All non-variadic types (TI64, TU8, TBool, TStr, TCustom, etc.)
            arg_type := til_type_to_c(func_def_arg.value_type)?

            // Bug #60: Type is already const char*, don't add extra indirection
            // is_type_param is true only for TCustom("Type")
            mut is_type_param := false
            switch func_def_arg.value_type {
            case ValueType.TCustom(type_name):
                is_type_param = type_name.eq("Type")
            case:
            }

            if func_def_arg.is_mut {
                // mut: pass by pointer so mutations are visible to caller
                output.push_str(arg_type)
                output.push_str("* ")
            } else if func_def_arg.is_own {
                // own: pass by pointer, caller transfers ownership
                // Type is already a pointer, so pass by value
                if is_type_param {
                    output.push_str(arg_type)
                    output.push_str(" ")
                } else {
                    output.push_str(arg_type)
                    output.push_str("* ")
                }
            } else if func_def_arg.is_copy {
                // copy: pass by value, caller's copy is made
                output.push_str(arg_type)
                output.push_str(" ")
            } else {
                // const (default): pass by const pointer, read-only
                // Type is already a pointer, so pass by value
                if is_type_param {
                    output.push_str(arg_type)
                    output.push_str(" ")
                } else {
                    output.push_str("const ")
                    output.push_str(arg_type)
                    output.push_str("* ")
                }
            }
            // Bug #97: Use type-mangled parameter name
            param_type_prefix := value_type_to_c_prefix(func_def_arg.value_type)
            output.push_str(til_var_name(func_def_arg.name, param_type_prefix))
            param_count.inc()
        }
    }

    if param_count.eq(0) {
        output.push_str("void")
    }

    output.push_str(")")

    catch (err: IndexOutOfBoundsError) {
        throw err.msg
    }
}

// Emit a function prototype (forward declaration)
emit_func_prototype := proc(expr: Expr, context: Context, mut output: Str) throws Str {
    switch expr.node_type {
    case NodeType.Declaration(decl):
        if not(expr.params.is_empty()) {
            first_param := expr.get(0)?
            dont_delete(first_param)
            switch first_param.node_type {
            case NodeType.FuncDef(func_def):
                // Skip external functions (ext_proc/ext_func) - they're just declarations
                if func_def.is_ext() {
                    return
                }

                func_name := til_name(decl.name)
                emit_func_signature(func_name, func_def, context, output)?
                output.push_str(";\n")
                return
            case:
            }
        }
    case:
    }


    throw "emit_func_prototype: not a function declaration"
}

// Check if an expression is a function declaration (name := proc/func)
is_func_declaration := func(expr: Expr) returns Bool throws Str {
    switch expr.node_type {
    case NodeType.Declaration(decl):
        if not(expr.params.is_empty()) {
            first_param := expr.get(0)?
            dont_delete(first_param)
            switch first_param.node_type {
            case NodeType.FuncDef(fd):
                return true
            case:
            }
        }
    case:
    }
    return false
}

// Emit a function declaration as a C function
emit_func_declaration := proc(expr: Expr, mut output: Str, mut ctx: CodegenContext, mut context: Context) throws Str {
    switch expr.node_type {
    case NodeType.Declaration(decl):
        if not(expr.params.is_empty()) {
            first_param := expr.get(0)?
            dont_delete(first_param)
            switch first_param.node_type {
            case NodeType.FuncDef(func_def):
                // Skip external functions (ext_proc/ext_func) - they're just declarations
                if func_def.is_ext() {
                    return
                }

                // Set current function context for return/throw generation
                ctx.current_throw_types = func_def.throw_types.clone()
                ctx.current_return_types = func_def.return_types.clone()
                // Track ref params for pointer dereference (-> vs .)
                ctx.current_ref_params.clear()
                // Track variadic params - they're passed as til_Array* so need dereference
                ctx.current_variadic_params.clear()
                for func_def_arg: Declaration in func_def.args {
                    // Bug #60: All non-copy args are passed by pointer (mut, own, and const/default)
                    if not(func_def_arg.is_copy) {
                        _ := ctx.current_ref_params.insert(func_def_arg.name.clone())
                    }
                    switch func_def_arg.value_type {
                    case ValueType.TMulti(elem_type):
                        // elem_type is the type name string like "Bool"
                        ctx.current_variadic_params.set(func_def_arg.name.clone(), til_name(elem_type))
                    case:
                    }
                }

                // Push a new scope frame for this function (like interpreter does)
                mut function_frame := ScopeFrame()
                function_frame.scope_type = ScopeType.Function
                // Register function parameters in the frame
                // For variadic params (TMulti), register as Array type
                for func_def_arg: Declaration in func_def.args {
                    mut value_type := func_def_arg.value_type
                    switch func_def_arg.value_type {
                    case ValueType.TMulti(elem):
                        value_type = ValueType.TCustom("Array")
                    case:
                    }
                    mut info := SymbolInfo()
                    info.value_type = value_type
                    info.is_mut = func_def_arg.is_mut
                    info.is_copy = func_def_arg.is_copy
                    info.is_own = func_def_arg.is_own
                    info.is_comptime_const = false
                    function_frame.symbols.set(func_def_arg.name.clone(), info)
                }
                context.scope_stack.frames.push(function_frame)

                // Clear declared_vars for new function scope
                ctx.declared_vars.clear()

                func_name := til_name(decl.name)

                // Save and set current function name for nested function mangling
                prev_function_name := ctx.current_function_name.clone()
                prev_mangling_counter := ctx.mangling_counter
                ctx.current_function_name = decl.name.clone()
                ctx.mangling_counter = 0  // Reset counter per-function for determinism

                emit_func_signature(func_name, func_def, context, output)?
                output.push_str(" {\n")

                // Bug #99: Suppress -Wunused-parameter for intentionally unused params
                // _-prefixed params are always unused; all params unused in empty-body functions
                for arg: Declaration in func_def.args {
                    if arg.name.starts_with("_").or(func_def.body.is_empty()) {
                        c_name := til_var_name(arg.name, value_type_to_c_prefix(arg.value_type))
                        output.push_str("    (void)")
                        output.push_str(c_name)
                        output.push_str(";\n")
                    }
                }

                // Bug #168: Pre-scan for return variable placement optimization
                ctx.ret_var_alias = find_ret_var_for_placement(func_def)?
                if not(ctx.ret_var_alias.is_empty()) {
                    _ := ctx.current_ref_params.insert(ctx.ret_var_alias.clone())
                }

                // Emit function body with catch pattern detection
                emit_stmts(func_def.body, output, 1, ctx, context)?

                // Add implicit return at end to silence gcc -Wreturn-type warnings
                if not(func_def.throw_types.is_empty()) {
                    output.push_str("    return 0;\n")
                } else if not(func_def.return_types.is_empty()) {
                    ret_type_vt_ref := func_def.return_types.get_by_ref(0)?
                    create_alias(ret_type_vt, ValueType, ret_type_vt_ref.data)
                    ret_type := til_type_to_c(ret_type_vt)?
                    output.push_str("    return (")
                    output.push_str(ret_type)
                    output.push_str("){0};\n")
                }

                output.push_str("}\n\n")

                // Pop the function scope frame
                mut _popped_frame := ScopeFrame()
                context.scope_stack.frames.pop(_popped_frame)?

                // Restore previous function name and counter
                ctx.current_function_name = prev_function_name
                ctx.mangling_counter = prev_mangling_counter

                // Clear current function context
                ctx.current_throw_types.clear()
                ctx.current_return_types.clear()
                ctx.ret_var_alias = ""

                return
            case:
            }
        }
    case:
    }

    catch (err: IndexOutOfBoundsError) {
        throw err.msg
    }

    throw "emit_func_declaration: not a function declaration"
}

emit_expr := proc(expr: Expr, mut output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) throws Str {
    // Bug #143: Hoisting now happens via emit_arg_string at the call site
    // Expressions are processed once and the result is used directly

    switch expr.node_type {
    case NodeType.Body:
        emit_body(expr, output, indent, ctx, context)?
    case NodeType.FCall(_):
        emit_fcall(expr, output, indent, ctx, context)?
    case NodeType.LLiteral(lit):
        emit_literal(lit, output, context)?
    case NodeType.Declaration(decl):
        emit_declaration(decl, expr, output, indent, ctx, context)?
    case NodeType.Identifier(name):
        // Bug #32 fix: Handle field access on expression results
        // When name is "_", params[0] is an expression to emit first,
        // then params[1..] are the field chain
        if name.eq("_").and(not(expr.params.is_empty())) {
            // Emit the base expression (params[0])
            first_param := expr.get(0)?
            dont_delete(first_param)
            emit_expr(first_param, output, indent, ctx, context)?
            // Then emit the field chain
            mut i := 1
            while i.lt(expr.params.len()) {
                param := expr.get(i)?
                dont_delete(param)
                switch param.node_type {
                case NodeType.Identifier(field):
                    output.push_str(".")
                    output.push_str(field)
                case:
                }
                i.inc()
            }
            return
        }

        // Check for type-qualified access (Type.field or Type.Variant)
        if not(expr.params.is_empty()) {
            type_first_param := expr.get(0)?
            dont_delete(type_first_param)
            switch type_first_param.node_type {
            case NodeType.Identifier(type_field):
                // Check if this is an enum variant by looking up in context
                mut type_have_enum_def := true
                type_enum_def := context.scope_stack.lookup_enum(name)?
                catch (err: KeyNotFoundError) {
                    type_have_enum_def = false
                }
                if type_have_enum_def {
                    if type_enum_def.contains_key(type_field) {
                        // Enum variant: Type.Variant -> til_Type_make_Variant()
                        output.push_str(TIL_PREFIX)
                        output.push_str(name)
                        output.push_str("_make_")
                        output.push_str(type_field)
                        output.push_str("()")
                        return
                    }
                }
                // Check if this is a struct constant access
                _type_struct_def := context.scope_stack.lookup_struct(name)?
                // Struct constant: Type.constant -> til_Type_constant
                output.push_str(til_name(name))
                output.push_str("_")
                output.push_str(type_field)
                return
                catch (err: KeyNotFoundError) {
                    // Not a struct constant, continue
                }
            case:
            }
        }
        // Regular identifier or field access (b.val -> til_b.val)
        // For ref params (which are pointers in C), use -> for field access
        is_ref_param := ctx.current_ref_params.contains(name)
        // Bug #97: Use type-mangled name for variables
        // Bug #168: resolve_var_name maps ret_var_alias to "_ret"
        c_var_name := resolve_var_name(name, ctx, context)
        if is_ref_param.and(not(expr.params.is_empty())) {
            // Ref param with field access: til_Type_self->field1.field2.field3
            // First field uses -> (self is a pointer), rest use . (struct values)
            output.push_str(c_var_name)
            mut ref_field_i := 0
            for ref_field_param: Expr in expr.params {
                switch ref_field_param.node_type {
                case NodeType.Identifier(ref_field_name):
                    if ref_field_i.eq(0) {
                        output.push_str("->")
                    } else {
                        output.push_str(".")
                    }
                    output.push_str(ref_field_name)
                case:
                }
                ref_field_i.inc()
            }
        } else if is_ref_param {
            // Ref param used as value: dereference with *
            output.push_str("(*")
            output.push_str(c_var_name)
            output.push_str(")")
        } else {
            // Regular identifier or field access
            output.push_str(c_var_name)
            for param: Expr in expr.params {
                switch param.node_type {
                case NodeType.Identifier(field):
                    output.push_str(".")
                    output.push_str(field)  // Field names stay as-is (C struct fields)
                case:
                }
            }
        }
    case NodeType.FuncDef(func_def):
        emit_funcdef(func_def, expr, output, indent, ctx, context)?
    case NodeType.Assignment(name):
        emit_assignment(name, expr, output, indent, ctx, context)?
    case NodeType.Return:
        emit_return(expr, output, indent, ctx, context)?
    case NodeType.If:
        emit_if(expr, output, indent, ctx, context)?
    case NodeType.While:
        emit_while(expr, output, indent, ctx, context)?
    case NodeType.Break:
        emit_break(expr, output, indent)
    case NodeType.Continue:
        emit_continue(expr, output, indent)
    case NodeType.Catch:
        // Catch blocks handled at call site
    case NodeType.Throw:
        emit_throw(expr, output, indent, ctx, context)?
    case NodeType.StructDef(sd):
        throw "ccodegen: StructDef should be handled at top level, not in emit_expr"
    case NodeType.EnumDef(ed):
        throw "ccodegen: EnumDef should be handled at top level, not in emit_expr"
    case NodeType.Switch:
        throw expr.lang_error(context.path, "ccodegen", "Switch should have been desugared to if/else by desugarer phase")
    case NodeType.DefaultCase:
        throw expr.lang_error(context.path, "ccodegen", "DefaultCase should have been desugared with Switch")
    case NodeType.Range:
        throw "ccodegen: Range not yet supported"
    case NodeType.Pattern(p):
        throw expr.lang_error(context.path, "ccodegen", "Pattern should have been desugared with Switch")
    case NodeType.NamedArg(na):
        throw expr.error(context.path, "ccodegen", "NamedArg should be reordered before reaching emit_expr")
    // Issue #108: NamespaceDef already processed by init - members merged into type
    case NodeType.NamespaceDef(_ns_def):
        // Nothing to emit - namespace functions are emitted in pass 5b2
    case NodeType.ForIn(fi):
        throw expr.lang_error(context.path, "ccodegen", "ForIn should be desugared in precomp before reaching ccodegen")
    case:
    }
}

emit_body := proc(expr: Expr, mut output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) throws Str {
    // Bug #44 fix: Save and restore local_catch_labels around nested blocks
    // Without this, nested blocks clear the outer catches when processing their
    // own throwing calls, causing missing status checks for outer throwing calls.
    saved_catch_labels := ctx.local_catch_labels.clone()
    emit_stmts(expr.params, output, indent, ctx, context)?
    ctx.local_catch_labels = saved_catch_labels
    dont_delete(saved_catch_labels)

}

/// Emit a sequence of statements with catch pattern detection
/// This is the core logic shared between emit_body and emit_func_declaration
emit_stmts := proc(stmts: Vec, mut output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) throws Str {
    mut i := 0
    indent_str := repeat("    ", indent)

    // Pre-scan for function-level catches (at the end of the block)
    func_level_catches := prescan_func_level_catches(stmts)

    // Hoist declarations from catch blocks to function level
    // (TIL has function-level scoping, not block-level scoping)
    for catch_block: Expr in func_level_catches {
        if catch_block.params.len().gteq(3) {
            // Temporarily add the catch error variable to scope for type inference
            // catch (err: ErrType) { body } -> err_var=params[0], err_type=params[1], body=params[2]
            err_var_param := catch_block.get(0)?
            dont_delete(err_var_param)
            err_type_param := catch_block.get(1)?
            dont_delete(err_type_param)
            switch err_var_param.node_type {
            case NodeType.Identifier(err_var_name):
                switch err_type_param.node_type {
                case NodeType.Identifier(err_type_name):
                    mut info := SymbolInfo()
                    info.value_type = ValueType.TCustom(err_type_name.clone())
                    info.is_mut = false
                    info.is_copy = false
                    info.is_own = false
                    info.is_comptime_const = false
                    context.scope_stack.declare_symbol(err_var_name.clone(), info)?
                case:
                }
            case:
            }

            // Bug #97: Use type-mangled names for shadowing support
            catch_body := catch_block.get(2)?
            dont_delete(catch_body)
            decls := collect_declarations_in_body(catch_body, context)?
            for decl: CollectedDeclaration in decls {
                type_prefix := value_type_to_c_prefix(decl.value_type)
                c_var_name := til_var_name(decl.name, type_prefix)
                if not(ctx.declared_vars.contains(c_var_name)) {
                    mut have_c_type := true
                    c_type := til_type_to_c(decl.value_type)?
                    catch (err: Str) {
                        have_c_type = false
                    }
                    if have_c_type {
                        output.push_str(indent_str)
                        output.push_str(c_type)
                        output.push_str(" ")
                        output.push_str(c_var_name)
                        output.push_str(";\n")
                        _ := ctx.declared_vars.insert(c_var_name)
                        // Also register in scope_stack so get_value_type can find it
                        mut sym_info := SymbolInfo()
                        sym_info.value_type = decl.value_type
                        sym_info.is_mut = false
                        sym_info.is_copy = false
                        sym_info.is_own = false
                        sym_info.is_comptime_const = false
                        context.scope_stack.declare_symbol(decl.name.clone(), sym_info)?
                    }
                }
            }
        }
    }

    // Bug #39 fix: Give each catch block its own unique label using the global counter
    // Store catch info with statement index so we can match calls to their NEXT catch
    mut all_catch_info := Vec.new(CatchLabelInfoEntry)
    mut scan_idx := 0
    for scan_stmt: Expr in stmts {
        switch scan_stmt.node_type {
        case NodeType.Catch:
            if scan_stmt.params.len().gteq(3) {
                scan_err_type_param := scan_stmt.get(1)?
                dont_delete(scan_err_type_param)
                switch scan_err_type_param.node_type {
                case NodeType.Identifier(scan_err_type_name):
                    catch_suffix := next_mangled(ctx)
                    mut entry := CatchLabelInfoEntry()
                    entry.stmt_index = scan_idx
                    entry.type_name = scan_err_type_name.clone()
                    entry.label = concat("_catch_", scan_err_type_name).concat("_").concat(catch_suffix)
                    entry.temp_var = concat("_thrown_", scan_err_type_name).concat("_").concat(catch_suffix)
                    all_catch_info.push(entry)
                case:
                }
            }
        case:
        }
        scan_idx.inc()
    }

    // Declare temp variables for all catches at the start
    for catch_entry: CatchLabelInfoEntry in all_catch_info {
        output.push_str(indent_str)
        output.push_str(til_name(catch_entry.type_name))
        output.push_str(" ")
        output.push_str(catch_entry.temp_var)
        output.push_str(";\n")
    }

    // For backward compatibility, also populate local_catch_labels with first catch of each type
    // This is used by throw statements and nested blocks
    mut registered_types := Set.new(Str)
    for catch_entry: CatchLabelInfoEntry in all_catch_info {
        if not(registered_types.contains(catch_entry.type_name)) {
            _ := registered_types.insert(catch_entry.type_name.clone())
            mut cli := CatchLabelInfo()
            cli.label = catch_entry.label.clone()
            cli.temp_var = catch_entry.temp_var.clone()
            ctx.local_catch_labels.set(catch_entry.type_name.clone(), cli)
        }
    }

    // Track which catch labels have been emitted to avoid duplicates
    // (when multiple catches of same type exist, only emit the label once)
    mut emitted_catch_labels := Set.new(Str)

    // Process all statements
    while i.lt(stmts.len()) {
        stmt_ref := stmts.get_by_ref(i)?
        create_alias(stmt, Expr, stmt_ref.data)
        effective_indent := indent  // Use same indent for all statements (no if-block nesting)

        // Bug #39 fix: Handle catch blocks inline with if(0) { label: ... }
        // This keeps catches where they appear in source, and execution falls through after
        switch stmt.node_type {
        case NodeType.Catch:
            // Get the error type this catch handles
            if stmt.params.len().gteq(3) {
                stmt_err_type_param := stmt.get(1)?
                dont_delete(stmt_err_type_param)
                switch stmt_err_type_param.node_type {
                case NodeType.Identifier(stmt_err_type_name):
                    // Look up this specific catch's label from all_catch_info by statement index
                    mut catch_entry_found := false
                    mut catch_entry_label := ""
                    mut catch_entry_temp_var := ""
                    for e: CatchLabelInfoEntry in all_catch_info {
                        if e.stmt_index.eq(i) {
                            catch_entry_found = true
                            catch_entry_label = e.label.clone()
                            catch_entry_temp_var = e.temp_var.clone()
                            break
                        }
                    }
                    if catch_entry_found {
                        // Only emit if we haven't already emitted this label
                        if not(emitted_catch_labels.contains(catch_entry_label)) {
                            _ := emitted_catch_labels.insert(catch_entry_label.clone())

                            output.push_str(indent_str)
                            output.push_str("if (0) { ")
                            output.push_str(catch_entry_label)
                            // Add empty statement after label - C11 requires statement after label, not declaration
                            output.push_str(":;\n")

                            // Bind error variable
                            emit_err_var_param := stmt.get(0)?
                            dont_delete(emit_err_var_param)
                            switch emit_err_var_param.node_type {
                            case NodeType.Identifier(emit_err_var_name):
                                emit_inner_indent := repeat("    ", indent.add(1))
                                output.push_str(emit_inner_indent)
                                output.push_str(til_name(stmt_err_type_name))
                                output.push_str(" ")
                                // Bug #97: Use type-mangled name for catch error variable
                                output.push_str(til_var_name(emit_err_var_name, stmt_err_type_name))
                                output.push_str(" = ")
                                output.push_str(catch_entry_temp_var)
                                output.push_str(";\n")

                                // Add error variable to scope for type resolution in catch body
                                mut emit_sym_info := SymbolInfo()
                                emit_sym_info.value_type = ValueType.TCustom(stmt_err_type_name.clone())
                                emit_sym_info.is_mut = false
                                emit_sym_info.is_copy = false
                                emit_sym_info.is_own = false
                                emit_sym_info.is_comptime_const = false
                                context.scope_stack.declare_symbol(emit_err_var_name.clone(), emit_sym_info)?
                            case:
                            }

                            // Emit catch body
                            // IMPORTANT: Temporarily clear local_catch_labels so throwing calls inside
                            // the catch body don't try to jump to catches - catches shouldn't catch
                            // errors from their own body, only from code before them
                            saved_catch_labels := ctx.local_catch_labels.clone()
                            saved_ref_params := ctx.current_ref_params.clone()
                            ctx.local_catch_labels.clear()
                            catch_body_expr := stmt.get(2)?
                            dont_delete(catch_body_expr)
                            emit_expr(catch_body_expr, output, indent.add(1), ctx, context)?
                            ctx.current_ref_params = saved_ref_params
                            dont_delete(saved_ref_params)
                            ctx.local_catch_labels = saved_catch_labels
                            dont_delete(saved_catch_labels)

                            // Close the if(0) block - execution falls through to code after
                            output.push_str(indent_str)
                            output.push_str("}\n")

                            // Bug #46 fix: Remove this catch from local_catch_labels
                            // Catches should only handle throws from BEFORE them, not after.
                            ctx.local_catch_labels.remove(stmt_err_type_name)
                        }
                    }
                case:
                }
            }
            i.inc()
            continue
        case:
        }

        // Check if this statement is followed by catch blocks
        // And if it's a call to a throwing function (FCall, Declaration with FCall, or Assignment with FCall)
        mut maybe_fcall_found := false
        mut maybe_fcall := stmt
        mut maybe_decl_name := ""
        mut maybe_assign_name := ""

        switch stmt.node_type {
        case NodeType.FCall(_):
            maybe_fcall_found = true
            maybe_fcall = stmt
        case NodeType.Declaration(decl):
            // Check if declaration has an FCall as initializer
            if not(stmt.params.is_empty()) {
                first_param := stmt.get(0)?
                dont_delete(first_param)
                switch first_param.node_type {
                case NodeType.FCall(_):
                    maybe_fcall_found = true
                    maybe_fcall = first_param
                    maybe_decl_name = decl.name.clone()
                case:
                }
            }
        case NodeType.Assignment(assign_name):
            // Check if assignment has an FCall as RHS
            if not(stmt.params.is_empty()) {
                assign_first_param := stmt.get(0)?
                dont_delete(assign_first_param)
                switch assign_first_param.node_type {
                case NodeType.FCall(_):
                    maybe_fcall_found = true
                    maybe_fcall = assign_first_param
                    maybe_assign_name = assign_name.clone()
                case:
                }
            }
        case:
        }

        if maybe_fcall_found {
            // eval functions from std.meta are only available in interpret mode
            if not(maybe_fcall.params.is_empty()) {
                func_name_param := maybe_fcall.get(0)?
                dont_delete(func_name_param)
                func_name := get_til_func_name_string(func_name_param)?
                if func_name.eq("eval_file").or(func_name.eq("eval_to_str")).or(func_name.eq("eval_to_ast_str")).or(func_name.eq("eval_to_expr")) {
                    throw maybe_fcall.error(context.path, "ccodegen", func_name.concat("() is only available in interpret mode, not in build/run"))
                }
            }

            // Get function name from the FCall
            // Check if this function is a throwing function
            fd := get_fcall_func_def(context, maybe_fcall)?
            if not(fd.throw_types.is_empty()) {
                throw_types := fd.throw_types

                // Collect subsequent catch blocks
                mut catch_blocks := Vec.new(Expr)
                mut j := i.add(1)
                while j.lt(stmts.len()) {
                    next_stmt_ref := stmts.get_by_ref(j)?
                    create_alias(next_stmt, Expr, next_stmt_ref.data)
                    switch next_stmt.node_type {
                    case NodeType.Catch:
                        catch_blocks.push(next_stmt)
                        j.inc()
                    case:
                        break
                    }
                }

                if not(catch_blocks.is_empty()).and(func_level_catches.is_empty()) {
                    // Immediate catches AND no function-level catches - handle inline
                    emit_throwing_call(maybe_fcall, throw_types, catch_blocks, maybe_decl_name, maybe_assign_name, output, effective_indent, ctx, context)?
                    i = j  // Skip past catch blocks
                    continue
                } else if not(ctx.current_throw_types.is_empty()).and(ctx.local_catch_labels.is_empty()).and(func_level_catches.is_empty()) {
                    // Bug #68 fix: Also check func_level_catches.is_empty()
                    // No catch blocks, we're inside a throwing function, and no catches in this block
                    // Emit error propagation pattern
                    emit_throwing_call_propagate(maybe_fcall, throw_types, maybe_decl_name, maybe_assign_name, output, effective_indent, ctx, context)?
                    i.inc()
                    continue
                } else if not(func_level_catches.is_empty()).or(not(ctx.local_catch_labels.is_empty())) {
                    // Has function-level catches or outer catches registered
                    // Bug #39 fix: Update local_catch_labels to point to NEXT catch for each type
                    // Build map with only catches AFTER current position
                    mut next_catches := Map.new(Str, CatchLabelInfo)
                    for catch_entry: CatchLabelInfoEntry in all_catch_info {
                        if catch_entry.stmt_index.gt(i).and(not(next_catches.contains_key(catch_entry.type_name))) {
                            // First catch of this type after current position
                            mut next_cli := CatchLabelInfo()
                            next_cli.label = catch_entry.label.clone()
                            next_cli.temp_var = catch_entry.temp_var.clone()
                            next_catches.set(catch_entry.type_name.clone(), next_cli)
                        }
                    }
                    // Replace local_catch_labels with only the next catches
                    ctx.local_catch_labels.clear()
                    for type_name: Str in next_catches.keys {
                        mut catch_info := CatchLabelInfo()
                        next_catches.get(type_name, catch_info)?
                        catch (err: KeyNotFoundError) {
                            panic(loc(), "KeyNotFoundError iterating keys: ", err.msg)
                        }
                        mut local_cli := CatchLabelInfo()
                        local_cli.label = catch_info.label.clone()
                        local_cli.temp_var = catch_info.temp_var.clone()
                        ctx.local_catch_labels.set(type_name.clone(), local_cli)
                    }
                    emit_throwing_call_with_goto(maybe_fcall, throw_types, maybe_decl_name, maybe_assign_name, output, effective_indent, ctx, context)?
                    i.inc()
                    continue
                } else {
                    // No catches - typer should have caught this if we're in non-throwing context
                    // Just use propagate (will silently succeed on error if we're not throwing)
                    emit_throwing_call_propagate(maybe_fcall, throw_types, maybe_decl_name, maybe_assign_name, output, effective_indent, ctx, context)?
                    i.inc()
                    continue
                }
            }
            catch (err: KeyNotFoundError) {
                // Not a function call, continue
            }
        }

        // Check for non-throwing variadic calls in declarations/assignments
        // These need special handling because variadic array must be constructed first
        if maybe_fcall_found {
            mut variadic_fcall_info := VariadicFCallInfo()
            variadic_fcall_info = detect_variadic_fcall(maybe_fcall, ctx)?
            catch (err: KeyNotFoundError) {
                // REM: Not variadic - variadic_fcall_info stays at default
            }
            if not(variadic_fcall_info.elem_type.is_empty()) {
                // Check that this is NOT a throwing function (those are handled above)
                variadic_fd := get_fcall_func_def(context, maybe_fcall)?
                mut is_throwing := not(variadic_fd.throw_types.is_empty())
                catch (err: KeyNotFoundError) {
                    is_throwing = false
                }
                if not(is_throwing) {
                    emit_variadic_call(maybe_fcall, variadic_fcall_info.elem_type, variadic_fcall_info.regular_count, maybe_decl_name, maybe_assign_name, output, effective_indent, ctx, context)?
                    i.inc()
                    continue
                }
            }
        }

        // Regular statement handling
        emit_expr(stmt, output, effective_indent, ctx, context)?
        i.inc()
    }

    // Clean up local_catch_labels for this block
    for catch_entry: CatchLabelInfoEntry in all_catch_info {
        ctx.local_catch_labels.remove(catch_entry.type_name)
    }

    catch (err: IndexOutOfBoundsError) {
        throw err.msg
    }
}

/// Get the function name from an FCall expression (returns underscore format like Type_method)
/// This returns the name WITHOUT til_ prefix.
/// For C output, use til_name() on the result.
get_fcall_func_name := func(expr: Expr) returns Str throws Str {
    // get_func_name_string already handles both nested identifiers (Type.method)
    // and precomp'd strings ("Type.method"), returning "Type_method" format
    first_param := expr.get(0)?
    dont_delete(first_param)
    return get_func_name_string(first_param)?
}

/// Pre-scan function body to collect all catch blocks
/// Returns all catch blocks found in the statements
prescan_func_level_catches := func(stmts: Vec) returns Vec {
    mut catches := Vec.new(Expr)

    // Collect ALL catch blocks - we'll register them all for local throw handling
    // The emit_throwing_call handles immediate catch chains for function calls,
    // but we need labels for throw statements that jump to these catches
    for stmt: Expr in stmts {
        switch stmt.node_type {
        case NodeType.Catch:
            catches.push(stmt)
        case:
        }
    }
    return catches
}

/// Emit a non-throwing variadic function call at statement level
/// Handles constructing the variadic array and cleaning it up after the call
emit_variadic_call := proc(fcall: Expr, elem_type: Str, regular_count: I64, decl_name: Str, assign_name: Str, mut output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) throws Str {
    indent_str := repeat("    ", indent)

    // Get function name
    func_name := get_fcall_func_name(fcall)?

    // Calculate param info for by-ref param handling
    func_def_opt := get_fcall_func_def(context, fcall)?
    catch (err: KeyNotFoundError) {
        // REM: No function definition found
    }

    // Bug #143: Use emit_arg_string for each arg
    // For variadic args (i >= regular_count), use by_ref=false
    mut arg_strings := Vec.new(Str)
    if fcall.params.len().gt(1) {
        mut i := 0
        mut arg_idx := 1
        while arg_idx.lt(fcall.params.len()) {
            arg := fcall.get(arg_idx)?
            dont_delete(arg)
            is_variadic_arg := i.gteq(regular_count)
            mut param_type := Ptr()
            mut by_ref := false
            if i.lt(func_def_opt.args.len()) {
                param_decl_ref := func_def_opt.args.get_by_ref(i)?
                create_alias(param_decl, Declaration, param_decl_ref.data)
                param_type.data = to_ptr(param_decl.value_type)
                if not(is_variadic_arg) {
                    by_ref = param_needs_by_ref(param_decl)
                }
            }
            arg_str := emit_arg_string(arg, param_type, by_ref, output, indent, ctx, context)?
            arg_strings.push(arg_str)
            i.inc()
            arg_idx.inc()
        }
    }

    // Construct variadic array using pre-computed arg_strings
    mut variadic_arg_strings := Vec.new(Str)
    mut vi := regular_count
    while vi.lt(arg_strings.len()) {
        vas_ref := arg_strings.get_by_ref(vi)?
        create_alias(vas, Str, vas_ref.data)
        variadic_arg_strings.push(vas)
        vi.inc()
    }
    variadic_arr_var := emit_variadic_array_with_strings(elem_type, variadic_arg_strings, output, indent, ctx)?

    // Determine return type if we need to declare a variable
    mut ret_type := "int"
    if not(decl_name.eq("")).or(not(assign_name.eq(""))) {
        fd2 := get_fcall_func_def(context, fcall)?
        if not(fd2.return_types.is_empty()) {
            first_ret_ref := fd2.return_types.get_by_ref(0)?
            create_alias(first_ret, ValueType, first_ret_ref.data)
            ret_type = til_type_to_c(first_ret)?
            catch (err: Str) {
                ret_type = "int"
            }
            catch (err: IndexOutOfBoundsError) {
                ret_type = "int"
            }
        }
        catch (err: KeyNotFoundError) { }
    }

    func_name_c := func_name.replace(".", "_")

    // For declarations: declare the variable (skip for "_" - Bug #35)
    if not(decl_name.eq("")) {
        if decl_name.eq("_") {
            // Bug #35: For underscore, just call the function (discard result)
            output.push_str(indent_str)
            // Emit call inline
            output.push_str(TIL_PREFIX)
            output.push_str(func_name_c)
            output.push_str("(")
            mut ci := 0
            while ci.lt(regular_count) {
                if ci.gt(0) {
                    output.push_str(", ")
                }
                arg_str_discard_ref := arg_strings.get_by_ref(ci)?
                create_alias(arg_str_discard, Str, arg_str_discard_ref.data)
                output.push_str(arg_str_discard)
                ci.inc()
            }
            if regular_count.gt(0) {
                output.push_str(", ")
            }
            output.push_str("&")
            output.push_str(variadic_arr_var)
            output.push_str(");\n")
        } else {
            // Bug #97: Register in scope FIRST so we can get the type-mangled name
            fd3 := get_fcall_func_def(context, fcall)?
            if not(fd3.return_types.is_empty()) {
                first_type_ref := fd3.return_types.get_by_ref(0)?
                create_alias(first_type, ValueType, first_type_ref.data)
                mut sym_info := SymbolInfo()
                sym_info.value_type = first_type
                sym_info.is_mut = true
                sym_info.is_copy = false
                sym_info.is_own = false
                sym_info.is_comptime_const = false
                context.scope_stack.declare_symbol(decl_name.clone(), sym_info)?
            }
            catch (err: KeyNotFoundError) { }
            variadic_c_var_name := til_var_name_from_context(decl_name, context)
            output.push_str(indent_str)
            output.push_str(ret_type)
            output.push_str(" ")
            output.push_str(variadic_c_var_name.clone())
            output.push_str(" = ")
            // Emit call inline
            output.push_str(TIL_PREFIX)
            output.push_str(func_name_c)
            output.push_str("(")
            mut di := 0
            while di.lt(regular_count) {
                if di.gt(0) {
                    output.push_str(", ")
                }
                arg_str_decl_ref := arg_strings.get_by_ref(di)?
                create_alias(arg_str_decl, Str, arg_str_decl_ref.data)
                output.push_str(arg_str_decl)
                di.inc()
            }
            if regular_count.gt(0) {
                output.push_str(", ")
            }
            output.push_str("&")
            output.push_str(variadic_arr_var)
            output.push_str(");\n")

            _ := ctx.declared_vars.insert(variadic_c_var_name)
        }
    } else if not(assign_name.eq("")) {
        // Assignment
        output.push_str(indent_str)
        // Bug #97: Use type-mangled names for variables
        // Check if assignment target is a field access on a mut param (self.field)
        // If so, emit with -> instead of .
        // Bug #168: resolve_var_name maps ret_var_alias to "_ret"
        dot_pos := assign_name.find(".")
        if dot_pos.gteq(0) {
            base := assign_name.get_substr(0, dot_pos)?
            rest := assign_name.get_substr(dot_pos.add(1), assign_name.len())?
            if ctx.current_ref_params.contains(base) {
                // Mut param field access: til_Type_self->field
                output.push_str(resolve_var_name(base, ctx, context))
                output.push_str("->")
                output.push_str(rest)
            } else {
                output.push_str(resolve_var_name(base, ctx, context))
                output.push_str(".")
                output.push_str(rest)
            }
        } else if ctx.current_ref_params.contains(assign_name) {
            // Direct assignment to mut param: *til_Type_self = value
            output.push_str("*")
            output.push_str(resolve_var_name(assign_name, ctx, context))
        } else {
            output.push_str(resolve_var_name(assign_name, ctx, context))
        }
        output.push_str(" = ")
        // Emit call inline
        output.push_str(TIL_PREFIX)
        output.push_str(func_name_c)
        output.push_str("(")
        mut ai := 0
        while ai.lt(regular_count) {
            if ai.gt(0) {
                output.push_str(", ")
            }
            arg_str_assign_ref := arg_strings.get_by_ref(ai)?
            create_alias(arg_str_assign, Str, arg_str_assign_ref.data)
            output.push_str(arg_str_assign)
            ai.inc()
        }
        if regular_count.gt(0) {
            output.push_str(", ")
        }
        output.push_str("&")
        output.push_str(variadic_arr_var)
        output.push_str(");\n")
    } else {
        // Standalone variadic call (no return value used)
        output.push_str(indent_str)
        // Emit call inline
        output.push_str(TIL_PREFIX)
        output.push_str(func_name_c)
        output.push_str("(")
        mut si := 0
        while si.lt(regular_count) {
            if si.gt(0) {
                output.push_str(", ")
            }
            arg_str_standalone_ref := arg_strings.get_by_ref(si)?
            create_alias(arg_str_standalone, Str, arg_str_standalone_ref.data)
            output.push_str(arg_str_standalone)
            si.inc()
        }
        if regular_count.gt(0) {
            output.push_str(", ")
        }
        output.push_str("&")
        output.push_str(variadic_arr_var)
        output.push_str(");\n")
    }

    catch (err: IndexOutOfBoundsError) { throw err.msg }

    // Clean up variadic array
    output.push_str(indent_str)
    output.push_str(TIL_PREFIX)
    output.push_str("Array_delete(&")
    output.push_str(variadic_arr_var)
    output.push_str(");\n")
}

/// Emit a call to a throwing function with catch handling
emit_throwing_call := proc(fcall: Expr, throw_types: Vec, catch_blocks: Vec, decl_name: Str, assign_name: Str, mut output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) throws Str {
    indent_str := repeat("    ", indent)

    // Get function name
    mut func_name := get_fcall_func_name(fcall)?

    // Check if this is a call to a nested (hoisted) function - use mangled name
    if ctx.nested_func_names.contains_key(func_name) {
        mut mangled_name := ""
        ctx.nested_func_names.get(func_name, mangled_name)?
        func_name = mangled_name
    }

    // Generate unique temp names for this call
    temp_suffix := next_mangled(ctx)

    // Determine if we need a return value temp variable
    // Only if function actually returns something AND we're capturing it
    fd := get_fcall_func_def(context, fcall)?
    mut func_has_return := not(fd.return_types.is_empty())
    mut needs_ret := func_has_return.and(not(decl_name.eq("")).or(not(assign_name.eq(""))))
    catch (err: KeyNotFoundError) {
        func_has_return = false
        needs_ret = false
    }

    // Check if this is a variadic function call
    mut variadic_regular_count := 9999999  // usize::MAX equivalent
    vi := detect_variadic_fcall(fcall, ctx)?
    variadic_regular_count = vi.regular_count
    catch (err: KeyNotFoundError) { }

    // Get function def for param info
    func_def_opt := get_fcall_func_def(context, fcall)?
    catch (err: KeyNotFoundError) {
        // REM: No function def
    }

    // Bug #143: Use emit_arg_string for each arg
    mut arg_strings := Vec.new(Str)
    if fcall.params.len().gt(1) {
        mut i := 0
        mut arg_idx := 1
        while arg_idx.lt(fcall.params.len()) {
            arg := fcall.get(arg_idx)?
            dont_delete(arg)
            is_variadic_arg := i.gteq(variadic_regular_count)
            mut param_type := Ptr()
            mut by_ref := false
            if i.lt(func_def_opt.args.len()) {
                param_decl_ref := func_def_opt.args.get_by_ref(i)?
                create_alias(param_decl, Declaration, param_decl_ref.data)
                param_type.data = to_ptr(param_decl.value_type)
                if not(is_variadic_arg) {
                    by_ref = param_needs_by_ref(param_decl)
                }
            }
            arg_str := emit_arg_string(arg, param_type, by_ref, output, indent, ctx, context)?
            arg_strings.push(arg_str)
            i.inc()
            arg_idx.inc()
        }
    }
    catch (err: IndexOutOfBoundsError) { throw err.msg }

    // Declare local variables for return value and errors
    mut ret_type := "int"
    if needs_ret {
        fd3 := get_fcall_func_def(context, fcall)?
        if not(fd3.return_types.is_empty()) {
            first_ret_ref := fd3.return_types.get_by_ref(0)?
            create_alias(first_ret, ValueType, first_ret_ref.data)
            ret_type = til_type_to_c(first_ret)?
            catch (err: Str) {
                ret_type = "int"
            }
        }
        catch (err: KeyNotFoundError) { }
        catch (err: IndexOutOfBoundsError) { }
    }

    // For declarations: declare the variable BEFORE the temp so dest_ptr can reference it
    // Skip for underscore _ which is just a discard
    if not(decl_name.eq("")).and(not(decl_name.eq("_"))) {
        // Bug #97: Add to scope_stack FIRST so we can get the type-mangled name
        fd4 := get_fcall_func_def(context, fcall)?
        if not(fd4.return_types.is_empty()) {
            first_type_ref := fd4.return_types.get_by_ref(0)?
            create_alias(first_type, ValueType, first_type_ref.data)
            mut sym_info := SymbolInfo()
            sym_info.value_type = first_type
            sym_info.is_mut = true
            sym_info.is_copy = false
            sym_info.is_own = false
            sym_info.is_comptime_const = false
            context.scope_stack.declare_symbol(decl_name.clone(), sym_info)?
        }
        catch (err: KeyNotFoundError) { }
        catch (err: IndexOutOfBoundsError) { }
        throw_c_var_name := til_var_name_from_context(decl_name, context)
        // Bug #168: Skip emitting declaration when var is ret_var_alias (already _ret pointer)
        is_ret_alias := not(ctx.ret_var_alias.is_empty()).and(decl_name.eq(ctx.ret_var_alias))
        if not(is_ret_alias) {
            output.push_str(indent_str)
            output.push_str(ret_type)
            output.push_str(" ")
            output.push_str(throw_c_var_name.clone())
            output.push_str(";\n")
        }
        _ := ctx.declared_vars.insert(throw_c_var_name)
    }

    // Bug #168: Try to write directly to destination instead of intermediate _ret_N
    mut dest_ptr := ""
    if needs_ret {
        dest_ptr = build_dest_ptr_expr(decl_name, assign_name, ctx, context)?
    }

    // Declare temp for return value if needed (Bug #168: skip when writing to dest directly)
    if needs_ret.and(dest_ptr.eq("")) {
        output.push_str(indent_str)
        output.push_str(ret_type)
        output.push_str(" _ret_")
        output.push_str(temp_suffix)
        output.push_str(";\n")
    }

    // Declare error structs for each throw type (Issue #119: skip empty struct errors)
    for idx in 0..throw_types.len() {
        throw_type_ref := throw_types.get_by_ref(idx)?
        create_alias(throw_type, ValueType, throw_type_ref.data)
        if is_empty_error_struct(context, throw_type) {
            continue
        }
        switch throw_type {
        case ValueType.TCustom(type_name):
            output.push_str(indent_str)
            output.push_str(til_name(type_name))
            output.push_str(" _err")
            output.push_str(I64.to_str(idx))
            output.push_str("_")
            output.push_str(temp_suffix)
            output.push_str(" = {};\n")
        case:
        }
    }

    // Construct variadic array if needed, using pre-computed arg_strings
    mut has_variadic := false
    mut variadic_arr_var := ""
    mut vi_regular_count := 0
    vi_info := detect_variadic_fcall(fcall, ctx)?
    vi_regular_count = vi_info.regular_count
    mut variadic_arg_strings := Vec.new(Str)
    mut vi_j := vi_regular_count
    while vi_j.lt(arg_strings.len()) {
        vi_as_ref := arg_strings.get_by_ref(vi_j)?
        create_alias(vi_as, Str, vi_as_ref.data)
        variadic_arg_strings.push(vi_as)
        vi_j.inc()
    }
    variadic_arr_var = emit_variadic_array_with_strings(vi_info.elem_type, variadic_arg_strings, output, indent, ctx)?
    has_variadic = true
    catch (err: KeyNotFoundError) {
    }
    catch (err: IndexOutOfBoundsError) { throw err.msg }

    // Generate the function call with output parameters
    output.push_str(indent_str)
    output.push_str("int __attribute__((unused)) _status_")
    output.push_str(temp_suffix)
    output.push_str(" = ")
    output.push_str(til_func_name(func_name))
    output.push_str("(")

    // First: return value pointer (if function returns something)
    // Bug #168: use destination pointer directly when available
    if needs_ret {
        if not(dest_ptr.eq("")) {
            output.push_str(dest_ptr)
        } else {
            output.push_str("&_ret_")
            output.push_str(temp_suffix)
        }
    }

    // Then: error pointers (Issue #119: skip empty struct errors)
    mut has_emitted_params := needs_ret
    mut err_idx := 0
    while err_idx.lt(throw_types.len()) {
        err_throw_type_ref := throw_types.get_by_ref(err_idx)?
        create_alias(err_throw_type, ValueType, err_throw_type_ref.data)
        if not(is_empty_error_struct(context, err_throw_type)) {
            if has_emitted_params {
                output.push_str(", ")
            }
            output.push_str("&_err")
            output.push_str(I64.to_str(err_idx))
            output.push_str("_")
            output.push_str(temp_suffix)
            has_emitted_params = true
        }
        err_idx.inc()
    }

    // Emit arguments using pre-computed arg_strings
    if has_variadic {
        // Variadic call: emit regular args first, then variadic array pointer
        mut vi_arg_idx := 0
        while vi_arg_idx.lt(vi_regular_count) {
            if has_emitted_params.or(vi_arg_idx.gt(0)) {
                output.push_str(", ")
            }
            vi_arg_str_ref := arg_strings.get_by_ref(vi_arg_idx)?
            create_alias(vi_arg_str, Str, vi_arg_str_ref.data)
            output.push_str(vi_arg_str)
            vi_arg_idx.inc()
        }
        // Emit variadic array pointer
        if needs_ret.or(not(throw_types.is_empty())).or(vi_regular_count.gt(0)) {
            output.push_str(", ")
        }
        output.push_str("&")
        output.push_str(variadic_arr_var)
    } else {
        // Non-variadic: emit all args directly
        mut nv_arg_i := 0
        while nv_arg_i.lt(arg_strings.len()) {
            if has_emitted_params.or(nv_arg_i.gt(0)) {
                output.push_str(", ")
            }
            nv_arg_str_ref := arg_strings.get_by_ref(nv_arg_i)?
            create_alias(nv_arg_str, Str, nv_arg_str_ref.data)
            output.push_str(nv_arg_str)
            nv_arg_i.inc()
        }
    }
    catch (err: IndexOutOfBoundsError) { throw err.msg }

    output.push_str(");\n")

    // Generate if/else chain for error handling
    output.push_str(indent_str)
    output.push_str("if (_status_")
    output.push_str(temp_suffix)
    output.push_str(" == 0) {\n")

    // Success case: assign return value to target variable
    // Bug #168: skip when dest_ptr was used (function wrote to destination directly)
    if dest_ptr.eq("") {
        // Skip for underscore _ which is just a discard
        // Bug #97: Use type-mangled names for variables
        if not(decl_name.eq("")).and(not(decl_name.eq("_"))) {
            decl_inner_indent := repeat("    ", indent.add(1))
            output.push_str(decl_inner_indent)
            output.push_str(til_var_name_from_context(decl_name, context))
            output.push_str(" = _ret_")
            output.push_str(temp_suffix)
            output.push_str(";\n")
        } else if not(assign_name.eq("")) {
            // Assignment: assign to existing variable
            assign_inner_indent := repeat("    ", indent.add(1))
            output.push_str(assign_inner_indent)
            // Check if assignment target is a field access on a mut param (self.field)
            // Bug #168: resolve_var_name maps ret_var_alias to "_ret"
            dot_pos := assign_name.find(".")
            if dot_pos.gteq(0) {
                base := assign_name.get_substr(0, dot_pos)?
                rest := assign_name.get_substr(dot_pos.add(1), assign_name.len())?
                if ctx.current_ref_params.contains(base) {
                    // Mut param field access: til_Type_self->field
                    output.push_str(resolve_var_name(base, ctx, context))
                    output.push_str("->")
                    output.push_str(rest)
                } else {
                    output.push_str(resolve_var_name(base, ctx, context))
                    output.push_str(".")
                    output.push_str(rest)
                }
            } else if ctx.current_ref_params.contains(assign_name) {
                // Direct assignment to mut param: *til_Type_var = value
                output.push_str("*")
                output.push_str(resolve_var_name(assign_name, ctx, context))
            } else {
                output.push_str(resolve_var_name(assign_name, ctx, context))
            }
            output.push_str(" = _ret_")
            output.push_str(temp_suffix)
            output.push_str(";\n")
        }
    }

    output.push_str(indent_str)
    output.push_str("}")

    // Generate else-if branches for each catch block
    for catch_block: Expr in catch_blocks {
        if catch_block.params.len().gteq(3) {
            // Get error type name from catch block
            err_type_param := catch_block.get(1)?
            dont_delete(err_type_param)
            switch err_type_param.node_type {
            case NodeType.Identifier(err_type_name):
                // Find index of this error type
                mut found_idx := 0
                mut found := false
                for catch_err_idx in 0..throw_types.len() {
                    mut vt := ValueType.TCustom("")
                    throw_types.get(catch_err_idx, vt)?
                    switch vt {
                    case ValueType.TCustom(name):
                        if name.eq(err_type_name) {
                            found = true
                            found_idx = catch_err_idx
                            break
                        }
                    case:
                    }
                }

                if found {
                    output.push_str(" else if (_status_")
                    output.push_str(temp_suffix)
                    output.push_str(" == ")
                    output.push_str(I64.to_str(found_idx.add(1)))
                    output.push_str(") {\n")

                    // Bind error variable and add to scope for type resolution
                    err_var_param := catch_block.get(0)?
                    dont_delete(err_var_param)
                    switch err_var_param.node_type {
                    case NodeType.Identifier(err_var_name):
                        inner_indent := repeat("    ", indent.add(1))
                        output.push_str(inner_indent)
                        output.push_str(til_name(err_type_name))
                        output.push_str(" ")
                        // Bug #97: Use type-mangled name for catch error variable
                        output.push_str(til_var_name(err_var_name, err_type_name))
                        // Issue #119: For empty struct errors, use empty initializer since _err var doesn't exist
                        catch_throw_type := ValueType.TCustom(err_type_name.clone())
                        if is_empty_error_struct(context, catch_throw_type) {
                            output.push_str(" = {};\n")
                        } else {
                            output.push_str(" = _err")
                            output.push_str(I64.to_str(found_idx))
                            output.push_str("_")
                            output.push_str(temp_suffix)
                            output.push_str(";\n")
                        }

                        // Add error variable to scope for type resolution in catch body
                        mut catch_sym_info := SymbolInfo()
                        catch_sym_info.value_type = ValueType.TCustom(err_type_name.clone())
                        catch_sym_info.is_mut = false
                        catch_sym_info.is_copy = false
                        catch_sym_info.is_own = false
                        catch_sym_info.is_comptime_const = false
                        context.scope_stack.declare_symbol(err_var_name.clone(), catch_sym_info)?
                    case:
                    }

                    // Emit catch body
                    catch_body := catch_block.get(2)?
                    dont_delete(catch_body)
                    emit_expr(catch_body, output, indent.add(1), ctx, context)?

                    output.push_str(indent_str)
                    output.push_str("}")
                }
            case:
            }
        }
    }

    output.push_str("\n")

    catch (err: IndexOutOfBoundsError) {
        throw err.msg
    }
}

// Bug #168: Compute destination pointer expression for throwing calls.
// Returns the address expression to pass directly as _ret instead of using _ret_N temp.
// Returns "" when result is discarded (need temp as sink).
build_dest_ptr_expr := func(decl_name: Str, assign_name: Str, ctx: CodegenContext, context: Context) returns Str throws Str {
    if not(decl_name.eq("")) {
        if decl_name.eq("_") {
            return ""
        }
        // Bug #168: ret_var_alias is already mapped to _ret pointer
        if not(ctx.ret_var_alias.is_empty()) {
            if decl_name.eq(ctx.ret_var_alias) {
                return "_ret"
            }
        }
        decl_c_name := til_var_name_from_context(decl_name, context)
        return format("&", decl_c_name)
    }
    if not(assign_name.eq("")) {
        if assign_name.starts_with("*") {
            // *_ret -> _ret (already a pointer)
            return assign_name.get_substr(1, assign_name.len())?
        }
        dot_pos := assign_name.find(".")
        if dot_pos.gteq(0) {
            base := assign_name.get_substr(0, dot_pos)?
            rest := assign_name.get_substr(dot_pos.add(1), assign_name.len())?
            c_base := resolve_var_name(base, ctx, context)
            if ctx.current_ref_params.contains(base) {
                return format("&(", c_base, "->", rest, ")")
            } else {
                return format("&(", c_base, ".", rest, ")")
            }
        }
        if ctx.current_ref_params.contains(assign_name) {
            // Ref param: already a pointer
            return resolve_var_name(assign_name, ctx, context)
        }
        assign_c_name := til_var_name_from_context(assign_name, context)
        return format("&", assign_c_name)
    }
    return ""

    catch (err: IndexOutOfBoundsError) { throw err.msg }
}

/// Emit a throwing function call that propagates errors to the caller (no catch blocks)
/// This is used when a throwing function calls another throwing function without catching
emit_throwing_call_propagate := proc(fcall: Expr, throw_types: Vec, decl_name: Str, assign_name: Str, mut output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) throws Str {
    indent_str := repeat("    ", indent)

    // Get function name
    mut func_name := get_fcall_func_name(fcall)?

    // Check if this is a call to a nested (hoisted) function - use mangled name
    if ctx.nested_func_names.contains_key(func_name) {
        mut mangled_name := ""
        ctx.nested_func_names.get(func_name, mangled_name)?
        func_name = mangled_name
    }

    // Generate unique temp names for this call
    temp_suffix := next_mangled(ctx)

    // Determine if we need a return value temp variable
    // Only if function actually returns something AND we're capturing it
    func_def_opt := get_fcall_func_def(context, fcall)?
    mut have_func_def := true
    mut func_has_return := not(func_def_opt.return_types.is_empty())
    mut needs_ret := func_has_return.and(not(decl_name.eq("")).or(not(assign_name.eq(""))))
    catch (err: KeyNotFoundError) {
        have_func_def = false
        func_has_return = false
        needs_ret = false
    }

    // Check if this is a variadic function call
    mut prop_variadic_regular_count := 9999999  // usize::MAX equivalent
    prop_vi := detect_variadic_fcall(fcall, ctx)?
    prop_variadic_regular_count = prop_vi.regular_count
    catch (err: KeyNotFoundError) { }

    // Bug #143: Use emit_arg_string for each arg
    mut arg_strings := Vec.new(Str)
    if fcall.params.len().gt(1) {
        mut i := 0
        mut arg_idx := 1
        while arg_idx.lt(fcall.params.len()) {
            arg := fcall.get(arg_idx)?
            dont_delete(arg)
            is_variadic_arg := i.gteq(prop_variadic_regular_count)
            mut param_type := Ptr()
            mut by_ref := false
            if have_func_def.and(i.lt(func_def_opt.args.len())) {
                param_decl_ref := func_def_opt.args.get_by_ref(i)?
                create_alias(param_decl, Declaration, param_decl_ref.data)
                param_type.data = to_ptr(param_decl.value_type)
                if not(is_variadic_arg) {
                    by_ref = param_needs_by_ref(param_decl)
                }
            }
            arg_str := emit_arg_string(arg, param_type, by_ref, output, indent, ctx, context)?
            arg_strings.push(arg_str)
            i.inc()
            arg_idx.inc()
        }
    }
    catch (err: IndexOutOfBoundsError) { throw err.msg }

    // Look up the actual return type
    mut ret_type := "int"
    if needs_ret {
        fd3 := get_fcall_func_def(context, fcall)?
        if not(fd3.return_types.is_empty()) {
            first_ret_ref := fd3.return_types.get_by_ref(0)?
            create_alias(first_ret, ValueType, first_ret_ref.data)
            ret_type = til_type_to_c(first_ret)?
            catch (err: Str) { ret_type = "int" }
        }
        catch (err: KeyNotFoundError) { }
        catch (err: IndexOutOfBoundsError) { }
    }

    // For declarations: declare the variable BEFORE the temp so dest_ptr can reference it
    if not(decl_name.eq("")).and(not(decl_name.eq("_"))) {
        // Bug #97: Add to scope_stack FIRST so we can get the type-mangled name
        fd4 := get_fcall_func_def(context, fcall)?
        if not(fd4.return_types.is_empty()) {
            first_type_ref := fd4.return_types.get_by_ref(0)?
            create_alias(first_type, ValueType, first_type_ref.data)
            mut sym_info := SymbolInfo()
            sym_info.value_type = first_type
            sym_info.is_mut = true
            sym_info.is_copy = false
            sym_info.is_own = false
            sym_info.is_comptime_const = false
            context.scope_stack.declare_symbol(decl_name.clone(), sym_info)?
        }
        catch (err: KeyNotFoundError) { }
        catch (err: IndexOutOfBoundsError) { }
        throwing_decl_c_var := til_var_name_from_context(decl_name, context)
        // Bug #168: Skip emitting declaration when var is ret_var_alias (already _ret pointer)
        is_ret_alias := not(ctx.ret_var_alias.is_empty()).and(decl_name.eq(ctx.ret_var_alias))
        if not(is_ret_alias) {
            output.push_str(indent_str)
            output.push_str(ret_type)
            output.push_str(" ")
            output.push_str(throwing_decl_c_var.clone())
            output.push_str(";\n")
        }
        _ := ctx.declared_vars.insert(throwing_decl_c_var)
    }

    // Bug #168: Try to write directly to destination instead of intermediate _ret_N
    mut dest_ptr := ""
    if needs_ret {
        dest_ptr = build_dest_ptr_expr(decl_name, assign_name, ctx, context)?
    }

    // Declare temp for return value if needed (Bug #168: skip when writing to dest directly)
    if needs_ret.and(dest_ptr.eq("")) {
        output.push_str(indent_str)
        output.push_str(ret_type)
        output.push_str(" _ret_")
        output.push_str(temp_suffix)
        output.push_str(";\n")
    }

    // Declare error structs for each throw type of the called function (Issue #119: skip empty struct errors)
    for idx in 0..throw_types.len() {
        throw_type_ref := throw_types.get_by_ref(idx)?
        create_alias(throw_type, ValueType, throw_type_ref.data)
        if is_empty_error_struct(context, throw_type) {
            continue
        }
        switch throw_type {
        case ValueType.TCustom(type_name):
            output.push_str(indent_str)
            output.push_str(til_name(type_name))
            output.push_str(" _err")
            output.push_str(I64.to_str(idx))
            output.push_str("_")
            output.push_str(temp_suffix)
            output.push_str(" = {};\n")
        case:
        }
    }

    // Construct variadic array if needed, using pre-computed arg_strings
    mut variadic_arr_var := ""
    mut has_variadic := false
    mut prop_vi_regular_count := 0
    prop_vi2 := detect_variadic_fcall(fcall, ctx)?
    has_variadic = true
    prop_vi_regular_count = prop_vi2.regular_count
    mut variadic_arg_strings := Vec.new(Str)
    mut va_j := prop_vi_regular_count
    while va_j.lt(arg_strings.len()) {
        va_s_ref := arg_strings.get_by_ref(va_j)?
        create_alias(va_s, Str, va_s_ref.data)
        variadic_arg_strings.push(va_s)
        va_j.inc()
    }
    variadic_arr_var = emit_variadic_array_with_strings(prop_vi2.elem_type, variadic_arg_strings, output, indent, ctx)?
    catch (err: KeyNotFoundError) {
        // REM: Not variadic
    }
    catch (err: IndexOutOfBoundsError) { throw err.msg }

    // Generate the function call with output parameters
    output.push_str(indent_str)
    output.push_str("int __attribute__((unused)) _status_")
    output.push_str(temp_suffix)
    output.push_str(" = ")
    output.push_str(til_func_name(func_name))
    output.push_str("(")

    // First: return value pointer
    // Bug #168: use destination pointer directly when available
    if needs_ret {
        if not(dest_ptr.eq("")) {
            output.push_str(dest_ptr)
        } else {
            output.push_str("&_ret_")
            output.push_str(temp_suffix)
        }
    }

    // Then: error pointers (Issue #119: skip empty struct errors)
    mut has_emitted_params := needs_ret
    mut err_idx := 0
    while err_idx.lt(throw_types.len()) {
        err_throw_type_ref := throw_types.get_by_ref(err_idx)?
        create_alias(err_throw_type, ValueType, err_throw_type_ref.data)
        if not(is_empty_error_struct(context, err_throw_type)) {
            if has_emitted_params {
                output.push_str(", ")
            }
            output.push_str("&_err")
            output.push_str(I64.to_str(err_idx))
            output.push_str("_")
            output.push_str(temp_suffix)
            has_emitted_params = true
        }
        err_idx.inc()
    }

    // Emit arguments using pre-computed arg_strings
    if has_variadic {
        mut var_arg_i := 0
        while var_arg_i.lt(prop_vi_regular_count) {
            if has_emitted_params.or(var_arg_i.gt(0)) {
                output.push_str(", ")
            }
            var_arg_str_ref := arg_strings.get_by_ref(var_arg_i)?
            create_alias(var_arg_str, Str, var_arg_str_ref.data)
            output.push_str(var_arg_str)
            var_arg_i.inc()
        }
        if has_emitted_params.or(prop_vi_regular_count.gt(0)) {
            output.push_str(", ")
        }
        output.push_str("&")
        output.push_str(variadic_arr_var)
    } else {
        // Non-variadic: emit all args directly
        mut nv_arg_i := 0
        while nv_arg_i.lt(arg_strings.len()) {
            if has_emitted_params.or(nv_arg_i.gt(0)) {
                output.push_str(", ")
            }
            nv_arg_str_ref := arg_strings.get_by_ref(nv_arg_i)?
            create_alias(nv_arg_str, Str, nv_arg_str_ref.data)
            output.push_str(nv_arg_str)
            nv_arg_i.inc()
        }
    }
    catch (err: IndexOutOfBoundsError) { throw err.msg }

    output.push_str(");\n")

    // Generate error propagation: if status != 0, copy error to caller's error param and return
    for called_idx in 0..throw_types.len() {
        called_throw_type_ref := throw_types.get_by_ref(called_idx)?
        create_alias(called_throw_type, ValueType, called_throw_type_ref.data)
        switch called_throw_type {
        case ValueType.TCustom(called_type_name):
            // Find matching error type in current function's throw types
            mut current_idx := 0
            mut found := false
            for cur_throw_idx in 0..ctx.current_throw_types.len() {
                mut cur_vt := ValueType.TCustom("")
                ctx.current_throw_types.get(cur_throw_idx, cur_vt)?
                switch cur_vt {
                case ValueType.TCustom(cur_name):
                    if cur_name.eq(called_type_name) {
                        found = true
                        current_idx = cur_throw_idx
                        break
                    }
                case:
                }
            }
            if found {
                // Issue #119: For empty struct errors, don't copy - just return status
                is_empty := is_empty_error_struct(context, called_throw_type)
                output.push_str(indent_str)
                output.push_str("if (_status_")
                output.push_str(temp_suffix)
                output.push_str(" == ")
                output.push_str(I64.to_str(called_idx.add(1)))
                output.push_str(") { ")
                if not(is_empty) {
                    output.push_str("*_err")
                    output.push_str(I64.to_str(current_idx.add(1)))
                    output.push_str(" = _err")
                    output.push_str(I64.to_str(called_idx))
                    output.push_str("_")
                    output.push_str(temp_suffix)
                    output.push_str("; ")
                }
                output.push_str("return ")
                output.push_str(I64.to_str(current_idx.add(1)))
                output.push_str("; }\n")
            }
        case:
        }
    }

    // Success case: assign return value to target variable if needed
    // Bug #168: skip when dest_ptr was used (function wrote to destination directly)
    if dest_ptr.eq("") {
        // Bug #97: Use type-mangled names for variables
        if not(decl_name.eq("")).and(not(decl_name.eq("_"))) {
            output.push_str(indent_str)
            output.push_str(til_var_name_from_context(decl_name, context))
            output.push_str(" = _ret_")
            output.push_str(temp_suffix)
            output.push_str(";\n")
        } else if not(assign_name.eq("")) {
            output.push_str(indent_str)
            // Bug #168: resolve_var_name maps ret_var_alias to "_ret"
            dot_pos := assign_name.find(".")
            if dot_pos.gteq(0) {
                base := assign_name.get_substr(0, dot_pos)?
                rest := assign_name.get_substr(dot_pos.add(1), assign_name.len())?
                if ctx.current_ref_params.contains(base) {
                    output.push_str(resolve_var_name(base, ctx, context))
                    output.push_str("->")
                    output.push_str(rest)
                } else {
                    output.push_str(resolve_var_name(base, ctx, context))
                    output.push_str(".")
                    output.push_str(rest)
                }
            } else if ctx.current_ref_params.contains(assign_name) {
                output.push_str("*")
                output.push_str(resolve_var_name(assign_name, ctx, context))
            } else {
                output.push_str(resolve_var_name(assign_name, ctx, context))
            }
            output.push_str(" = _ret_")
            output.push_str(temp_suffix)
            output.push_str(";\n")
        }
    }

    catch (err: IndexOutOfBoundsError) {
        throw err.msg
    }
}

/// Emit a throwing function call that uses goto for error handling
/// Used when there are outer catch blocks registered in ctx.local_catch_labels
emit_throwing_call_with_goto := proc(fcall: Expr, throw_types: Vec, decl_name: Str, assign_name: Str, mut output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) throws Str {
    indent_str := repeat("    ", indent)

    // Get function name
    mut func_name := get_fcall_func_name(fcall)?

    // Check if this is a call to a nested (hoisted) function - use mangled name
    if ctx.nested_func_names.contains_key(func_name) {
        mut mangled_name := ""
        ctx.nested_func_names.get(func_name, mangled_name)?
        func_name = mangled_name
    }

    // Generate unique temp names for this call
    temp_suffix := next_mangled(ctx)

    // Determine if we need a return value temp variable
    // Only if function actually returns something AND we're capturing it
    func_def_opt := get_fcall_func_def(context, fcall)?
    mut have_func_def := true
    mut func_has_return := not(func_def_opt.return_types.is_empty())
    mut needs_ret := func_has_return.and(not(decl_name.eq("")).or(not(assign_name.eq(""))))
    catch (err: KeyNotFoundError) {
        have_func_def = false
        func_has_return = false
        needs_ret = false
    }

    // Check if this is a variadic function call
    mut goto_variadic_regular_count := 9999999
    goto_vi := detect_variadic_fcall(fcall, ctx)?
    goto_variadic_regular_count = goto_vi.regular_count
    catch (err: KeyNotFoundError) { }

    // Bug #143: Use emit_arg_string for each arg
    mut arg_strings := Vec.new(Str)
    if fcall.params.len().gt(1) {
        mut i := 0
        mut arg_idx := 1
        while arg_idx.lt(fcall.params.len()) {
            arg := fcall.get(arg_idx)?
            dont_delete(arg)
            is_variadic_arg := i.gteq(goto_variadic_regular_count)
            mut param_type := Ptr()
            mut by_ref := false
            if have_func_def.and(i.lt(func_def_opt.args.len())) {
                param_decl_ref := func_def_opt.args.get_by_ref(i)?
                create_alias(param_decl, Declaration, param_decl_ref.data)
                param_type.data = to_ptr(param_decl.value_type)
                if not(is_variadic_arg) {
                    by_ref = param_needs_by_ref(param_decl)
                }
            }
            arg_str := emit_arg_string(arg, param_type, by_ref, output, indent, ctx, context)?
            arg_strings.push(arg_str)
            i.inc()
            arg_idx.inc()
        }
    }
    catch (err: IndexOutOfBoundsError) { throw err.msg }

    // Look up the actual return type
    mut ret_type := "int"
    if needs_ret {
        fd3 := get_fcall_func_def(context, fcall)?
        if not(fd3.return_types.is_empty()) {
            first_ret_ref := fd3.return_types.get_by_ref(0)?
            create_alias(first_ret, ValueType, first_ret_ref.data)
            ret_type = til_type_to_c(first_ret)?
            catch (err: Str) { ret_type = "int" }
        }
        catch (err: KeyNotFoundError) { }
        catch (err: IndexOutOfBoundsError) { }
    }

    // For declarations: declare the variable BEFORE the temp so dest_ptr can reference it
    if not(decl_name.eq("")).and(not(decl_name.eq("_"))) {
        // Bug #97: Add to scope_stack FIRST so we can get the type-mangled name
        fd4 := get_fcall_func_def(context, fcall)?
        if not(fd4.return_types.is_empty()) {
            first_type_ref := fd4.return_types.get_by_ref(0)?
            create_alias(first_type, ValueType, first_type_ref.data)
            mut sym_info := SymbolInfo()
            sym_info.value_type = first_type
            sym_info.is_mut = true
            sym_info.is_copy = false
            sym_info.is_own = false
            sym_info.is_comptime_const = false
            context.scope_stack.declare_symbol(decl_name.clone(), sym_info)?
        }
        catch (err: KeyNotFoundError) { }
        catch (err: IndexOutOfBoundsError) { }
        throwing_decl_c_var := til_var_name_from_context(decl_name, context)
        // Bug #168: Skip emitting declaration when var is ret_var_alias (already _ret pointer)
        is_ret_alias := not(ctx.ret_var_alias.is_empty()).and(decl_name.eq(ctx.ret_var_alias))
        if not(is_ret_alias) {
            output.push_str(indent_str)
            output.push_str(ret_type)
            output.push_str(" ")
            output.push_str(throwing_decl_c_var.clone())
            output.push_str(";\n")
        }
        _ := ctx.declared_vars.insert(throwing_decl_c_var)
    }

    // Bug #168: Try to write directly to destination instead of intermediate _ret_N
    mut dest_ptr := ""
    if needs_ret {
        dest_ptr = build_dest_ptr_expr(decl_name, assign_name, ctx, context)?
    }

    // Declare temp for return value if needed (Bug #168: skip when writing to dest directly)
    if needs_ret.and(dest_ptr.eq("")) {
        output.push_str(indent_str)
        output.push_str(ret_type)
        output.push_str(" _ret_")
        output.push_str(temp_suffix)
        output.push_str(";\n")
    }

    // Declare error structs for each throw type of the called function (Issue #119: skip empty struct errors)
    for idx in 0..throw_types.len() {
        throw_type_ref := throw_types.get_by_ref(idx)?
        create_alias(throw_type, ValueType, throw_type_ref.data)
        if is_empty_error_struct(context, throw_type) {
            continue
        }
        switch throw_type {
        case ValueType.TCustom(type_name):
            output.push_str(indent_str)
            output.push_str(til_name(type_name))
            output.push_str(" _err")
            output.push_str(I64.to_str(idx))
            output.push_str("_")
            output.push_str(temp_suffix)
            output.push_str(" = {};\n")
        case:
        }
    }

    // Construct variadic array if needed, using pre-computed arg_strings
    mut variadic_arr_var := ""
    mut has_variadic := false
    mut goto_vi_regular_count := 0
    goto_vi2 := detect_variadic_fcall(fcall, ctx)?
    has_variadic = true
    goto_vi_regular_count = goto_vi2.regular_count
    mut variadic_arg_strings := Vec.new(Str)
    mut va_j := goto_vi_regular_count
    while va_j.lt(arg_strings.len()) {
        va_s_ref := arg_strings.get_by_ref(va_j)?
        create_alias(va_s, Str, va_s_ref.data)
        variadic_arg_strings.push(va_s)
        va_j.inc()
    }
    variadic_arr_var = emit_variadic_array_with_strings(goto_vi2.elem_type, variadic_arg_strings, output, indent, ctx)?
    catch (err: KeyNotFoundError) {
        // REM: Not variadic
    }
    catch (err: IndexOutOfBoundsError) { throw err.msg }

    // Generate the function call with output parameters
    output.push_str(indent_str)
    output.push_str("int __attribute__((unused)) _status_")
    output.push_str(temp_suffix)
    output.push_str(" = ")
    output.push_str(til_func_name(func_name))
    output.push_str("(")

    // First: return value pointer
    // Bug #168: use destination pointer directly when available
    if needs_ret {
        if not(dest_ptr.eq("")) {
            output.push_str(dest_ptr)
        } else {
            output.push_str("&_ret_")
            output.push_str(temp_suffix)
        }
    }

    // Then: error pointers (Issue #119: skip empty struct errors)
    mut has_emitted_params := needs_ret
    mut err_idx := 0
    while err_idx.lt(throw_types.len()) {
        err_throw_type_ref := throw_types.get_by_ref(err_idx)?
        create_alias(err_throw_type, ValueType, err_throw_type_ref.data)
        if not(is_empty_error_struct(context, err_throw_type)) {
            if has_emitted_params {
                output.push_str(", ")
            }
            output.push_str("&_err")
            output.push_str(I64.to_str(err_idx))
            output.push_str("_")
            output.push_str(temp_suffix)
            has_emitted_params = true
        }
        err_idx.inc()
    }

    // Emit arguments using pre-computed arg_strings
    if has_variadic {
        mut var_arg_i := 0
        while var_arg_i.lt(goto_vi_regular_count) {
            if has_emitted_params.or(var_arg_i.gt(0)) {
                output.push_str(", ")
            }
            var_arg_str_ref := arg_strings.get_by_ref(var_arg_i)?
            create_alias(var_arg_str, Str, var_arg_str_ref.data)
            output.push_str(var_arg_str)
            var_arg_i.inc()
        }
        if has_emitted_params.or(goto_vi_regular_count.gt(0)) {
            output.push_str(", ")
        }
        output.push_str("&")
        output.push_str(variadic_arr_var)
    } else {
        // Non-variadic: emit all args directly
        mut nv_arg_i := 0
        while nv_arg_i.lt(arg_strings.len()) {
            if has_emitted_params.or(nv_arg_i.gt(0)) {
                output.push_str(", ")
            }
            nv_arg_str_ref := arg_strings.get_by_ref(nv_arg_i)?
            create_alias(nv_arg_str, Str, nv_arg_str_ref.data)
            output.push_str(nv_arg_str)
            nv_arg_i.inc()
        }
    }
    catch (err: IndexOutOfBoundsError) { throw err.msg }

    output.push_str(");\n")

    // Bug #39 fix: Use simple status checks with goto, not if-block nesting
    for goto_err_idx in 0..throw_types.len() {
        goto_throw_type_ref := throw_types.get_by_ref(goto_err_idx)?
        create_alias(goto_throw_type, ValueType, goto_throw_type_ref.data)
        switch goto_throw_type {
        case ValueType.TCustom(type_name):
            // Check if there's a local catch for this error type
            mut catch_info := CatchLabelInfo()
            ctx.local_catch_labels.get(type_name, catch_info)?
            // Jump to local catch: copy error to temp var, then goto label
            output.push_str(indent_str)
            output.push_str("if (_status_")
            output.push_str(temp_suffix)
            output.push_str(" == ")
            output.push_str(I64.to_str(goto_err_idx.add(1)))
            output.push_str(") { ")
            // Issue #119: Skip copy for empty struct errors
            if not(is_empty_error_struct(context, goto_throw_type)) {
                output.push_str(catch_info.temp_var)
                output.push_str(" = _err")
                output.push_str(I64.to_str(goto_err_idx))
                output.push_str("_")
                output.push_str(temp_suffix)
                output.push_str("; ")
            }
            output.push_str("goto ")
            output.push_str(catch_info.label)
            output.push_str("; }\n")
            catch (err: KeyNotFoundError) {
                // Check if this error type is in current_throw_types (propagate to caller)
                mut prop_idx := 0
                mut found_prop := false
                for prop_i in 0..ctx.current_throw_types.len() {
                    mut prop_vt := ValueType.TCustom("")
                    ctx.current_throw_types.get(prop_i, prop_vt)?
                    switch prop_vt {
                    case ValueType.TCustom(n):
                        if n.eq(type_name) {
                            found_prop = true
                            prop_idx = prop_i
                            break
                        }
                    case:
                    }
                }
                if found_prop {
                    output.push_str(indent_str)
                    output.push_str("if (_status_")
                    output.push_str(temp_suffix)
                    output.push_str(" == ")
                    output.push_str(I64.to_str(goto_err_idx.add(1)))
                    output.push_str(") { ")
                    // Issue #119: Skip copy for empty struct errors
                    if not(is_empty_error_struct(context, goto_throw_type)) {
                        output.push_str("*_err")
                        output.push_str(I64.to_str(prop_idx.add(1)))
                        output.push_str(" = _err")
                        output.push_str(I64.to_str(goto_err_idx))
                        output.push_str("_")
                        output.push_str(temp_suffix)
                        output.push_str("; ")
                    }
                    output.push_str("return ")
                    output.push_str(I64.to_str(prop_idx.add(1)))
                    output.push_str("; }\n")
                }
            }
        case:
        }
    }

    // Success case: assign return value to target variable if needed
    // Bug #168: skip when dest_ptr was used (function wrote to destination directly)
    if dest_ptr.eq("") {
        // Bug #97: Use type-mangled names for variables
        if not(decl_name.eq("")).and(not(decl_name.eq("_"))) {
            output.push_str(indent_str)
            output.push_str(til_var_name_from_context(decl_name, context))
            output.push_str(" = _ret_")
            output.push_str(temp_suffix)
            output.push_str(";\n")
        } else if not(assign_name.eq("")) {
            output.push_str(indent_str)
            // Bug #168: resolve_var_name maps ret_var_alias to "_ret"
            dot_pos := assign_name.find(".")
            if dot_pos.gteq(0) {
                base := assign_name.get_substr(0, dot_pos)?
                rest := assign_name.get_substr(dot_pos.add(1), assign_name.len())?
                if ctx.current_ref_params.contains(base) {
                    output.push_str(resolve_var_name(base, ctx, context))
                    output.push_str("->")
                    output.push_str(rest)
                } else {
                    output.push_str(resolve_var_name(base, ctx, context))
                    output.push_str(".")
                    output.push_str(rest)
                }
            } else if ctx.current_ref_params.contains(assign_name) {
                output.push_str("*")
                output.push_str(resolve_var_name(assign_name, ctx, context))
            } else {
                output.push_str(resolve_var_name(assign_name, ctx, context))
            }
            output.push_str(" = _ret_")
            output.push_str(temp_suffix)
            output.push_str(";\n")
        }
    }

    catch (err: IndexOutOfBoundsError) {
        throw err.msg
    }
}

emit_declaration := proc(decl: Declaration, expr: Expr, mut output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) throws Str {
    // Skip inline ext_func/ext_proc declarations - they're just declaring external functions exist
    if not(expr.params.is_empty()) {
        first_param := expr.get(0)?
        dont_delete(first_param)
        switch first_param.node_type {
        case NodeType.FuncDef(func_def):
            if func_def.is_ext() {
                return
            }

            // Handle nested function declarations - hoist to top level with mangled name
            if not(ctx.current_function_name.is_empty()) {
                parent_name := ctx.current_function_name.clone()
                mut mangled_name := parent_name.clone()
                mangled_name.push_str("_")
                mangled_name.push_str(decl.name)

                // Register the name mapping so function calls can find it
                ctx.nested_func_names.set(decl.name.clone(), mangled_name.clone())

                // Register function definition so get_value_type can resolve return types
                context.scope_stack.declare_func(decl.name.clone(), func_def.clone())?

                // Generate the hoisted function prototype
                mut proto_output := ""
                emit_func_signature(til_name(mangled_name), func_def, context, proto_output)?
                proto_output.push_str(";\n")
                ctx.hoisted_prototypes.push(proto_output)

                // Generate the hoisted function definition
                mut func_output := ""

                // Save and set context for nested function
                prev_throw_types := ctx.current_throw_types.clone()
                prev_return_types := ctx.current_return_types.clone()
                prev_declared_vars := ctx.declared_vars.clone()
                prev_mut_params := ctx.current_ref_params.clone()
                prev_variadic_params := ctx.current_variadic_params.clone()
                prev_function_name := ctx.current_function_name.clone()
                prev_mangling_counter := ctx.mangling_counter
                prev_ret_var_alias := ctx.ret_var_alias.clone()

                ctx.current_throw_types = func_def.throw_types.clone()
                ctx.current_return_types = func_def.return_types.clone()
                ctx.current_function_name = mangled_name.clone()
                ctx.mangling_counter = 0  // Reset counter per-function for determinism

                // Bug #60: Track ref and variadic params - all non-copy args are by pointer
                for func_def_arg: Declaration in func_def.args {
                    if not(func_def_arg.is_copy) {
                        _ := ctx.current_ref_params.insert(func_def_arg.name.clone())
                    }
                    switch func_def_arg.value_type {
                    case ValueType.TMulti(elem_type):
                        ctx.current_variadic_params.set(func_def_arg.name.clone(), til_name(elem_type))
                    case:
                        // REM: not variadic
                    }
                }

                // Push scope frame for function
                mut function_frame := ScopeFrame()
                function_frame.scope_type = ScopeType.Function
                for func_def_arg: Declaration in func_def.args {
                    mut value_type := func_def_arg.value_type
                    switch func_def_arg.value_type {
                    case ValueType.TMulti(_):
                        value_type = ValueType.TCustom("Array")
                    case:
                        // REM: keep original type
                    }
                    mut nested_symbol_info := SymbolInfo()
                    nested_symbol_info.value_type = value_type
                    nested_symbol_info.is_mut = func_def_arg.is_mut
                    nested_symbol_info.is_copy = func_def_arg.is_copy
                    nested_symbol_info.is_own = func_def_arg.is_own
                    nested_symbol_info.is_comptime_const = false
                    function_frame.symbols.set(func_def_arg.name.clone(), nested_symbol_info)
                }
                context.scope_stack.frames.push(function_frame)

                // Emit the function
                emit_func_signature(til_name(mangled_name), func_def, context, func_output)?
                func_output.push_str(" {\n")

                // Bug #99: Suppress -Wunused-parameter for intentionally unused params
                // _-prefixed params are always unused; all params unused in empty-body functions
                for arg: Declaration in func_def.args {
                    if arg.name.starts_with("_").or(func_def.body.is_empty()) {
                        c_name := til_var_name(arg.name, value_type_to_c_prefix(arg.value_type))
                        func_output.push_str("    (void)")
                        func_output.push_str(c_name)
                        func_output.push_str(";\n")
                    }
                }

                // Bug #168: Pre-scan for return variable placement optimization
                ctx.ret_var_alias = find_ret_var_for_placement(func_def)?
                if not(ctx.ret_var_alias.is_empty()) {
                    _ := ctx.current_ref_params.insert(ctx.ret_var_alias.clone())
                }

                emit_stmts(func_def.body, func_output, 1, ctx, context)?
                // Add implicit return at end to silence gcc -Wreturn-type warnings
                if not(func_def.throw_types.is_empty()) {
                    func_output.push_str("    return 0;\n")
                } else if not(func_def.return_types.is_empty()) {
                    ret_type_vt_ref := func_def.return_types.get_by_ref(0)?
                    create_alias(ret_type_vt, ValueType, ret_type_vt_ref.data)
                    ret_type := til_type_to_c(ret_type_vt)?
                    func_output.push_str("    return (")
                    func_output.push_str(ret_type)
                    func_output.push_str("){0};\n")
                }
                func_output.push_str("}\n\n")

                // Pop scope frame
                mut _popped_frame := ScopeFrame()
                context.scope_stack.frames.pop(_popped_frame)?

                // Restore context
                ctx.current_throw_types = prev_throw_types
                ctx.current_return_types = prev_return_types
                ctx.declared_vars = prev_declared_vars
                ctx.current_ref_params = prev_mut_params
                ctx.current_variadic_params = prev_variadic_params
                ctx.current_function_name = prev_function_name
                ctx.mangling_counter = prev_mangling_counter
                ctx.ret_var_alias = prev_ret_var_alias

                ctx.hoisted_functions.push(func_output)

                // Don't emit anything at the declaration site - the function is hoisted
                return
            }
        case:
            // REM: not a FuncDef
        }
    }

    indent_str := repeat("    ", indent)

    // Bug #143: Process RHS FCall with emit_arg_string
    // This handles builtins (to_ptr, size_of, etc.), throwing calls, by-ref params, and Dynamic params properly
    // Bug #157: Also handle field access on throwing calls (e.g., get_wrapper()?.value)
    mut rhs_string := ""
    mut has_rhs_string := false
    if not(expr.params.is_empty()) {
        rhs := expr.get(0)?
        dont_delete(rhs)
        switch rhs.node_type {
        case NodeType.FCall(_):
            rhs_string = emit_arg_string(rhs, Ptr(), false, output, indent, ctx, context)?
            has_rhs_string = true
        case NodeType.Identifier(name):
            // Bug #157: Check for UFCS field access on throwing call
            // Pattern: Identifier("_") with params[0] = throwing FCall, params[1..] = field chain
            if name.eq("_").and(not(rhs.params.is_empty())) {
                rhs_first_param := rhs.get(0)?
                dont_delete(rhs_first_param)
                switch rhs_first_param.node_type {
                case NodeType.FCall(_):
                    mut fd := SFuncDef()
                    mut found_fd := false
                    fd = get_fcall_func_def(context, rhs_first_param)?
                    found_fd = true
                    catch (err: KeyNotFoundError) {
                        // Not a function call we can look up
                    }
                    if found_fd.and(not(fd.throw_types.is_empty())) {
                        // Hoist the throwing call
                        base_temp := emit_arg_string(rhs_first_param, Ptr(), false, output, indent, ctx, context)?
                        // Append field chain: temp.field1.field2...
                        mut result := base_temp
                        for field_idx in 1..rhs.params.len() {
                            field_expr := rhs.get(field_idx)?
                            dont_delete(field_expr)
                            switch field_expr.node_type {
                            case NodeType.Identifier(field):
                                result = result.concat(".")
                                result = result.concat(field)
                            case:
                            }
                        }
                        rhs_string = result
                        has_rhs_string = true
                    }
                case:
                }
            }
        case:
        }
    }

    // Bug #35: For underscore declarations, just emit the expression (discard result)
    // This avoids C redeclaration errors and matches the semantics of discarding
    if decl.name.eq("_") {
        if not(expr.params.is_empty()) {
            output.push_str(indent_str)
            // FCall at statement level (indent > 0) adds ";\n" itself
            // Other expressions need semicolon added explicitly
            discard_first_param := expr.get(0)?
            dont_delete(discard_first_param)
            switch discard_first_param.node_type {
            case NodeType.FCall(_):
                emit_expr(discard_first_param, output, indent, ctx, context)?
            case:
                emit_expr(discard_first_param, output, 0, ctx, context)?
                output.push_str(";\n")
            }
        }
        return
    }

    // Bug #133: Precomputed globals are initialized at declaration site, skip in main()
    mut have_static_info := true
    mut static_info := PrecomputedStaticInfo()
    ctx.precomputed_static_arrays.get(decl.name, static_info)?
    catch (err: KeyNotFoundError) {
        have_static_info = false
    }
    if have_static_info {
        return
    }

    name := decl.name.clone()
    is_mut := decl.is_mut

    // Check if this is a struct construction (TypeName())
    mut struct_type := ""
    mut has_struct_type := false
    if not(expr.params.is_empty()) {
        struct_first_param := expr.get(0)?
        dont_delete(struct_first_param)
        struct_type = get_struct_construction_type(struct_first_param, context)?
        if not(struct_type.is_empty()) {
            has_struct_type = true
        }
    }

    // Check if this is an enum construction (Type.Variant or Type.Variant(value))
    mut enum_type := ""
    mut has_enum_type := false
    if not(expr.params.is_empty()) {
        enum_first_param := expr.get(0)?
        dont_delete(enum_first_param)
        enum_type = get_enum_construction_type(enum_first_param, context)?
        if not(enum_type.is_empty()) {
            has_enum_type = true
        }
    }

    // Track variable type for method mangling
    // Bug #97: Use declared type for the variable name to match C type declaration
    // Only fall back to RHS type inference when declared type is INFER_TYPE
    mut var_type := decl.value_type
    if has_struct_type {
        var_type = ValueType.TCustom(struct_type.clone())
    } else if has_enum_type {
        var_type = ValueType.TCustom(enum_type.clone())
    } else {
        // Check if declared type is INFER_TYPE
        mut is_infer := false
        switch decl.value_type {
        case ValueType.TCustom(type_name):
            if type_name.eq(INFER_TYPE) {
                is_infer = true
            }
        case:
        }
        if is_infer {
            // INFER_TYPE: need to infer from RHS
            if not(expr.params.is_empty()) {
                type_first_param := expr.get(0)?
                dont_delete(type_first_param)
                var_type = get_value_type(context, type_first_param)?
                catch (err: Str) {
                    var_type = decl.value_type
                }
            }
        }
        // Otherwise, declared type is explicit - use it (var_type already set to decl.value_type)
    }
    // Add to scope_stack so get_value_type can find it
    mut decl_symbol_info := SymbolInfo()
    decl_symbol_info.value_type = var_type
    decl_symbol_info.is_mut = decl.is_mut
    decl_symbol_info.is_copy = false
    decl_symbol_info.is_own = false
    decl_symbol_info.is_comptime_const = false
    context.scope_stack.declare_symbol(name.clone(), decl_symbol_info)?

    // Bug #97: Compute type-mangled C variable name
    type_prefix := value_type_to_c_prefix(var_type)
    orig_c_var_name := til_var_name(name, type_prefix)

    // Check if variable already declared in this function (avoid C redefinition errors)
    // Bug #97: Use type-mangled name so same name with different types are different variables
    mut already_declared := ctx.declared_vars.contains(orig_c_var_name)

    // Bug #168: If this variable is the ret_var_alias, use (*_ret) as lvalue
    mut is_ret_alias := false
    if not(ctx.ret_var_alias.is_empty()) {
        if name.eq(ctx.ret_var_alias).and(not(already_declared)) {
            _ := ctx.declared_vars.insert(orig_c_var_name)
            already_declared = true
            is_ret_alias = true
        }
    }
    mut c_var_name := orig_c_var_name
    if is_ret_alias {
        c_var_name = "(*_ret)"
    }

    if has_struct_type {
        // Struct variable declaration with values (defaults or named args)

        // Build map of named arg values from struct literal
        mut named_values := Map.new(Str, Expr)
        if not(expr.params.is_empty()) {
            named_first_param := expr.get(0)?
            dont_delete(named_first_param)
            mut arg_idx := 1
            while arg_idx.lt(named_first_param.params.len()) {
                named_arg := named_first_param.get(arg_idx)?
                dont_delete(named_arg)
                switch named_arg.node_type {
                case NodeType.NamedArg(field_name):
                    if not(named_arg.params.is_empty()) {
                        named_value_expr := named_arg.get(0)?
                        dont_delete(named_value_expr)
                        named_values.set(field_name.clone(), named_value_expr.clone())
                    }
                case:
                    // REM: not a named arg
                }
                arg_idx.inc()
            }
        }

        // Look up struct definition to get default values
        // Clone to avoid borrow issues with emit_expr
        struct_def := context.scope_stack.lookup_struct(struct_type)?
        if struct_def.members.is_empty() {
            // Empty struct - use empty initializer
            output.push_str(indent_str)
            if not(already_declared) {
                // Issue #117: No const for struct locals - garbager inserts Type.delete() calls
                output.push_str(til_name(struct_type))
                output.push_str(" ")
                _ := ctx.declared_vars.insert(c_var_name.clone())
            }
            output.push_str(c_var_name.clone())
            emit_struct_literal_assign(output, struct_type, already_declared, "{}")
        } else if struct_def.default_values.is_empty().and(named_values.is_empty()) {
            // No defaults and no named args - zero initialize
            output.push_str(indent_str)
            if not(already_declared) {
                // Issue #117: No const for struct locals - garbager inserts Type.delete() calls
                output.push_str(til_name(struct_type))
                output.push_str(" ")
                _ := ctx.declared_vars.insert(c_var_name.clone())
            }
            output.push_str(c_var_name.clone())
            emit_struct_literal_assign(output, struct_type, already_declared, "{0}")
        } else {
            // Has default values or named args - emit designated initializer
            // Issue #108: Pre-hoist any function calls in member values before struct literal
            // C doesn't allow statements inside struct initializers, so we must hoist first
            // Use emit_arg_string which handles nested hoisting (function call arguments)
            mut hoisted_values := Map.new(Str, Str)
            for member: Declaration in struct_def.members {
                if not(member.is_mut) {
                    continue
                }
                // Get value expr from named_values or default_values
                mut value_expr := Expr()
                mut has_value := false
                if named_values.contains_key(member.name) {
                    named_values.get(member.name, value_expr)?
                    has_value = true
                } else {
                    struct_def.default_values.get(member.name, value_expr)?
                    has_value = true
                    catch (err: KeyNotFoundError) {
                        has_value = false
                    }
                }
                if has_value {
                    mut is_fcall := false
                    switch value_expr.node_type {
                    case NodeType.FCall(_):
                        is_fcall = true
                    case:
                    }
                    if is_fcall {
                        // This is a function call - must hoist to a temp variable
                        // Use emit_arg_string which properly hoists nested function call arguments
                        mut member_type_ptr := Ptr()
                        member_type_ptr.data = to_ptr(member.value_type)
                        call_str := emit_arg_string(value_expr, member_type_ptr, false, output, indent, ctx, context)?
                        // Now hoist the call result to a temp var
                        temp_var := next_mangled(ctx)
                        hoisted_value_type := get_value_type(context, value_expr)?
                        hoisted_c_type := til_type_to_c(hoisted_value_type)?
                        output.push_str(indent_str)
                        output.push_str(hoisted_c_type)
                        output.push_str(" ")
                        output.push_str(temp_var)
                        output.push_str(" = ")
                        output.push_str(call_str)
                        output.push_str(";\n")
                        hoisted_values.insert(member.name.clone(), temp_var)?
                        catch (err: DuplicatedKeyError) {
                            // Should not happen - each member name is unique
                        }
                    }
                }
            }

            output.push_str(indent_str)
            if not(already_declared) {
                // Issue #117: No const for struct locals - garbager inserts Type.delete() calls
                output.push_str(til_name(struct_type))
                output.push_str(" ")
                _ := ctx.declared_vars.insert(c_var_name.clone())
            }
            output.push_str(c_var_name.clone())
            emit_struct_literal_start(output, struct_type, already_declared)
            output.push_str("{")
            mut first := true
            for member: Declaration in struct_def.members {
                // Only include mut fields (actual struct data members)
                // Skip functions, constants, and non-mut fields
                if not(member.is_mut) {
                    continue
                }
                if not(first) {
                    output.push_str(", ")
                }
                first = false
                output.push_str(".")
                output.push_str(member.name)
                output.push_str(" = ")
                // Use hoisted temp if available, otherwise emit inline
                if hoisted_values.contains_key(member.name) {
                    hv_ref := hoisted_values.get_by_ref(member.name)?
                    create_alias(temp_var, Str, hv_ref.data)
                    output.push_str(temp_var)
                } else if named_values.contains_key(member.name) {
                    nv_ref := named_values.get_by_ref(member.name)?
                    create_alias(value_expr, Expr, nv_ref.data)
                    emit_expr(value_expr, output, 0, ctx, context)?
                } else {
                    dv_ref := struct_def.default_values.get_by_ref(member.name)?
                    create_alias(member_default_expr, Expr, dv_ref.data)
                    emit_expr(member_default_expr, output, 0, ctx, context)?
                    catch (err: KeyNotFoundError) {
                        // No default - use zero
                        output.push_str("0")
                    }
                }
            }
            output.push_str("};\n")
        }

        catch (err: KeyNotFoundError) {
            // Struct not found - fall back to zero init
            output.push_str(indent_str)
            if not(already_declared) {
                // Issue #117: No const for struct locals - garbager inserts Type.delete() calls
                output.push_str(til_name(struct_type))
                output.push_str(" ")
                _ := ctx.declared_vars.insert(c_var_name.clone())
            }
            output.push_str(c_var_name.clone())
            emit_struct_literal_assign(output, struct_type, already_declared, "{0}")
        }
    } else if has_enum_type {
        // Enum variable declaration
        output.push_str(indent_str)
        if not(already_declared) {
            // Issue #117: No const for enum locals - garbager inserts Type.delete() calls
            output.push_str(til_name(enum_type))
            output.push_str(" ")
            _ := ctx.declared_vars.insert(c_var_name.clone())
        }
        output.push_str(c_var_name.clone())
        output.push_str(" = ")
        enum_emit_first_param := expr.get(0)?
        dont_delete(enum_emit_first_param)
        emit_expr(enum_emit_first_param, output, 0, ctx, context)?
        output.push_str(";\n")
    } else {
        if is_mut {
            // Bug #143: emit_fcall handles hoisting via emit_arg_string
            output.push_str(indent_str)
            if not(already_declared) {
                // INFER_TYPE should have been resolved by typer
                switch decl.value_type {
                case ValueType.TCustom(s):
                    if s.eq(INFER_TYPE) {
                        throw expr.lang_error(context.path, "ccodegen", format("Declaration '", decl.name, "' has INFER_TYPE - should have been resolved by typer"))
                    }
                case:
                }
                c_type := til_type_to_c(decl.value_type)?
                catch (err: Str) {
                    throw expr.lang_error(context.path, "ccodegen", err)
                }
                output.push_str(c_type)
                output.push_str(" ")
                _ := ctx.declared_vars.insert(c_var_name.clone())
            }
            output.push_str(c_var_name.clone())
            if not(expr.params.is_empty()) {
                output.push_str(" = ")
                // Issue #159: Clone logic moved to garbager phase - it transforms
                // `mut x := y` to `mut x := Type.clone(y)` at AST level
                if has_rhs_string {
                    output.push_str(rhs_string)
                } else {
                    init_first_param := expr.get(0)?
                    dont_delete(init_first_param)
                    emit_expr(init_first_param, output, 0, ctx, context)?
                }
            }
            output.push_str(";\n")
        } else {
            // const declaration - Bug #143: Use rhs_string if available (already hoisted args)
            output.push_str(indent_str)
            if not(already_declared) {
                // INFER_TYPE should have been resolved by typer
                switch decl.value_type {
                case ValueType.TCustom(s):
                    if s.eq(INFER_TYPE) {
                        throw expr.lang_error(context.path, "ccodegen", format("Declaration '", decl.name, "' has INFER_TYPE - should have been resolved by typer"))
                    }
                case:
                }
                const_c_type := til_type_to_c(decl.value_type)?
                catch (err: Str) {
                    throw expr.lang_error(context.path, "ccodegen", err)
                }
                // Issue #117: No const for struct/enum locals - garbager inserts Type.delete() calls
                mut is_struct_local := false
                switch decl.value_type {
                case ValueType.TCustom(s):
                    is_struct_local = not(s.eq("I64").or(s.eq("U8")).or(s.eq("Type")).or(s.eq("Dynamic")))
                case:
                }
                if not(is_struct_local) {
                    output.push_str("const ")
                }
                output.push_str(const_c_type)
                output.push_str(" ")
                _ := ctx.declared_vars.insert(c_var_name.clone())
            }
            output.push_str(c_var_name.clone())
            if not(expr.params.is_empty()) {
                output.push_str(" = ")
                // Bug #143: Use rhs_string if available (already hoisted args)
                if has_rhs_string {
                    output.push_str(rhs_string)
                } else {
                    const_emit_first_param := expr.get(0)?
                    dont_delete(const_emit_first_param)
                    emit_expr(const_emit_first_param, output, 0, ctx, context)?
                }
            }
            output.push_str(";\n")
        }
    }

    catch (err: IndexOutOfBoundsError) {
        throw err.msg
    }
}

// Check if an expression is a struct construction call (TypeName() or TypeName(x=1, y=2))
// Returns the type name if it is, empty string otherwise
get_struct_construction_type := func(expr: Expr, context: Context) returns Str throws Str {
    switch expr.node_type {
    case NodeType.FCall(_):
        if not(expr.params.is_empty()) {
            first_param := expr.get(0)?
            dont_delete(first_param)
            switch first_param.node_type {
            case NodeType.Identifier(name):
                // Use lookup_struct to check if this is a known struct type
                if first_param.params.is_empty() {
                    _ := context.scope_stack.lookup_struct(name)?
                    catch (err: KeyNotFoundError) {
                        return ""
                    }
                    mut only_named_args := expr.params.len().eq(1)
                    if not(only_named_args) {
                        only_named_args = true
                        mut i := 1
                        while i.lt(expr.params.len()) {
                            arg := expr.get(i)?
                            dont_delete(arg)
                            switch arg.node_type {
                            case NodeType.NamedArg(_):
                                // ok
                            case:
                                only_named_args = false
                            }
                            i.inc()
                        }
                    }
                    if only_named_args {
                        return name.clone()
                    }
                }
            case:
            }
        }
    case:
    }
    return ""
}

// Check if an expression is an enum construction (Type.Variant or Type.Variant(value))
// Returns the type name if it is, empty string otherwise
// AST structure for Color.Red(42): FCall -> [Identifier("Color") -> [Identifier("Red")], Literal(42)]
// AST structure for Color.Unknown: Identifier("Color") -> [Identifier("Unknown")]
get_enum_construction_type := func(expr: Expr, context: Context) returns Str throws Str {
    // Check FCall case: Type.Variant(value) or Type.Variant()
    switch expr.node_type {
    case NodeType.FCall(_):
        if not(expr.params.is_empty()) {
            first_param := expr.get(0)?
            dont_delete(first_param)
            switch first_param.node_type {
            case NodeType.Identifier(type_name):
                // Use lookup_enum to check if this is a known enum type
                mut enum_def := SEnumDef()
                mut enum_def_found := false
                enum_def = context.scope_stack.lookup_enum(type_name)?
                enum_def_found = true
                catch (err: KeyNotFoundError) {
                    // REM: Not an enum - continue to check Identifier case below
                }
                if enum_def_found {
                    // Check if there's a nested identifier (the variant)
                    if not(first_param.params.is_empty()) {
                        variant_param := first_param.get(0)?
                        dont_delete(variant_param)
                        switch variant_param.node_type {
                        case NodeType.Identifier(variant_name):
                            // Verify the variant exists in the enum
                            if enum_def.contains_key(variant_name) {
                                return type_name.clone()
                            }
                        case:
                        }
                    }
                }
            case:
            }
        }
    case NodeType.Identifier(type_name):
        // Check Identifier case: Type.Variant (no parentheses, no payload)
        // Use lookup_enum to check if this is a known enum type
        ident_enum_def := context.scope_stack.lookup_enum(type_name)?
        catch (err: KeyNotFoundError) {
            return ""
        }
        if not(expr.params.is_empty()) {
            ident_variant_param := expr.get(0)?
            dont_delete(ident_variant_param)
            switch ident_variant_param.node_type {
            case NodeType.Identifier(variant_name):
                // Verify the variant exists in the enum
                if ident_enum_def.contains_key(variant_name) {
                    return type_name.clone()
                }
            case:
            }
        }
    case:
    }
    return ""
}

emit_funcdef := proc(_func_def: SFuncDef, expr: Expr, mut output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) throws Str {
    // For now, just inline the function body (we're inside main anyway)
    // TODO: proper function generation with prototypes
    indent_str := repeat("    ", indent)
    output.push_str(indent_str)
    output.push_str("{\n")
    for stmt: Expr in expr.params {
        emit_expr(stmt, output, indent.add(1), ctx, context)?
    }
    output.push_str(indent_str)
    output.push_str("}\n")
}

emit_assignment := proc(name: Str, expr: Expr, mut output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) throws Str {
    indent_str := repeat("    ", indent)

    // Bug #143: Use emit_arg_string to handle RHS hoisting
    mut rhs_str := ""
    mut has_rhs := false
    if not(expr.params.is_empty()) {
        rhs_expr := expr.get(0)?
        dont_delete(rhs_expr)

        // Check if RHS is a call to a throwing function
        switch rhs_expr.node_type {
        case NodeType.FCall(_):
            mut fd := SFuncDef()
            mut fd_found := true
            fd = get_fcall_func_def(context, rhs_expr)?
            catch (err: KeyNotFoundError) {
                fd_found = false
            }
            if fd_found {
            if not(fd.throw_types.is_empty()) {
                // RHS is a throwing function call - emit with error propagation
                // (typer should ensure non-throwing context doesn't call throwing functions without catch)
                // Pass raw name so function can properly handle field access on mut params
                emit_throwing_call_propagate(rhs_expr, fd.throw_types, "", name, output, indent, ctx, context)?
                return
            }
            }
        case:
        }

        // Process RHS with emit_arg_string (handles hoisting)
        rhs_str = emit_arg_string(rhs_expr, Ptr(), false, output, indent, ctx, context)?
        has_rhs = true
    }

    // Regular assignment
    output.push_str(indent_str)
    // Check if assignment target is a field access on a mut param (self.field)
    // If so, emit with -> instead of .
    // Bug #97: Use type-mangled variable names
    // Bug #168: resolve_var_name maps ret_var_alias to "_ret"
    dot_pos := name.find(".")
    if dot_pos.gteq(0) {
        base := name.get_substr(0, dot_pos)?
        rest := name.get_substr(dot_pos.add(1), name.len())?
        if ctx.current_ref_params.contains(base) {
            // Mut param field access: til_Type_self->field
            output.push_str(resolve_var_name(base, ctx, context))
            output.push_str("->")
            output.push_str(rest)
        } else {
            // Regular field access: til_Type_var.field
            output.push_str(resolve_var_name(base, ctx, context))
            output.push_str(".")
            output.push_str(rest)
        }
    } else if ctx.current_ref_params.contains(name) {
        // Direct assignment to mut param: *til_Type_self = value
        output.push_str("*")
        output.push_str(resolve_var_name(name, ctx, context))
    } else {
        output.push_str(resolve_var_name(name, ctx, context))
    }
    output.push_str(" = ")

    // Check if LHS is Dynamic type - need to cast RHS to avoid int-to-pointer warning
    mut is_dynamic_target := false
    sym := context.scope_stack.lookup_symbol(name)?
    switch sym.value_type {
    case ValueType.TCustom(t):
        if t.eq("Dynamic") {
            is_dynamic_target = true
        }
    case:
    }
    catch (err: KeyNotFoundError) {
        // REM: Symbol not found means we don't know its type, so is_dynamic_target stays false
    }

    if has_rhs {
        if is_dynamic_target {
            output.push_str("(")
            output.push_str(TIL_PREFIX)
            output.push_str("Dynamic)")
        }
        output.push_str(rhs_str)
    }
    output.push_str(";\n")

    catch (err: IndexOutOfBoundsError) {
        throw err.msg
    }
}

emit_return := proc(expr: Expr, mut output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) throws Str {
    indent_str := repeat("    ", indent)
    is_throwing := not(ctx.current_throw_types.is_empty())

    if is_throwing {
        // Throwing function: store value through _ret pointer and return 0 (success)
        if not(expr.params.is_empty()) {
            thr_return_expr := expr.get(0)?
            dont_delete(thr_return_expr)

            // Check if return expression is a call to a throwing function
            switch thr_return_expr.node_type {
            case NodeType.FCall(_):
                // Bug #80: Use flag pattern for if-let-Some translation
                mut thr_fd := SFuncDef()
                mut thr_fd_found := false
                thr_fd = get_fcall_func_def(context, thr_return_expr)?
                thr_fd_found = true
                catch (err: KeyNotFoundError) {
                    // REM: no func def found
                }
                if thr_fd_found.and(not(thr_fd.throw_types.is_empty())) {
                    // Return expression is a throwing function call - emit with error propagation
                    // The result will be stored via the assign_name "*_ret"
                    emit_throwing_call_propagate(thr_return_expr, thr_fd.throw_types, "", "*_ret", output, indent, ctx, context)?
                    output.push_str(indent_str)
                    output.push_str("return 0;\n")
                    return
                }
            case:
            }

            // Bug #168: If returning the ret_var_alias, skip the copy - it's already in _ret
            mut skip_ret_copy := false
            switch thr_return_expr.node_type {
            case NodeType.Identifier(ret_name):
                if thr_return_expr.params.is_empty() {
                    if not(ctx.ret_var_alias.is_empty()) {
                        if ret_name.eq(ctx.ret_var_alias) {
                            output.push_str(indent_str)
                            output.push_str("return 0;\n")
                            skip_ret_copy = true
                        }
                    }
                }
            case:
            }
            if skip_ret_copy {
                return
            }

            // Bug #143: Use emit_arg_string to handle hoisting
            return_str := emit_arg_string(thr_return_expr, Ptr(), false, output, indent, ctx, context)?

            // Regular return value - just emit it
            output.push_str(indent_str)
            output.push_str("*_ret = ")
            output.push_str(return_str)
            output.push_str(";\n")
        }
        output.push_str(indent_str)
        output.push_str("return 0;\n")
    } else {
        // Non-throwing function: normal return
        // Check if return expression is a variadic function call
        if not(expr.params.is_empty()) {
            return_expr := expr.get(0)?
            dont_delete(return_expr)
            switch return_expr.node_type {
            case NodeType.FCall(_):
                mut variadic_fcall_info := VariadicFCallInfo()
                variadic_fcall_info = detect_variadic_fcall(return_expr, ctx)?
                catch (err: KeyNotFoundError) {
                    // REM: not variadic - variadic_fcall_info stays at default
                }
                if not(variadic_fcall_info.elem_type.is_empty()) {
                    // Variadic call in return - need to hoist it
                    // Bug #143: Use emit_arg_string for regular args, pass empty map to hoist_variadic_args
                    // (hoist_variadic_args already uses emit_arg_string internally)
                    mut variadic_args := Vec.new(Expr)
                    mut va_idx := add(1, variadic_fcall_info.regular_count)
                    while va_idx.lt(return_expr.params.len()) {
                        va_arg := return_expr.get(va_idx)?
                        dont_delete(va_arg)
                        variadic_args.push(va_arg)
                        va_idx.inc()
                    }
                    empty_hoisted := Map.new(I64, Str)
                    arr_var := hoist_variadic_args(variadic_fcall_info.elem_type, variadic_args, empty_hoisted, variadic_fcall_info.regular_count, output, indent, ctx, context)?

                    // Bug #143: Get regular arg strings using emit_arg_string
                    mut regular_args := Vec.new(Expr)
                    mut ra_idx := 1
                    while ra_idx.lt(add(1, variadic_fcall_info.regular_count)).and(ra_idx.lt(return_expr.params.len())) {
                        ra_arg := return_expr.get(ra_idx)?
                        dont_delete(ra_arg)
                        regular_args.push(ra_arg)
                        ra_idx.inc()
                    }
                    var_fd := get_fcall_func_def(context, return_expr)?
                    catch (err: KeyNotFoundError) { }
                    mut regular_arg_strings := Vec.new(Str)
                    mut i := 0
                    while i.lt(regular_args.len()) {
                        arg_ref := regular_args.get_by_ref(i)?
                        create_alias(arg, Expr, arg_ref.data)
                        mut param_type := Ptr()
                        mut by_ref := true
                        if i.lt(var_fd.args.len()) {
                            param_decl_ref := var_fd.args.get_by_ref(i)?
                            create_alias(param_decl, Declaration, param_decl_ref.data)
                            param_type.data = to_ptr(param_decl.value_type)
                            by_ref = param_needs_by_ref(param_decl)
                        }
                        arg_str := emit_arg_string(arg, param_type, by_ref, output, indent, ctx, context)?
                        regular_arg_strings.push(arg_str)
                        i.inc()
                    }

                    // Emit the function call storing result
                    temp_var := next_mangled(ctx)
                    ret_type_vt := get_value_type(context, return_expr)?
                    catch (err: Str) {
                        throw return_expr.lang_error(context.path, "ccodegen", err)
                    }
                    ret_type := til_type_to_c(ret_type_vt)?
                    catch (err2: Str) {
                        throw return_expr.lang_error(context.path, "ccodegen", err2)
                    }
                    output.push_str(indent_str)
                    output.push_str(ret_type)
                    output.push_str(" ")
                    output.push_str(temp_var)
                    output.push_str(" = ")
                    // Emit the function call
                    var_first_param := return_expr.get(0)?
                    dont_delete(var_first_param)
                    switch var_first_param.node_type {
                    case NodeType.Identifier(func_name):
                        output.push_str(til_func_name(func_name))
                    case:
                    }
                    output.push_str("(")
                    // Emit regular args
                    mut vret_arg_i := 0
                    while vret_arg_i.lt(regular_arg_strings.len()) {
                        if vret_arg_i.gt(0) {
                            output.push_str(", ")
                        }
                        vret_arg_str_ref := regular_arg_strings.get_by_ref(vret_arg_i)?
                        create_alias(vret_arg_str, Str, vret_arg_str_ref.data)
                        output.push_str(vret_arg_str)
                        vret_arg_i.inc()
                    }
                    // Emit variadic array pointer
                    if variadic_fcall_info.regular_count.gt(0) {
                        output.push_str(", ")
                    }
                    output.push_str("&")
                    output.push_str(arr_var)
                    output.push_str(");\n")

                    // Delete array
                    output.push_str(indent_str)
                    output.push_str(TIL_PREFIX)
                    output.push_str("Array_delete(&")
                    output.push_str(arr_var)
                    output.push_str(");\n")

                    // Return the result
                    output.push_str(indent_str)
                    output.push_str("return ")
                    output.push_str(temp_var)
                    output.push_str(";\n")
                    return
                }
            case:
            }
        }

        // Bug #143: Use emit_arg_string to handle hoisting
        mut nv_return_str := ""
        mut has_nv_return := false
        if not(expr.params.is_empty()) {
            ret_first_param := expr.get(0)?
            dont_delete(ret_first_param)
            nv_return_str = emit_arg_string(ret_first_param, Ptr(), false, output, indent, ctx, context)?
            has_nv_return = true
        }

        // Regular non-variadic return
        output.push_str(indent_str)
        output.push_str("return")
        if has_nv_return {
            output.push_str(" ")
            output.push_str(nv_return_str)
        }
        output.push_str(";\n")
    }

    catch (err: IndexOutOfBoundsError) {
        throw err.msg
    }
}

emit_throw := proc(expr: Expr, mut output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) throws Str {
    // Throw: params[0] = the value to throw (typically a struct constructor like DivideByZero())
    if expr.params.is_empty() {
        throw "ccodegen: throw requires a value"
    }

    indent_str := repeat("    ", indent)
    thrown_expr := expr.get(0)?
    dont_delete(thrown_expr)

    // Bug #143: Use emit_arg_string to handle hoisting of thrown expression
    thrown_str := emit_arg_string(thrown_expr, Ptr(), false, output, indent, ctx, context)?

    // Get the thrown type name from the expression
    // For FCall, we need to determine if it's:
    // 1. A constructor like DivideByZero() - use the type name
    // 2. A function that returns an error type like format() - use the return type
    mut nh_thrown_type_name := ""
    switch thrown_expr.node_type {
    case NodeType.FCall(_):
        if not(thrown_expr.params.is_empty()) {
            nh_first_param := thrown_expr.get(0)?
            dont_delete(nh_first_param)
            switch nh_first_param.node_type {
            case NodeType.Identifier(name):
                // Check if this is a constructor (struct/enum) or a function call
                // If it's a function that returns a type, use the return type
                mut func_def := SFuncDef()
                func_def = get_fcall_func_def(context, thrown_expr)?
                catch (err: KeyNotFoundError) {
                    // Not a function, assume it's a constructor
                    nh_thrown_type_name = name.clone()
                }
                // Only check func_def's return types if we successfully got it (nh_thrown_type_name still empty)
                if nh_thrown_type_name.is_empty() {
                    // It's a function - use its return type as the thrown type
                    if not(func_def.return_types.is_empty()) {
                        nh_ret_type_ref := func_def.return_types.get_by_ref(0)?
                        create_alias(nh_ret_type, ValueType, nh_ret_type_ref.data)
                        switch nh_ret_type {
                        case ValueType.TCustom(type_name):
                            nh_thrown_type_name = type_name.clone()
                        case:
                            // Return type is a primitive (like Str) - convert to name
                            nh_thrown_type_name = value_type_to_str(nh_ret_type)
                        }
                    } else {
                        // No return type, assume it's a constructor
                        nh_thrown_type_name = name.clone()
                    }
                }
            case:
                throw "ccodegen: throw FCall must have identifier as first param"
            }
        } else {
            throw "ccodegen: throw FCall has no params"
        }
    case NodeType.Identifier(name):
        // Look up the type of the identifier (could be a variable or type name)
        mut nh_have_value_type := true
        nh_value_type := get_value_type(context, thrown_expr)?
        catch (err: Str) {
            // Fallback: assume identifier is a type name (for struct constructors without args)
            nh_thrown_type_name = name.clone()
            nh_have_value_type = false
        }
        if nh_have_value_type {
            switch nh_value_type {
            case ValueType.TCustom(type_name):
                nh_thrown_type_name = type_name.clone()
            case:
                nh_thrown_type_name = value_type_to_str(nh_value_type)
            }
        }
    case NodeType.LLiteral(lit):
        // Literal values - determine their type
        switch lit {
        case Literal.Str(_):
            nh_thrown_type_name = "Str"
        case Literal.Number(_):
            nh_thrown_type_name = "I64"
        case Literal.List(_):
            throw "ccodegen: cannot throw a list literal"
        }
    case:
        throw "ccodegen: throw expression must be a constructor"
    }

    // Check if this type is locally caught (has a catch block at function level)
    mut nh_catch_info := CatchLabelInfo()
    ctx.local_catch_labels.get(nh_thrown_type_name.clone(), nh_catch_info)?
    catch (err: KeyNotFoundError) {
        // REM: Rust's None case - type not locally caught, continue to next check
    }
    if not(nh_catch_info.label.eq("")) {
        nh_label := nh_catch_info.label.clone()
        nh_temp_var := nh_catch_info.temp_var.clone()

        // Issue #119: Check if this is an empty struct error
        mut is_empty_local_err := false
        nh_struct_def := context.scope_stack.lookup_struct(nh_thrown_type_name.clone())?
        is_empty_local_err = nh_struct_def.members.is_empty()
        catch (err: KeyNotFoundError) {
            is_empty_local_err = false
        }

        // Issue #119: Skip storing value for empty struct errors - just goto
        if is_empty_local_err {
            output.push_str(indent_str)
            output.push_str("goto ")
            output.push_str(nh_label)
            output.push_str(";\n")
            return
        }

        // Bug #143: Use thrown_str (already hoisted by emit_arg_string)
        output.push_str(indent_str)
        output.push_str(nh_temp_var)
        output.push_str(" = ")
        output.push_str(thrown_str)
        output.push_str(";\n")

        // Jump to the catch block
        output.push_str(indent_str)
        output.push_str("goto ")
        output.push_str(nh_label)
        output.push_str(";\n")
        return
    }

    // Find the index of this type in current_throw_types
    // Note: Str is represented as TCustom("Str") in the type system
    mut nh_error_index := sub(0, 1)
    mut nh_i := 0
    while nh_i.lt(ctx.current_throw_types.len()) {
        nh_vt_ref := ctx.current_throw_types.get_by_ref(nh_i)?
        create_alias(nh_vt, ValueType, nh_vt_ref.data)
        switch nh_vt {
        case ValueType.TCustom(name):
            if name.eq(nh_thrown_type_name) {
                nh_error_index = nh_i
                break
            }
        case:
        }
        nh_i.inc()
    }

    if nh_error_index.gteq(0) {
        // Issue #119: Check if this is an empty struct error
        nh_prop_err_type_ref := ctx.current_throw_types.get_by_ref(nh_error_index)?
        create_alias(nh_prop_err_type, ValueType, nh_prop_err_type_ref.data)
        is_empty_err := is_empty_error_struct(context, nh_prop_err_type)

        // Issue #119: Skip storing error value for empty struct errors
        if not(is_empty_err) {
            // Bug #143: Use thrown_str (already hoisted by emit_arg_string)
            output.push_str(indent_str)
            output.push_str("*_err")
            output.push_str(I64.to_str(nh_error_index.add(1)))
            output.push_str(" = ")
            output.push_str(thrown_str)
            output.push_str(";\n")
        }

        // Return the error index (1-based, since 0 = success)
        output.push_str(indent_str)
        output.push_str("return ")
        output.push_str(I64.to_str(nh_error_index.add(1)))
        output.push_str(";\n")
    } else {
        throw concat("ccodegen: thrown type '", nh_thrown_type_name).concat("' not found in function's throw types")
    }

    catch (err: IndexOutOfBoundsError) {
        throw err.msg
    }
}

emit_if := proc(expr: Expr, mut output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) throws Str {
    // If: params[0] = condition, params[1] = then-body, params[2] = else-body (optional)
    if expr.params.len().lt(2) {
        throw "ccodegen: If requires condition and body"
    }

    indent_str := repeat("    ", indent)

    // Hoist declarations from both branches to before the if statement
    // (TIL has function-level scoping, not block-level scoping)
    // Bug #97: Use type-mangled names for shadowing support
    then_body := expr.get(1)?
    dont_delete(then_body)
    then_decls := collect_declarations_in_body(then_body, context)?
    for decl: CollectedDeclaration in then_decls {
        type_prefix := value_type_to_c_prefix(decl.value_type)
        c_var_name := til_var_name(decl.name, type_prefix)
        if not(ctx.declared_vars.contains(c_var_name)) {
            c_type := til_type_to_c(decl.value_type)?
            catch (err: Str) {
                continue
            }
            output.push_str(indent_str)
            output.push_str(c_type)
            output.push_str(" ")
            output.push_str(c_var_name.clone())
            output.push_str(";\n")
            _ := ctx.declared_vars.insert(c_var_name)
            // Also register in scope_stack so get_value_type can find it
            mut sym_info := SymbolInfo()
            sym_info.value_type = decl.value_type
            sym_info.is_mut = false
            sym_info.is_copy = false
            sym_info.is_own = false
            sym_info.is_comptime_const = false
            context.scope_stack.declare_symbol(decl.name.clone(), sym_info)?
        }
    }
    if expr.params.len().gt(2) {
        hoist_else_body := expr.get(2)?
        dont_delete(hoist_else_body)
        else_decls := collect_declarations_in_body(hoist_else_body, context)?
        for else_decl: CollectedDeclaration in else_decls {
            else_type_prefix := value_type_to_c_prefix(else_decl.value_type)
            else_c_var_name := til_var_name(else_decl.name, else_type_prefix)
            if not(ctx.declared_vars.contains(else_c_var_name)) {
                else_c_type := til_type_to_c(else_decl.value_type)?
                catch (err: Str) {
                    continue
                }
                output.push_str(indent_str)
                output.push_str(else_c_type)
                output.push_str(" ")
                output.push_str(else_c_var_name.clone())
                output.push_str(";\n")
                _ := ctx.declared_vars.insert(else_c_var_name)
                // Also register in scope_stack so get_value_type can find it
                mut else_sym_info := SymbolInfo()
                else_sym_info.value_type = else_decl.value_type
                else_sym_info.is_mut = false
                else_sym_info.is_copy = false
                else_sym_info.is_own = false
                else_sym_info.is_comptime_const = false
                context.scope_stack.declare_symbol(else_decl.name.clone(), else_sym_info)?
            }
        }
    }

    // Bug #143: Use emit_arg_string to handle hoisting in condition
    condition := expr.get(0)?
    dont_delete(condition)
    cond_str := emit_arg_string(condition, Ptr(), false, output, indent, ctx, context)?
    output.push_str(indent_str)
    output.push_str("if (")
    output.push_str(cond_str)
    // Bool is a struct with .data field - extract for C truthiness
    mut have_vt := true
    vt := get_value_type(context, condition)?
    catch (err: Str) {
        have_vt = false
    }
    if have_vt {
        switch vt {
        case ValueType.TCustom(type_name):
            if type_name.eq("Bool") {
                output.push_str(".data")
            }
        case:
        }
    }
    output.push_str(") {\n")

    // Don't save/restore declared_vars - TIL has function-level scoping
    // Variables declared in if branches stay declared for the rest of the function
    // Bug #144: Save/restore current_ref_params to scope pointer semantics from create_alias
    saved_ref_params := ctx.current_ref_params.clone()
    emit_body(then_body, output, indent.add(1), ctx, context)?
    ctx.current_ref_params = saved_ref_params
    dont_delete(saved_ref_params)

    output.push_str(indent_str)
    output.push_str("}")

    // Else branch (optional)
    if expr.params.len().gt(2) {
        emit_else_body := expr.get(2)?
        dont_delete(emit_else_body)
        // Check if it's an else-if (nested If) or else block
        switch emit_else_body.node_type {
        case NodeType.If:
            // Always wrap else-if in braces to ensure hoisted temp vars
            // from nested if conditions have proper scope
            output.push_str(" else {\n")
            saved_ref_params2 := ctx.current_ref_params.clone()
            emit_if(emit_else_body, output, indent.add(1), ctx, context)?
            ctx.current_ref_params = saved_ref_params2
            dont_delete(saved_ref_params2)
            output.push_str(indent_str)
            output.push_str("}\n")
        case:
            output.push_str(" else {\n")
            saved_ref_params3 := ctx.current_ref_params.clone()
            emit_body(emit_else_body, output, indent.add(1), ctx, context)?
            ctx.current_ref_params = saved_ref_params3
            dont_delete(saved_ref_params3)
            output.push_str(indent_str)
            output.push_str("}\n")
        }
    } else {
        output.push_str("\n")
    }
}

emit_while := proc(expr: Expr, mut output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) throws Str {
    // While: params[0] = condition, params[1] = body
    if expr.params.len().lt(2) {
        throw "ccodegen: While requires condition and body"
    }

    indent_str := repeat("    ", indent)

    // Hoist declarations from loop body to before the while statement
    // (TIL has function-level scoping, not block-level scoping)
    // Bug #97: Use type-mangled names for shadowing support
    body := expr.get(1)?
    dont_delete(body)
    body_decls := collect_declarations_in_body(body, context)?
    for decl: CollectedDeclaration in body_decls {
        type_prefix := value_type_to_c_prefix(decl.value_type)
        c_var_name := til_var_name(decl.name, type_prefix)
        if not(ctx.declared_vars.contains(c_var_name)) {
            c_type := til_type_to_c(decl.value_type)?
            catch (err: Str) {
                continue
            }
            output.push_str(indent_str)
            output.push_str(c_type)
            output.push_str(" ")
            output.push_str(c_var_name.clone())
            output.push_str(";\n")
            _ := ctx.declared_vars.insert(c_var_name)
            // Also register in scope_stack so get_value_type can find it
            mut sym_info := SymbolInfo()
            sym_info.value_type = decl.value_type
            sym_info.is_mut = false
            sym_info.is_copy = false
            sym_info.is_own = false
            sym_info.is_comptime_const = false
            context.scope_stack.declare_symbol(decl.name.clone(), sym_info)?
        }
    }

    // Bug #143: Use emit_arg_string to process condition (handles hoisting for by-ref/throwing)
    // Emit hoisting to a separate buffer first to detect if needed
    mut hoist_buffer := ""
    condition := expr.get(0)?
    dont_delete(condition)
    cond_str := emit_arg_string(condition, Ptr(), false, hoist_buffer, indent.add(1), ctx, context)?

    if hoist_buffer.is_empty() {
        // No hoisting needed - use simple while
        output.push_str(indent_str)
        output.push_str("while (")
        output.push_str(cond_str)
        // Bool is a struct with .data field - extract for C truthiness
        mut have_while_vt := true
        vt := get_value_type(context, condition)?
        catch (err: Str) {
            have_while_vt = false
        }
        if have_while_vt {
            switch vt {
            case ValueType.TCustom(type_name):
                if type_name.eq("Bool") {
                    output.push_str(".data")
                }
            case:
            }
        }
        output.push_str(") {\n")
    } else {
        // Hoisting needed in condition - transform to while(1) with hoisted calls and break
        output.push_str(indent_str)
        output.push_str("while (1) {\n")

        // Emit hoisted statements inside the loop
        output.push_str(hoist_buffer)

        // Emit condition check with break
        inner_indent_str := repeat("    ", indent.add(1))
        output.push_str(inner_indent_str)
        output.push_str("if (!(")
        output.push_str(cond_str)
        // Bool is a struct with .data field - extract for C truthiness
        mut have_inner_vt := true
        inner_vt := get_value_type(context, condition)?
        catch (err: Str) {
            have_inner_vt = false
        }
        if have_inner_vt {
            switch inner_vt {
            case ValueType.TCustom(type_name):
                if type_name.eq("Bool") {
                    output.push_str(".data")
                }
            case:
            }
        }
        output.push_str(")) break;\n")
    }

    // Don't save/restore declared_vars - TIL has function-level scoping
    // Variables declared in loops stay declared for the rest of the function
    // Bug #144: Save/restore current_ref_params to scope pointer semantics from create_alias
    saved_ref_params := ctx.current_ref_params.clone()
    emit_body(body, output, indent.add(1), ctx, context)?
    ctx.current_ref_params = saved_ref_params
    dont_delete(saved_ref_params)

    output.push_str(indent_str)
    output.push_str("}\n")
}

emit_break := proc(_expr: Expr, mut output: Str, indent: I64) {
    indent_str := repeat("    ", indent)
    output.push_str(indent_str)
    output.push_str("break;\n")
}

emit_continue := proc(_expr: Expr, mut output: Str, indent: I64) {
    indent_str := repeat("    ", indent)
    output.push_str(indent_str)
    output.push_str("continue;\n")
}

// Info about a variadic parameter
VariadicParamInfo := struct {
    mut elem_type: Str = ""
    mut regular_count: I64 = 0
}

// Info about a catch label for local throw/catch
CatchLabelInfo := struct {
    mut label: Str = ""
    mut temp_var: Str = ""
}

// Info about a catch block for code generation
// Note: TIL version also has catch_block field, but Rust accesses stmts[i] directly
CatchLabelInfoEntry := struct {
    mut stmt_index: I64 = 0        // Index in stmts array
    mut type_name: Str = ""        // Error type being caught (e.g., "Str", "IndexOutOfBoundsError")
    mut label: Str = ""            // Goto label for this catch
    mut temp_var: Str = ""         // Temp variable holding the thrown value
}

// Info about a function parameter's type and whether passed by reference
ParamTypeInfo := struct {
    mut value_type: ValueType = ValueType.TCustom("")
    mut has_value_type: Bool = false  // tracks if value_type is set (Option pattern)
    mut by_ref: Bool = false
}

// Info about a collected variable declaration for hoisting
CollectedDeclaration := struct {
    mut name: Str = ""
    mut value_type: ValueType = ValueType.TCustom("")
}

// Info about a variadic function call
VariadicFCallInfo := struct {
    mut elem_type: Str = ""
    mut regular_count: I64 = 0
}

/// Collect all variable declarations in a body (recursively) for hoisting
/// Returns Vec of CollectedDeclaration structs
collect_declarations_in_body := proc(body: Expr, mut context: Context) returns Vec throws Str {
    mut decls := Vec.new(CollectedDeclaration)
    // Track collected declarations for lookups during type inference
    mut collected := Map.new(Str, ValueType)

    switch body.node_type {
    case NodeType.Body:
        for stmt: Expr in body.params {
            collect_declarations_recursive(stmt, decls, collected, context)?
        }
    case:
        collect_declarations_recursive(body, decls, collected, context)?
    }

    return decls
}

collect_declarations_recursive := proc(expr: Expr, mut decls: Vec, mut collected: Map, mut context: Context) throws Str {
    switch expr.node_type {
    case NodeType.Declaration(decl):
        // INFER_TYPE should have been resolved by typer
        switch decl.value_type {
        case ValueType.TCustom(name):
            if name.eq(INFER_TYPE) {
                panic(loc(), "Declaration '", decl.name, "' has INFER_TYPE in ccodegen - should have been resolved by typer")
            }
        case:
        }
        value_type := decl.value_type
        // Track this declaration for future lookups (both in collected and scope_stack)
        collected.set(decl.name.clone(), value_type.clone())
        // Also add to scope_stack so get_value_type can find it for subsequent declarations
        mut symbol_info := SymbolInfo()
        symbol_info.value_type = value_type
        symbol_info.is_mut = decl.is_mut
        symbol_info.is_copy = false
        symbol_info.is_own = false
        symbol_info.is_comptime_const = false
        context.scope_stack.declare_symbol(decl.name.clone(), symbol_info)?
        mut collected_decl := CollectedDeclaration()
        collected_decl.name = decl.name.clone()
        collected_decl.value_type = value_type
        decls.push(collected_decl)
    case NodeType.Body:
        for stmt: Expr in expr.params {
            collect_declarations_recursive(stmt, decls, collected, context)?
        }
    case NodeType.If:
        // Recurse into if branches
        if expr.params.len().gteq(2) {
            second_param := expr.get(1)?
            dont_delete(second_param)
            collect_declarations_recursive(second_param, decls, collected, context)?
        }
        if expr.params.len().gteq(3) {
            third_param := expr.get(2)?
            dont_delete(third_param)
            collect_declarations_recursive(third_param, decls, collected, context)?
        }
    case NodeType.While:
        // Recurse into while body
        if expr.params.len().gteq(2) {
            while_body := expr.get(1)?
            dont_delete(while_body)
            collect_declarations_recursive(while_body, decls, collected, context)?
        }
    case NodeType.Catch:
        // Recurse into catch body (params[2] is catch body)
        // params[0] = err_var, params[1] = err_type, params[2] = body
        if expr.params.len().gteq(3) {
            catch_body := expr.get(2)?
            dont_delete(catch_body)
            collect_declarations_recursive(catch_body, decls, collected, context)?
        }
    case:
        // REM: other node types don't contain declarations
    }

}

emit_fcall := proc(expr: Expr, mut output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) throws Str {
    // First param is the function name (or Type.method for type-qualified calls)
    if expr.params.is_empty() {
        throw "ccodegen: FCall with no params"
    }

    // Get function name (handles both nested identifiers and precomp'd "Type.method" strings)
    first_param := expr.get(0)?
    dont_delete(first_param)
    mut func_name := get_func_name_string(first_param)?
    if func_name.is_empty() {
        throw "ccodegen: FCall first param not Identifier"
    }

    // For builtins, we need the original name with dots
    orig_func_name := get_til_func_name_string(first_param)?

    // Check if this is a call to a nested (hoisted) function - use mangled name
    if ctx.nested_func_names.contains_key(orig_func_name) {
        ctx.nested_func_names.get(orig_func_name, func_name)?
    }

    // Named args are already reordered by precomp, so no reordering needed here

    // Check for struct literal: TypeName(field=value, ...) -> (til_TypeName){.field = value, ...}
    // Uses lookup_struct to properly identify struct types (like interpreter.rs)
    mut has_named_args := false
    mut arg_idx := 1
    while arg_idx.lt(expr.params.len()) {
        arg := expr.get(arg_idx)?
        dont_delete(arg)
        switch arg.node_type {
        case NodeType.NamedArg(_):
            has_named_args = true
        case:
            // REM: not a named arg
        }
        arg_idx.inc()
    }

    if has_named_args {
        struct_def := context.scope_stack.lookup_struct(func_name)?
        // Emit C compound literal: (til_TypeName){.field1 = val1, .field2 = val2, ...}
        output.push_str("(")
            output.push_str(TIL_PREFIX)
            output.push_str(func_name)
            output.push_str(")")

            // Build map of named arg values
            mut named_values := Map.new(Str, Expr)
            mut na_idx := 1
            while na_idx.lt(expr.params.len()) {
                na_arg := expr.get(na_idx)?
                dont_delete(na_arg)
                switch na_arg.node_type {
                case NodeType.NamedArg(field_name):
                    if not(na_arg.params.is_empty()) {
                        na_value_expr := na_arg.get(0)?
                        dont_delete(na_value_expr)
                        named_values.set(field_name.clone(), na_value_expr.clone())
                    }
                case:
                    // REM: not a named arg
                }
                na_idx.inc()
            }

            if struct_def.members.is_empty().or(struct_def.default_values.is_empty()) {
                output.push_str("{}")
            } else {
                output.push_str("{")
                mut first := true
                for member: Declaration in struct_def.members {
                    if not(member.is_mut) {
                        continue
                    }
                    if not(first) {
                        output.push_str(", ")
                    }
                    first = false
                    output.push_str(".")
                    output.push_str(member.name)
                    output.push_str(" = ")
                    // Use named arg value if provided, otherwise use default
                    // Bug #143: At expression level, use emit_expr - caller should have used emit_arg_string for hoisting
                    if named_values.contains_key(member.name) {
                        nv_ref := named_values.get_by_ref(member.name)?
                        create_alias(member_value_expr, Expr, nv_ref.data)
                        emit_expr(member_value_expr, output, 0, ctx, context)?
                    } else {
                        val_ref := struct_def.default_values.get_by_ref(member.name)?
                        create_alias(default_expr, Expr, val_ref.data)
                        emit_expr(default_expr, output, 0, ctx, context)?
                        catch (err: KeyNotFoundError) {
                            output.push_str("0")
                        }
                    }
                }
                output.push_str("}")
            }
            return
        catch (err: KeyNotFoundError) {
            // REM: not a struct, continue below
        }
    }

    indent_str := repeat("    ", indent)

    // Statement level vs expression level:
    // - indent > 0: statement level (can emit hoisting statements, adds semicolon)
    // - indent == 0: expression level (nested in larger expression, no hoisting, no semicolons)
    is_stmt_level := indent.gt(0)

    // Get variadic info to know which args are variadic (need different handling)
    mut variadic_regular_count := 9999999
    mut have_variadic_info := true
    mut variadic_info_check := VariadicFCallInfo()
    ctx.func_variadic_args.get(orig_func_name, variadic_info_check)?
    variadic_regular_count = variadic_info_check.regular_count
    catch (err: KeyNotFoundError) {
        have_variadic_info = false
    }

    // Bug #143: Process ALL args upfront using single-pass emit_arg_string
    mut arg_strings := Vec.new(Str)
    if is_stmt_level.and(expr.params.len().gt(1)) {
        // Get param info for by-ref handling
        mut param_info := Vec.new(ParamTypeInfo)
        fd := get_fcall_func_def(context, expr)?
        for fd_arg: Declaration in fd.args {
            mut pi := ParamTypeInfo()
            pi.value_type = fd_arg.value_type
            pi.has_value_type = true
            pi.by_ref = param_needs_by_ref(fd_arg)
            param_info.push(pi)
        }
        catch (err: KeyNotFoundError) {
            // Handle builtins that don't have function definitions
            if orig_func_name.eq("enum_to_str").or(orig_func_name.eq("type_as_str")) {
                // These builtins take Dynamic arg - need proper hoisting
                mut builtin_pi := ParamTypeInfo()
                builtin_pi.value_type = ValueType.TCustom("Dynamic")
                builtin_pi.has_value_type = true
                builtin_pi.by_ref = false
                param_info.push(builtin_pi)
            }
        }
        mut prep_i := 0
        mut prep_arg_idx := 1
        while prep_arg_idx.lt(expr.params.len()) {
            prep_arg := expr.get(prep_arg_idx)?
            dont_delete(prep_arg)
            // For variadic args (i >= regular_count), use by_ref=false because
            // they get stored in temp vars, not passed directly to the function.
            prep_is_variadic_arg := prep_i.gteq(variadic_regular_count)
            mut prep_param_type := Ptr()
            mut prep_by_ref := false
            if prep_i.lt(param_info.len()) {
                prep_loop_pi_ref := param_info.get_by_ref(prep_i)?
                create_alias(prep_loop_pi, ParamTypeInfo, prep_loop_pi_ref.data)
                prep_param_type.data = to_ptr(prep_loop_pi.value_type)
                if prep_is_variadic_arg {
                    prep_by_ref = false
                } else {
                    prep_by_ref = prep_loop_pi.by_ref
                }
            }
            prep_arg_str := emit_arg_string(prep_arg, prep_param_type, prep_by_ref, output, indent, ctx, context)?
            arg_strings.push(prep_arg_str)
            prep_i.inc()
            prep_arg_idx.inc()
        }
    }
    catch (err: IndexOutOfBoundsError) { throw err.msg }

    // Hardcoded builtins (compile-time intrinsics that can't be implemented in TIL)
    if orig_func_name.eq("type_as_str") {
        // type_as_str(T) - get type name as string
        if expr.params.len().lt(2) {
            throw "ccodegen: type_as_str requires 1 argument"
        }
        type_arg := expr.get(1)?
        dont_delete(type_arg)
        switch type_arg.node_type {
        case NodeType.Identifier(type_name):
            // Check if this is a Type variable or a literal type name
            mut is_type_var := false
            sym := context.scope_stack.lookup_symbol(type_name)?
            switch sym.value_type {
            case ValueType.TCustom(t):
                if t.eq("Type") {
                    is_type_var = true
                }
            case:
                // REM: not Type
            }
            catch (err: KeyNotFoundError) {
                // REM: symbol not found
            }
            if is_type_var {
                // Type variable - already a const char*, wrap in Str struct literal
                // Bug #97: Use type-mangled name for Type variable reference
                // Use new Str format with Ptr { data, is_borrowed, alloc_size, elem_type, elem_size }
                type_c_var_name := til_var_name_from_context(type_name, context)
                output.push_str("((")
                output.push_str(TIL_PREFIX)
                output.push_str("Str){((")
                output.push_str(TIL_PREFIX)
                output.push_str("Ptr){(")
                output.push_str(TIL_PREFIX)
                output.push_str("I64)")
                output.push_str(type_c_var_name)
                output.push_str(", 1, 0, 0, 0}), strlen(")
                output.push_str(type_c_var_name)
                output.push_str("), 0})")
            } else {
                // Literal type name - create Str compound literal
                _ := context.scope_stack.lookup_struct("Str")?
                emit_str_literal(type_name, output)
                catch (err: KeyNotFoundError) {
                    output.push_str("\"")
                    output.push_str(type_name)
                    output.push_str("\"")
                }
            }
        case:
            emit_str_literal("unknown", output)
        }
        return
    } else if orig_func_name.eq("enum_to_str") {
        // enum_to_str(e) - get enum variant name as string
        if expr.params.len().lt(2) {
            throw "ccodegen: enum_to_str requires 1 argument"
        }
        enum_arg := expr.get(1)?
        dont_delete(enum_arg)
        value_type := get_value_type(context, enum_arg)?
        switch value_type {
        case ValueType.TCustom(enum_type_name):
            // Verify it's an enum type
            _ := context.scope_stack.lookup_enum(enum_type_name)?
            output.push_str(til_name(enum_type_name))
            output.push_str("_to_str(")
            // Bug #143: At expression level, just use & - caller handles hoisting via emit_arg_string
            output.push_str("&")
            emit_expr(enum_arg, output, 0, ctx, context)?
            output.push_str(")")
            return
            catch (err: KeyNotFoundError) {
                mut err_msg := "ccodegen: enum_to_str argument '"
                err_msg.push_str(enum_type_name)
                err_msg.push_str("' is not an enum type")
                throw err_msg
            }
        case:
            throw "ccodegen: enum_to_str argument has non-custom type"
        }
    } else if orig_func_name.eq("enum_get_payload") {
        // enum_get_payload(e, payload_type, out) - extract enum payload into out parameter
        // Emits call to til_enum_get_payload runtime function
        if expr.params.len().lt(4) {
            throw "ccodegen: enum_get_payload requires 3 arguments"
        }
        payload_enum_arg := expr.get(1)?
        dont_delete(payload_enum_arg)
        payload_type_arg := expr.get(2)?
        dont_delete(payload_type_arg)
        payload_out_arg := expr.get(3)?
        dont_delete(payload_out_arg)

        output.push_str(TIL_PREFIX)
        output.push_str("enum_get_payload((")
        output.push_str(TIL_PREFIX)
        output.push_str("Dynamic*)&")
        emit_expr(payload_enum_arg, output, 0, ctx, context)?
        output.push_str(", &")
        // Emit type argument as Str (similar to size_of handling)
        switch payload_type_arg.node_type {
        case NodeType.Identifier(payload_type_name):
            // Check if this is a Type variable or a literal type name
            mut payload_is_type_var := false
            payload_sym := context.scope_stack.lookup_symbol(payload_type_name)?
            switch payload_sym.value_type {
            case ValueType.TCustom(t):
                if t.eq("Type") {
                    payload_is_type_var = true
                }
            case:
                // REM: not Type
            }
            catch (err: KeyNotFoundError) {
                // REM: symbol not found
            }
            if payload_is_type_var {
                // Type variable - wrap in Str struct literal
                // Bug #97: Use type-mangled name for Type variable reference
                // Use new Str format with Ptr { data, is_borrowed, alloc_size, elem_type, elem_size }
                payload_c_var_name := til_var_name_from_context(payload_type_name, context)
                output.push_str("((")
                output.push_str(TIL_PREFIX)
                output.push_str("Str){((")
                output.push_str(TIL_PREFIX)
                output.push_str("Ptr){(")
                output.push_str(TIL_PREFIX)
                output.push_str("I64)")
                output.push_str(payload_c_var_name)
                output.push_str(", 1, 0, 0, 0}), strlen(")
                output.push_str(payload_c_var_name)
                output.push_str("), 0})")
            } else {
                // Literal type name - create Str compound literal
                emit_str_literal(payload_type_name, output)
            }
        case:
            throw "ccodegen: enum_get_payload type argument must be a type name"
        }
        output.push_str(", (")
        output.push_str(TIL_PREFIX)
        output.push_str("Dynamic*)&")
        emit_expr(payload_out_arg, output, 0, ctx, context)?
        output.push_str(");")
        return
    } else if orig_func_name.eq("enum_get_payload_type") {
        // enum_get_payload_type: generates PayloadType_to_str(&enum_expr.payload.VariantName)
        // Args: enum_get_payload_type(enum_expr, VariantName, PayloadType)
        if expr.params.len().lt(4) {
            throw arg.lang_error(context.path, "ccodegen", "enum_get_payload_type requires 3 arguments")
        }
        ept_enum_arg := expr.get(1)?
        dont_delete(ept_enum_arg)
        variant_arg := expr.get(2)?
        dont_delete(variant_arg)
        ept_payload_type_arg := expr.get(3)?
        dont_delete(ept_payload_type_arg)

        // Get variant name
        mut variant_name := ""
        switch variant_arg.node_type {
        case NodeType.Identifier(name):
            variant_name = name
        case:
            throw arg.lang_error(context.path, "ccodegen", "enum_get_payload_type: variant must be identifier")
        }

        // Get payload type name
        mut payload_type_name := ""
        switch ept_payload_type_arg.node_type {
        case NodeType.Identifier(name):
            payload_type_name = name
        case:
            throw arg.lang_error(context.path, "ccodegen", "enum_get_payload_type: payload type must be identifier")
        }

        // Emit enum expression
        mut enum_str := Str()
        emit_expr(ept_enum_arg, enum_str, 0, ctx, context)?

        // Generate: PayloadType_to_str(&enum_expr.payload.VariantName)
        output.push_str(til_name(payload_type_name))
        output.push_str("_to_str(&")
        output.push_str(enum_str)
        output.push_str(".payload.")
        output.push_str(variant_name)
        output.push_str(")")
        return
    } else if orig_func_name.eq("size_of") {
        // size_of(T) - runtime type size lookup via til_size_of function
        if expr.params.len().lt(2) {
            throw "ccodegen: size_of requires 1 argument"
        }
        output.push_str(TIL_PREFIX)
        output.push_str("size_of(&")
        size_type_arg := expr.get(1)?
        dont_delete(size_type_arg)
        switch size_type_arg.node_type {
        case NodeType.Identifier(type_name):
            // Check if this is a Type variable or a literal type name
            mut size_is_type_var := false
            size_sym := context.scope_stack.lookup_symbol(type_name)?
            switch size_sym.value_type {
            case ValueType.TCustom(t):
                if t.eq("Type") {
                    size_is_type_var = true
                }
            case:
                // REM: not Type
            }
            catch (err: KeyNotFoundError) {
                // REM: symbol not found
            }
            if size_is_type_var {
                // Type variable - already a const char*, wrap in Str struct literal
                // Bug #97: Use type-mangled name for Type variable reference
                // Use new Str format with Ptr { data, is_borrowed, alloc_size, elem_type, elem_size }
                size_c_var_name := til_var_name_from_context(type_name, context)
                output.push_str("((")
                output.push_str(TIL_PREFIX)
                output.push_str("Str){((")
                output.push_str(TIL_PREFIX)
                output.push_str("Ptr){(")
                output.push_str(TIL_PREFIX)
                output.push_str("I64)")
                output.push_str(size_c_var_name)
                output.push_str(", 1, 0, 0, 0}), strlen(")
                output.push_str(size_c_var_name)
                output.push_str("), 0})")
            } else {
                // Literal type name - create Str compound literal
                emit_str_literal(type_name, output)
            }
        case:
            // Not an identifier - emit as expression
            // Use new Str format with Ptr { data, is_borrowed, alloc_size, elem_type, elem_size }
            output.push_str("((")
            output.push_str(TIL_PREFIX)
            output.push_str("Str){((")
            output.push_str(TIL_PREFIX)
            output.push_str("Ptr){(")
            output.push_str(TIL_PREFIX)
            output.push_str("I64)")
            emit_expr(size_type_arg, output, 0, ctx, context)?
            output.push_str(", 1, 0, 0, 0}), strlen(")
            emit_expr(size_type_arg, output, 0, ctx, context)?
            output.push_str("), 0})")
        }
        output.push_str(")")
        return
    } else if orig_func_name.eq("to_ptr") {
        // to_ptr(var) - get address of variable
        if expr.params.len().lt(2) {
            throw "ccodegen: to_ptr requires 1 argument"
        }
        ptr_arg := expr.get(1)?
        dont_delete(ptr_arg)
        // Check if ptr_arg is a Dynamic parameter (void*) - just use the pointer directly
        mut is_dynamic_param := false
        switch ptr_arg.node_type {
        case NodeType.Identifier(name):
            if ptr_arg.params.is_empty() {
                ptr_sym := context.scope_stack.lookup_symbol(name)?
                switch ptr_sym.value_type {
                case ValueType.TCustom(t):
                    if t.eq("Dynamic") {
                        is_dynamic_param = true
                    }
                case:
                    // REM: not Dynamic
                }
                catch (err: KeyNotFoundError) {
                    // REM: symbol not found
                }
            }
        case:
            // REM: not identifier
        }
        output.push_str("(")
        output.push_str(TIL_PREFIX)
        output.push_str("I64)")
        if is_dynamic_param {
            // Dynamic param: just output the variable name (it's already a pointer)
            // Bug #97: Use type-mangled name
            switch ptr_arg.node_type {
            case NodeType.Identifier(name):
                output.push_str(til_var_name_from_context(name, context))
            case:
                // REM: should not happen
            }
        } else {
            // Bug #143: At expression level, just use & - caller handles hoisting via emit_arg_string
            output.push_str("&")
            emit_expr(ptr_arg, output, 0, ctx, context)?
        }
        return
    } else if orig_func_name.eq("create_alias") {
        // Bug #144: create_alias(var, type, addr) - compound directive
        // Declares variable, then emits: type var; memcpy(&var, (void*)addr, sizeof(type));\n
        if expr.params.len().lt(4) {
            throw "ccodegen: create_alias requires 3 arguments"
        }
        ca_var_arg2 := expr.get(1)?
        dont_delete(ca_var_arg2)
        ca_type_arg2 := expr.get(2)?
        dont_delete(ca_type_arg2)
        ca_addr_arg2 := expr.get(3)?
        dont_delete(ca_addr_arg2)
        mut ca_var_name2 := ""
        switch ca_var_arg2.node_type {
        case NodeType.Identifier(name):
            ca_var_name2 = name
        case:
            throw "ccodegen: create_alias: first argument must be an identifier"
        }
        mut ca_type_name2 := ""
        switch ca_type_arg2.node_type {
        case NodeType.Identifier(name):
            ca_type_name2 = name
        case:
            throw "ccodegen: create_alias: second argument must be an identifier"
        }
        ca_var_type2 := ValueType.TCustom(ca_type_name2)
        // Declare variable in ccodegen's scope_stack
        context.scope_stack.declare_symbol(
            ca_var_name2,
            SymbolInfo(
                value_type=ca_var_type2,
                is_mut=true,
                is_copy=false,
                is_own=false,
                is_comptime_const=false
            )
        )?
        ca_c_var_name2 := til_var_name_from_context(ca_var_name2, context)
        ca_c_type_name2 := value_type_to_c_name(ca_var_type2)?
        // Emit C variable declaration (inline, uses C99 block scoping to shadow hoisted value-type)
        ca_c_type2 := til_type_to_c(ca_var_type2)?
        output.push_str(indent_str)
        output.push_str(ca_c_type2)
        output.push_str("* ")
        output.push_str(ca_c_var_name2)
        output.push_str(";\n")
        catch (err: Str) {
            // til_type_to_c failed - skip declaration
        }
        _ := ctx.current_ref_params.insert(ca_var_name2)
        output.push_str(indent_str)
        output.push_str(ca_c_var_name2)
        output.push_str(" = (")
        output.push_str(ca_c_type_name2)
        output.push_str("*)")
        emit_expr(ca_addr_arg2, output, 0, ctx, context)?
        output.push_str(";\n")
        return
    }

    // User-defined function call
    output.push_str(indent_str)

    // Check for type-qualified call on FCall result: func_name(fcall_result, args...)
    if first_param.params.is_empty().and(expr.params.len().gteq(2)) {
        second_param := expr.get(1)?
        dont_delete(second_param)
        switch second_param.node_type {
        case NodeType.FCall(_):
            fcall_ret_type := get_value_type(context, second_param)?
            switch fcall_ret_type {
            case ValueType.TCustom(type_name):
                mut candidate := type_name.clone()
                candidate.push_str(".")
                candidate.push_str(orig_func_name)
                _ := context.scope_stack.lookup_func(candidate)?
                // Emit as Type_method(fcall_result, args...)
                output.push_str(TIL_PREFIX)
                output.push_str(type_name)
                output.push_str("_")
                output.push_str(orig_func_name)
                output.push_str("(")
                emit_expr(second_param, output, 0, ctx, context)?
                mut ri := 2
                while ri.lt(expr.params.len()) {
                    output.push_str(", ")
                    remaining_arg := expr.get(ri)?
                    dont_delete(remaining_arg)
                    emit_expr(remaining_arg, output, 0, ctx, context)?
                    ri.inc()
                }
                output.push_str(")")
                if is_stmt_level {
                    output.push_str(";\n")
                }
                return
                catch (err: KeyNotFoundError) {
                    // REM: function not found, continue below
                }
            case:
                // REM: not TCustom
            }
            catch (err: Str) {
                // REM: get_value_type failed, continue below
            }
        case:
            // REM: not FCall
        }
    }

    // Check for struct construction: TypeName() -> (til_TypeName){defaults...}
    mut struct_def_opt_exists := true
    struct_def_opt := context.scope_stack.lookup_struct(func_name)?
    catch (err: KeyNotFoundError) {
        struct_def_opt_exists = false
    }

    if struct_def_opt_exists {
    if expr.params.len().eq(1) {
        // Use the struct definition to get default values
        if struct_def_opt.members.is_empty().or(struct_def_opt.default_values.is_empty()) {
            output.push_str("(")
            output.push_str(TIL_PREFIX)
            output.push_str(func_name)
            output.push_str("){}")
        } else {
            // Bug #143: emit_fcall_arg_string handles throwing defaults via emit_arg_string

            output.push_str(indent_str)
            output.push_str("(")
            output.push_str(TIL_PREFIX)
            output.push_str(func_name)
            output.push_str("){")
            mut member_first := true
            for member: Declaration in struct_def_opt.members {
                if not(member.is_mut) {
                    continue
                }
                if not(member_first) {
                    output.push_str(", ")
                }
                member_first = false
                output.push_str(".")
                output.push_str(member.name)
                output.push_str(" = ")
                out_val_ref := struct_def_opt.default_values.get_by_ref(member.name)?
                create_alias(out_default_expr, Expr, out_val_ref.data)
                // Bug #143: At expression level, use emit_expr - caller handles hoisting via emit_arg_string
                emit_expr(out_default_expr, output, 0, ctx, context)?
                catch (err: KeyNotFoundError) {
                    output.push_str("0")
                }
            }
            output.push_str("}")
        }

        if is_stmt_level {
            output.push_str(";\n")
        }
        return
    }
    }

    // Regular function call
    // Detect and construct variadic array if needed (only at statement level)
    mut variadic_arr_var := ""
    mut has_variadic_arr := false
    if is_stmt_level.and(have_variadic_info) {
        mut variadic_info := VariadicFCallInfo()
        ctx.func_variadic_args.get(orig_func_name, variadic_info)?
        elem_type := variadic_info.elem_type.clone()
        regular_count := variadic_info.regular_count
        // Bug #143: Use pre-computed arg_strings
        mut variadic_arg_strings := Vec.new(Str)
        mut vi := regular_count
        while vi.lt(arg_strings.len()) {
            varg_str_ref := arg_strings.get_by_ref(vi)?
            create_alias(varg_str, Str, varg_str_ref.data)
            variadic_arg_strings.push(varg_str)
            vi.inc()
        }
        variadic_arr_var = emit_variadic_array_with_strings(elem_type, variadic_arg_strings, output, indent, ctx)?
        has_variadic_arr = true
        catch (err: KeyNotFoundError) {
            panic(loc(), "func_variadic_args.get failed: ", err.msg)
        }
        catch (err: IndexOutOfBoundsError) {
            panic(loc(), "arg_strings indexing failed: ", err.msg)
        }
    }

    // Check if this is an enum variant constructor (Type.Variant or Type_Variant)
    mut is_enum_constructor := false
    if func_name.contains("_") {
        parts := func_name.split("_")?
        if parts.len().gteq(2) {
            type_name_part_ref := parts.get_by_ref(0)?
            create_alias(type_name_part, Str, type_name_part_ref.data)
            variant_name_part_ref := parts.get_by_ref(1)?
            create_alias(variant_name_part, Str, variant_name_part_ref.data)
            mut ctor_have_enum_def := true
            enum_def := context.scope_stack.lookup_enum(type_name_part)?
            catch (err: KeyNotFoundError) {
                ctor_have_enum_def = false
            }
            if ctor_have_enum_def {
                if enum_def.contains_key(variant_name_part) {
                    is_enum_constructor = true
                }
            }
        }
    }

    output.push_str(TIL_PREFIX)
    if is_enum_constructor {
        ctor_parts := func_name.split("_")?
        ctor_type_name_part_ref := ctor_parts.get_by_ref(0)?
        create_alias(ctor_type_name_part, Str, ctor_type_name_part_ref.data)
        ctor_variant_name_part_ref := ctor_parts.get_by_ref(1)?
        create_alias(ctor_variant_name_part, Str, ctor_variant_name_part_ref.data)
        output.push_str(ctor_type_name_part)
        output.push_str("_make_")
        output.push_str(ctor_variant_name_part)
    } else {
        output.push_str(func_name)
    }
    output.push_str("(")

    // Check if this is a variadic function call
    if have_variadic_info {
        mut var_fcall_info := VariadicFCallInfo()
        ctx.func_variadic_args.get(orig_func_name, var_fcall_info)?
        var_regular_count := var_fcall_info.regular_count

        // Bug #143: Use pre-computed arg_strings
        if is_stmt_level {
            // Emit regular args from arg_strings
            mut vstmt_arg_i := 0
            while vstmt_arg_i.lt(var_regular_count) {
                if vstmt_arg_i.gt(0) {
                    output.push_str(", ")
                }
                vstmt_arg_str_ref := arg_strings.get_by_ref(vstmt_arg_i)?
                create_alias(vstmt_arg_str, Str, vstmt_arg_str_ref.data)
                output.push_str(vstmt_arg_str)
                vstmt_arg_i.inc()
            }
        } else {
            // Expression-level variadic calls - emit regular args with by-ref handling
            mut var_param_info := Vec.new(ParamTypeInfo)
            var_fd := get_fcall_func_def(context, expr)?
            for fd_arg: Declaration in var_fd.args {
                mut var_pi := ParamTypeInfo()
                var_pi.value_type = fd_arg.value_type
                var_pi.has_value_type = true
                var_pi.by_ref = param_needs_by_ref(fd_arg)
                var_param_info.push(var_pi)
            }
            catch (err: KeyNotFoundError) {
                // REM: function not found
            }
            mut vexpr_arg_i := 0
            mut vexpr_arg_idx := 1
            while vexpr_arg_i.lt(var_regular_count) {
                if vexpr_arg_i.gt(0) {
                    output.push_str(", ")
                }
                vexpr_arg := expr.get(vexpr_arg_idx)?
                dont_delete(vexpr_arg)
                mut vexpr_by_ref := false
                if vexpr_arg_i.lt(var_param_info.len()) {
                    vexpr_loop_pi_ref := var_param_info.get_by_ref(vexpr_arg_i)?
                    create_alias(vexpr_loop_pi, ParamTypeInfo, vexpr_loop_pi_ref.data)
                    vexpr_by_ref = vexpr_loop_pi.by_ref
                }
                if vexpr_by_ref.and(is_pure_lvalue(vexpr_arg, context)?) {
                    switch vexpr_arg.node_type {
                    case NodeType.Identifier(name):
                        if vexpr_arg.params.is_empty().and(ctx.current_ref_params.contains(name).or(ctx.current_variadic_params.contains_key(name))) {
                            // Already a pointer - emit name directly without dereference
                            // Bug #97: Use type-mangled name
                            output.push_str(til_var_name_from_context(name, context))
                        } else {
                            output.push_str("&")
                            emit_expr(vexpr_arg, output, 0, ctx, context)?
                        }
                    case:
                        output.push_str("&")
                        emit_expr(vexpr_arg, output, 0, ctx, context)?
                    }
                } else {
                    emit_expr(vexpr_arg, output, 0, ctx, context)?
                }
                vexpr_arg_i.inc()
                vexpr_arg_idx.inc()
            }
        }

        // Emit variadic array pointer
        if has_variadic_arr {
            if var_regular_count.gt(0) {
                output.push_str(", ")
            }
            output.push_str("&")
            output.push_str(variadic_arr_var)
        }
        catch (err: KeyNotFoundError) {
            // REM: variadic info lookup failed
        }
    } else {
        // Regular non-variadic function call
        // Bug #143: Use pre-computed arg_strings
        if is_stmt_level {
            mut nstmt_arg_i := 0
            while nstmt_arg_i.lt(arg_strings.len()) {
                if nstmt_arg_i.gt(0) {
                    output.push_str(", ")
                }
                nstmt_arg_str_ref := arg_strings.get_by_ref(nstmt_arg_i)?
                create_alias(nstmt_arg_str, Str, nstmt_arg_str_ref.data)
                output.push_str(nstmt_arg_str)
                nstmt_arg_i.inc()
            }
        } else {
            // Expression-level call: emit args with by-ref handling
            mut nv_param_info := Vec.new(ParamTypeInfo)
            nv_fd := get_fcall_func_def(context, expr)?
            for fd_arg: Declaration in nv_fd.args {
                mut nv_pi := ParamTypeInfo()
                nv_pi.value_type = fd_arg.value_type
                nv_pi.has_value_type = true
                nv_pi.by_ref = param_needs_by_ref(fd_arg)
                nv_param_info.push(nv_pi)
            }
            catch (err: KeyNotFoundError) {
                // REM: function not found
            }

            // Bug #143: Process all args through emit_arg_string to handle hoisting
            mut nexpr_arg_hoist := ""
            mut nexpr_arg_strings := Vec.new(Str)
            mut nexpr_arg_i := 0
            mut nexpr_expr_i := 1
            while nexpr_expr_i.lt(expr.params.len()) {
                nexpr_arg := expr.get(nexpr_expr_i)?
                dont_delete(nexpr_arg)
                // Check if nexpr_arg is a type identifier - emit as string literal
                mut nexpr_type_arg_name := ""
                nexpr_type_arg_name = get_type_arg_name(nexpr_arg, context)?
                catch (err: KeyNotFoundError) {
                    nexpr_type_arg_name = ""
                }
                if not(nexpr_type_arg_name.is_empty()) {
                    nexpr_arg_strings.push(format("\"", nexpr_type_arg_name, "\""))
                } else {
                    mut nexpr_param_type := Ptr()
                    mut nexpr_by_ref := false
                    if nexpr_arg_i.lt(nv_param_info.len()) {
                        nexpr_loop_pi_ref := nv_param_info.get_by_ref(nexpr_arg_i)?
                        create_alias(nexpr_loop_pi, ParamTypeInfo, nexpr_loop_pi_ref.data)
                        nexpr_param_type.data = to_ptr(nexpr_loop_pi.value_type)
                        nexpr_by_ref = nexpr_loop_pi.by_ref
                    }
                    nexpr_arg_str := emit_arg_string(nexpr_arg, nexpr_param_type, nexpr_by_ref, nexpr_arg_hoist, indent, ctx, context)?
                    nexpr_arg_strings.push(nexpr_arg_str)
                }
                nexpr_arg_i.inc()
                nexpr_expr_i.inc()
            }

            // Emit the collected arg strings
            mut nexpr_first_arg := true
            for nexpr_out_str: Str in nexpr_arg_strings {
                if not(nexpr_first_arg) {
                    output.push_str(", ")
                }
                nexpr_first_arg = false
                output.push_str(nexpr_out_str)
            }
        }
    }

    output.push_str(")")
    if is_stmt_level {
        output.push_str(";\n")
    }

    // Emit Array.delete if variadic array was constructed
    if has_variadic_arr {
        output.push_str(indent_str)
        output.push_str(TIL_PREFIX)
        output.push_str("Array_delete(&")
        output.push_str(variadic_arr_var)
        output.push_str(");\n")
    }

    catch (err: IndexOutOfBoundsError) {
        throw err.msg
    }
}

// Emit a Str literal as a compound literal
// is_borrowed=1 for string literals (they point to static .rodata, don't free)
emit_str_literal := proc(s: Str, mut output: Str) {
    // Str { c_string: Ptr { data: I64, is_borrowed: I64 }, _len: I64, cap: I64 }
    output.push_str("((")
    output.push_str(TIL_PREFIX)
    output.push_str("Str){((")
    output.push_str(TIL_PREFIX)
    output.push_str("Ptr){(")
    output.push_str(TIL_PREFIX)
    output.push_str("I64)\"")
    // Escape special characters for C string literals
    mut i := 0
    while i.lt(s.len()) {
        c := s.char_at(i)
        null_byte := U8.from_i64(0)?
        newline := U8.from_i64(10)?
        carriage_return := U8.from_i64(13)?
        tab := U8.from_i64(9)?
        backslash := U8.from_i64(92)?
        quote := U8.from_i64(34)?
        if c.eq(null_byte) {
            output.push_str("\\0")
        } else if c.eq(newline) {
            output.push_str("\\n")
        } else if c.eq(carriage_return) {
            output.push_str("\\r")
        } else if c.eq(tab) {
            output.push_str("\\t")
        } else if c.eq(backslash) {
            output.push_str("\\\\")
        } else if c.eq(quote) {
            output.push_str("\\\"")
        } else {
            char_str := s.get_substr(i, add(i, 1))?
            catch (err: IndexOutOfBoundsError) {
                panic(loc(), "IndexOutOfBoundsError in emit_str_literal: ", err.msg)
            }
            output.push_str(char_str)
        }
        i.inc()
        catch (err: U8_Overflow) {
            panic(loc(), "U8_Overflow: ASCII values are valid U8, should never happen: ", err.msg)
        }
    }
    // Close Ptr (data, is_borrowed=1, alloc_size=0, elem_type=0, elem_size=0), then Str (c_string, _len, cap=0 for literals)
    output.push_str("\", 1, 0, 0, 0}), ")
    output.push_str(s.len().to_str())
    output.push_str(", 0})")
}

emit_literal := proc(lit: Literal, mut output: Str, context: Context) throws Str {
    switch lit {
    case Literal.Str(s):
        _has_str := context.scope_stack.lookup_struct("Str")?
        emit_str_literal(s, output)
        catch (err: KeyNotFoundError) {
            // No Str struct - emit raw string
            output.push_str("\"")
            // Escape special characters for C string literals
            mut j := 0
            while j.lt(s.len()) {
                c := s.char_at(j)
                null_byte := U8.from_i64(0)?
                newline := U8.from_i64(10)?
                carriage_return := U8.from_i64(13)?
                tab := U8.from_i64(9)?
                backslash := U8.from_i64(92)?
                quote := U8.from_i64(34)?
                if c.eq(null_byte) {
                    output.push_str("\\0")
                } else if c.eq(newline) {
                    output.push_str("\\n")
                } else if c.eq(carriage_return) {
                    output.push_str("\\r")
                } else if c.eq(tab) {
                    output.push_str("\\t")
                } else if c.eq(backslash) {
                    output.push_str("\\\\")
                } else if c.eq(quote) {
                    output.push_str("\\\"")
                } else {
                    output.push_str(c.to_str())
                }
                j.inc()
                catch (err: U8_Overflow) {
                    panic(loc(), "U8_Overflow: ASCII values are valid U8, should never happen: ", err.msg)
                }
            }
            output.push_str("\"")
        }
    case Literal.Number(n):
        output.push_str(n)
    case Literal.List(_):
        throw "ccodegen: List literals not yet supported"
    }
}
