// C code generator for TIL
// Translates TIL AST to C source code

mode liba

import("self.parser")
import("self.init")
import("self.typer")

// Prefix for all TIL-generated names in C code (structs, functions, types)
TIL_PREFIX := "til_"

// Codegen context for tracking function info
CodegenContext := struct {
    // Counter for generating unique temp variable names (deterministic per-compilation)
    mut mangling_counter: I64 = 0
    // Map function name -> variadic arg info
    mut func_variadic_args: Map = Map.new(Str, VariadicParamInfo)
    // Currently generating function's throw types (if any)
    mut current_throw_types: Vec = Vec.new(ValueType)
    // Currently generating function's return types (if any)
    mut current_return_types: Vec = Vec.new(ValueType)
    // Set of declared variable names in current function (to avoid redefinition)
    mut declared_vars: Set = Set.new(Str)
    // Set of by-ref param names in current function - for using -> instead of . for field access
    mut current_ref_params: Set = Set.new(Str)
    // Map of variadic param names to their element type (e.g., "args" -> "Bool")
    // Passed as til_Array* so need dereference, and need type info for Array.get casting
    mut current_variadic_params: Map = Map.new(Str, Str)
    // All known type names for generating til_size_of function
    mut known_types: Vec = Vec.new(Str)
    // Map of hoisted expression addresses to their temp variable names
    // Used to track deeply nested variadic/throwing calls that have been hoisted
    mut hoisted_exprs: Map = Map.new(I64, Str)
    // Map of locally-caught error types to catch label info
    // For explicit throw statements that have a local catch block
    mut local_catch_labels: Map = Map.new(Str, CatchLabelInfo)
    // Current function name for nested function name mangling (empty at top-level)
    mut current_function_name: Str = ""
    // C code for hoisted nested function definitions
    mut hoisted_functions: Vec = Vec.new(Str)
    // C code for hoisted nested function prototypes
    mut hoisted_prototypes: Vec = Vec.new(Str)
    // Map original function name -> mangled name for nested functions
    mut nested_func_names: Map = Map.new(Str, Str)
    // Map of hoisted struct default expressions ("struct_name:member_name" -> temp var name)
    // Used to track which struct literal defaults were hoisted as temp vars
    mut hoisted_struct_defaults: Map = Map.new(Str, Str)

    new := func() returns CodegenContext {
        return CodegenContext()
    }
}

// Generate unique mangled name using context counter (deterministic)
// Includes function name prefix for uniqueness across functions
next_mangled := func(mut ctx: CodegenContext) returns Str {
    n := ctx.mangling_counter
    ctx.mangling_counter = ctx.mangling_counter.add(1)
    func_name := ctx.current_function_name
    mut result := ""
    if not(func_name.is_empty()) {
        result = format("_tmp_", func_name, "_", n.to_str())
    } else {
        result = format("_tmp_", n.to_str())
    }

    return result
}

// Returns the C name for a TIL identifier - adds TIL_PREFIX
// Exceptions: C keywords (true, false), pointer deref (*name), and generated names (_ prefix)
til_name := func(name: Str) returns Str {
    mut result := name
    switch name {
    case "true":
    case "false":
    case:
        if name.starts_with("*").or(name.starts_with("_")) {
        } else {
            result = format(TIL_PREFIX, name)
        }
    }

    return result
}

// Emit a struct literal, using compound literal syntax if already_declared
// e.g., " = {0}" vs " = (til_TypeName){0}"
emit_struct_literal_assign := proc(mut output: Str, type_name: Str, already_declared: Bool, literal_content: Str) {
    output.push_str(" = ")
    if already_declared {
        output.push_str("(")
        output.push_str(til_name(type_name))
        output.push_str(")")
    }
    output.push_str(literal_content)
    output.push_str(";\n")
}

// Start a struct literal assignment - caller will emit content and closing brace
// e.g., " = " vs " = (til_TypeName)"
emit_struct_literal_start := proc(mut output: Str, type_name: Str, already_declared: Bool) {
    output.push_str(" = ")
    if already_declared {
        output.push_str("(")
        output.push_str(til_name(type_name))
        output.push_str(")")
    }
}

// Returns the C name for a TIL function - adds TIL_PREFIX and converts dots to underscores
// Used for function names like Array.len -> til_Array_len
til_func_name := func(name: Str) returns Str throws Str {
    result := format(TIL_PREFIX, name.replace(".", "_"))

    catch (err: AllocError) { throw err.msg }

    return result
}

// Get function name from FCall's first param, handling both AST patterns:
// - Identifier("func") with params = [] -> "func"
// - Identifier("I64.inc") with params = [] -> "I64_inc" (from precomp)
// - Identifier("Vec") with params = [Identifier("new")] -> "Vec_new" (from parser)
// Returns the C-ready name with underscores (not dots)
get_func_name_string := func(first_param: Expr) returns Str throws Str {
    switch first_param.node_type {
    case NodeType.Identifier(name):
        if first_param.params.is_empty() {
            return name.replace(".", "_")
        } else {
            mut parts := Vec.new(Str)
            parts.push(name)
            for p: Expr in first_param.params {
                switch p.node_type {
                case NodeType.Identifier(part_name):
                    parts.push(part_name)
                case:
                }
            }
            return parts.join("_")
        }
    case:
        throw "get_func_name_string: not an Identifier"
    }

    catch (err: AllocError) { throw err.msg }
}

// Get TIL function name from FCall's first param (for scope lookups)
// Returns the dot-separated name like "Vec.new" or "I64.inc"
// This avoids the fragile .replacen('_', ".", 1) hack which breaks on type names with underscores
get_til_func_name_string := func(first_param: Expr) returns Str throws Str {
    switch first_param.node_type {
    case NodeType.Identifier(name):
        if first_param.params.is_empty() {
            // Already has dots (from precomp) or is a simple name
            return name
        } else {
            // Build dotted name from nested identifiers
            mut parts := Vec.new(Str)
            parts.push(name)
            for p: Expr in first_param.params {
                switch p.node_type {
                case NodeType.Identifier(part_name):
                    parts.push(part_name)
                case:
                }
            }
            return parts.join(".")
        }
    case:
        throw "get_til_func_name_string: not an Identifier"
    }

    catch (err: AllocError) { throw err.msg }
}

/// Check if an expression is a type identifier (a Type parameter at call site)
/// Returns the type name if it is, so it can be emitted as a string literal
/// Matches interpreter.rs behavior (line 1703-1713)
/// Only matches STANDALONE identifiers - not field access like Vec.INIT_CAP
get_type_arg_name := func(expr: Expr, context: Context) returns Str throws KeyNotFoundError {
    switch expr.node_type {
    case NodeType.Identifier(name):
        // Only match standalone identifiers (no field access)
        if not(expr.params.is_empty()) {
            throw KeyNotFoundError(msg="field access, not type arg")
        }
        // Check if this identifier is a type name (struct, enum, or builtin type)
        sym := context.scope_stack.lookup_symbol(name)
        switch sym.value_type {
        case ValueType.TType:
            return name
        case:
            throw KeyNotFoundError(msg="not a type")
        }
    case:
        throw KeyNotFoundError(msg="not an identifier")
    }
}

// Get function definition for an FCall expression
// Uses get_func_def_for_fcall_with_expr from typer.rs which handles all cases correctly
// (regular functions, struct methods, UFCS - though UFCS is already resolved by precomp)
// Returns None for struct/enum constructors
get_fcall_func_def := proc(context: Context, fcall_expr: Expr) returns SFuncDef throws KeyNotFoundError {
    mut expr_clone := fcall_expr.clone()
    result := get_func_def_for_fcall_with_expr(context, expr_clone)

    catch (err: AllocError) { throw KeyNotFoundError(msg=err.msg) }
    catch (err: Str) { throw KeyNotFoundError(msg=err) }

    return result
}

/// Bug #60: Determine if a parameter should be passed by reference (pointer)
/// - copy params: pass by value (copy is made)
/// - Type params: already pointers (const char*), pass by value
/// - All other const/own/mut params (including Dynamic): pass by pointer
param_needs_by_ref := func(param: Declaration) returns Bool {
    if param.is_copy {
        return false
    }
    // Type is already a pointer type in C (const char*), no extra indirection needed
    switch param.value_type {
    case ValueType.TCustom(type_name):
        if type_name.eq("Type") {
            return false
        }
        return true
    case:
        return true
    }
}

/// Extract struct field type dependencies for topological sorting
/// Returns the type name if it's a custom type that needs to be defined first
get_field_type_dependency := func(value_type: ValueType) returns Str throws KeyNotFoundError {
    switch value_type {
    case ValueType.TCustom(name):
        // I64 and U8 are primitives, not struct dependencies
        switch name {
        case "I64":
            throw KeyNotFoundError(msg="primitive")
        case "U8":
            throw KeyNotFoundError(msg="primitive")
        case:
            if name.eq(INFER_TYPE) {
                throw KeyNotFoundError(msg="infer type")
            }
            return name
        }
    case:
        throw KeyNotFoundError(msg="not custom type")
    }
}

/// Get struct dependencies (other struct types used as fields)
get_struct_dependencies := func(expr: Expr) returns Vec throws AllocError, Str {
    mut deps := Vec.new(Str)
    switch expr.node_type {
    case NodeType.Declaration:
        if not(expr.params.is_empty()) {
            mut first_param := Expr()
            expr.params.get(0, first_param)
            switch first_param.node_type {
            case NodeType.StructDef(struct_def):
                for member: Declaration in struct_def.members {
                    if member.is_mut {
                        dep := get_field_type_dependency(member.value_type)
                        deps.push(dep)
                        catch (err: KeyNotFoundError) {
                            // REM: Not a custom type dependency, skip
                        }
                    }
                }
            case:
            }
        }
    case:
    }

    catch (err: IndexOutOfBoundsError) { throw err.msg }

    return deps
}

/// Get struct name from a struct declaration expression
get_struct_name := func(expr: Expr) returns Str throws KeyNotFoundError, Str {
    switch expr.node_type {
    case NodeType.Declaration(decl):
        if not(expr.params.is_empty()) {
            mut first_param := Expr()
            expr.params.get(0, first_param)
            switch first_param.node_type {
            case NodeType.StructDef:
                return decl.name
            case:
            }
        }
    case:
    }

    catch (err: IndexOutOfBoundsError) { throw err.msg }

    throw KeyNotFoundError(msg="not a struct declaration")
}

/// Get enum dependencies (types used in payloads) as a Vec
get_enum_dependencies := func(expr: Expr) returns Vec throws AllocError, Str {
    mut deps := Vec.new(Str)
    switch expr.node_type {
    case NodeType.Declaration:
        if not(expr.params.is_empty()) {
            mut first_param := Expr()
            expr.params.get(0, first_param)
            switch first_param.node_type {
            case NodeType.EnumDef(enum_def):
                for variant: EnumVariant in enum_def.variants {
                    if not(variant.payload_type.is_null()) {
                        mut pt := ValueType.TCustom("")
                        memcpy(to_ptr(pt), variant.payload_type.data, size_of(ValueType))
                        switch pt {
                        case ValueType.TCustom(type_name):
                            // Skip primitives (but NOT Str or Bool - they're structs)
                            if not(type_name.eq("I64")).and(not(type_name.eq("U8"))).and(not(type_name.eq("Dynamic"))).and(not(type_name.eq("Type"))) {
                                deps.push(type_name.clone())
                            }
                        case:
                        }
                    }
                }
            case:
            }
        }
    case:
    }

    catch (err: IndexOutOfBoundsError) { throw err.msg }

    return deps
}

/// Get enum name from an enum-with-payloads declaration expression
get_enum_name := func(expr: Expr) returns Str throws KeyNotFoundError, Str {
    switch expr.node_type {
    case NodeType.Declaration(decl):
        if not(expr.params.is_empty()) {
            mut first_param := Expr()
            expr.params.get(0, first_param)
            switch first_param.node_type {
            case NodeType.EnumDef:
                return decl.name
            case:
            }
        }
    case:
    }

    catch (err: IndexOutOfBoundsError) { throw err.msg }

    throw KeyNotFoundError(msg="not an enum declaration")
}

/// Get dependencies for a type (struct or enum-with-payloads)
get_type_dependencies := func(expr: Expr) returns Vec throws AllocError, Str {
    if is_struct_declaration(expr) {
        return get_struct_dependencies(expr)
    } else if is_enum_declaration(expr).and(is_enum_with_payloads(expr)) {
        return get_enum_dependencies(expr)
    } else {
        return Vec.new(Str)
    }
}

/// Get name for a type (struct or enum-with-payloads)
get_type_name := func(expr: Expr) returns Str throws KeyNotFoundError, Str {
    if is_struct_declaration(expr) {
        return get_struct_name(expr)
    } else if is_enum_declaration(expr).and(is_enum_with_payloads(expr)) {
        return get_enum_name(expr)
    } else {
        throw KeyNotFoundError(msg="not a type declaration")
    }
}

/// Topologically sort type declarations (structs and enums-with-payloads) by their dependencies
/// Returns indices into the original vector in sorted order
topological_sort_types := func(types: Vec) returns Vec throws Str {
    // Build name -> index map
    mut name_to_idx := Map.new(Str, I64)
    mut idx := 0
    while idx.lt(types.len()) {
        mut expr := Expr()
        types.get(idx, expr)
        name := get_type_name(expr)
        name_to_idx.set(name, idx)
        catch (err: KeyNotFoundError) {
            // REM: Not a type declaration, skip
        }
        idx.inc()
    }

    // Build adjacency list (dependencies)
    mut deps := Vec.new(Vec)
    mut i := 0
    while i.lt(types.len()) {
        deps.push(Vec.new(I64))
        i.inc()
    }
    idx = 0
    while idx.lt(types.len()) {
        mut expr := Expr()
        types.get(idx, expr)
        dep_names := get_type_dependencies(expr)
        for dep_name: Str in dep_names {
            if name_to_idx.contains_key(dep_name) {
                mut dep_idx := 0
                name_to_idx.get(dep_name, dep_idx)
                if not(dep_idx.eq(idx)) {
                    mut dep_vec := Vec.new(I64)
                    deps.get(idx, dep_vec)
                    dep_vec.push(dep_idx)
                    deps.set(idx, dep_vec)
                }
            }
        }
        idx.inc()
    }

    // Kahn's algorithm for topological sort
    // If A depends on B, B must come first, so reverse the edges
    mut reverse_deps := Vec.new(Vec)
    i = 0
    while i.lt(types.len()) {
        reverse_deps.push(Vec.new(I64))
        i.inc()
    }
    idx = 0
    while idx.lt(deps.len()) {
        mut dep_list := Vec.new(I64)
        deps.get(idx, dep_list)
        for dep: I64 in dep_list {
            mut rev_dep_vec := Vec.new(I64)
            reverse_deps.get(dep, rev_dep_vec)
            rev_dep_vec.push(idx)
            reverse_deps.set(dep, rev_dep_vec)
        }
        idx.inc()
    }

    // Calculate in-degree for reversed graph
    mut in_degree := Vec.new(I64)
    i = 0
    while i.lt(types.len()) {
        in_degree.push(0)
        i.inc()
    }
    for dep_list: Vec in reverse_deps {
        for dep: I64 in dep_list {
            mut degree := 0
            in_degree.get(dep, degree)
            in_degree.set(dep, degree.add(1))
        }
    }

    mut queue := Vec.new(I64)
    idx = 0
    while idx.lt(in_degree.len()) {
        mut degree := 0
        in_degree.get(idx, degree)
        if degree.eq(0) {
            queue.push(idx)
        }
        idx.inc()
    }

    mut result := Vec.new(I64)
    while not(queue.is_empty()) {
        mut pop_idx := 0
        queue.pop(pop_idx)
        result.push(pop_idx)
        mut rev_dep_list := Vec.new(I64)
        reverse_deps.get(pop_idx, rev_dep_list)
        for next: I64 in rev_dep_list {
            mut next_degree := 0
            in_degree.get(next, next_degree)
            next_degree = next_degree.sub(1)
            in_degree.set(next, next_degree)
            if next_degree.eq(0) {
                queue.push(next)
            }
        }
    }

    // If we couldn't sort all (cycle), just append remaining in original order
    if result.len().lt(types.len()) {
        idx = 0
        while idx.lt(types.len()) {
            mut found := false
            for r: I64 in result {
                if r.eq(idx) {
                    found = true
                    break
                }
            }
            if not(found) {
                result.push(idx)
            }
            idx.inc()
        }
    }

    catch (err: IndexOutOfBoundsError) { throw err.msg }
    catch (err: AllocError) { throw err.msg }
    catch (err: KeyNotFoundError) { throw err.msg }

    return result
}

/// Hoist a single expression if it's a throwing call, or recursively hoist throwing calls within it.
/// Returns Some(temp_var_name) if the expression itself was hoisted, None otherwise.
/// The hoisted_exprs map in ctx is updated with any sub-expressions that were hoisted.
hoist_throwing_expr := proc(expr: Expr, mut output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) returns Str throws Str, KeyNotFoundError {
    indent_str := repeat("    ", indent)

    // Check if this expression itself is a throwing call
    switch expr.node_type {
    case NodeType.FCall:
        throwing_fd := get_fcall_func_def(context, expr)
        catch (err: KeyNotFoundError) {
            // REM: Not a function call we can look up, continue
        }
        if not(throwing_fd.throw_types.is_empty()) {
            // First, recursively hoist any throwing calls in this call's arguments
            mut nested_hoisted := Map.new(I64, Str)
            if expr.params.len().gt(1) {
                mut nested_args := Vec.new(Expr)
                mut i := 1
                while i.lt(expr.params.len()) {
                    mut arg := Expr()
                    expr.params.get(i, arg)
                    nested_args.push(arg)
                    i.inc()
                }
                nested_vec := hoist_throwing_args(nested_args, output, indent, ctx, context)
                for h: HoistedArg in nested_vec {
                    nested_hoisted.set(h.index, h.temp_var)
                }
            }

            temp_var := next_mangled(ctx)

            // Determine the C type for the temp variable
            mut c_type := ""
            if not(throwing_fd.return_types.is_empty()) {
                mut first_ret := ValueType.TCustom("")
                throwing_fd.return_types.get(0, first_ret)
                c_type = til_type_to_c(first_ret)
            } else {
                throw expr.lang_error(context.path, "ccodegen", "Cannot hoist throwing call with no return type")
            }

            // Declare temp variable
            output.push_str(indent_str)
            output.push_str(c_type)
            output.push_str(" ")
            output.push_str(temp_var)
            output.push_str(";\n")

            // Declare error variables for each throw type
            temp_suffix := next_mangled(ctx)
            mut err_idx := 0
            for throw_type: ValueType in throwing_fd.throw_types {
                switch throw_type {
                case ValueType.TCustom(type_name):
                    output.push_str(indent_str)
                    output.push_str(til_name(type_name))
                    output.push_str(" _err")
                    output.push_str(err_idx.to_str())
                    output.push_str("_")
                    output.push_str(temp_suffix)
                    output.push_str(";\n")
                case:
                }
                err_idx.inc()
            }

            // Detect and construct variadic array if needed
            variadic_info := detect_variadic_fcall(expr, ctx)
            mut variadic_arr_var := ""
            mut has_variadic := false
            catch (err: KeyNotFoundError) {
                // REM: Not variadic, continue
            }
            if not(variadic_info.elem_type.is_empty()) {
                has_variadic = true
                mut variadic_args := Vec.new(Expr)
                mut j := add(1, variadic_info.regular_count)
                while j.lt(expr.params.len()) {
                    mut arg := Expr()
                    expr.params.get(j, arg)
                    variadic_args.push(arg)
                    j.inc()
                }
                variadic_arr_var = hoist_variadic_args(variadic_info.elem_type, variadic_args, nested_hoisted, variadic_info.regular_count, output, indent, ctx, context)
            }

            // Emit the function call with output pointers
            output.push_str(indent_str)
            output.push_str("int _status_")
            output.push_str(temp_suffix)
            output.push_str(" = ")

            // Emit the function name and args (using nested hoisted temps)
            mut variadic_ptr := ""
            if has_variadic {
                variadic_ptr = variadic_arr_var
            }
            emit_fcall_name_and_args_for_throwing(expr, temp_var, temp_suffix, throwing_fd.throw_types, nested_hoisted, variadic_ptr, output, ctx, context)

            output.push_str(";\n")

            // Emit error checking - propagate or goto based on context
            // Check if there are local catch labels for these error types
            mut has_local_catch := false
            for throw_type: ValueType in throwing_fd.throw_types {
                switch throw_type {
                case ValueType.TCustom(type_name):
                    if ctx.local_catch_labels.contains_key(type_name) {
                        has_local_catch = true
                        break
                    }
                case:
                }
            }

            if has_local_catch {
                // Use goto for local catches
                err_idx = 0
                for throw_type: ValueType in throwing_fd.throw_types {
                    switch throw_type {
                    case ValueType.TCustom(type_name):
                        if ctx.local_catch_labels.contains_key(type_name) {
                            mut catch_info := CatchLabelInfo()
                            ctx.local_catch_labels.get(type_name, catch_info)
                            output.push_str(indent_str)
                            output.push_str("if (_status_")
                            output.push_str(temp_suffix)
                            output.push_str(" == ")
                            output.push_str(add(err_idx, 1).to_str())
                            output.push_str(") { ")
                            output.push_str(catch_info.temp_var)
                            output.push_str(" = _err")
                            output.push_str(err_idx.to_str())
                            output.push_str("_")
                            output.push_str(temp_suffix)
                            output.push_str("; goto ")
                            output.push_str(catch_info.label)
                            output.push_str("; }\n")
                        }
                    case:
                    }
                    err_idx.inc()
                }
            } else {
                // Propagate errors
                output.push_str(indent_str)
                output.push_str("if (_status_")
                output.push_str(temp_suffix)
                output.push_str(" != 0) {\n")

                err_idx = 0
                for throw_type: ValueType in throwing_fd.throw_types {
                    switch throw_type {
                    case ValueType.TCustom(type_name):
                        mut curr_idx := 0
                        for curr_throw: ValueType in ctx.current_throw_types {
                            switch curr_throw {
                            case ValueType.TCustom(curr_type_name):
                                if curr_type_name.eq(type_name) {
                                    output.push_str(indent_str)
                                    output.push_str("    if (_status_")
                                    output.push_str(temp_suffix)
                                    output.push_str(" == ")
                                    output.push_str(add(err_idx, 1).to_str())
                                    output.push_str(") { *_err")
                                    output.push_str(add(curr_idx, 1).to_str())
                                    output.push_str(" = _err")
                                    output.push_str(err_idx.to_str())
                                    output.push_str("_")
                                    output.push_str(temp_suffix)
                                    output.push_str("; return ")
                                    output.push_str(add(curr_idx, 1).to_str())
                                    output.push_str("; }\n")
                                    break
                                }
                            case:
                            }
                            curr_idx.inc()
                        }
                    case:
                    }
                    err_idx.inc()
                }

                output.push_str(indent_str)
                output.push_str("}\n")
            }

            // Emit Array.delete if variadic array was constructed
            if has_variadic {
                output.push_str(indent_str)
                output.push_str(TIL_PREFIX)
                output.push_str("Array_delete(&")
                output.push_str(variadic_arr_var)
                output.push_str(");\n")
            }

            // Record in hoisted_exprs map using expression address
            expr_addr := to_ptr(expr)
            ctx.hoisted_exprs.set(expr_addr, temp_var.clone())

            return temp_var
        }
    case:
    }

    // Check if this is a non-throwing variadic call - also needs hoisting
    switch expr.node_type {
    case NodeType.FCall:
        variadic_fcall_info := detect_variadic_fcall(expr, ctx)
        catch (err: KeyNotFoundError) {
            // REM: Not variadic, continue to next check
        }
        if not(variadic_fcall_info.elem_type.is_empty()) {
            // Recursively hoist any throwing/variadic calls in this call's arguments first
            mut nested_hoisted := Map.new(I64, Str)
            if expr.params.len().gt(1) {
                mut nested_args := Vec.new(Expr)
                mut i := 1
                while i.lt(expr.params.len()) {
                    mut arg := Expr()
                    expr.params.get(i, arg)
                    nested_args.push(arg)
                    i.inc()
                }
                nested_vec := hoist_throwing_args(nested_args, output, indent, ctx, context)
                for h: HoistedArg in nested_vec {
                    nested_hoisted.set(h.index, h.temp_var)
                }
            }

            temp_var := next_mangled(ctx)

            // Determine return type from function
            func_name := get_fcall_func_name(expr)
            fd := get_fcall_func_def(context, expr)
            catch (err: KeyNotFoundError) {
                throw expr.lang_error(context.path, "ccodegen", format("Function not found: ", func_name))
            }
            mut ret_type := ValueType.TCustom("")
            fd.return_types.get(0, ret_type)
            catch (err: IndexOutOfBoundsError) {
                throw expr.lang_error(context.path, "ccodegen", "Function has no return type")
            }
            c_type := til_type_to_c(ret_type)

            // Declare temp variable
            output.push_str(indent_str)
            output.push_str(c_type)
            output.push_str(" ")
            output.push_str(temp_var)
            output.push_str(";\n")

            // Construct variadic array
            mut variadic_args := Vec.new(Expr)
            mut j := add(1, variadic_fcall_info.regular_count)
            while j.lt(expr.params.len()) {
                mut arg := Expr()
                expr.params.get(j, arg)
                variadic_args.push(arg)
                j.inc()
            }
            variadic_arr_var := hoist_variadic_args(variadic_fcall_info.elem_type, variadic_args, nested_hoisted, variadic_fcall_info.regular_count, output, indent, ctx, context)

            // Calculate param_by_ref and param_types for proper by-ref handling
            mut param_by_ref := Vec.new(Bool)
            mut param_types := Vec.new(ValueType)
            mut i := 0
            while i.lt(expr.params.len().sub(1)) {
                if i.lt(fd.args.len()) {
                    mut arg_decl := Declaration()
                    fd.args.get(i, arg_decl)
                    param_by_ref.push(param_needs_by_ref(arg_decl))
                    param_types.push(arg_decl.value_type)
                } else {
                    param_by_ref.push(false)
                    param_types.push(ValueType.TCustom(""))
                }
                i.inc()
            }

            // Emit the function call (non-throwing, so direct assignment)
            output.push_str(indent_str)
            output.push_str(temp_var)
            output.push_str(" = ")

            // Emit function name
            fcall_func_name := get_fcall_func_name(expr)
            output.push_str(til_func_name(fcall_func_name))
            output.push_str("(")

            // Emit regular args (using nested hoisted temps)
            mut first := true
            i = 0
            while i.lt(variadic_fcall_info.regular_count) {
                if not(first) {
                    output.push_str(", ")
                }
                first = false
                mut param := Expr()
                expr.params.get(add(1, i), param)
                mut ptype := ValueType.TCustom("")
                param_types.get(i, ptype)
                catch (err: IndexOutOfBoundsError) { }
                mut by_ref := false
                param_by_ref.get(i, by_ref)
                catch (err: IndexOutOfBoundsError) { }
                emit_arg_with_param_type(param, i, nested_hoisted, ptype, by_ref, output, ctx, context)
                i.inc()
            }

            // Emit variadic array pointer
            if not(first) {
                output.push_str(", ")
            }
            output.push_str("&")
            output.push_str(variadic_arr_var)
            output.push_str(");\n")

            // Emit Array.delete for the variadic array
            output.push_str(indent_str)
            output.push_str(TIL_PREFIX)
            output.push_str("Array_delete(&")
            output.push_str(variadic_arr_var)
            output.push_str(");\n")

            // Record in hoisted_exprs map using expression address
            expr_addr := to_ptr(expr)
            ctx.hoisted_exprs.set(expr_addr, temp_var.clone())

            return temp_var
        }
    case:
    }

    // Check if this is a struct literal with throwing default values
    switch expr.node_type {
    case NodeType.FCall:
        mut first_param := Expr()
        expr.params.get(0, first_param)
        catch (err: IndexOutOfBoundsError) { }
        func_name := get_func_name_string(first_param)
        catch (err: Str) { }
        mut has_named_args := false
        mut k := 1
        while k.lt(expr.params.len()) {
            mut arg := Expr()
            expr.params.get(k, arg)
            switch arg.node_type {
            case NodeType.NamedArg:
                has_named_args = true
                break
            case:
            }
            k.inc()
        }
        struct_def := context.scope_stack.lookup_struct(func_name)
        catch (err: KeyNotFoundError) { }
        if expr.params.len().eq(1).or(has_named_args) {
            // This is a struct literal - check for throwing defaults
            // Build map of named arg names that are provided
            mut provided_names := Set.new(Str)
            mut m := 1
            while m.lt(expr.params.len()) {
                mut arg := Expr()
                expr.params.get(m, arg)
                switch arg.node_type {
                case NodeType.NamedArg(field_name):
                    _ := provided_names.insert(field_name)
                case:
                }
                m.inc()
            }

            // For each member with a default that's a throwing call and not overridden, emit temp var
            for member: Declaration in struct_def.members {
                if not(member.is_mut) {
                    continue
                }
                if provided_names.contains(member.name) {
                    continue  // Named arg overrides default
                }
                if struct_def.default_values.contains_key(member.name) {
                    mut default_expr := Expr()
                    struct_def.default_values.get(member.name, default_expr)
                    if is_throwing_fcall(default_expr, context) {
                        // Get the function's throw types
                        mut throw_types := Vec.new(ValueType)
                        if not(default_expr.params.is_empty()) {
                            mut def_first_param := Expr()
                            default_expr.params.get(0, def_first_param)
                            lookup_name := get_til_func_name_string(def_first_param)
                            catch (err: Str) { }
                            lookup_fd := context.scope_stack.lookup_func(lookup_name)
                            catch (err: KeyNotFoundError) { }
                            throw_types = lookup_fd.throw_types.clone()
                        }

                        temp_name := next_mangled(ctx)
                        // Emit the function call with out-param using emit_throwing_call_propagate
                        emit_throwing_call_propagate(default_expr, throw_types, temp_name, "", output, indent, ctx, context)
                        // Record that this struct default was hoisted using "struct_name:member_name" key
                        key := format(func_name, ":", member.name)
                        ctx.hoisted_struct_defaults.set(key, temp_name)
                    } else {
                        // Also recursively hoist any throwing expressions in the default
                        // This handles nested struct constructors with throwing defaults
                        _ := hoist_throwing_expr(default_expr, output, indent, ctx, context)
                        catch (err: KeyNotFoundError) { }
                    }
                }
            }
        }
    case:
    }

    // Not a throwing call or variadic call - recursively check sub-expressions
    // For FCall, check all arguments
    switch expr.node_type {
    case NodeType.FCall:
        if expr.params.len().gt(1) {
            mut n := 1
            while n.lt(expr.params.len()) {
                mut arg := Expr()
                expr.params.get(n, arg)
                _ := hoist_throwing_expr(arg, output, indent, ctx, context)
                catch (err: KeyNotFoundError) { }
                n.inc()
            }
        }
    case:
        // For other expression types with sub-expressions, recurse into them
        for param: Expr in expr.params {
            _ := hoist_throwing_expr(param, output, indent, ctx, context)
            catch (err: KeyNotFoundError) { }
        }
    }

    catch (err: IndexOutOfBoundsError) { throw err.msg }
    catch (err: AllocError) { throw err.msg }

    throw KeyNotFoundError(msg="expression not hoisted")
}

/// Hoist throwing function calls from arguments (recursively)
/// Returns a vector of HoistedArg for arguments that were hoisted
hoist_throwing_args := proc(args: Vec, mut output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) returns Vec throws Str {
    mut hoisted := Vec.new(HoistedArg)
    indent_str := repeat("    ", indent)

    mut idx := 0
    for arg: Expr in args {
        // Check if it's a throwing call
        mut throwing_fd_opt := SFuncDef()
        mut has_throwing_fd := false
        switch arg.node_type {
        case NodeType.FCall:
            throwing_fd := get_fcall_func_def(context, arg)
            if not(throwing_fd.throw_types.is_empty()) {
                throwing_fd_opt = throwing_fd
                has_throwing_fd = true
            }
            catch (err: KeyNotFoundError) {
                // REM: Not a function call we can look up
            }
        case:
        }

        // Check if it's a variadic call (even if not throwing)
        variadic_info := detect_variadic_fcall(arg, ctx)
        mut has_variadic_info := not(variadic_info.elem_type.is_empty())
        catch (err: KeyNotFoundError) {
            has_variadic_info = false
        }

        // Handle throwing calls (may also be variadic)
        if has_throwing_fd {
            throwing_fd := throwing_fd_opt
            // RECURSIVELY hoist any throwing calls in this call's arguments first
            mut nested_hoisted := Map.new(I64, Str)
            if arg.params.len().gt(1) {
                mut nested_args := Vec.new(Expr)
                mut i := 1
                while i.lt(arg.params.len()) {
                    mut nested_arg := Expr()
                    arg.params.get(i, nested_arg)
                    nested_args.push(nested_arg)
                    i.inc()
                }
                nested_vec := hoist_throwing_args(nested_args, output, indent, ctx, context)
                for h: HoistedArg in nested_vec {
                    nested_hoisted.set(h.index, h.temp_var)
                }
            }

            temp_var := next_mangled(ctx)

            // Determine the C type for the temp variable
            mut c_type := ""
            if not(throwing_fd.return_types.is_empty()) {
                mut first_ret := ValueType.TCustom("")
                throwing_fd.return_types.get(0, first_ret)
                c_type = til_type_to_c(first_ret)
                catch (err: KeyNotFoundError) {
                    throw arg.lang_error(context.path, "ccodegen", err.msg)
                }
            } else {
                throw arg.lang_error(context.path, "ccodegen", "Cannot hoist throwing call with no return type")
            }

            // Declare temp variable
            output.push_str(indent_str)
            output.push_str(c_type)
            output.push_str(" ")
            output.push_str(temp_var)
            output.push_str(";\n")

            // Declare error variables for each throw type
            temp_suffix := next_mangled(ctx)
            mut err_idx := 0
            for throw_type: ValueType in throwing_fd.throw_types {
                switch throw_type {
                case ValueType.TCustom(type_name):
                    output.push_str(indent_str)
                    output.push_str(til_name(type_name))
                    output.push_str(" _err")
                    output.push_str(err_idx.to_str())
                    output.push_str("_")
                    output.push_str(temp_suffix)
                    output.push_str(";\n")
                case:
                }
                err_idx.inc()
            }

            // Detect and construct variadic array if needed
            mut variadic_arr_var := ""
            mut has_variadic := false
            if has_variadic_info {
                vi := variadic_info
                mut variadic_args := Vec.new(Expr)
                mut j := add(1, vi.regular_count)
                while j.lt(arg.params.len()) {
                    mut va := Expr()
                    arg.params.get(j, va)
                    variadic_args.push(va)
                    j.inc()
                }
                if not(variadic_args.is_empty()) {
                    variadic_arr_var = hoist_variadic_args(vi.elem_type, variadic_args, nested_hoisted, vi.regular_count, output, indent, ctx, context)
                } else {
                    // Empty variadic - still need an array (with 0 elements)
                    variadic_arr_var = hoist_variadic_args(vi.elem_type, Vec.new(Expr), nested_hoisted, vi.regular_count, output, indent, ctx, context)
                }
                has_variadic = true
            }

            // Emit the function call with output pointers
            output.push_str(indent_str)
            output.push_str("int _status_")
            output.push_str(temp_suffix)
            output.push_str(" = ")

            // Emit the function name and args (using nested hoisted temps)
            mut variadic_ptr := ""
            if has_variadic {
                variadic_ptr = variadic_arr_var
            }
            emit_fcall_name_and_args_for_throwing(arg, temp_var, temp_suffix, throwing_fd.throw_types, nested_hoisted, variadic_ptr, output, ctx, context)

            output.push_str(";\n")

            // Emit error checking - propagate if any error occurred
            output.push_str(indent_str)
            output.push_str("if (_status_")
            output.push_str(temp_suffix)
            output.push_str(" != 0) {\n")

            // Propagate error based on status value
            // For now, propagate to corresponding error pointer in current function
            err_idx = 0
            for throw_type: ValueType in throwing_fd.throw_types {
                switch throw_type {
                case ValueType.TCustom(type_name):
                    // Find matching throw type in current function
                    mut curr_idx := 0
                    for curr_throw: ValueType in ctx.current_throw_types {
                        switch curr_throw {
                        case ValueType.TCustom(curr_type_name):
                            if curr_type_name.eq(type_name) {
                                output.push_str(indent_str)
                                output.push_str("    if (_status_")
                                output.push_str(temp_suffix)
                                output.push_str(" == ")
                                output.push_str(add(err_idx, 1).to_str())
                                output.push_str(") { *_err")
                                output.push_str(add(curr_idx, 1).to_str())
                                output.push_str(" = _err")
                                output.push_str(err_idx.to_str())
                                output.push_str("_")
                                output.push_str(temp_suffix)
                                output.push_str("; return ")
                                output.push_str(add(curr_idx, 1).to_str())
                                output.push_str("; }\n")
                                break
                            }
                        case:
                        }
                        curr_idx.inc()
                    }
                case:
                }
                err_idx.inc()
            }

            output.push_str(indent_str)
            output.push_str("}\n")

            // Emit Array.delete if variadic array was constructed
            if has_variadic {
                output.push_str(indent_str)
                output.push_str(TIL_PREFIX)
                output.push_str("Array_delete(&")
                output.push_str(variadic_arr_var)
                output.push_str(");\n")
            }

            // Record in hoisted_exprs map using expression address
            expr_addr := to_ptr(arg)
            ctx.hoisted_exprs.set(expr_addr, temp_var.clone())
            hoisted.push(HoistedArg(index=idx, temp_var=temp_var))
        }
        // Handle non-throwing variadic calls
        else if has_variadic_info {
            vi := variadic_info
            // RECURSIVELY hoist any throwing/variadic calls in this call's arguments first
            mut nested_hoisted := Map.new(I64, Str)
            if arg.params.len().gt(1) {
                mut nested_args := Vec.new(Expr)
                mut i := 1
                while i.lt(arg.params.len()) {
                    mut nested_arg := Expr()
                    arg.params.get(i, nested_arg)
                    nested_args.push(nested_arg)
                    i.inc()
                }
                nested_vec := hoist_throwing_args(nested_args, output, indent, ctx, context)
                for h: HoistedArg in nested_vec {
                    nested_hoisted.set(h.index, h.temp_var)
                }
            }

            temp_var := next_mangled(ctx)

            // Determine return type from function
            func_name := get_fcall_func_name(arg)
            fd := get_fcall_func_def(context, arg)
            catch (err: KeyNotFoundError) {
                throw arg.lang_error(context.path, "ccodegen", format("Function not found: ", func_name))
            }
            mut ret_type := ValueType.TCustom("")
            fd.return_types.get(0, ret_type)
            catch (err: IndexOutOfBoundsError) {
                throw arg.lang_error(context.path, "ccodegen", "Function has no return type")
            }
            c_type := til_type_to_c(ret_type)
            catch (err: KeyNotFoundError) {
                throw arg.lang_error(context.path, "ccodegen", err.msg)
            }

            // Declare temp variable
            output.push_str(indent_str)
            output.push_str(c_type)
            output.push_str(" ")
            output.push_str(temp_var)
            output.push_str(";\n")

            // Construct variadic array
            mut variadic_args := Vec.new(Expr)
            mut j := add(1, vi.regular_count)
            while j.lt(arg.params.len()) {
                mut va := Expr()
                arg.params.get(j, va)
                variadic_args.push(va)
                j.inc()
            }
            variadic_arr_var := ""
            if not(variadic_args.is_empty()) {
                variadic_arr_var = hoist_variadic_args(vi.elem_type, variadic_args, nested_hoisted, vi.regular_count, output, indent, ctx, context)
            } else {
                variadic_arr_var = hoist_variadic_args(vi.elem_type, Vec.new(Expr), nested_hoisted, vi.regular_count, output, indent, ctx, context)
            }

            // Calculate param_by_ref and param_types for proper by-ref handling
            mut param_by_ref := Vec.new(Bool)
            mut param_types := Vec.new(ValueType)
            mut i := 0
            while i.lt(arg.params.len().sub(1)) {
                if i.lt(fd.args.len()) {
                    mut arg_decl := Declaration()
                    fd.args.get(i, arg_decl)
                    param_by_ref.push(param_needs_by_ref(arg_decl))
                    param_types.push(arg_decl.value_type)
                } else {
                    param_by_ref.push(false)
                    param_types.push(ValueType.TCustom(""))
                }
                i.inc()
            }

            // Emit the function call (non-throwing, so direct assignment)
            output.push_str(indent_str)
            output.push_str(temp_var)
            output.push_str(" = ")

            // Emit function name
            fcall_func_name := get_fcall_func_name(arg)
            catch (err: KeyNotFoundError) {
                throw arg.lang_error(context.path, "ccodegen", err.msg)
            }
            output.push_str(til_func_name(fcall_func_name))
            output.push_str("(")

            // Emit regular args with proper by-ref handling
            mut first := true
            i = 0
            while i.lt(vi.regular_count) {
                if not(first) {
                    output.push_str(", ")
                }
                first = false
                mut param := Expr()
                arg.params.get(add(1, i), param)
                mut ptype := ValueType.TCustom("")
                param_types.get(i, ptype)
                catch (err: IndexOutOfBoundsError) { }
                mut by_ref := false
                param_by_ref.get(i, by_ref)
                catch (err: IndexOutOfBoundsError) { }
                emit_arg_with_param_type(param, i, nested_hoisted, ptype, by_ref, output, ctx, context)
                i.inc()
            }

            // Emit variadic array pointer
            if not(first) {
                output.push_str(", ")
            }
            output.push_str("&")
            output.push_str(variadic_arr_var)

            output.push_str(");\n")

            // Delete the variadic array
            output.push_str(indent_str)
            output.push_str(TIL_PREFIX)
            output.push_str("Array_delete(&")
            output.push_str(variadic_arr_var)
            output.push_str(");\n")

            // Record in hoisted_exprs map using expression address
            expr_addr := to_ptr(arg)
            ctx.hoisted_exprs.set(expr_addr, temp_var.clone())
            hoisted.push(HoistedArg(index=idx, temp_var=temp_var))
        }
        // Handle non-throwing, non-variadic FCalls - still need to recurse into their arguments
        // to find deeply nested variadic/throwing calls (e.g., not(or(false)))
        // and also to hoist Dynamic params (e.g., Vec.contains(v, "bar"))
        else {
            mut is_fcall := false
            switch arg.node_type {
            case NodeType.FCall:
                is_fcall = true
            case:
            }
            if is_fcall.and(arg.params.len().gt(1)) {
                mut nested_args := Vec.new(Expr)
                mut i := 1
                while i.lt(arg.params.len()) {
                    mut nested_arg := Expr()
                    arg.params.get(i, nested_arg)
                    nested_args.push(nested_arg)
                    i.inc()
                }
                // Recurse to hoist any nested throwing/variadic calls
                _ := hoist_throwing_args(nested_args, output, indent, ctx, context)

                // Also hoist Dynamic params for this nested FCall
                fd := get_fcall_func_def(context, arg)
                catch (err: KeyNotFoundError) { }
                mut param_types := Vec.new(ValueType)
                for a: Declaration in fd.args {
                    param_types.push(a.value_type)
                }
                empty_hoisted := Map.new(I64, Str)
                dynamic_hoisted := hoist_for_dynamic_params(nested_args, param_types, empty_hoisted, output, indent, ctx, context)
                // Record hoisted Dynamic params in hoisted_exprs with & prefix
                // (needed because emit_expr won't know to add & for Dynamic params)
                for h: HoistedArg in dynamic_hoisted {
                    mut nested_arg := Expr()
                    nested_args.get(h.index, nested_arg)
                    catch (err: IndexOutOfBoundsError) { }
                    expr_addr := to_ptr(nested_arg)
                    ctx.hoisted_exprs.set(expr_addr, format("&", h.temp_var))
                }
            }
            // Handle struct literal arguments (no args, or only named args) - need to hoist their throwing defaults
            // For struct literals like Expr() passed as arguments, their throwing default field values
            // (e.g., params = Vec.new(Expr)) need to be hoisted BEFORE the struct literal is created
            else if is_fcall {
                func_name := get_fcall_func_name(arg)
                _ := context.scope_stack.lookup_struct(func_name)
                catch (err: KeyNotFoundError) {
                    // REM: Not a struct, continue
                }
                // This is a struct literal - hoist its throwing defaults
                _ := hoist_throwing_expr(arg, output, indent, ctx, context)
                catch (err: KeyNotFoundError) { }
            }
            // Bug #55 fix: Handle non-FCall arguments (like NamedArg) by recursing via hoist_throwing_expr
            // This ensures throwing calls inside named args are hoisted properly
            else {
                _ := hoist_throwing_expr(arg, output, indent, ctx, context)
                catch (err: KeyNotFoundError) { }
            }
        }
        idx.inc()
    }

    catch (err: IndexOutOfBoundsError) { throw err.msg }
    catch (err: AllocError) { throw err.msg }

    return hoisted
}

/// Hoist non-lvalue args (string literals, function calls) when the param type is Dynamic
/// Returns additional hoisted (arg_idx, temp_var_name) pairs to merge with throwing hoists
hoist_for_dynamic_params := proc(args: Vec, param_types: Vec, already_hoisted: Map, mut output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) returns Vec throws Str {
    mut hoisted := Vec.new(HoistedArg)
    indent_str := repeat("    ", indent)

    mut idx := 0
    for arg: Expr in args {
        // Skip if already hoisted by throwing args hoister or by a previous function call
        arg_addr := to_ptr(arg)
        if already_hoisted.contains_key(idx).or(ctx.hoisted_exprs.contains_key(arg_addr)) {
            idx.inc()
            continue
        }

        // Check if param type is Dynamic
        mut is_dynamic := false
        if idx.lt(param_types.len()) {
            mut ptype := ValueType.TCustom("")
            param_types.get(idx, ptype)
            switch ptype {
            case ValueType.TCustom(ptype_name):
                if ptype_name.eq("Dynamic") {
                    is_dynamic = true
                }
            case:
            }
        }

        if not(is_dynamic) {
            idx.inc()
            continue
        }

        // Check if arg is NOT a simple identifier (i.e., needs hoisting)
        mut needs_hoisting := true
        switch arg.node_type {
        case NodeType.Identifier:
            // Simple identifier with no params is an lvalue, doesn't need hoisting
            // But identifier with params (type-qualified call like I64.to_str) does need hoisting
            needs_hoisting = not(arg.params.is_empty())
        case NodeType.LLiteral(lit):
            switch lit {
            case Literal.Str:
                needs_hoisting = true  // String literals need hoisting
            case:
            }
        case NodeType.FCall:
            needs_hoisting = true  // Function calls need hoisting
        case:
            needs_hoisting = true  // Default to hoisting for safety
        }

        if not(needs_hoisting) {
            idx.inc()
            continue
        }

        // Determine the C type of the arg based on what it is
        mut c_type := ""
        switch arg.node_type {
        case NodeType.LLiteral(lit2):
            switch lit2 {
            case Literal.Str:
                c_type = format(TIL_PREFIX, "Str")
            case Literal.Number:
                c_type = "int64_t"
            case Literal.List:
                c_type = "int64_t"  // TODO: proper list type
            case:
            }
        case NodeType.Identifier(name):
            if not(arg.params.is_empty()) {
                // Could be: enum constructor (Type.Variant), field access (var.field), or method call
                mut first_param := Expr()
                arg.params.get(0, first_param)
                switch first_param.node_type {
                case NodeType.Identifier(field_or_variant):
                    // Check if this is an enum constructor
                    enum_def := context.scope_stack.lookup_enum(name)
                    mut is_enum_variant := false
                    for v: EnumVariant in enum_def.variants {
                        if v.name.eq(field_or_variant) {
                            is_enum_variant = true
                            break
                        }
                    }
                    catch (err: KeyNotFoundError) { }
                    if is_enum_variant {
                        // Enum constructor returns the enum type
                        c_type = til_name(name)
                    } else {
                        sym := context.scope_stack.lookup_symbol(name)
                        catch (err: KeyNotFoundError) {
                            throw arg.lang_error(context.path, "ccodegen", format("Symbol not found: ", name))
                        }
                        // Field access - look up variable's type, then field's type
                        switch sym.value_type {
                        case ValueType.TCustom(struct_name):
                            struct_def := context.scope_stack.lookup_struct(struct_name)
                            catch (err: KeyNotFoundError) {
                                throw arg.lang_error(context.path, "ccodegen", format("Struct not found: ", struct_name))
                            }
                            mut found_member := false
                            for m: Declaration in struct_def.members {
                                if m.name.eq(field_or_variant) {
                                    c_type = til_type_to_c(m.value_type)
                                    found_member = true
                                    break
                                }
                            }
                            if not(found_member) {
                                throw arg.lang_error(context.path, "ccodegen", format("Field not found: ", field_or_variant))
                            }
                        case:
                            throw arg.lang_error(context.path, "ccodegen", format("Expected struct type for field access on ", name))
                        }
                    }
                case:
                    throw arg.lang_error(context.path, "ccodegen", "Expected identifier for field/variant")
                }
            } else {
                // Struct constructor like Ptr() - identifier with no params
                _ := context.scope_stack.lookup_struct(name)
                catch (err: KeyNotFoundError) {
                    throw arg.lang_error(context.path, "ccodegen", format("Unknown type: ", name))
                }
                c_type = til_name(name)
            }
        case NodeType.FCall:
            // For function calls, try to determine return type
            fd := get_fcall_func_def(context, arg)
            catch (err: KeyNotFoundError) {
                func_name := get_fcall_func_name(arg)
                catch (err: KeyNotFoundError) {
                    throw arg.lang_error(context.path, "ccodegen", err.msg)
                }
                // get_fcall_func_def returned None - check for constructors
                if func_name.contains("_") {
                    // Check if this is an enum constructor (Type_Variant)
                    parts := func_name.split("_")
                    if parts.len().eq(2) {
                        mut type_name := ""
                        parts.get(0, type_name)
                        mut variant_name := ""
                        parts.get(1, variant_name)
                        enum_def := context.scope_stack.lookup_enum(type_name)
                        mut is_enum_variant := false
                        for v: EnumVariant in enum_def.variants {
                            if v.name.eq(variant_name) {
                                is_enum_variant = true
                                break
                            }
                        }
                        catch (err: KeyNotFoundError) { }
                        if is_enum_variant {
                            // Enum constructor returns the enum type
                            c_type = til_name(type_name)
                        } else {
                            throw arg.lang_error(context.path, "ccodegen", format("Unknown enum variant: ", func_name))
                        }
                    } else {
                        throw arg.lang_error(context.path, "ccodegen", format("Unknown function: ", func_name))
                    }
                } else {
                    _ := context.scope_stack.lookup_struct(func_name)
                    catch (err: KeyNotFoundError) {
                        throw arg.lang_error(context.path, "ccodegen", format("Unknown function: ", func_name))
                    }
                    // Struct constructor like Ptr() - returns the struct type
                    c_type = til_name(func_name)
                }
            }
            if not(fd.throw_types.is_empty()) {
                // Throwing call
                if fd.return_types.is_empty() {
                    throw arg.lang_error(context.path, "ccodegen", "Throwing call has no return type")
                }
                mut first_ret := ValueType.TCustom("")
                fd.return_types.get(0, first_ret)
                c_type = til_type_to_c(first_ret)
                catch (err: KeyNotFoundError) {
                    throw arg.lang_error(context.path, "ccodegen", err.msg)
                }
            } else {
                // Non-throwing call
                mut ret_type := ValueType.TCustom("")
                fd.return_types.get(0, ret_type)
                catch (err: IndexOutOfBoundsError) {
                    throw arg.lang_error(context.path, "ccodegen", "Function has no return type")
                }
                c_type = til_type_to_c(ret_type)
                catch (err: KeyNotFoundError) {
                    throw arg.lang_error(context.path, "ccodegen", err.msg)
                }
            }
        case:
            throw arg.lang_error(context.path, "ccodegen", "Cannot determine type for dynamic param")
        }

        temp_var := next_mangled(ctx)

        // Emit: til_Str _tmpXX = <expression>;
        output.push_str(indent_str)
        output.push_str(c_type)
        output.push_str(" ")
        output.push_str(temp_var)
        output.push_str(" = ")
        emit_expr(arg, output, 0, ctx, context)
        output.push_str(";\n")

        hoisted.push(HoistedArg(index=idx, temp_var=temp_var))
        idx.inc()
    }

    catch (err: IndexOutOfBoundsError) { throw err.msg }
    catch (err: AllocError) { throw err.msg }

    return hoisted
}

/// Bug #60: Hoist function call results and number literals when passed to by-ref params
/// These are rvalues that can't have & taken directly
hoist_for_ref_params := proc(args: Vec, param_by_ref: Vec, already_hoisted: Map, mut output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) returns Vec throws Str {
    mut hoisted := Vec.new(HoistedArg)
    indent_str := repeat("    ", indent)

    mut idx := 0
    for arg: Expr in args {
        // Skip if already hoisted
        arg_addr := to_ptr(arg)
        if already_hoisted.contains_key(idx).or(ctx.hoisted_exprs.contains_key(arg_addr)) {
            idx.inc()
            continue
        }

        // Check if param is by-ref
        mut is_by_ref := false
        if idx.lt(param_by_ref.len()) {
            param_by_ref.get(idx, is_by_ref)
        }
        if not(is_by_ref) {
            idx.inc()
            continue
        }

        // Check if arg needs hoisting (rvalues that can't have & taken)
        mut needs_hoisting := false
        switch arg.node_type {
        case NodeType.Identifier(name):
            // Simple identifier is lvalue - doesn't need hoisting
            // But type-qualified calls like I64.to_str need hoisting
            if arg.params.is_empty() {
                needs_hoisting = false
            } else {
                // Check if this is a type-qualified method call (not field access)
                // e.g., Str.len(s) is a method call, but s.len is field access
                // Also check if it's a function call followed by field access (e.g., get_foo().bar)
                // Function call results with field access ARE lvalues, don't need hoisting
                // Also check for UFCS placeholder "_" which is a chained expression
                if name.eq("_") {
                    // UFCS placeholder - this is a chained expression like get_foo().bar
                    // The result is an lvalue, doesn't need hoisting
                    needs_hoisting = false
                } else {
                    is_variable := context.scope_stack.lookup_symbol(name)
                    mut has_variable := true
                    catch (err: KeyNotFoundError) { has_variable = false }
                    is_function := context.scope_stack.lookup_func(name)
                    mut has_function := true
                    catch (err: KeyNotFoundError) { has_function = false }
                    // Only needs hoisting if it's not a variable AND not a function call
                    // (i.e., it's a Type.method call like I64.to_str)
                    needs_hoisting = not(has_variable).and(not(has_function))
                }
            }
        case NodeType.LLiteral(lit):
            switch lit {
            case Literal.Str:
                needs_hoisting = false  // Compound literal, can use &
            case Literal.Number:
                needs_hoisting = true  // Can't take address of integer literal
            case:
            }
        case NodeType.FCall:
            // Check if this is an enum or struct constructor - those become compound literals
            // which CAN have & taken directly, so no hoisting needed
            if not(arg.params.is_empty()) {
                mut first := Expr()
                arg.params.get(0, first)
                combined := get_combined_name(context.path, first)
                needs_hoisting = not(context.scope_stack.is_type_constructor(combined))
            } else {
                needs_hoisting = true
            }
        case:
            needs_hoisting = false
        }

        if not(needs_hoisting) {
            idx.inc()
            continue
        }

        // Determine the C type based on what the arg evaluates to
        mut c_type := ""
        switch arg.node_type {
        case NodeType.LLiteral(lit):
            switch lit {
            case Literal.Number:
                c_type = format(TIL_PREFIX, "I64")
            case:
            }
        case NodeType.FCall:
            // Look up the function's return type
            fd := get_fcall_func_def(context, arg)
            catch (err: KeyNotFoundError) {
                throw arg.lang_error(context.path, "ccodegen", format("Cannot find function definition for: ", arg.line.to_str()))
            }
            if not(fd.return_types.is_empty()) {
                mut ret_type := ValueType.TCustom("")
                fd.return_types.get(0, ret_type)
                c_type = til_type_to_c(ret_type)
                catch (err: KeyNotFoundError) {
                    throw arg.lang_error(context.path, "ccodegen", err.msg)
                }
            } else {
                throw arg.lang_error(context.path, "ccodegen", "Function has no return type")
            }
        case NodeType.Identifier(var_name):
            if not(arg.params.is_empty()) {
                // Type-qualified method call like I64.to_str(x) - look up return type
                mut first_param := Expr()
                arg.params.get(0, first_param)
                switch first_param.node_type {
                case NodeType.Identifier(method_name):
                    // Look up Type.method function
                    func_name := format(var_name, ".", method_name)
                    fd := context.scope_stack.lookup_func(func_name)
                    catch (err: KeyNotFoundError) {
                        throw arg.lang_error(context.path, "ccodegen", format("Cannot find function: ", func_name))
                    }
                    if not(fd.return_types.is_empty()) {
                        mut ret_type := ValueType.TCustom("")
                        fd.return_types.get(0, ret_type)
                        c_type = til_type_to_c(ret_type)
                        catch (err: KeyNotFoundError) {
                            throw arg.lang_error(context.path, "ccodegen", err.msg)
                        }
                    } else {
                        throw arg.lang_error(context.path, "ccodegen", format("Function ", func_name, " has no return type"))
                    }
                case:
                    throw arg.lang_error(context.path, "ccodegen", "Expected method name")
                }
            }
        case:
            throw arg.lang_error(context.path, "ccodegen", "Cannot determine type for ref param hoisting")
        }

        temp_var := next_mangled(ctx)

        // Emit: Type _tmpXX = <expression>;
        output.push_str(indent_str)
        output.push_str(c_type)
        output.push_str(" ")
        output.push_str(temp_var)
        output.push_str(" = ")
        emit_expr(arg, output, 0, ctx, context)
        output.push_str(";\n")

        hoisted.push(HoistedArg(index=idx, temp_var=temp_var))
        idx.inc()
    }

    catch (err: IndexOutOfBoundsError) { throw err.msg }
    catch (err: AllocError) { throw err.msg }

    return hoisted
}

/// Hoist variadic arguments into a til_Array
/// Returns the array variable name, or None if no variadic args
/// Also returns the element type for use in Array.delete
hoist_variadic_args := proc(elem_type: Str, variadic_args: Vec, already_hoisted: Map, regular_count: I64, mut output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) returns Str throws Str {
    indent_str := repeat("    ", indent)
    arr_var := next_mangled(ctx)
    err_suffix := next_mangled(ctx)
    variadic_count := variadic_args.len()

    // Declare the array variable
    output.push_str(indent_str)
    output.push_str(TIL_PREFIX)
    output.push_str("Array ")
    output.push_str(arr_var)
    output.push_str(";\n")

    // Declare error vars for Array.new (AllocError) and Array.set (IndexOutOfBoundsError)
    output.push_str(indent_str)
    output.push_str(TIL_PREFIX)
    output.push_str("AllocError _err_alloc_")
    output.push_str(err_suffix)
    output.push_str(";\n")

    output.push_str(indent_str)
    output.push_str(TIL_PREFIX)
    output.push_str("IndexOutOfBoundsError _err_idx_")
    output.push_str(err_suffix)
    output.push_str(";\n")

    // Hoist variadic args into temp vars (needed to pass address to Array.set)
    mut arg_temps := Vec.new(Str)
    c_elem_type := format(TIL_PREFIX, elem_type)
    mut i := 0
    for arg: Expr in variadic_args {
        hoisted_idx := add(regular_count, i)
        if already_hoisted.contains_key(hoisted_idx) {
            // Already hoisted, use that temp
            mut temp := ""
            already_hoisted.get(hoisted_idx, temp)
            catch (err: KeyNotFoundError) {
                panic(loc(), "already_hoisted.get failed after contains_key: ", err.msg)
            }
            arg_temps.push(temp)
        } else {
            // Need to hoist into a temp
            temp_var := next_mangled(ctx)
            output.push_str(indent_str)
            output.push_str(c_elem_type)
            output.push_str(" ")
            output.push_str(temp_var)
            output.push_str(" = ")
            emit_expr(arg, output, 0, ctx, context)
            output.push_str(";\n")
            arg_temps.push(temp_var)
        }
        i.inc()
    }

    // Bug #60: Hoist Type and I64 literals for Array.new call
    // Type param needs hoisting because we can't take address of string literal for const char* const*
    type_temp := next_mangled(ctx)
    output.push_str(indent_str)
    output.push_str(TIL_PREFIX)
    output.push_str("Type ")
    output.push_str(type_temp)
    output.push_str(" = \"")
    output.push_str(elem_type)
    output.push_str("\";\n")

    // Capacity param needs hoisting because we can't take address of integer literal
    count_temp := next_mangled(ctx)
    output.push_str(indent_str)
    output.push_str(TIL_PREFIX)
    output.push_str("I64 ")
    output.push_str(count_temp)
    output.push_str(" = ")
    output.push_str(variadic_count.to_str())
    output.push_str(";\n")

    // Emit Array.new call with error handling
    // int _status = til_Array_new(&arr, &_err_alloc, &type_temp, &count_temp);
    output.push_str(indent_str)
    output.push_str("int _arr_status_")
    output.push_str(err_suffix)
    output.push_str(" = ")
    output.push_str(TIL_PREFIX)
    output.push_str("Array_new(&")
    output.push_str(arr_var)
    output.push_str(", &_err_alloc_")
    output.push_str(err_suffix)
    // Bug #60: Type is already const char*, pass by value (no &)
    output.push_str(", ")
    output.push_str(type_temp)
    output.push_str(", &")
    output.push_str(count_temp)
    output.push_str(");\n")

    // Emit error check for Array.new (AllocError -> propagate if current function throws it)
    output.push_str(indent_str)
    output.push_str("if (_arr_status_")
    output.push_str(err_suffix)
    output.push_str(" != 0) {\n")
    // Propagate AllocError if current function throws it
    mut curr_idx := 0
    for curr_throw: ValueType in ctx.current_throw_types {
        switch curr_throw {
        case ValueType.TCustom(curr_type_name):
            if curr_type_name.eq("AllocError") {
                output.push_str(indent_str)
                output.push_str("    *_err")
                output.push_str(add(curr_idx, 1).to_str())
                output.push_str(" = _err_alloc_")
                output.push_str(err_suffix)
                output.push_str("; return ")
                output.push_str(add(curr_idx, 1).to_str())
                output.push_str(";\n")
                break
            }
        case:
        }
        curr_idx.inc()
    }
    output.push_str(indent_str)
    output.push_str("}\n")

    // Emit Array.set for each variadic arg
    i = 0
    for temp: Str in arg_temps {
        // Bug #60: Hoist index literal because we can't take address of integer literal
        idx_temp := next_mangled(ctx)
        output.push_str(indent_str)
        output.push_str(TIL_PREFIX)
        output.push_str("I64 ")
        output.push_str(idx_temp)
        output.push_str(" = ")
        output.push_str(i.to_str())
        output.push_str(";\n")

        // int _status = til_Array_set(&_err_idx, &arr, &idx_temp, (til_Dynamic*)&temp);
        output.push_str(indent_str)
        output.push_str("_arr_status_")
        output.push_str(err_suffix)
        output.push_str(" = ")
        output.push_str(TIL_PREFIX)
        output.push_str("Array_set(&_err_idx_")
        output.push_str(err_suffix)
        output.push_str(", &")
        output.push_str(arr_var)
        output.push_str(", &")
        output.push_str(idx_temp)
        output.push_str(", (")
        output.push_str(TIL_PREFIX)
        output.push_str("Dynamic*)&")
        output.push_str(temp)
        output.push_str(");\n")

        // Error check for Array.set (IndexOutOfBoundsError - shouldn't happen but propagate if thrown)
        output.push_str(indent_str)
        output.push_str("if (_arr_status_")
        output.push_str(err_suffix)
        output.push_str(" != 0) {\n")
        curr_idx = 0
        for curr_throw: ValueType in ctx.current_throw_types {
            switch curr_throw {
            case ValueType.TCustom(curr_type_name):
                if curr_type_name.eq("IndexOutOfBoundsError") {
                    output.push_str(indent_str)
                    output.push_str("    *_err")
                    output.push_str(add(curr_idx, 1).to_str())
                    output.push_str(" = _err_idx_")
                    output.push_str(err_suffix)
                    output.push_str("; return ")
                    output.push_str(add(curr_idx, 1).to_str())
                    output.push_str(";\n")
                    break
                }
            case:
            }
            curr_idx.inc()
        }
        output.push_str(indent_str)
        output.push_str("}\n")
        i.inc()
    }

    catch (err: AllocError) { throw err.msg }

    return arr_var
}

/// Detect if an expression is a variadic function call
/// Returns VariadicFCallInfo if it's a variadic call
detect_variadic_fcall := func(expr: Expr, ctx: CodegenContext) returns VariadicFCallInfo throws KeyNotFoundError {
    if expr.params.is_empty() {
        throw KeyNotFoundError(msg="not a variadic call")
    }

    // Get original TIL function name (with dots) for variadic map lookup
    mut first_param := Expr()
    expr.params.get(0, first_param)
    orig_func_name := get_til_func_name_string(first_param)
    catch (err: Str) {
        throw KeyNotFoundError(msg=err)
    }

    if ctx.func_variadic_args.contains_key(orig_func_name) {
        mut info := VariadicParamInfo()
        ctx.func_variadic_args.get(orig_func_name, info)
        return VariadicFCallInfo(elem_type=info.elem_type.clone(), regular_count=info.regular_count)
    } else {
        throw KeyNotFoundError(msg="not a variadic call")
    }

    catch (err: IndexOutOfBoundsError) {
        throw KeyNotFoundError(msg=err.msg)
    }
    catch (err: AllocError) {
        throw KeyNotFoundError(msg=err.msg)
    }
}

/// Emit a throwing function call's name and arguments for hoisting
/// Outputs: func_name(&temp_var, &err1, &err2, ..., args...)
/// If variadic_arr_var is Some, use that pre-constructed array instead of building one
emit_fcall_name_and_args_for_throwing := proc(expr: Expr, temp_var: Str, temp_suffix: Str, throw_types: Vec, nested_hoisted: Map, variadic_arr_var: Str, mut output: Str, mut ctx: CodegenContext, mut context: Context) throws Str {
    if expr.params.is_empty() {
        throw "emit_fcall_name_and_args_for_throwing: FCall with no params"
    }

    // Get function name (handles both nested identifiers and precomp'd "Type.method" strings)
    mut first_param := Expr()
    expr.params.get(0, first_param)
    mut func_name := get_func_name_string(first_param)

    // For lookups, we need the original name with dots
    mut orig_func_name := get_til_func_name_string(first_param)
    catch (err: Str) {
        orig_func_name = func_name.clone()
    }

    // Check if this is a call to a nested (hoisted) function - use mangled name
    if ctx.nested_func_names.contains_key(orig_func_name) {
        ctx.nested_func_names.get(orig_func_name, func_name)
        catch (err: KeyNotFoundError) {
            panic(loc(), "nested_func_names.get failed after contains_key: ", err.msg)
        }
    }

    // Emit function name (func_name already has underscores for type-qualified calls)
    output.push_str(TIL_PREFIX)
    output.push_str(func_name)
    output.push_str("(&")
    output.push_str(temp_var)

    // Add error output pointers
    mut idx := 0
    while idx.lt(throw_types.len()) {
        output.push_str(", &_err")
        output.push_str(idx.to_str())
        output.push_str("_")
        output.push_str(temp_suffix)
        idx.inc()
    }

    // Check if this is a variadic function call
    if ctx.func_variadic_args.contains_key(orig_func_name) {
        mut variadic_info := VariadicParamInfo()
        ctx.func_variadic_args.get(orig_func_name, variadic_info)
        regular_count := variadic_info.regular_count

        // Bug #60: Get function param info for proper by-ref handling of regular args
        found_func := get_fcall_func_def(context, expr)
        mut param_info := Vec.new(ParamTypeInfo)
        catch (err: KeyNotFoundError) { }
        for p: Declaration in found_func.args {
            param_info.push(ParamTypeInfo(value_type=p.value_type, by_ref=param_needs_by_ref(p)))
        }

        // Emit regular args first (skip first param which is function name)
        mut arg_idx := 0
        while arg_idx.lt(regular_count) {
            output.push_str(", ")
            mut arg := Expr()
            expr.params.get(add(1, arg_idx), arg)
            // Bug #60: Use emit_arg_with_param_type for proper by-ref handling
            mut param_type := ValueType.TCustom("")
            mut param_by_ref := false
            if arg_idx.lt(param_info.len()) {
                mut info := ParamTypeInfo()
                param_info.get(arg_idx, info)
                param_type = info.value_type
                param_by_ref = info.by_ref
            }
            emit_arg_with_param_type(arg, arg_idx, nested_hoisted, param_type, param_by_ref, output, ctx, context)
            arg_idx.inc()
        }

        // Emit variadic array pointer
        if not(variadic_arr_var.is_empty()) {
            output.push_str(", &")
            output.push_str(variadic_arr_var)
        } else {
            // No pre-constructed array - this shouldn't happen for throwing calls
            throw "emit_fcall_name_and_args_for_throwing: variadic call without pre-constructed array"
        }
    } else {
        // Get function param info for Dynamic casting, mut handling
        found_func := get_fcall_func_def(context, expr)
        mut param_info := Vec.new(ParamTypeInfo)
        catch (err: KeyNotFoundError) { }
        for p: Declaration in found_func.args {
            param_info.push(ParamTypeInfo(value_type=p.value_type, by_ref=param_needs_by_ref(p)))
        }

        // Emit remaining arguments (using hoisted temps where available)
        mut arg_idx := 0
        mut i := 1
        while i.lt(expr.params.len()) {
            output.push_str(", ")
            mut arg := Expr()
            expr.params.get(i, arg)

            // Get expected param type and mutability
            mut param_type := ValueType.TCustom("")
            mut param_by_ref := false
            if arg_idx.lt(param_info.len()) {
                mut info := ParamTypeInfo()
                param_info.get(arg_idx, info)
                param_type = info.value_type
                param_by_ref = info.by_ref
            }

            emit_arg_with_param_type(arg, arg_idx, nested_hoisted, param_type, param_by_ref, output, ctx, context)
            arg_idx.inc()
            i.inc()
        }
    }

    output.push_str(")")

    catch (err: IndexOutOfBoundsError) { throw err.msg }
    catch (err: AllocError) { throw err.msg }
}

/// Emit an argument, using hoisted temp var if available
/// Also handles Type arguments by emitting them as string literals
emit_arg_or_hoisted := proc(arg: Expr, arg_idx: I64, hoisted: Map, mut output: Str, mut ctx: CodegenContext, mut context: Context) throws Str {
    emit_arg_with_param_type(arg, arg_idx, hoisted, ValueType.TCustom(""), false, output, ctx, context)
}

/// Emit an argument with knowledge of expected parameter type and by-ref flag
/// Handles: Type args as string literals, Dynamic args with &, by-ref args with &
emit_arg_with_param_type := proc(arg: Expr, arg_idx: I64, hoisted: Map, param_type: ValueType, param_by_ref: Bool, mut output: Str, mut ctx: CodegenContext, mut context: Context) throws Str {
    if hoisted.contains_key(arg_idx) {
        // Hoisted temp var is an lvalue - add & if param is Dynamic or by-ref
        mut temp_var := ""
        hoisted.get(arg_idx, temp_var)
        if param_by_ref {
            output.push_str("&")
        } else {
            switch param_type {
            case ValueType.TCustom(param_type_name):
                if param_type_name.eq("Dynamic") {
                    output.push_str("&")
                }
            case:
            }
        }
        output.push_str(temp_var)
        return
    }

    // Also check ctx.hoisted_exprs for expressions hoisted via other mechanisms
    arg_addr := to_ptr(arg)
    if ctx.hoisted_exprs.contains_key(arg_addr) {
        mut temp_expr := ""
        ctx.hoisted_exprs.get(arg_addr, temp_expr)
        // hoisted_exprs may store just the temp var name or with & prefix
        // Add & if param is by-ref and temp_expr doesn't already start with &
        if param_by_ref.and(not(temp_expr.starts_with("&"))) {
            output.push_str("&")
        }
        output.push_str(temp_expr)
        return
    }

    // Check if arg is a type identifier - emit as string literal (matches interpreter.rs)
    type_name := get_type_arg_name(arg, context)
    catch (err: KeyNotFoundError) { }
    if not(type_name.is_empty()) {
        output.push_str("\"")
        output.push_str(type_name)
        output.push_str("\"")
        return
    }

    // Check if parameter type is Dynamic (including mut Dynamic)
    // Must check BEFORE the general mut check since Dynamic needs casting
    mut is_dynamic := false
    switch param_type {
    case ValueType.TCustom(param_type_name):
        if param_type_name.eq("Dynamic") {
            is_dynamic = true
        }
    case:
    }

    if is_dynamic {
        // Check if arg is a simple identifier (can take address directly)
        switch arg.node_type {
        case NodeType.Identifier(name):
            if arg.params.is_empty() {
                // Check if this identifier is already a pointer:
                // - mut params are passed as pointers
                // - variadic params are passed as Array pointers
                // - Dynamic params are already void* (no need to take address)
                is_already_pointer := ctx.current_ref_params.contains(name).or(ctx.current_variadic_params.contains_key(name))
                mut is_dynamic_var := false
                sym := context.scope_stack.lookup_symbol(name)
                switch sym.value_type {
                case ValueType.TCustom(type_name):
                    if type_name.eq("Dynamic") {
                        is_dynamic_var = true
                    }
                case:
                }
                catch (err: KeyNotFoundError) { }
                if is_already_pointer.or(is_dynamic_var) {
                    // Already a pointer - just cast without &
                    output.push_str("(")
                    output.push_str(TIL_PREFIX)
                    output.push_str("Dynamic*)")
                    output.push_str(til_name(name))
                } else {
                    // Simple variable - cast to til_Dynamic* (void**) and take address
                    output.push_str("(")
                    output.push_str(TIL_PREFIX)
                    output.push_str("Dynamic*)&")
                    output.push_str(til_name(name))
                }
                return
            } else {
                // Check if this is an enum constructor (Type.Variant) - need temp variable
                if not(arg.params.is_empty()) {
                    mut first_param := Expr()
                    arg.params.get(0, first_param)
                    switch first_param.node_type {
                    case NodeType.Identifier(field_name):
                        combined := format(name, ".", field_name)
                        if context.scope_stack.is_enum_constructor(combined) {
                            // Enum constructor passed as Dynamic - need temp variable
                            c_type := format(TIL_PREFIX, name)
                            temp_var := next_mangled(ctx)
                            output.push_str("(")
                            output.push_str(TIL_PREFIX)
                            output.push_str("Dynamic*)({ ")
                            output.push_str(c_type)
                            output.push_str(" ")
                            output.push_str(temp_var)
                            output.push_str(" = ")
                            emit_expr(arg, output, 0, ctx, context)
                            output.push_str("; &")
                            output.push_str(temp_var)
                            output.push_str("; })")
                            return
                        }
                    case:
                    }
                }
                // Field access like member.name - need (til_Dynamic*)&(var.field)
                // Check if base is a mut param (pointer) or regular variable
                base_is_pointer := ctx.current_ref_params.contains(name)
                output.push_str("(")
                output.push_str(TIL_PREFIX)
                output.push_str("Dynamic*)&")
                output.push_str(til_name(name))
                mut i := 0
                for param: Expr in arg.params {
                    switch param.node_type {
                    case NodeType.Identifier(field):
                        if base_is_pointer.and(i.eq(0)) {
                            output.push_str("->")
                        } else {
                            output.push_str(".")
                        }
                        // Field names don't get til_ prefix
                        output.push_str(field)
                    case:
                    }
                    i.inc()
                }
                return
            }
        case:
        }
        // For non-identifier args (literals, compound literals), emit with (til_Dynamic*)&
        // But first check if already hoisted (hoisted_exprs already includes & prefix)
        arg_addr := to_ptr(arg)
        if ctx.hoisted_exprs.contains_key(arg_addr) {
            // Already hoisted with & prefix - just emit it
            emit_expr(arg, output, 0, ctx, context)
        } else {
            switch arg.node_type {
            case NodeType.LLiteral:
                // Compound literals are lvalues in C99+, so &((til_Str){...}) works
                output.push_str("(")
                output.push_str(TIL_PREFIX)
                output.push_str("Dynamic*)&")
                emit_expr(arg, output, 0, ctx, context)
            case:
                // Other non-identifier args (function calls, etc.) - emit as-is
                emit_expr(arg, output, 0, ctx, context)
            }
        }
        return
    }

    // Check if param is mut (but not Dynamic) - emit &arg for pointer
    if param_by_ref {
        switch arg.node_type {
        case NodeType.Identifier(name):
            if arg.params.is_empty() {
                // Check if this identifier is already a mut param (already a pointer)
                // or a variadic param (also a pointer) - don't add & again
                is_already_pointer := ctx.current_ref_params.contains(name).or(ctx.current_variadic_params.contains_key(name))
                if is_already_pointer {
                    // Already a pointer - just emit the name
                    output.push_str(til_name(name))
                } else {
                    // Simple variable - emit &var
                    output.push_str("&")
                    output.push_str(til_name(name))
                }
                return
            } else {
                // Check if this is a struct constant access (Type.CONSTANT) vs field access (var.field)
                if not(arg.params.is_empty()) {
                    mut first_param := Expr()
                    arg.params.get(0, first_param)
                    switch first_param.node_type {
                    case NodeType.Identifier(field_name):
                        // Check if 'name' is a struct type and 'field_name' is a constant
                        struct_def := context.scope_stack.lookup_struct(name)
                        catch (err: KeyNotFoundError) { }
                        if struct_def.default_values.contains_key(field_name) {
                            // Struct constant - emit &til_StructName_constant
                            output.push_str("&")
                            output.push_str(til_name(name))
                            output.push_str("_")
                            output.push_str(field_name)
                            return
                        }
                        // Bug #60: Check if this is an enum constructor (Type.Variant)
                        // Enum constructors need a temp variable to take address
                        combined := format(name, ".", field_name)
                        if context.scope_stack.is_enum_constructor(combined) {
                            // Get the enum type for C type name
                            c_type := format(TIL_PREFIX, name)
                            temp_var := next_mangled(ctx)
                            output.push_str("({ ")
                            output.push_str(c_type)
                            output.push_str(" ")
                            output.push_str(temp_var)
                            output.push_str(" = ")
                            emit_expr(arg, output, 0, ctx, context)
                            output.push_str("; &")
                            output.push_str(temp_var)
                            output.push_str("; })")
                            return
                        }
                    case:
                    }
                }
                // Handle UFCS placeholder "_" - chained expression like get_foo().bar
                // The base expression (params[0]) may be an rvalue (function call), so use temp var
                if name.eq("_").and(not(arg.params.is_empty())) {
                    // Get the type of the full expression for the temp var
                    vt := get_value_type(context, arg)
                    mut c_type := til_type_to_c(vt)
                    catch (err: Str) {
                        c_type = "int"
                    }
                    catch (err: KeyNotFoundError) {
                        c_type = "int"
                    }
                    temp_var := next_mangled(ctx)
                    output.push_str("({ ")
                    output.push_str(c_type)
                    output.push_str(" ")
                    output.push_str(temp_var)
                    output.push_str(" = ")
                    emit_expr(arg, output, 0, ctx, context)
                    output.push_str("; &")
                    output.push_str(temp_var)
                    output.push_str("; })")
                    return
                }
                // Field access like self.type_names - need &(self->field) or &(var.field)
                // Check if base is a mut param (pointer) or regular variable
                base_is_pointer := ctx.current_ref_params.contains(name)
                output.push_str("&")
                output.push_str(til_name(name))
                mut i := 0
                for param: Expr in arg.params {
                    switch param.node_type {
                    case NodeType.Identifier(field):
                        if base_is_pointer.and(i.eq(0)) {
                            output.push_str("->")
                        } else {
                            output.push_str(".")
                        }
                        output.push_str(field)
                    case:
                    }
                    i.inc()
                }
                return
            }
        case:
        }
        // For non-identifier args that need by-ref passing:
        // - Str literals become compound literals, can use &((til_Str){...})
        // - FCall results are rvalues, use compound literal: &(Type){fcall()}
        // - I64 literals use compound literal: &(til_I64){42}
        switch arg.node_type {
        case NodeType.LLiteral(lit):
            switch lit {
            case Literal.Str:
                output.push_str("&")
                emit_expr(arg, output, 0, ctx, context)
            case Literal.Number:
                // Bug #60: Number literal, use compound literal: &(til_I64){42}
                output.push_str("&(")
                output.push_str(TIL_PREFIX)
                output.push_str("I64){")
                emit_expr(arg, output, 0, ctx, context)
                output.push_str("}")
            case:
                emit_expr(arg, output, 0, ctx, context)
            }
        case NodeType.FCall:
            // Bug #60: FCall result is rvalue, can't take address directly
            // For simple typedefs like I64: use compound literal &(Type){fcall()}
            // For struct types: use GCC statement expression ({ Type _t = fcall(); &_t; })
            fd := get_fcall_func_def(context, arg)
            catch (err: KeyNotFoundError) {
                // Bug #60: No function def found - might be enum/struct constructor FCall
                // Use get_value_type to infer the result type
                vt := get_value_type(context, arg)
                switch vt {
                case ValueType.TCustom(type_name):
                    // Custom type constructor - use statement expression
                    c_type := format(TIL_PREFIX, type_name)
                    temp_var := next_mangled(ctx)
                    output.push_str("({ ")
                    output.push_str(c_type)
                    output.push_str(" ")
                    output.push_str(temp_var)
                    output.push_str(" = ")
                    emit_expr(arg, output, 0, ctx, context)
                    output.push_str("; &")
                    output.push_str(temp_var)
                    output.push_str("; })")
                case:
                    emit_expr(arg, output, 0, ctx, context)
                }
                catch (err: Str) {
                    emit_expr(arg, output, 0, ctx, context)
                }
            }
            if not(fd.return_types.is_empty()) {
                mut ret_type := ValueType.TCustom("")
                fd.return_types.get(0, ret_type)
                c_type := til_type_to_c(ret_type)
                catch (err: KeyNotFoundError) {
                    throw arg.lang_error(context.path, "ccodegen", err.msg)
                }
                // Check if return type is a simple typedef (I64) vs struct
                // I64 is the only simple typedef - all others are structs
                mut is_simple_type := false
                switch ret_type {
                case ValueType.TCustom(ret_type_name):
                    if ret_type_name.eq("I64") {
                        is_simple_type = true
                    }
                case:
                }
                if is_simple_type {
                    output.push_str("&(")
                    output.push_str(c_type)
                    output.push_str("){")
                    emit_expr(arg, output, 0, ctx, context)
                    output.push_str("}")
                } else {
                    // Struct type - use GCC statement expression
                    temp_var := next_mangled(ctx)
                    output.push_str("({ ")
                    output.push_str(c_type)
                    output.push_str(" ")
                    output.push_str(temp_var)
                    output.push_str(" = ")
                    emit_expr(arg, output, 0, ctx, context)
                    output.push_str("; &")
                    output.push_str(temp_var)
                    output.push_str("; })")
                }
            } else {
                emit_expr(arg, output, 0, ctx, context)
            }
        case:
            emit_expr(arg, output, 0, ctx, context)
        }
        return
    }

    // Check if arg is a variadic param (which is til_Array*) but expected param is NOT mut
    // In this case we need to dereference: (*til_args) to get til_Array by value
    switch arg.node_type {
    case NodeType.Identifier(name):
        if arg.params.is_empty().and(ctx.current_variadic_params.contains_key(name)) {
            output.push_str("(*")
            output.push_str(til_name(name))
            output.push_str(")")
            return
        }
    case:
    }

    emit_expr(arg, output, 0, ctx, context)

    catch (err: IndexOutOfBoundsError) { throw err.msg }
}

// Emit C code from AST (multi-pass architecture)
emit := proc(ast: Expr, mut context: Context) returns Str throws Str {
    mut output := ""
    mut ctx := CodegenContext.new()

    // C boilerplate
    output.push_str("#include <stdio.h>\n")
    output.push_str("#include <stdlib.h>\n")
    output.push_str("#include <string.h>\n\n")
    output.push_str(format("typedef unsigned char ", TIL_PREFIX, "U8;\n"))
    output.push_str(format("typedef long long ", TIL_PREFIX, "I64;\n"))
    output.push_str(format("typedef struct ", TIL_PREFIX, "Bool { ", TIL_PREFIX, "U8 data; } ", TIL_PREFIX, "Bool;\n"))
    // Dynamic and Type are special placeholder types
    output.push_str(format("typedef void* ", TIL_PREFIX, "Dynamic;\n"))
    output.push_str(format("typedef const char* ", TIL_PREFIX, "Type;\n\n"))

    // Pass 0: collect function info (throw types, return types) for call-site generation
    switch ast.node_type {
    case NodeType.Body:
        for child: Expr in ast.params {
            collect_func_info(child, ctx)
        }
    case:
    }

    // Pass 0a: collect nested function info for hoisting (populates hoisted_prototypes and nested_func_names)
    switch ast.node_type {
    case NodeType.Body:
        for child: Expr in ast.params {
            collect_nested_func_info(child, ctx, "")
        }
    case:
    }

    // Pass 0b: emit forward declarations for all structs and enums-with-payloads
    // (enums with payloads are implemented as structs in C, so they need forward declarations too)
    // Skip I64, U8, Bool, Dynamic, Type - these are primitive typedefs defined in boilerplate
    switch ast.node_type {
    case NodeType.Body:
        for child: Expr in ast.params {
            if is_struct_declaration(child) {
                switch child.node_type {
                case NodeType.Declaration(decl):
                    if decl.name.eq("I64").or(decl.name.eq("U8")).or(decl.name.eq("Bool")).or(decl.name.eq("Dynamic")).or(decl.name.eq("Type")) {
                        continue  // Skip - these are primitive typedefs
                    }
                    struct_name := til_name(decl.name)
                    output.push_str("typedef struct ")
                    output.push_str(struct_name)
                    output.push_str(" ")
                    output.push_str(struct_name)
                    output.push_str(";\n")
                case:
                }
            }
            // Also forward-declare enums with payloads (they're implemented as structs)
            if is_enum_declaration(child).and(is_enum_with_payloads(child)) {
                switch child.node_type {
                case NodeType.Declaration(decl):
                    enum_name := til_name(decl.name)
                    output.push_str("typedef struct ")
                    output.push_str(enum_name)
                    output.push_str(" ")
                    output.push_str(enum_name)
                    output.push_str(";\n")
                case:
                }
            }
        }
        output.push_str("\n")
    case:
    }

    // Pass 1a: emit simple enums (no payloads) - safe to emit early, structs may use them as fields
    switch ast.node_type {
    case NodeType.Body:
        for child: Expr in ast.params {
            if is_enum_declaration(child).and(not(is_enum_with_payloads(child))) {
                emit_enum_declaration(child, output)
            }
        }
    case:
    }

    // Pass 1b: emit all structs and enums-with-payloads in topologically sorted order
    // Both are "complex types" that can depend on each other
    switch ast.node_type {
    case NodeType.Body:
        mut type_decls := Vec.new(Expr)
        for child: Expr in ast.params {
            if is_struct_declaration(child).or(is_enum_declaration(child).and(is_enum_with_payloads(child))) {
                type_decls.push(child)
            }
        }
        sorted_indices := topological_sort_types(type_decls)
        for idx: I64 in sorted_indices {
            mut child := Expr()
            type_decls.get(idx, child)
            if is_struct_declaration(child) {
                emit_struct_declaration(child, output)
            } else {
                emit_enum_declaration(child, output)
            }
        }
    case:
    }

    // Pass 2: emit function prototypes (forward declarations)
    // 2a: top-level functions
    switch ast.node_type {
    case NodeType.Body:
        for child: Expr in ast.params {
            if is_func_declaration(child) {
                emit_func_prototype(child, output)
            }
        }
    case:
    }
    // 2b: struct functions (with mangled names)
    switch ast.node_type {
    case NodeType.Body:
        for child: Expr in ast.params {
            if is_struct_declaration(child) {
                emit_struct_func_prototypes(child, output)
            }
        }
    case:
    }
    // 2c: hoisted nested function prototypes (collected in Pass 0a)
    if not(ctx.hoisted_prototypes.is_empty()) {
        output.push_str("\n// Nested function prototypes (hoisted)\n")
        for proto: Str in ctx.hoisted_prototypes {
            output.push_str(proto)
        }
    }
    output.push_str("\n")

    // Pass 3: include external C interface (after structs and forward decls)
    // Use angle brackets to search only -I paths (src/), not the current directory
    // This avoids including generated c/self/ext.c instead of the hand-written src/ext.c
    output.push_str("#include <ext.c>\n\n")

    // Pass 4: emit struct constants (non-mut, non-function fields with mangled names)
    // Also emits size_of constants for each struct
    switch ast.node_type {
    case NodeType.Body:
        for child: Expr in ast.params {
            if is_struct_declaration(child) {
                emit_struct_constants(child, output, ctx, context)
            }
        }
    case:
    }

    // Pass 4a: emit size_of constants for enums
    switch ast.node_type {
    case NodeType.Body:
        for child: Expr in ast.params {
            if is_enum_declaration(child) {
                emit_enum_size_of_constant(child, output, ctx)
            }
        }
    case:
    }

    // Pass 4a2: emit enum_to_str functions for all enums
    // This must come after structs are defined (Str is needed for return type)
    switch ast.node_type {
    case NodeType.Body:
        for child: Expr in ast.params {
            if is_enum_declaration(child) {
                emit_enum_to_str_for_declaration(child, output, context)
            }
        }
    case:
    }

    // Pass 4b: emit top-level constants (non-mut declarations with literal values)
    switch ast.node_type {
    case NodeType.Body:
        for child: Expr in ast.params {
            if is_constant_declaration(child) {
                emit_constant_declaration(child, output, ctx, context)
            }
        }
    case:
    }

    // Pass 4c: emit global declarations (non-constant, non-func/struct/enum declarations)
    // These need to be file-scope statics so functions can access them
    switch ast.node_type {
    case NodeType.Body:
        for child: Expr in ast.params {
            if is_global_declaration(child) {
                emit_global_declaration(child, output, ctx, context)
            }
        }
    case:
    }

    // Pass 4d: emit til_size_of function (runtime type size lookup)
    emit_size_of_function(output, ctx)

    output.push_str("\n")

    // Pass 5: emit function definitions
    // 5a: top-level functions
    switch ast.node_type {
    case NodeType.Body:
        for child: Expr in ast.params {
            if is_func_declaration(child) {
                emit_func_declaration(child, output, ctx, context)
            }
        }
    case:
    }
    // 5b: struct functions (with mangled names)
    switch ast.node_type {
    case NodeType.Body:
        for child: Expr in ast.params {
            if is_struct_declaration(child) {
                emit_struct_func_bodies(child, output, ctx, context)
            }
        }
    case:
    }

    // 5c: hoisted nested function definitions
    // These were collected during emit_func_declaration when encountering nested functions
    // (prototypes were already emitted in Pass 2c)
    if not(ctx.hoisted_functions.is_empty()) {
        output.push_str("\n// Hoisted nested function definitions\n")
        for func_code: Str in ctx.hoisted_functions {
            output.push_str(func_code)
        }
    }

    // Main function
    output.push_str("int main(int argc, char** argv) {\n")

    // Clear hoisted_exprs to avoid cross-contamination from function passes
    ctx.hoisted_exprs.clear()

    // Re-populate declared_vars with global declarations (functions clear declared_vars)
    // This ensures global declarations emit only assignments in main(), not redeclarations
    switch ast.node_type {
    case NodeType.Body:
        for child: Expr in ast.params {
            if is_global_declaration(child) {
                switch child.node_type {
                case NodeType.Declaration(decl):
                    _ := ctx.declared_vars.insert(til_name(decl.name))
                case:
                }
            }
        }
    case:
    }

    // Pass 6: emit non-struct, non-function, non-enum, non-constant statements
    // Collect them into a Vec and use emit_stmts for proper variadic/throwing call handling
    // Note: global declarations are still included but will emit only assignments since they're already declared
    switch ast.node_type {
    case NodeType.Body:
        mut main_stmts := Vec.new(Expr)
        for child: Expr in ast.params {
            // Skip true/false declarations - they're now #defines
            mut skip := false
            switch child.node_type {
            case NodeType.Declaration(decl):
                if decl.name.eq("true").or(decl.name.eq("false")) {
                    skip = true
                }
            case:
            }
            if not(skip).and(not(is_func_declaration(child))).and(not(is_struct_declaration(child))).and(not(is_enum_declaration(child))).and(not(is_constant_declaration(child))) {
                main_stmts.push(child)
            }
        }
        emit_stmts(main_stmts, output, 1, ctx, context)
    case:
    }

    // Call til_main() for modes that require a main proc (like cli)
    if context.mode_def.needs_main_proc {
        // Check if main has variadic args by looking up the function
        // Variadic params have ValueType::TMulti as their type
        mut main_has_variadic := false
        fd := context.scope_stack.lookup_func("main")
        catch (err: KeyNotFoundError) { }
        for arg: Declaration in fd.args {
            switch arg.value_type {
            case ValueType.TMulti:
                main_has_variadic = true
                break
            case:
            }
        }

        if main_has_variadic {
            // Convert argc/argv to til_Array and pass to til_main
            // Skip argv[0] (exe path) to match interpreter behavior
            output.push_str("    til_Array _main_args;\n")
            output.push_str("    til_AllocError _main_args_err;\n")
            output.push_str("    til_Array_new(&_main_args, &_main_args_err, \"Str\", &(til_I64){argc - 1});\n")
            output.push_str("    for (int i = 1; i < argc; i++) {\n")
            output.push_str("        til_Str _arg = {(til_I64)argv[i], strlen(argv[i])};\n")
            output.push_str("        til_IndexOutOfBoundsError _set_err;\n")
            output.push_str("        til_Array_set(&_set_err, &_main_args, &(til_I64){i - 1}, &_arg);\n")
            output.push_str("    }\n")
            output.push_str("    til_main(&_main_args);\n")
        } else {
            output.push_str("    til_main();\n")
        }
    }

    output.push_str("    return 0;\n")
    output.push_str("}\n")

    catch (err: IndexOutOfBoundsError) { throw err.msg }
    catch (err: AllocError) { throw err.msg }

    return output
}

// Check if an expression is a struct declaration (Name := struct {...})
is_struct_declaration := func(expr: Expr) returns Bool {
    switch expr.node_type {
    case NodeType.Declaration:
        if not(expr.params.is_empty()) {
            mut first_param := Expr()
            expr.params.get(0, first_param)
            switch first_param.node_type {
            case NodeType.StructDef:
                return true
            case:
            }
        }
    case:
    }

    catch (err: IndexOutOfBoundsError) { }

    return false
}

// Check if an expression is an enum declaration (Name := enum {...})
is_enum_declaration := func(expr: Expr) returns Bool {
    switch expr.node_type {
    case NodeType.Declaration:
        if not(expr.params.is_empty()) {
            mut first_param := Expr()
            expr.params.get(0, first_param)
            switch first_param.node_type {
            case NodeType.EnumDef:
                return true
            case:
            }
        }
    case:
    }

    catch (err: IndexOutOfBoundsError) { }

    return false
}

// Check if an expression is a top-level constant declaration (name := literal)
// Constants are non-mut declarations with literal values (numbers, strings, bools)
is_constant_declaration := func(expr: Expr) returns Bool {
    switch expr.node_type {
    case NodeType.Declaration(decl):
        // Must not be mutable
        if decl.is_mut {
            return false
        }
        if not(expr.params.is_empty()) {
            mut first_param := Expr()
            expr.params.get(0, first_param)
            switch first_param.node_type {
            case NodeType.LLiteral:
                // Literal values are constants
                return true
            case NodeType.Identifier(name):
                // Bool identifiers (true/false) are constants
                if name.eq("true").or(name.eq("false")) {
                    return true
                }
            case NodeType.StructDef:
                // Skip struct definitions
                return false
            case NodeType.EnumDef:
                // Skip enum definitions
                return false
            case NodeType.FuncDef:
                // Skip function definitions
                return false
            case:
                return false
            }
        }
    case:
    }

    catch (err: IndexOutOfBoundsError) { }

    return false
}

// Check if an expression is a function call that uses out-params (i.e., the function throws)
// Such calls cannot be used inline in struct initializers - they need separate statements
is_throwing_fcall := proc(expr: Expr, context: Context) returns Bool {
    switch expr.node_type {
    case NodeType.FCall:
        fd := get_fcall_func_def(context, expr)
        catch (err: KeyNotFoundError) {
            return false
        }
        return not(fd.throw_types.is_empty())
    case:
    }
    return false
}

// Emit a top-level constant declaration at file scope
emit_constant_declaration := proc(expr: Expr, mut output: Str, mut ctx: CodegenContext, context: Context) throws Str {
    switch expr.node_type {
    case NodeType.Declaration(decl):
        if not(expr.params.is_empty()) {
            // Bug #35: Use unique name for "_" declarations to avoid C redefinition errors
            mut var_name := ""
            if decl.name.eq("_") {
                var_name = next_mangled(ctx)
            } else {
                var_name = til_name(decl.name)
            }

            // Handle literal constants (numbers, strings)
            mut first_param := Expr()
            expr.params.get(0, first_param)
            switch first_param.node_type {
            case NodeType.LLiteral(lit):
                has_str := context.scope_stack.lookup_struct("Str")
                mut has_str_bool := true
                catch (err: KeyNotFoundError) {
                    has_str_bool = false
                }
                mut c_type := ""
                switch lit {
                case Literal.Number(n):
                    c_type = TIL_PREFIX.concat("I64")
                case Literal.Str(s):
                    if has_str_bool {
                        c_type = TIL_PREFIX.concat("Str")
                    } else {
                        c_type = "const char*"
                    }
                case Literal.List(l):
                    return // Skip list literals for now
                case:
                }
                // Non-mut declarations are constants
                if not(decl.is_mut) {
                    output.push_str("const ")
                }
                output.push_str(c_type)
                output.push_str(" ")
                output.push_str(var_name)
                output.push_str(" = ")
                emit_literal(lit, output, context)
                output.push_str(";\n")
            // Handle Bool constants (true/false identifiers)
            case NodeType.Identifier(name):
                if name.eq("true").or(name.eq("false")) {
                    if not(decl.is_mut) {
                        output.push_str("const ")
                    }
                    output.push_str(TIL_PREFIX)
                    output.push_str("Bool ")
                    output.push_str(var_name)
                    output.push_str(" = ")
                    // Use struct literal instead of macro (valid constant initializer)
                    if name.eq("true") {
                        output.push_str("((")
                        output.push_str(TIL_PREFIX)
                        output.push_str("Bool){1})")
                    } else {
                        output.push_str("((")
                        output.push_str(TIL_PREFIX)
                        output.push_str("Bool){0})")
                    }
                    output.push_str(";\n")
                }
            case:
            }
        }
    case:
    }

    catch (err: IndexOutOfBoundsError) {
        throw err.msg
    }
}

// Check if an expression is a non-constant top-level declaration that needs to be a global
// These are declarations that are NOT: functions, structs, enums, or literal constants
// but are still non-mut and could be referenced from function bodies
is_global_declaration := func(expr: Expr) returns Bool {
    switch expr.node_type {
    case NodeType.Declaration(decl):
        // Skip true/false declarations - they're handled specially
        if decl.name.eq("true").or(decl.name.eq("false")) {
            return false
        }
        if not(expr.params.is_empty()) {
            mut first_param := Expr()
            expr.params.get(0, first_param)
            switch first_param.node_type {
            // Skip these - they have their own handling
            case NodeType.StructDef(sd):
                return false
            case NodeType.EnumDef(ed):
                return false
            case NodeType.FuncDef(fd):
                return false
            // Skip literal constants - they're handled by is_constant_declaration
            // (only non-mut literals are constants)
            case NodeType.LLiteral(lit):
                if not(decl.is_mut) {
                    return false
                }
                return true
            // Skip true/false RHS - they're handled by is_constant_declaration
            // (only non-mut booleans are constants)
            case NodeType.Identifier(name):
                if name.eq("true").or(name.eq("false")).and(not(decl.is_mut)) {
                    return false
                }
                return true
            // Everything else (function calls like EvalArena.new(), etc.) is a global
            case:
                return true
            }
            catch (err: IndexOutOfBoundsError) { }
        }
    case:
    }
    return false
}

// Emit a global declaration as a static variable at file scope (type only, no initializer)
// The initializer will be emitted in main()
emit_global_declaration := proc(expr: Expr, mut output: Str, mut ctx: CodegenContext, context: Context) throws Str {
    switch expr.node_type {
    case NodeType.Declaration(decl):
        if not(expr.params.is_empty()) {
            // Bug #35: Use unique name for "_" declarations to avoid C redefinition errors
            mut var_name := ""
            if decl.name.eq("_") {
                var_name = next_mangled(ctx)
            } else {
                var_name = til_name(decl.name)
            }

            // Determine the type from the initializer expression using get_value_type
            mut first_param := Expr()
            expr.params.get(0, first_param)
            vt := get_value_type(context, first_param)
            mut c_type := til_type_to_c(vt)
            catch (err: Str) {
                c_type = til_type_to_c(decl.value_type)
                catch (err2: Str) {
                    c_type = "int"
                }
            }
            catch (err: KeyNotFoundError) {
                c_type = til_type_to_c(decl.value_type)
                catch (err2: Str) {
                    c_type = "int"
                }
                catch (err2: KeyNotFoundError) {
                    c_type = "int"
                }
            }

            // Emit static declaration (no initializer - will be set in main)
            output.push_str("static ")
            output.push_str(c_type)
            output.push_str(" ")
            output.push_str(var_name)
            output.push_str(";\n")

            // Track that this variable has been declared globally
            _ := ctx.declared_vars.insert(var_name)
        }
    case:
    }

    catch (err: IndexOutOfBoundsError) {
        throw err.msg
    }
    catch (err: AllocError) {
        throw err.msg
    }
}

// Collect function info (throw types, return types, names) from AST into context
// Handles both top-level functions and struct methods
collect_func_info := proc(expr: Expr, mut ctx: CodegenContext) throws Str {
    switch expr.node_type {
    case NodeType.Declaration(decl):
        if not(expr.params.is_empty()) {
            mut first_param := Expr()
            expr.params.get(0, first_param)
            switch first_param.node_type {
            case NodeType.FuncDef(func_def):
                // Top-level function - check for variadic args (TMulti)
                mut idx := 0
                for arg: Declaration in func_def.args {
                    switch arg.value_type {
                    case ValueType.TMulti(elem_type):
                        mut info := VariadicParamInfo()
                        info.elem_type = elem_type.clone()
                        info.regular_count = idx
                        ctx.func_variadic_args.set(decl.name.clone(), info)
                        break // Only one variadic arg per function
                    case:
                    }
                    idx.inc()
                }
            case NodeType.StructDef(struct_def):
                // Struct methods - use mangled names (StructName_methodName)
                struct_name := decl.name
                for entry: MapEntry in struct_def.default_values.entries() {
                    member_name := entry.key
                    default_expr := entry.value
                    switch default_expr.node_type {
                    case NodeType.FuncDef(func_def):
                        mangled_name := struct_name.concat("_").concat(member_name)
                        // Check for variadic args (TMulti) in struct methods
                        mut idx := 0
                        for arg: Declaration in func_def.args {
                            switch arg.value_type {
                            case ValueType.TMulti(elem_type):
                                info := VariadicParamInfo()
                                info.elem_type = elem_type.clone()
                                info.regular_count = idx
                                ctx.func_variadic_args.set(mangled_name.clone(), info)
                                break
                            case:
                            }
                            idx.inc()
                        }
                    case:
                    }
                }
            case:
            }
        }
    case:
    }

    catch (err: IndexOutOfBoundsError) {
        throw err.msg
    }
    catch (err: AllocError) {
        throw err.msg
    }
}

// Collect nested function info (for hoisting): scan function bodies for nested FuncDef declarations
// This populates ctx.nested_func_names and ctx.hoisted_prototypes before we emit function bodies
collect_nested_func_info := proc(expr: Expr, mut ctx: CodegenContext, parent_func_name: Str) throws Str {
    switch expr.node_type {
    case NodeType.Declaration(decl):
        if not(expr.params.is_empty()) {
            mut first_param := Expr()
            expr.params.get(0, first_param)
            switch first_param.node_type {
            case NodeType.FuncDef(func_def):
                if not(parent_func_name.eq("")) {
                    // This is a nested function - record it for hoisting
                    mangled_name := parent_func_name.concat("_").concat(decl.name)
                    ctx.nested_func_names.set(decl.name.clone(), mangled_name.clone())

                    // Generate prototype using emit_func_signature (handles throwing functions properly)
                    mut proto := ""
                    emit_func_signature(til_name(mangled_name), func_def, proto)
                    proto.push_str(";\n")
                    ctx.hoisted_prototypes.push(proto)

                    // Recursively check for nested functions within this nested function
                    new_parent := mangled_name.clone()
                    for body_expr: Expr in func_def.body {
                        collect_nested_func_info(body_expr, ctx, new_parent)
                    }
                } else {
                    // Top-level function - scan its body for nested functions
                    for body_expr: Expr in func_def.body {
                        collect_nested_func_info(body_expr, ctx, decl.name)
                    }
                }
            case:
            }
        }
    case NodeType.Body:
        for child: Expr in expr.params {
            collect_nested_func_info(child, ctx, parent_func_name)
        }
    case NodeType.If:
        // Check then and else branches (params[0] = condition, params[1] = then, params[2] = else)
        if expr.params.len().gt(1) {
            mut then_branch := Expr()
            expr.params.get(1, then_branch)
            collect_nested_func_info(then_branch, ctx, parent_func_name)
        }
        if expr.params.len().gt(2) {
            mut else_branch := Expr()
            expr.params.get(2, else_branch)
            collect_nested_func_info(else_branch, ctx, parent_func_name)
        }
    case NodeType.While:
        // Check loop body (params[0] = condition, params[1] = body)
        if expr.params.len().gt(1) {
            mut body := Expr()
            expr.params.get(1, body)
            collect_nested_func_info(body, ctx, parent_func_name)
        }
    case NodeType.Switch:
        // Check all case bodies (params[0] = value, rest are cases)
        mut i := 1
        while i.lt(expr.params.len()) {
            mut case_expr := Expr()
            expr.params.get(i, case_expr)
            collect_nested_func_info(case_expr, ctx, parent_func_name)
            i.inc()
        }
    case:
        // Recursively check all params for other node types
        for child: Expr in expr.params {
            collect_nested_func_info(child, ctx, parent_func_name)
        }
    }

    catch (err: IndexOutOfBoundsError) {
        throw err.msg
    }
    catch (err: AllocError) {
        throw err.msg
    }
}

// Emit a struct declaration as a C struct (only mut fields become struct fields)
// Forward declarations are emitted separately, so we use "struct Name { ... };" here
emit_struct_declaration := proc(expr: Expr, mut output: Str) throws Str {
    switch expr.node_type {
    case NodeType.Declaration(decl):
        // Skip I64, U8, Bool, Dynamic, Type - these are primitive typedefs defined in boilerplate
        if decl.name.eq("I64").or(decl.name.eq("U8")).or(decl.name.eq("Bool")).or(decl.name.eq("Dynamic")).or(decl.name.eq("Type")) {
            return
        }
        if not(expr.params.is_empty()) {
            mut first_param := Expr()
            expr.params.get(0, first_param)
            switch first_param.node_type {
            case NodeType.StructDef(struct_def):
                output.push_str("struct ")
                output.push_str(til_name(decl.name))
                output.push_str(" {\n")
                for member: Declaration in struct_def.members {
                    // Only emit mut fields as struct members
                    // Skip functions and non-mut fields (constants)
                    if member.is_mut {
                        c_type := til_type_to_c(member.value_type)
                        output.push_str("    ")
                        output.push_str(c_type)
                        output.push_str(" ")
                        output.push_str(member.name)
                        output.push_str(";\n")
                        catch (err: Str) { }
                        catch (err: KeyNotFoundError) { }
                    }
                }
                output.push_str("};\n\n")
                return
            case:
            }
        }
    case:
    }

    catch (err: IndexOutOfBoundsError) {
        throw err.msg
    }

    throw "emit_struct_declaration: not a struct declaration"
}

// Emit struct constants (non-mut, non-function fields) with mangled names: StructName_constant
// Also emits size_of constant: til_size_of_StructName = sizeof(til_StructName)
emit_struct_constants := proc(expr: Expr, mut output: Str, mut ctx: CodegenContext, mut context: Context) throws Str {
    switch expr.node_type {
    case NodeType.Declaration(decl):
        if not(expr.params.is_empty()) {
            mut first_param := Expr()
            expr.params.get(0, first_param)
            switch first_param.node_type {
            case NodeType.StructDef(struct_def):
                struct_name := til_name(decl.name)
                for member: Declaration in struct_def.members {
                    // Only emit non-mut, non-function fields as constants
                    if not(member.is_mut) {
                        c_type := til_type_to_c(member.value_type)
                        // Get the default value
                        mut default_val := Expr()
                        struct_def.default_values.get(member.name, default_val)
                        output.push_str("const ")
                        output.push_str(c_type)
                        output.push_str(" ")
                        output.push_str(struct_name)
                        output.push_str("_")
                        output.push_str(member.name)
                        output.push_str(" = ")
                        emit_expr(default_val, output, 0, ctx, context)
                        output.push_str(";\n")
                        catch (err: Str) { }
                        catch (err: KeyNotFoundError) { }
                    }
                }
                // Emit size_of constant for this struct
                output.push_str("const ")
                output.push_str(TIL_PREFIX)
                output.push_str("I64 ")
                output.push_str(TIL_PREFIX)
                output.push_str("size_of_")
                output.push_str(decl.name)
                output.push_str(" = sizeof(")
                output.push_str(struct_name)
                output.push_str(");\n")
                // Track this type for til_size_of function generation
                ctx.known_types.push(decl.name.clone())
                return
            case:
            }
        }
    case:
    }
    // Not a struct, nothing to emit

    catch (err: IndexOutOfBoundsError) {
        throw err.msg
    }
    catch (err: AllocError) {
        throw err.msg
    }
}

// Emit size_of constant for an enum: til_size_of_EnumName = sizeof(til_EnumName)
emit_enum_size_of_constant := proc(expr: Expr, mut output: Str, mut ctx: CodegenContext) throws Str {
    switch expr.node_type {
    case NodeType.Declaration(decl):
        if not(expr.params.is_empty()) {
            mut first_param := Expr()
            expr.params.get(0, first_param)
            switch first_param.node_type {
            case NodeType.EnumDef(enum_def):
                enum_name := til_name(decl.name)
                output.push_str("const ")
                output.push_str(TIL_PREFIX)
                output.push_str("I64 ")
                output.push_str(TIL_PREFIX)
                output.push_str("size_of_")
                output.push_str(decl.name)
                output.push_str(" = sizeof(")
                output.push_str(enum_name)
                output.push_str(");\n")
                // Track this type for til_size_of function generation
                ctx.known_types.push(decl.name.clone())
            case:
            }
        }
    case:
    }

    catch (err: IndexOutOfBoundsError) {
        throw err.msg
    }
    catch (err: AllocError) {
        throw err.msg
    }
}

// Emit til_size_of function for runtime type size lookup
emit_size_of_function := proc(mut output: Str, ctx: CodegenContext) {
    output.push_str("\n")
    output.push_str("static inline ")
    output.push_str(TIL_PREFIX)
    output.push_str("I64 ")
    output.push_str(TIL_PREFIX)
    output.push_str("size_of(const ")
    output.push_str(TIL_PREFIX)
    output.push_str("Str* type_name) {\n")

    // All known types from structs and enums
    for type_name: Str in ctx.known_types {
        output.push_str("    if (strcmp((char*)type_name->c_string, \"")
        output.push_str(type_name)
        output.push_str("\") == 0) return ")
        output.push_str(TIL_PREFIX)
        output.push_str("size_of_")
        output.push_str(type_name)
        output.push_str(";\n")
    }

    output.push_str("    fprintf(stderr, \"size_of: unknown type %s\\n\", (char*)type_name->c_string);\n")
    output.push_str("    exit(1);\n")
    output.push_str("}\n")
}

// Check if an enum has any payloads
enum_has_payloads := func(enum_def: SEnumDef) returns Bool {
    for v: EnumVariant in enum_def.variants {
        if not(v.payload_type.is_null()) {
            return true
        }
    }
    return false
}

// Check if an expression is an enum declaration with payloads
is_enum_with_payloads := func(expr: Expr) returns Bool {
    switch expr.node_type {
    case NodeType.Declaration(decl):
        if not(expr.params.is_empty()) {
            mut first_param := Expr()
            expr.params.get(0, first_param)
            switch first_param.node_type {
            case NodeType.EnumDef(enum_def):
                return enum_has_payloads(enum_def)
            case:
            }
            catch (err: IndexOutOfBoundsError) { }
        }
    case:
    }
    return false
}

// Emit an enum with payloads as a tagged union
emit_enum_with_payloads := proc(enum_name: Str, enum_def: SEnumDef, mut output: Str) throws Str {
    // Sort variants by name for deterministic output
    mut variants := Vec.new(EnumVariant)
    for v: EnumVariant in enum_def.variants {
        variants.push(v.clone())
    }
    variants.sort_by_key_str(proc(v: EnumVariant) returns Str { return v.name })

    // 1. Emit tag enum: typedef enum { Color_Unknown = 0, ... } Color_Tag;
    output.push_str("typedef enum {\n")
    mut index := 0
    for v: EnumVariant in variants {
        output.push_str("    ")
        output.push_str(enum_name)
        output.push_str("_")
        output.push_str(v.name)
        output.push_str(" = ")
        output.push_str(I64.to_str(index))
        output.push_str(",\n")
        index.inc()
    }
    output.push_str("} ")
    output.push_str(enum_name)
    output.push_str("_Tag;\n\n")

    // 2. Emit payload union (only for variants that have payloads)
    // typedef union { unsigned char Green; long long Number; } Color_Payload;
    mut has_any_payload := false
    for v: EnumVariant in variants {
        if not(v.payload_type.is_null()) {
            has_any_payload = true
            break
        }
    }
    if has_any_payload {
        output.push_str("typedef union {\n")
        for v: EnumVariant in variants {
            if not(v.payload_type.is_null()) {
                // Read payload_type from Ptr
                mut payload_vt := ValueType.TCustom("")
                memcpy(to_ptr(payload_vt), v.payload_type.data, size_of(ValueType))
                c_type := til_type_to_c(payload_vt)
                output.push_str("    ")
                output.push_str(c_type)
                output.push_str(" ")
                output.push_str(v.name)
                output.push_str(";\n")
                catch (err: Str) { }
                catch (err: KeyNotFoundError) { }
            }
        }
        output.push_str("} ")
        output.push_str(enum_name)
        output.push_str("_Payload;\n\n")
    }

    // 3. Emit wrapper struct: struct Color { Color_Tag tag; Color_Payload payload; };
    // Note: typedef is already forward-declared, so we just define the struct body
    output.push_str("struct ")
    output.push_str(enum_name)
    output.push_str(" {\n")
    output.push_str("    ")
    output.push_str(enum_name)
    output.push_str("_Tag tag;\n")
    if has_any_payload {
        output.push_str("    ")
        output.push_str(enum_name)
        output.push_str("_Payload payload;\n")
    }
    output.push_str("};\n\n")

    // 4. Emit constructor functions for ALL variants (including no-payload ones)
    // This ensures consistent calling convention: Color_make_Red(42), Color_make_Unknown()
    for v: EnumVariant in variants {
        output.push_str("static inline ")
        output.push_str(enum_name)
        output.push_str(" ")
        output.push_str(enum_name)
        output.push_str("_make_")
        output.push_str(v.name)
        output.push_str("(")

        // Parameter for payload (if any)
        mut has_payload := false
        if not(v.payload_type.is_null()) {
            mut payload_vt := ValueType.TCustom("")
            memcpy(to_ptr(payload_vt), v.payload_type.data, size_of(ValueType))
            c_type := til_type_to_c(payload_vt)
            output.push_str(c_type)
            output.push_str(" value")
            has_payload = true
            catch (err: Str) { }
            catch (err: KeyNotFoundError) { }
        } else {
            output.push_str("void")
        }
        output.push_str(") {\n")

        // Constructor body
        output.push_str("    ")
        output.push_str(enum_name)
        output.push_str(" result = { .tag = ")
        output.push_str(enum_name)
        output.push_str("_")
        output.push_str(v.name)
        output.push_str(" };\n")

        // Set payload if present
        if has_payload {
            output.push_str("    result.payload.")
            output.push_str(v.name)
            output.push_str(" = value;\n")
        }

        output.push_str("    return result;\n")
        output.push_str("}\n\n")
    }

    catch (err: AllocError) {
        throw err.msg
    }
}

// Emit an enum declaration as a C typedef enum (for simple enums without payloads)
// or as a tagged union struct (for enums with payloads)
emit_enum_declaration := proc(expr: Expr, mut output: Str) throws Str {
    switch expr.node_type {
    case NodeType.Declaration(decl):
        if not(expr.params.is_empty()) {
            mut first_param := Expr()
            expr.params.get(0, first_param)
            switch first_param.node_type {
            case NodeType.EnumDef(enum_def):
                enum_name := til_name(decl.name)

                if enum_has_payloads(enum_def) {
                    // Phase 2: Enums with payloads - tagged union
                    emit_enum_with_payloads(enum_name, enum_def, output)
                    return
                }

                // Phase 1: Simple enum without payloads
                // typedef enum { EnumName_Variant1 = 0, ... } EnumName;
                output.push_str("typedef enum {\n")

                // Sort variants by name for deterministic output
                mut variants := Vec.new(Str)
                for v: EnumVariant in enum_def.variants {
                    variants.push(v.name)
                }
                variants.sort()

                mut index := 0
                for variant_name: Str in variants {
                    output.push_str("    ")
                    output.push_str(enum_name)
                    output.push_str("_")
                    output.push_str(variant_name)
                    output.push_str(" = ")
                    output.push_str(I64.to_str(index))
                    output.push_str(",\n")
                    index.inc()
                }

                output.push_str("} ")
                output.push_str(enum_name)
                output.push_str(";\n\n")

                // Generate constructor functions for consistency with payload enums
                // static inline EnumName EnumName_make_Variant(void) { return EnumName_Variant; }
                for variant_name: Str in variants {
                    output.push_str("static inline ")
                    output.push_str(enum_name)
                    output.push_str(" ")
                    output.push_str(enum_name)
                    output.push_str("_make_")
                    output.push_str(variant_name)
                    output.push_str("(void) { return ")
                    output.push_str(enum_name)
                    output.push_str("_")
                    output.push_str(variant_name)
                    output.push_str("; }\n")
                }
                output.push_str("\n")

                return
            case:
            }
        }
    case:
    }

    catch (err: IndexOutOfBoundsError) {
        throw err.msg
    }
    catch (err: AllocError) {
        throw err.msg
    }

    throw "emit_enum_declaration: not an enum declaration"
}

// Emit a _to_str function for an enum type
// Takes a pointer because enum_to_str(e: Dynamic) passes by reference
// For simple enums: til_Str til_EnumName_to_str(til_EnumName* e)
// For enums with payloads: til_Str til_EnumName_to_str(til_EnumName* e)
emit_enum_to_str_function := proc(enum_name: Str, enum_def: SEnumDef, mut output: Str) {
    has_payloads := enum_has_payloads(enum_def)
    mut variants := Vec.new(Str)
    for v: EnumVariant in enum_def.variants {
        variants.push(v.name)
    }
    variants.sort()

    // Function signature - takes pointer since Dynamic params are passed by reference
    output.push_str("static inline til_Str ")
    output.push_str(enum_name)
    output.push_str("_to_str(")
    output.push_str(enum_name)
    output.push_str("* e) {\n    switch(")
    if has_payloads {
        output.push_str("e->tag")
    } else {
        output.push_str("*e")
    }
    output.push_str(") {\n")

    // Cases - need the original (non-prefixed) enum name for the string
    mut original_name := enum_name
    if enum_name.starts_with(TIL_PREFIX) {
        original_name = enum_name.get_substr(TIL_PREFIX.len(), enum_name.len())
    }

    for variant: Str in variants {
        full_name := original_name.concat(".").concat(variant)
        output.push_str("        case ")
        output.push_str(enum_name)
        output.push_str("_")
        output.push_str(variant)
        output.push_str(": return (til_Str){(til_I64)\"")
        output.push_str(full_name)
        output.push_str("\", ")
        output.push_str(I64.to_str(full_name.len()))
        output.push_str("};\n")
    }

    // Default case (shouldn't happen but good for safety)
    unknown_name := original_name.concat(".?")
    output.push_str("    }\n    return (til_Str){(til_I64)\"")
    output.push_str(unknown_name)
    output.push_str("\", ")
    output.push_str(I64.to_str(unknown_name.len()))
    output.push_str("};\n}\n\n")

    catch (err: AllocError) { }
}

// Emit _to_str function for an enum declaration node
emit_enum_to_str_for_declaration := proc(expr: Expr, mut output: Str, context: Context) throws Str {
    switch expr.node_type {
    case NodeType.Declaration(decl):
        enum_name := decl.name
        enum_def := context.scope_stack.lookup_enum(enum_name)
        c_enum_name := til_name(enum_name)
        emit_enum_to_str_function(c_enum_name, enum_def, output)
        catch (err: KeyNotFoundError) {
            throw "emit_enum_to_str_for_declaration: enum not found"
        }
        return
    case:
    }
    throw "emit_enum_to_str_for_declaration: not an enum declaration"
}

// Emit struct function prototypes for all functions in a struct
emit_struct_func_prototypes := proc(expr: Expr, mut output: Str) throws Str {
    switch expr.node_type {
    case NodeType.Declaration(decl):
        if not(expr.params.is_empty()) {
            mut first_param := Expr()
            expr.params.get(0, first_param)
            switch first_param.node_type {
            case NodeType.StructDef(struct_def):
                struct_name := til_name(decl.name)
                for member: Declaration in struct_def.members {
                    // Check if default_value is a function definition
                    mut func_expr := Expr()
                    struct_def.default_values.get(member.name, func_expr)
                    switch func_expr.node_type {
                    case NodeType.FuncDef(func_def):
                        mangled_name := struct_name.concat("_").concat(member.name)
                        emit_func_signature(mangled_name, func_def, output)
                        output.push_str(";\n")
                    case:
                    }
                }
            case:
            }
        }
    case:
    }

    catch (err: IndexOutOfBoundsError) {
        throw err.msg
    }
}

// Emit a struct function body with mangled name: StructName_funcname
emit_struct_func_body := proc(struct_name: Str, member: Declaration, func_def: SFuncDef, mut output: Str, mut ctx: CodegenContext, mut context: Context) throws Str {
    // Skip external functions
    if func_def.is_ext() {
        return
    }

    // Set current function context
    ctx.current_throw_types = func_def.throw_types.clone()
    ctx.current_return_types = func_def.return_types.clone()
    // Track mut params for pointer dereference (-> vs .)
    ctx.current_ref_params.clear()
    // Track variadic params - they're passed as til_Array* so need dereference
    ctx.current_variadic_params.clear()
    for arg: Declaration in func_def.args {
        // Bug #60: All non-copy args are passed by pointer (mut, own, and const/default)
        if not(arg.is_copy) {
            _ := ctx.current_ref_params.insert(arg.name.clone())
        }
        switch arg.value_type {
        case ValueType.TMulti(elem_type):
            // elem_type is the type name string like "Bool"
            ctx.current_variadic_params.set(arg.name.clone(), til_name(elem_type))
        case:
        }
    }

    // Push a new scope frame for this function (like interpreter does)
    mut function_frame := ScopeFrame()
    function_frame.scope_type = ScopeType.Function
    // Register function parameters in the frame
    for arg: Declaration in func_def.args {
        mut info := SymbolInfo()
        info.value_type = arg.value_type.clone()
        info.is_mut = arg.is_mut
        info.is_copy = arg.is_copy
        info.is_own = arg.is_own
        info.is_comptime_const = false
        function_frame.symbols.set(arg.name.clone(), info)
    }
    context.scope_stack.frames.push(function_frame)

    // Clear declared_vars and hoisted_struct_defaults for new function scope
    ctx.declared_vars.clear()
    ctx.hoisted_struct_defaults.clear()

    mangled_name := struct_name.concat("_").concat(member.name)

    // Save and set current function name for deterministic temp naming (Bug #42 fix)
    prev_function_name := ctx.current_function_name.clone()
    prev_mangling_counter := ctx.mangling_counter
    ctx.current_function_name = mangled_name.clone()
    ctx.mangling_counter = 0

    emit_func_signature(mangled_name, func_def, output)
    output.push_str(" {\n")

    // Emit function body with catch pattern detection
    emit_stmts(func_def.body, output, 1, ctx, context)

    // For throwing void functions, add implicit return 0 at end
    if not(func_def.throw_types.is_empty()).and(func_def.return_types.is_empty()) {
        output.push_str("    return 0;\n")
    }

    output.push_str("}\n\n")

    // Restore previous function name and counter
    ctx.current_function_name = prev_function_name
    ctx.mangling_counter = prev_mangling_counter

    // Pop the function scope frame
    context.scope_stack.frames.pop()

    // Clear current function context
    ctx.current_throw_types.clear()
    ctx.current_return_types.clear()

    catch (err: IndexOutOfBoundsError) {
        throw err.msg
    }
    catch (err: AllocError) {
        throw err.msg
    }
}

// Emit struct function bodies for all functions in a struct
emit_struct_func_bodies := proc(expr: Expr, mut output: Str, mut ctx: CodegenContext, mut context: Context) throws Str {
    switch expr.node_type {
    case NodeType.Declaration(decl):
        if not(expr.params.is_empty()) {
            mut first_param := Expr()
            expr.params.get(0, first_param)
            switch first_param.node_type {
            case NodeType.StructDef(struct_def):
                struct_name := til_name(decl.name)
                for member: Declaration in struct_def.members {
                    // Check if default_value is a function definition
                    mut func_expr := Expr()
                    struct_def.default_values.get(member.name, func_expr)
                    switch func_expr.node_type {
                    case NodeType.FuncDef(func_def):
                        emit_struct_func_body(struct_name, member, func_def, output, ctx, context)
                    case:
                    }
                }
            case:
            }
        }
    case:
    }

    catch (err: IndexOutOfBoundsError) {
        throw err.msg
    }
}

// Convert TIL type to C type. Returns Err if the type can't be represented in C (e.g., functions, infer)
til_type_to_c := func(til_type: ValueType) returns Str throws Str, KeyNotFoundError {
    switch til_type {
    case ValueType.TCustom(name):
        if name.eq(INFER_TYPE) {
            throw "Cannot convert inferred type to C"
        } else {
            // All types get TIL_PREFIX
            return TIL_PREFIX.concat(name)
        }
    case ValueType.TFunction(ft):
        throw "Cannot convert function type to C"
    case ValueType.TType(tt):
        throw "Cannot convert Type to C"
    case ValueType.TMulti(mt):
        throw "Cannot convert multi-type to C"
    case:
        throw KeyNotFoundError()
    }
}

// Helper to get C type name for a ValueType (for error struct definitions)
value_type_to_c_name := func(vt: ValueType) returns Str throws Str {
    switch vt {
    case ValueType.TCustom(name):
        // All types get TIL_PREFIX
        return TIL_PREFIX.concat(name)
    case:
        throw "Cannot convert to C type name"
    }
}

// Emit function signature (used by both prototype and definition)
// For throwing functions:
//   int func_name(RetType* _ret, Error1* _err1, Error2* _err2, args...)
// For non-throwing:
//   RetType func_name(args...)
emit_func_signature := proc(func_name: Str, func_def: SFuncDef, mut output: Str) throws Str {
    is_throwing := not(func_def.throw_types.is_empty())

    if is_throwing {
        // Throwing function returns int status code
        output.push_str("int ")
    } else {
        // Non-throwing function returns its actual type
        if func_def.return_types.is_empty() {
            output.push_str("void ")
        } else {
            ret_type_vt := func_def.return_types.get(0)
            ret_type := til_type_to_c(ret_type_vt)
            output.push_str(ret_type)
            output.push_str(" ")
        }
    }

    output.push_str(func_name)
    output.push_str("(")

    mut param_count := 0

    if is_throwing {
        // Output params first: return value pointer, then error pointers
        if not(func_def.return_types.is_empty()) {
            ret_type_vt := func_def.return_types.get(0)
            ret_type := til_type_to_c(ret_type_vt)
            output.push_str(ret_type)
            output.push_str("* _ret")
            param_count.inc()
        }

        mut i := 0
        for throw_type: ValueType in func_def.throw_types {
            if param_count.gt(0) {
                output.push_str(", ")
            }
            err_type := value_type_to_c_name(throw_type)
            output.push_str(err_type)
            output.push_str("* _err")
            output.push_str(I64.to_str(i.add(1)))
            param_count.inc()
            i.inc()
        }
    }

    // Input parameters
    for arg: Declaration in func_def.args {
        if param_count.gt(0) {
            output.push_str(", ")
        }
        // Check for variadic arg (TMulti)
        switch arg.value_type {
        case ValueType.TMulti(elem_type):
            // Variadic args are passed as til_Array*
            output.push_str(TIL_PREFIX)
            output.push_str("Array* ")
            output.push_str(TIL_PREFIX)
            output.push_str(arg.name)
            param_count.inc()
            break // Variadic must be last
        case ValueType.TCustom(type_name):
            arg_type := til_type_to_c(arg.value_type)

            // Bug #60: Type is already const char*, don't add extra indirection
            is_type_param := type_name.eq("Type")
            if arg.is_mut {
                // mut: pass by pointer so mutations are visible to caller
                output.push_str(arg_type)
                output.push_str("* ")
            } else if arg.is_own {
                // own: pass by pointer, caller transfers ownership
                // Type is already a pointer, so pass by value
                if is_type_param {
                    output.push_str(arg_type)
                    output.push_str(" ")
                } else {
                    output.push_str(arg_type)
                    output.push_str("* ")
                }
            } else if arg.is_copy {
                // copy: pass by value, caller's copy is made
                output.push_str(arg_type)
                output.push_str(" ")
            } else {
                // const (default): pass by const pointer, read-only
                // Type is already a pointer, so pass by value
                if is_type_param {
                    output.push_str(arg_type)
                    output.push_str(" ")
                } else {
                    output.push_str("const ")
                    output.push_str(arg_type)
                    output.push_str("* ")
                }
            }
            output.push_str(TIL_PREFIX)
            output.push_str(arg.name)
            param_count.inc()
            catch (err: Str) { }
            catch (err: KeyNotFoundError) { }
        case:
        }
    }

    if param_count.eq(0) {
        output.push_str("void")
    }

    output.push_str(")")

    catch (err: IndexOutOfBoundsError) {
        throw err.msg
    }
    catch (err: KeyNotFoundError) {
        throw "emit_func_signature: KeyNotFoundError"
    }
}

// Emit a function prototype (forward declaration)
emit_func_prototype := proc(expr: Expr, output: Str) throws Str {
    switch expr.node_type {
    case NodeType.Declaration(decl):
        if not(expr.params.is_empty()) {
            first_param := expr.params.get(0)
            switch first_param.node_type {
            case NodeType.FuncDef(func_def):
                // Skip external functions (ext_proc/ext_func) - they're just declarations
                if func_def.is_ext() {
                    return
                }

                func_name := til_name(decl.name)
                emit_func_signature(func_name, func_def, output)
                output.push_str(";\n")
                return
            case:
            }
        }
    case:
    }

    catch (err: IndexOutOfBoundsError) {
        throw err.msg
    }

    throw "emit_func_prototype: not a function declaration"
}

// Check if an expression is a function declaration (name := proc/func)
is_func_declaration := func(expr: Expr) returns Bool {
    switch expr.node_type {
    case NodeType.Declaration(decl):
        if not(expr.params.is_empty()) {
            first_param := expr.params.get(0)
            switch first_param.node_type {
            case NodeType.FuncDef(fd):
                return true
            case:
            }
            catch (err: IndexOutOfBoundsError) { }
        }
    case:
    }
    return false
}

// Emit a function declaration as a C function
emit_func_declaration := proc(expr: Expr, output: Str, ctx: CodegenContext, context: Context) throws Str {
    switch expr.node_type {
    case NodeType.Declaration(decl):
        if not(expr.params.is_empty()) {
            first_param := expr.params.get(0)
            switch first_param.node_type {
            case NodeType.FuncDef(func_def):
                // Skip external functions (ext_proc/ext_func) - they're just declarations
                if func_def.is_ext() {
                    return
                }

                // Set current function context for return/throw generation
                ctx.current_throw_types = func_def.throw_types.clone()
                ctx.current_return_types = func_def.return_types.clone()
                // Track ref params for pointer dereference (-> vs .)
                ctx.current_ref_params.clear()
                // Track variadic params - they're passed as til_Array* so need dereference
                ctx.current_variadic_params.clear()
                for arg: Declaration in func_def.args {
                    // Bug #60: All non-copy args are passed by pointer (mut, own, and const/default)
                    if not(arg.is_copy) {
                        ctx.current_ref_params.insert(arg.name.clone())
                    }
                    switch arg.value_type {
                    case ValueType.TMulti(elem_type):
                        // elem_type is the type name string like "Bool"
                        ctx.current_variadic_params.set(arg.name.clone(), til_name(elem_type))
                    case:
                    }
                }

                // Push a new scope frame for this function (like interpreter does)
                mut function_frame := ScopeFrame()
                function_frame.scope_type = ScopeType.Function
                // Register function parameters in the frame
                // For variadic params (TMulti), register as Array type
                for arg: Declaration in func_def.args {
                    mut value_type := arg.value_type.clone()
                    switch arg.value_type {
                    case ValueType.TMulti(elem):
                        value_type = ValueType.TCustom("Array")
                    case:
                    }
                    mut info := SymbolInfo()
                    info.value_type = value_type
                    info.is_mut = arg.is_mut
                    info.is_copy = arg.is_copy
                    info.is_own = arg.is_own
                    info.is_comptime_const = false
                    function_frame.symbols.set(arg.name.clone(), info)
                }
                context.scope_stack.frames.push(function_frame)

                // Clear declared_vars and hoisted_struct_defaults for new function scope
                ctx.declared_vars.clear()
                ctx.hoisted_struct_defaults.clear()

                func_name := til_name(decl.name)

                // Save and set current function name for nested function mangling
                prev_function_name := ctx.current_function_name.clone()
                prev_mangling_counter := ctx.mangling_counter
                ctx.current_function_name = decl.name.clone()
                ctx.mangling_counter = 0  // Reset counter per-function for determinism

                emit_func_signature(func_name, func_def, output)
                output.push_str(" {\n")

                // Emit function body with catch pattern detection
                emit_stmts(func_def.body, output, 1, ctx, context)

                // For throwing void functions, add implicit return 0 at end
                if not(func_def.throw_types.is_empty()).and(func_def.return_types.is_empty()) {
                    output.push_str("    return 0;\n")
                }

                output.push_str("}\n\n")

                // Pop the function scope frame
                context.scope_stack.frames.pop()

                // Restore previous function name and counter
                ctx.current_function_name = prev_function_name
                ctx.mangling_counter = prev_mangling_counter

                // Clear current function context
                ctx.current_throw_types.clear()
                ctx.current_return_types.clear()

                return
            case:
            }
        }
    case:
    }

    catch (err: IndexOutOfBoundsError) {
        throw err.msg
    }
    catch (err: AllocError) {
        throw err.msg
    }

    throw "emit_func_declaration: not a function declaration"
}

emit_expr := proc(expr: Expr, mut output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) throws Str {
    // Check if this expression has been hoisted (for nested variadic/throwing calls)
    expr_addr := to_ptr(expr)
    mut temp_var := ""
    ctx.hoisted_exprs.get(expr_addr, temp_var)
    output.push_str(temp_var)
    return
    catch (err: KeyNotFoundError) {
        // Not hoisted, continue with normal emit
    }

    switch expr.node_type {
    case NodeType.Body:
        emit_body(expr, output, indent, ctx, context)
    case NodeType.FCall:
        emit_fcall(expr, output, indent, ctx, context)
    case NodeType.LLiteral(lit):
        emit_literal(lit, output, context)
    case NodeType.Declaration(decl):
        emit_declaration(decl, expr, output, indent, ctx, context)
    case NodeType.Identifier(name):
        // Bug #32 fix: Handle field access on expression results
        // When name is "_", params[0] is an expression to emit first,
        // then params[1..] are the field chain
        if name.eq("_").and(not(expr.params.is_empty())) {
            // Emit the base expression (params[0])
            first_param := expr.params.get(0)
            emit_expr(first_param, output, indent, ctx, context)
            // Then emit the field chain
            mut i := 1
            while i.lt(expr.params.len()) {
                param := expr.params.get(i)
                switch param.node_type {
                case NodeType.Identifier(field):
                    output.push_str(".")
                    output.push_str(field)
                case:
                }
                i.inc()
            }
            return
        }

        // Check for type-qualified access (Type.field or Type.Variant)
        if not(expr.params.is_empty()) {
            first_param := expr.params.get(0)
            switch first_param.node_type {
            case NodeType.Identifier(field):
                // Check if this is an enum variant by looking up in context
                enum_def := context.scope_stack.lookup_enum(name)
                if enum_def.contains_key(field) {
                    // Enum variant: Type.Variant -> til_Type_make_Variant()
                    output.push_str(TIL_PREFIX)
                    output.push_str(name)
                    output.push_str("_make_")
                    output.push_str(field)
                    output.push_str("()")
                    return
                }
                catch (err: KeyNotFoundError) {
                    // Not an enum, check for struct
                }
                // Check if this is a struct constant access
                struct_def := context.scope_stack.lookup_struct(name)
                // Struct constant: Type.constant -> til_Type_constant
                output.push_str(til_name(name))
                output.push_str("_")
                output.push_str(field)
                return
                catch (err: KeyNotFoundError) {
                    // Not a struct constant, continue
                }
            case:
            }
        }
        // Regular identifier or field access (b.val -> til_b.val)
        // For ref params (which are pointers in C), use -> for field access
        is_ref_param := ctx.current_ref_params.contains(name)
        if is_ref_param.and(not(expr.params.is_empty())) {
            // Ref param with field access: til_self->field1.field2.field3
            // First field uses -> (self is a pointer), rest use . (struct values)
            output.push_str(til_name(name))
            mut i := 0
            for param: Expr in expr.params {
                switch param.node_type {
                case NodeType.Identifier(field):
                    if i.eq(0) {
                        output.push_str("->")
                    } else {
                        output.push_str(".")
                    }
                    output.push_str(field)
                case:
                }
                i.inc()
            }
        } else if is_ref_param {
            // Ref param used as value: dereference with *
            output.push_str("(*")
            output.push_str(til_name(name))
            output.push_str(")")
        } else {
            // Regular identifier or field access
            output.push_str(til_name(name))
            for param: Expr in expr.params {
                switch param.node_type {
                case NodeType.Identifier(field):
                    output.push_str(".")
                    output.push_str(field)  // Field names stay as-is (C struct fields)
                case:
                }
            }
        }
    case NodeType.FuncDef(func_def):
        emit_funcdef(func_def, expr, output, indent, ctx, context)
    case NodeType.Assignment(name):
        emit_assignment(name, expr, output, indent, ctx, context)
    case NodeType.Return:
        emit_return(expr, output, indent, ctx, context)
    case NodeType.If:
        emit_if(expr, output, indent, ctx, context)
    case NodeType.While:
        emit_while(expr, output, indent, ctx, context)
    case NodeType.Break:
        emit_break(expr, output, indent)
    case NodeType.Continue:
        emit_continue(expr, output, indent)
    case NodeType.Catch:
        // Catch blocks handled at call site
    case NodeType.Throw:
        emit_throw(expr, output, indent, ctx, context)
    case NodeType.StructDef(sd):
        throw "ccodegen: StructDef should be handled at top level, not in emit_expr"
    case NodeType.EnumDef(ed):
        throw "ccodegen: EnumDef should be handled at top level, not in emit_expr"
    case NodeType.Switch:
        emit_switch(expr, output, indent, ctx, context)
    case NodeType.DefaultCase:
        throw "ccodegen: DefaultCase should be handled inside emit_switch"
    case NodeType.Range:
        throw "ccodegen: Range not yet supported"
    case NodeType.Pattern(p):
        throw "ccodegen: Pattern should be handled inside emit_switch"
    case NodeType.NamedArg(na):
        throw expr.error(context.path, "ccodegen", "NamedArg should be reordered before reaching emit_expr")
    case NodeType.ForIn(fi):
        throw expr.lang_error(context.path, "ccodegen", "ForIn should be desugared in precomp before reaching ccodegen")
    case:
    }

    catch (err: IndexOutOfBoundsError) {
        throw err.msg
    }
}

emit_body := proc(expr: Expr, output: Str, indent: I64, ctx: CodegenContext, context: Context) throws Str {
    // Bug #44 fix: Save and restore local_catch_labels around nested blocks
    // Without this, nested blocks clear the outer catches when processing their
    // own throwing calls, causing missing status checks for outer throwing calls.
    saved_catch_labels := ctx.local_catch_labels.clone()
    emit_stmts(expr.params, output, indent, ctx, context)
    ctx.local_catch_labels = saved_catch_labels

    catch (err: AllocError) {
        throw err.msg
    }
}

/// Emit a sequence of statements with catch pattern detection
/// This is the core logic shared between emit_body and emit_func_declaration
emit_stmts := proc(stmts: Vec, output: Str, indent: I64, ctx: CodegenContext, context: Context) throws Str {
    mut i := 0
    indent_str := indent_string(indent)

    // Pre-scan for function-level catches (at the end of the block)
    func_level_catches := prescan_func_level_catches(stmts)

    // Hoist declarations from catch blocks to function level
    // (TIL has function-level scoping, not block-level scoping)
    for catch_block: Expr in func_level_catches {
        if catch_block.params.len().gteq(3) {
            // Temporarily add the catch error variable to scope for type inference
            // catch (err: ErrType) { body } -> err_var=params[0], err_type=params[1], body=params[2]
            err_var_param := catch_block.params.get(0)
            err_type_param := catch_block.params.get(1)
            switch err_var_param.node_type {
            case NodeType.Identifier(err_var_name):
                switch err_type_param.node_type {
                case NodeType.Identifier(err_type_name):
                    mut info := SymbolInfo()
                    info.value_type = ValueType.TCustom(err_type_name.clone())
                    info.is_mut = false
                    info.is_copy = false
                    info.is_own = false
                    info.is_comptime_const = false
                    context.scope_stack.declare_symbol(err_var_name.clone(), info)
                case:
                }
            case:
            }

            catch_body := catch_block.params.get(2)
            decls := collect_declarations_in_body(catch_body, context)
            for decl: CollectedDeclaration in decls {
                c_var_name := til_name(decl.name)
                if not(ctx.declared_vars.contains(c_var_name)) {
                    c_type := til_type_to_c(decl.value_type)
                    output.push_str(indent_str)
                    output.push_str(c_type)
                    output.push_str(" ")
                    output.push_str(c_var_name)
                    output.push_str(";\n")
                    ctx.declared_vars.insert(c_var_name)
                    // Also register in scope_stack so get_value_type can find it
                    mut sym_info := SymbolInfo()
                    sym_info.value_type = decl.value_type.clone()
                    sym_info.is_mut = false
                    sym_info.is_copy = false
                    sym_info.is_own = false
                    sym_info.is_comptime_const = false
                    context.scope_stack.declare_symbol(decl.name.clone(), sym_info)
                    catch (err: Str) { }
                    catch (err: KeyNotFoundError) { }
                }
            }
        }
    }

    // Bug #39 fix: Give each catch block its own unique label using the global counter
    // Store catch info with statement index so we can match calls to their NEXT catch
    mut all_catch_info := Vec.new(CatchLabelInfoEntry)
    mut scan_idx := 0
    for scan_stmt: Expr in stmts {
        switch scan_stmt.node_type {
        case NodeType.Catch:
            if scan_stmt.params.len().gteq(3) {
                err_type_param := scan_stmt.params.get(1)
                switch err_type_param.node_type {
                case NodeType.Identifier(err_type_name):
                    catch_suffix := next_mangled(ctx)
                    mut entry := CatchLabelInfoEntry()
                    entry.stmt_index = scan_idx
                    entry.type_name = err_type_name.clone()
                    entry.label = concat("_catch_", err_type_name).concat("_").concat(catch_suffix)
                    entry.temp_var = concat("_thrown_", err_type_name).concat("_").concat(catch_suffix)
                    all_catch_info.push(entry)
                case:
                }
            }
        case:
        }
        scan_idx.inc()
    }

    // Declare temp variables for all catches at the start
    for catch_entry: CatchLabelInfoEntry in all_catch_info {
        output.push_str(indent_str)
        output.push_str(til_name(catch_entry.type_name))
        output.push_str(" ")
        output.push_str(catch_entry.temp_var)
        output.push_str(";\n")
    }

    // For backward compatibility, also populate local_catch_labels with first catch of each type
    // This is used by throw statements and nested blocks
    mut registered_types := Set.new(Str)
    for catch_entry: CatchLabelInfoEntry in all_catch_info {
        if not(registered_types.contains(catch_entry.type_name)) {
            registered_types.insert(catch_entry.type_name.clone())
            mut cli := CatchLabelInfo()
            cli.label = catch_entry.label.clone()
            cli.temp_var = catch_entry.temp_var.clone()
            ctx.local_catch_labels.set(catch_entry.type_name.clone(), cli)
        }
    }

    // Track which catch labels have been emitted to avoid duplicates
    // (when multiple catches of same type exist, only emit the label once)
    mut emitted_catch_labels := Set.new(Str)

    // Process all statements
    while i.lt(stmts.len()) {
        stmt := stmts.get(i)
        effective_indent := indent  // Use same indent for all statements (no if-block nesting)

        // Bug #39 fix: Handle catch blocks inline with if(0) { label: ... }
        // This keeps catches where they appear in source, and execution falls through after
        switch stmt.node_type {
        case NodeType.Catch:
            // Get the error type this catch handles
            if stmt.params.len().gteq(3) {
                err_type_param := stmt.params.get(1)
                switch err_type_param.node_type {
                case NodeType.Identifier(err_type_name):
                    // Look up this specific catch's label from all_catch_info by statement index
                    mut catch_entry_found := false
                    mut catch_entry_label := ""
                    mut catch_entry_temp_var := ""
                    for e: CatchLabelInfoEntry in all_catch_info {
                        if e.stmt_index.eq(i) {
                            catch_entry_found = true
                            catch_entry_label = e.label.clone()
                            catch_entry_temp_var = e.temp_var.clone()
                            break
                        }
                    }
                    if catch_entry_found {
                        // Only emit if we haven't already emitted this label
                        if not(emitted_catch_labels.contains(catch_entry_label)) {
                            emitted_catch_labels.insert(catch_entry_label.clone())

                            output.push_str(indent_str)
                            output.push_str("if (0) { ")
                            output.push_str(catch_entry_label)
                            output.push_str(":\n")

                            // Bind error variable
                            err_var_param := stmt.params.get(0)
                            switch err_var_param.node_type {
                            case NodeType.Identifier(err_var_name):
                                inner_indent := indent_string(indent.add(1))
                                output.push_str(inner_indent)
                                output.push_str(til_name(err_type_name))
                                output.push_str(" ")
                                output.push_str(til_name(err_var_name))
                                output.push_str(" = ")
                                output.push_str(catch_entry_temp_var)
                                output.push_str(";\n")

                                // Add error variable to scope for type resolution in catch body
                                mut sym_info := SymbolInfo()
                                sym_info.value_type = ValueType.TCustom(err_type_name.clone())
                                sym_info.is_mut = false
                                sym_info.is_copy = false
                                sym_info.is_own = false
                                sym_info.is_comptime_const = false
                                context.scope_stack.declare_symbol(err_var_name.clone(), sym_info)
                            case:
                            }

                            // Emit catch body
                            // IMPORTANT: Temporarily clear local_catch_labels so throwing calls inside
                            // the catch body don't try to jump to catches - catches shouldn't catch
                            // errors from their own body, only from code before them
                            saved_catch_labels := ctx.local_catch_labels.clone()
                            ctx.local_catch_labels.clear()
                            catch_body_expr := stmt.params.get(2)
                            emit_expr(catch_body_expr, output, indent.add(1), ctx, context)
                            ctx.local_catch_labels = saved_catch_labels

                            // Close the if(0) block - execution falls through to code after
                            output.push_str(indent_str)
                            output.push_str("}\n")

                            // Bug #46 fix: Remove this catch from local_catch_labels
                            // Catches should only handle throws from BEFORE them, not after.
                            ctx.local_catch_labels.remove(err_type_name)
                        }
                    }
                case:
                }
            }
            i.inc()
            continue
        case:
        }

        // Check if this statement is followed by catch blocks
        // And if it's a call to a throwing function (FCall, Declaration with FCall, or Assignment with FCall)
        mut maybe_fcall_found := false
        mut maybe_fcall := stmt
        mut maybe_decl_name := ""
        mut maybe_assign_name := ""

        switch stmt.node_type {
        case NodeType.FCall:
            maybe_fcall_found = true
            maybe_fcall = stmt
        case NodeType.Declaration(decl):
            // Check if declaration has an FCall as initializer
            if not(stmt.params.is_empty()) {
                first_param := stmt.params.get(0)
                switch first_param.node_type {
                case NodeType.FCall:
                    maybe_fcall_found = true
                    maybe_fcall = first_param
                    maybe_decl_name = decl.name.clone()
                case:
                }
            }
        case NodeType.Assignment(name):
            // Check if assignment has an FCall as RHS
            if not(stmt.params.is_empty()) {
                first_param := stmt.params.get(0)
                switch first_param.node_type {
                case NodeType.FCall:
                    maybe_fcall_found = true
                    maybe_fcall = first_param
                    maybe_assign_name = name.clone()
                case:
                }
            }
        case:
        }

        if maybe_fcall_found {
            // eval functions from std.meta are only available in interpret mode
            if not(maybe_fcall.params.is_empty()) {
                func_name_param := maybe_fcall.params.get(0)
                func_name := get_til_func_name_string(func_name_param)
                if func_name.eq("eval_file").or(func_name.eq("eval_to_str")).or(func_name.eq("eval_to_ast_str")).or(func_name.eq("eval_to_expr")) {
                    throw maybe_fcall.error(context.path, "ccodegen", func_name.concat("() is only available in interpret mode, not in build/run"))
                }
            }

            // Get function name from the FCall
            // Check if this function is a throwing function
            fd := get_fcall_func_def(context, maybe_fcall)
            if not(fd.throw_types.is_empty()) {
                throw_types := fd.throw_types

                // Collect subsequent catch blocks
                mut catch_blocks := Vec.new(Expr)
                mut j := i.add(1)
                while j.lt(stmts.len()) {
                    next_stmt := stmts.get(j)
                    switch next_stmt.node_type {
                    case NodeType.Catch:
                        catch_blocks.push(next_stmt)
                        j.inc()
                    case:
                        break
                    }
                }

                if not(catch_blocks.is_empty()).and(func_level_catches.is_empty()) {
                    // Immediate catches AND no function-level catches - handle inline
                    emit_throwing_call(maybe_fcall, throw_types, catch_blocks, maybe_decl_name, maybe_assign_name, output, effective_indent, ctx, context)
                    i = j  // Skip past catch blocks
                    continue
                } else if not(ctx.current_throw_types.is_empty()).and(ctx.local_catch_labels.is_empty()).and(func_level_catches.is_empty()) {
                    // Bug #68 fix: Also check func_level_catches.is_empty()
                    // No catch blocks, we're inside a throwing function, and no catches in this block
                    // Emit error propagation pattern
                    emit_throwing_call_propagate(maybe_fcall, throw_types, maybe_decl_name, maybe_assign_name, output, effective_indent, ctx, context)
                    i.inc()
                    continue
                } else if not(func_level_catches.is_empty()).or(not(ctx.local_catch_labels.is_empty())) {
                    // Has function-level catches or outer catches registered
                    // Bug #39 fix: Update local_catch_labels to point to NEXT catch for each type
                    // Build map with only catches AFTER current position
                    mut next_catches := Map.new(Str, CatchLabelInfo)
                    for catch_entry: CatchLabelInfoEntry in all_catch_info {
                        if catch_entry.stmt_index.gt(i).and(not(next_catches.contains_key(catch_entry.type_name))) {
                            // First catch of this type after current position
                            mut cli := CatchLabelInfo()
                            cli.label = catch_entry.label.clone()
                            cli.temp_var = catch_entry.temp_var.clone()
                            next_catches.set(catch_entry.type_name.clone(), cli)
                        }
                    }
                    // Replace local_catch_labels with only the next catches
                    ctx.local_catch_labels.clear()
                    for entry: MapEntry in next_catches.entries() {
                        type_name := entry.key
                        catch_info := entry.value
                        mut cli := CatchLabelInfo()
                        cli.label = catch_info.label.clone()
                        cli.temp_var = catch_info.temp_var.clone()
                        ctx.local_catch_labels.set(type_name.clone(), cli)
                    }
                    emit_throwing_call_with_goto(maybe_fcall, throw_types, maybe_decl_name, maybe_assign_name, output, effective_indent, ctx, context)
                    i.inc()
                    continue
                } else {
                    // No catches - typer should have caught this if we're in non-throwing context
                    // Just use propagate (will silently succeed on error if we're not throwing)
                    emit_throwing_call_propagate(maybe_fcall, throw_types, maybe_decl_name, maybe_assign_name, output, effective_indent, ctx, context)
                    i.inc()
                    continue
                }
            }
            catch (err: KeyNotFoundError) {
                // Not a function call, continue
            }
        }

        // Check for non-throwing variadic calls in declarations/assignments
        // These need special handling because variadic array must be constructed first
        if maybe_fcall_found {
            variadic_fcall_info := detect_variadic_fcall(maybe_fcall, ctx)
            // Check that this is NOT a throwing function (those are handled above)
            fd := get_fcall_func_def(context, maybe_fcall)
            is_throwing := not(fd.throw_types.is_empty())
            catch (err: KeyNotFoundError) {
                is_throwing = false
            }
            if not(is_throwing) {
                emit_variadic_call(maybe_fcall, variadic_fcall_info.elem_type, variadic_fcall_info.regular_count, maybe_decl_name, maybe_assign_name, output, effective_indent, ctx, context)
                i.inc()
                continue
            }
        }

        // Regular statement handling
        emit_expr(stmt, output, effective_indent, ctx, context)
        i.inc()
    }

    // Clean up local_catch_labels for this block
    for catch_entry: CatchLabelInfoEntry in all_catch_info {
        ctx.local_catch_labels.remove(catch_entry.type_name)
    }

    catch (err: IndexOutOfBoundsError) {
        throw err.msg
    }
    catch (err: AllocError) {
        throw err.msg
    }
}

/// Get the function name from an FCall expression (returns underscore format like Type_method)
/// This returns the name WITHOUT til_ prefix.
/// For C output, use til_name() on the result.
get_fcall_func_name := func(expr: Expr) returns Str throws KeyNotFoundError {
    // get_func_name_string already handles both nested identifiers (Type.method)
    // and precomp'd strings ("Type.method"), returning "Type_method" format
    first_param := expr.params.get(0)
    return get_func_name_string(first_param)

    catch (err: IndexOutOfBoundsError) {
        throw KeyNotFoundError()
    }
}

/// Pre-scan function body to collect all catch blocks
/// Returns all catch blocks found in the statements
prescan_func_level_catches := func(stmts: Vec) returns Vec {
    mut catches := Vec.new(Expr)

    // Collect ALL catch blocks - we'll register them all for local throw handling
    // The emit_throwing_call handles immediate catch chains for function calls,
    // but we need labels for throw statements that jump to these catches
    for stmt: Expr in stmts {
        switch stmt.node_type {
        case NodeType.Catch:
            catches.push(stmt)
        case:
        }
    }

    return catches

    catch (err: AllocError) { }
}

/// Emit a non-throwing variadic function call at statement level
/// Handles constructing the variadic array and cleaning it up after the call
emit_variadic_call := proc(fcall: Expr, elem_type: Str, regular_count: I64, decl_name: Str, assign_name: Str, output: Str, indent: I64, ctx: CodegenContext, context: Context) throws Str {
    indent_str := indent_string(indent)

    // Get function name
    func_name := get_fcall_func_name(fcall)
    catch (err: KeyNotFoundError) {
        throw "emit_variadic_call: could not get function name"
    }

    // Calculate param info for by-ref param handling
    fd := get_fcall_func_def(context, fcall)
    mut param_by_ref := Vec.new(Bool)
    mut param_types := Vec.new(ValueType)
    mut arg_idx := 0
    for arg: Expr in fcall.params {
        if arg_idx.eq(0) {
            arg_idx.inc()
            continue  // Skip function name
        }
        param_arg := fd.args.get(arg_idx.sub(1))
        param_by_ref.push(param_needs_by_ref(param_arg))
        param_types.push(param_arg.value_type.clone())
        arg_idx.inc()
        catch (err: IndexOutOfBoundsError) {
            param_by_ref.push(false)
            param_types.push(ValueType.TCustom(INFER_TYPE))
        }
    }
    catch (err: KeyNotFoundError) { }

    // Hoist any throwing function calls in arguments BEFORE constructing variadic array
    mut hoisted := Map.new(I64, Str)
    if fcall.params.len().gt(1) {
        hoisted_vec := hoist_throwing_args(fcall.params, 1, output, indent, ctx, context)
        for h: HoistedArg in hoisted_vec {
            hoisted.set(h.index, h.temp_var)
        }
    }

    // Construct variadic array
    variadic_arr_var := hoist_variadic_args(elem_type, fcall.params, hoisted, regular_count, output, indent, ctx, context)

    // Determine return type if we need to declare a variable
    mut ret_type := "int"
    if not(decl_name.eq("")).or(not(assign_name.eq(""))) {
        fd2 := get_fcall_func_def(context, fcall)
        if not(fd2.return_types.is_empty()) {
            first_ret := fd2.return_types.get(0)
            ret_type = til_type_to_c(first_ret)
            catch (err: Str) {
                ret_type = "int"
            }
            catch (err: KeyNotFoundError) {
                ret_type = "int"
            }
            catch (err: IndexOutOfBoundsError) {
                ret_type = "int"
            }
        }
        catch (err: KeyNotFoundError) { }
    }

    func_name_c := func_name.replace(".", "_")

    // For declarations: declare the variable (skip for "_" - Bug #35)
    if not(decl_name.eq("")) {
        if decl_name.eq("_") {
            // Bug #35: For underscore, just call the function (discard result)
            output.push_str(indent_str)
            output.push_str(TIL_PREFIX)
            output.push_str(func_name_c)
            output.push_str("(")

            mut i := 0
            while i.lt(regular_count) {
                if i.gt(0) {
                    output.push_str(", ")
                }
                arg := fcall.params.get(i.add(1))  // +1 to skip func name
                by_ref := param_by_ref.get(i)
                param_type := param_types.get(i)
                emit_arg_with_param_type(arg, i, hoisted, param_type, by_ref, output, ctx, context)
                i.inc()
                catch (err: IndexOutOfBoundsError) { }
            }

            if regular_count.gt(0) {
                output.push_str(", ")
            }
            output.push_str("&")
            output.push_str(variadic_arr_var)
            output.push_str(");\n")
        } else {
            output.push_str(indent_str)
            output.push_str(ret_type)
            output.push_str(" ")
            output.push_str(til_name(decl_name))
            output.push_str(" = ")

            // Emit function call
            output.push_str(TIL_PREFIX)
            output.push_str(func_name_c)
            output.push_str("(")

            // Emit regular args first
            mut i := 0
            while i.lt(regular_count) {
                if i.gt(0) {
                    output.push_str(", ")
                }
                arg := fcall.params.get(i.add(1))
                by_ref := param_by_ref.get(i)
                param_type := param_types.get(i)
                emit_arg_with_param_type(arg, i, hoisted, param_type, by_ref, output, ctx, context)
                i.inc()
                catch (err: IndexOutOfBoundsError) { }
            }

            // Emit variadic array pointer
            if regular_count.gt(0) {
                output.push_str(", ")
            }
            output.push_str("&")
            output.push_str(variadic_arr_var)
            output.push_str(");\n")

            // Add variable to scope
            fd3 := get_fcall_func_def(context, fcall)
            if not(fd3.return_types.is_empty()) {
                first_type := fd3.return_types.get(0)
                mut sym_info := SymbolInfo()
                sym_info.value_type = first_type.clone()
                sym_info.is_mut = true
                sym_info.is_copy = false
                sym_info.is_own = false
                sym_info.is_comptime_const = false
                context.scope_stack.declare_symbol(decl_name.clone(), sym_info)
            }
            ctx.declared_vars.insert(til_name(decl_name))
            catch (err: KeyNotFoundError) { }
            catch (err: IndexOutOfBoundsError) { }
        }
    } else if not(assign_name.eq("")) {
        // Assignment
        output.push_str(indent_str)
        output.push_str(til_name(assign_name))
        output.push_str(" = ")

        // Emit function call
        output.push_str(TIL_PREFIX)
        output.push_str(func_name_c)
        output.push_str("(")

        // Emit regular args first
        mut i := 0
        while i.lt(regular_count) {
            if i.gt(0) {
                output.push_str(", ")
            }
            arg := fcall.params.get(i.add(1))
            by_ref := param_by_ref.get(i)
            param_type := param_types.get(i)
            emit_arg_with_param_type(arg, i, hoisted, param_type, by_ref, output, ctx, context)
            i.inc()
            catch (err: IndexOutOfBoundsError) { }
        }

        // Emit variadic array pointer
        if regular_count.gt(0) {
            output.push_str(", ")
        }
        output.push_str("&")
        output.push_str(variadic_arr_var)
        output.push_str(");\n")
    } else {
        // Standalone variadic call (no return value used)
        output.push_str(indent_str)
        output.push_str(TIL_PREFIX)
        output.push_str(func_name_c)
        output.push_str("(")

        // Emit regular args first
        mut i := 0
        while i.lt(regular_count) {
            if i.gt(0) {
                output.push_str(", ")
            }
            arg := fcall.params.get(i.add(1))
            by_ref := param_by_ref.get(i)
            param_type := param_types.get(i)
            emit_arg_with_param_type(arg, i, hoisted, param_type, by_ref, output, ctx, context)
            i.inc()
            catch (err: IndexOutOfBoundsError) { }
        }

        // Emit variadic array pointer
        if regular_count.gt(0) {
            output.push_str(", ")
        }
        output.push_str("&")
        output.push_str(variadic_arr_var)
        output.push_str(");\n")
    }

    // Clean up variadic array
    output.push_str(indent_str)
    output.push_str(TIL_PREFIX)
    output.push_str("Array_delete(&")
    output.push_str(variadic_arr_var)
    output.push_str(");\n")
}

/// Emit a call to a throwing function with catch handling
emit_throwing_call := proc(fcall: Expr, throw_types: Vec, catch_blocks: Vec, decl_name: Str, assign_name: Str, output: Str, indent: I64, ctx: CodegenContext, context: Context) throws Str {
    indent_str := indent_string(indent)

    // Get function name
    mut func_name := get_fcall_func_name(fcall)
    catch (err: KeyNotFoundError) {
        throw "emit_throwing_call: could not get function name"
    }

    // Check if this is a call to a nested (hoisted) function - use mangled name
    mangled_name := ctx.nested_func_names.get(func_name)
    func_name = mangled_name
    catch (err: KeyNotFoundError) { }

    // Generate unique temp names for this call
    temp_suffix := next_mangled(ctx)

    // Determine if we need a return value temp variable
    // Only if function actually returns something AND we're capturing it
    fd := get_fcall_func_def(context, fcall)
    func_has_return := not(fd.return_types.is_empty())
    needs_ret := func_has_return.and(not(decl_name.eq("")).or(not(assign_name.eq(""))))
    catch (err: KeyNotFoundError) {
        func_has_return = false
        needs_ret = false
    }

    // Calculate param_types early for Dynamic hoisting
    mut param_types := Vec.new(ValueType)
    mut param_by_ref := Vec.new(Bool)
    mut arg_idx := 0
    for arg: Expr in fcall.params {
        if arg_idx.eq(0) {
            arg_idx.inc()
            continue  // Skip function name
        }
        fd2 := get_fcall_func_def(context, fcall)
        param_arg := fd2.args.get(arg_idx.sub(1))
        param_types.push(param_arg.value_type.clone())
        param_by_ref.push(param_needs_by_ref(param_arg))
        arg_idx.inc()
        catch (err: KeyNotFoundError) {
            param_types.push(ValueType.TCustom(INFER_TYPE))
            param_by_ref.push(false)
        }
        catch (err: IndexOutOfBoundsError) {
            param_types.push(ValueType.TCustom(INFER_TYPE))
            param_by_ref.push(false)
        }
    }

    // Hoist any throwing function calls in arguments BEFORE emitting this call
    mut hoisted := Map.new(I64, Str)
    if fcall.params.len().gt(1) {
        hoisted_vec := hoist_throwing_args(fcall.params, 1, output, indent, ctx, context)
        for h: HoistedArg in hoisted_vec {
            hoisted.set(h.index, h.temp_var)
        }
    }

    // Hoist non-lvalue args (string literals, function calls) when param type is Dynamic
    if fcall.params.len().gt(1) {
        dynamic_hoisted := hoist_for_dynamic_params(fcall.params, param_types, hoisted, output, indent, ctx, context)
        for h: HoistedArg in dynamic_hoisted {
            hoisted.set(h.index, h.temp_var)
        }
    }

    // Hoist struct-returning FCall args when param is by-ref (can't use compound literal for structs)
    if fcall.params.len().gt(1) {
        ref_hoisted := hoist_for_ref_params(fcall.params, param_by_ref, hoisted, output, indent, ctx, context)
        for h: HoistedArg in ref_hoisted {
            hoisted.set(h.index, h.temp_var)
        }
    }

    // Declare local variables for return value and errors
    mut ret_type := "int"
    if needs_ret {
        fd3 := get_fcall_func_def(context, fcall)
        if not(fd3.return_types.is_empty()) {
            first_ret := fd3.return_types.get(0)
            ret_type = til_type_to_c(first_ret)
            catch (err: Str) {
                ret_type = "int"
            }
            catch (err: KeyNotFoundError) {
                ret_type = "int"
            }
        }
        catch (err: KeyNotFoundError) { }
        catch (err: IndexOutOfBoundsError) { }
    }

    if needs_ret {
        output.push_str(indent_str)
        output.push_str(ret_type)
        output.push_str(" _ret_")
        output.push_str(temp_suffix)
        output.push_str(";\n")
    }

    // For declarations: declare the variable BEFORE the if block so it's visible after
    // Skip for underscore _ which is just a discard
    if not(decl_name.eq("")).and(not(decl_name.eq("_"))) {
        output.push_str(indent_str)
        output.push_str(ret_type)
        output.push_str(" ")
        output.push_str(til_name(decl_name))
        output.push_str(";\n")
        ctx.declared_vars.insert(til_name(decl_name))
        // Add to scope_stack for type resolution
        fd4 := get_fcall_func_def(context, fcall)
        if not(fd4.return_types.is_empty()) {
            first_type := fd4.return_types.get(0)
            mut sym_info := SymbolInfo()
            sym_info.value_type = first_type.clone()
            sym_info.is_mut = true
            sym_info.is_copy = false
            sym_info.is_own = false
            sym_info.is_comptime_const = false
            context.scope_stack.declare_symbol(decl_name.clone(), sym_info)
        }
        catch (err: KeyNotFoundError) { }
        catch (err: IndexOutOfBoundsError) { }
    }

    // Declare error structs for each throw type
    mut idx := 0
    for throw_type: ValueType in throw_types {
        switch throw_type {
        case ValueType.TCustom(type_name):
            output.push_str(indent_str)
            output.push_str(til_name(type_name))
            output.push_str(" _err")
            output.push_str(I64.to_str(idx))
            output.push_str("_")
            output.push_str(temp_suffix)
            output.push_str(" = {};\n")
        case:
        }
        idx.inc()
    }

    // Check if this is a variadic function call and construct array BEFORE the call
    variadic_info := detect_variadic_fcall(fcall, ctx)
    variadic_arr_var := hoist_variadic_args(variadic_info.elem_type, fcall.params, hoisted, variadic_info.regular_count, output, indent, ctx, context)
    mut has_variadic := true
    catch (err: KeyNotFoundError) {
        has_variadic = false
        variadic_arr_var = ""
    }

    // Generate the function call with output parameters
    output.push_str(indent_str)
    output.push_str("int _status_")
    output.push_str(temp_suffix)
    output.push_str(" = ")
    output.push_str(til_func_name(func_name))
    output.push_str("(")

    // First: return value pointer (if function returns something)
    if needs_ret {
        output.push_str("&_ret_")
        output.push_str(temp_suffix)
    }

    // Then: error pointers
    mut err_idx := 0
    while err_idx.lt(throw_types.len()) {
        if needs_ret.or(err_idx.gt(0)) {
            output.push_str(", ")
        }
        output.push_str("&_err")
        output.push_str(I64.to_str(err_idx))
        output.push_str("_")
        output.push_str(temp_suffix)
        err_idx.inc()
    }

    // Emit arguments - handle variadic separately from regular args
    if has_variadic {
        // Variadic call: emit regular args first, then variadic array pointer
        mut arg_i := 0
        while arg_i.lt(variadic_info.regular_count) {
            if needs_ret.or(not(throw_types.is_empty())).or(arg_i.gt(0)) {
                output.push_str(", ")
            }
            arg := fcall.params.get(arg_i.add(1))  // +1 to skip func name
            by_ref := param_by_ref.get(arg_i)
            param_type := param_types.get(arg_i)
            emit_arg_with_param_type(arg, arg_i, hoisted, param_type, by_ref, output, ctx, context)
            arg_i.inc()
            catch (err: IndexOutOfBoundsError) { }
        }
        // Emit variadic array pointer
        if needs_ret.or(not(throw_types.is_empty())).or(variadic_info.regular_count.gt(0)) {
            output.push_str(", ")
        }
        output.push_str("&")
        output.push_str(variadic_arr_var)
    } else {
        // Non-variadic: emit all args directly
        mut args_started := false
        mut arg_i := 0
        for arg: Expr in fcall.params {
            if arg_i.eq(0) {
                arg_i.inc()
                continue  // Skip function name
            }
            if needs_ret.or(not(throw_types.is_empty())).or(args_started) {
                output.push_str(", ")
            }
            by_ref := param_by_ref.get(arg_i.sub(1))
            param_type := param_types.get(arg_i.sub(1))
            emit_arg_with_param_type(arg, arg_i.sub(1), hoisted, param_type, by_ref, output, ctx, context)
            args_started = true
            arg_i.inc()
            catch (err: IndexOutOfBoundsError) { }
        }
    }

    output.push_str(");\n")

    // Generate if/else chain for error handling
    output.push_str(indent_str)
    output.push_str("if (_status_")
    output.push_str(temp_suffix)
    output.push_str(" == 0) {\n")

    // Success case: assign return value to target variable
    // Skip for underscore _ which is just a discard
    if not(decl_name.eq("")).and(not(decl_name.eq("_"))) {
        inner_indent := indent_string(indent.add(1))
        output.push_str(inner_indent)
        output.push_str(til_name(decl_name))
        output.push_str(" = _ret_")
        output.push_str(temp_suffix)
        output.push_str(";\n")
    } else if not(assign_name.eq("")) {
        // Assignment: assign to existing variable
        inner_indent := indent_string(indent.add(1))
        output.push_str(inner_indent)
        // Check if assignment target is a field access on a mut param (self.field)
        dot_pos := assign_name.find(".")
        if dot_pos.gteq(0) {
            base := assign_name.get_substr(0, dot_pos)
            rest := assign_name.get_substr(dot_pos.add(1), assign_name.len())
            if ctx.current_ref_params.contains(base) {
                // Mut param field access: til_self->field
                output.push_str(til_name(base))
                output.push_str("->")
                output.push_str(rest)
            } else {
                output.push_str(til_name(assign_name))
            }
        } else if ctx.current_ref_params.contains(assign_name) {
            // Direct assignment to mut param: *til_var = value
            output.push_str("*")
            output.push_str(til_name(assign_name))
        } else {
            output.push_str(til_name(assign_name))
        }
        output.push_str(" = _ret_")
        output.push_str(temp_suffix)
        output.push_str(";\n")
    }

    output.push_str(indent_str)
    output.push_str("}")

    // Generate else-if branches for each catch block
    for catch_block: Expr in catch_blocks {
        if catch_block.params.len().gteq(3) {
            // Get error type name from catch block
            err_type_param := catch_block.params.get(1)
            switch err_type_param.node_type {
            case NodeType.Identifier(err_type_name):
                // Find index of this error type
                mut catch_err_idx := 0
                mut found_idx := 0
                mut found := false
                for vt: ValueType in throw_types {
                    switch vt {
                    case ValueType.TCustom(name):
                        if name.eq(err_type_name) {
                            found = true
                            found_idx = catch_err_idx
                            break
                        }
                    case:
                    }
                    catch_err_idx.inc()
                }

                if found {
                    output.push_str(" else if (_status_")
                    output.push_str(temp_suffix)
                    output.push_str(" == ")
                    output.push_str(I64.to_str(found_idx.add(1)))
                    output.push_str(") {\n")

                    // Bind error variable and add to scope for type resolution
                    err_var_param := catch_block.params.get(0)
                    switch err_var_param.node_type {
                    case NodeType.Identifier(err_var_name):
                        inner_indent := indent_string(indent.add(1))
                        output.push_str(inner_indent)
                        output.push_str(til_name(err_type_name))
                        output.push_str(" ")
                        output.push_str(til_name(err_var_name))
                        output.push_str(" = _err")
                        output.push_str(I64.to_str(found_idx))
                        output.push_str("_")
                        output.push_str(temp_suffix)
                        output.push_str(";\n")

                        // Add error variable to scope for type resolution in catch body
                        mut sym_info := SymbolInfo()
                        sym_info.value_type = ValueType.TCustom(err_type_name.clone())
                        sym_info.is_mut = false
                        sym_info.is_copy = false
                        sym_info.is_own = false
                        sym_info.is_comptime_const = false
                        context.scope_stack.declare_symbol(err_var_name.clone(), sym_info)
                    case:
                    }

                    // Emit catch body
                    catch_body := catch_block.params.get(2)
                    emit_expr(catch_body, output, indent.add(1), ctx, context)

                    output.push_str(indent_str)
                    output.push_str("}")
                }
            case:
            }
        }
    }

    output.push_str("\n")

    catch (err: IndexOutOfBoundsError) {
        throw err.msg
    }
    catch (err: AllocError) {
        throw err.msg
    }
}

/// Emit a throwing function call that propagates errors to the caller (no catch blocks)
/// This is used when a throwing function calls another throwing function without catching
emit_throwing_call_propagate := proc(fcall: Expr, throw_types: Vec, decl_name: Str, assign_name: Str, output: Str, indent: I64, ctx: CodegenContext, context: Context) throws Str {
    indent_str := indent_string(indent)

    // Get function name
    mut func_name := get_fcall_func_name(fcall)
    catch (err: KeyNotFoundError) {
        throw "emit_throwing_call_propagate: could not get function name"
    }

    // Check if this is a call to a nested (hoisted) function - use mangled name
    mangled_name := ctx.nested_func_names.get(func_name)
    func_name = mangled_name
    catch (err: KeyNotFoundError) { }

    // Generate unique temp names for this call
    temp_suffix := next_mangled(ctx)

    // Determine if we need a return value temp variable
    fd := get_fcall_func_def(context, fcall)
    func_has_return := not(fd.return_types.is_empty())
    needs_ret := func_has_return.and(not(decl_name.eq("")).or(not(assign_name.eq(""))))
    catch (err: KeyNotFoundError) {
        func_has_return = false
        needs_ret = false
    }

    // Calculate param_types and param_by_ref
    mut param_types := Vec.new(ValueType)
    mut param_by_ref := Vec.new(Bool)
    mut arg_idx := 0
    for arg: Expr in fcall.params {
        if arg_idx.eq(0) {
            arg_idx.inc()
            continue
        }
        fd2 := get_fcall_func_def(context, fcall)
        param_arg := fd2.args.get(arg_idx.sub(1))
        param_types.push(param_arg.value_type.clone())
        param_by_ref.push(param_needs_by_ref(param_arg))
        arg_idx.inc()
        catch (err: KeyNotFoundError) {
            param_types.push(ValueType.TCustom(INFER_TYPE))
            param_by_ref.push(false)
        }
        catch (err: IndexOutOfBoundsError) {
            param_types.push(ValueType.TCustom(INFER_TYPE))
            param_by_ref.push(false)
        }
    }

    // Hoist any throwing function calls in arguments BEFORE emitting this call
    mut hoisted := Map.new(I64, Str)
    if fcall.params.len().gt(1) {
        hoisted_vec := hoist_throwing_args(fcall.params, 1, output, indent, ctx, context)
        for h: HoistedArg in hoisted_vec {
            hoisted.set(h.index, h.temp_var)
        }
    }

    // Hoist non-lvalue args when param type is Dynamic
    if fcall.params.len().gt(1) {
        dynamic_hoisted := hoist_for_dynamic_params(fcall.params, param_types, hoisted, output, indent, ctx, context)
        for h: HoistedArg in dynamic_hoisted {
            hoisted.set(h.index, h.temp_var)
        }
    }

    // Hoist struct-returning FCall args when param is by-ref
    if fcall.params.len().gt(1) {
        ref_hoisted := hoist_for_ref_params(fcall.params, param_by_ref, hoisted, output, indent, ctx, context)
        for h: HoistedArg in ref_hoisted {
            hoisted.set(h.index, h.temp_var)
        }
    }

    // Look up the actual return type
    mut ret_type := "int"
    if needs_ret {
        fd3 := get_fcall_func_def(context, fcall)
        if not(fd3.return_types.is_empty()) {
            first_ret := fd3.return_types.get(0)
            ret_type = til_type_to_c(first_ret)
            catch (err: Str) { ret_type = "int" }
            catch (err: KeyNotFoundError) { ret_type = "int" }
        }
        catch (err: KeyNotFoundError) { }
        catch (err: IndexOutOfBoundsError) { }
    }

    // Declare temp for return value if needed
    if needs_ret {
        output.push_str(indent_str)
        output.push_str(ret_type)
        output.push_str(" _ret_")
        output.push_str(temp_suffix)
        output.push_str(";\n")
    }

    // For declarations: declare the variable BEFORE the if block so it's visible after
    if not(decl_name.eq("")).and(not(decl_name.eq("_"))) {
        output.push_str(indent_str)
        output.push_str(ret_type)
        output.push_str(" ")
        output.push_str(til_name(decl_name))
        output.push_str(";\n")
        ctx.declared_vars.insert(til_name(decl_name))
        fd4 := get_fcall_func_def(context, fcall)
        if not(fd4.return_types.is_empty()) {
            first_type := fd4.return_types.get(0)
            mut sym_info := SymbolInfo()
            sym_info.value_type = first_type.clone()
            sym_info.is_mut = true
            sym_info.is_copy = false
            sym_info.is_own = false
            sym_info.is_comptime_const = false
            context.scope_stack.declare_symbol(decl_name.clone(), sym_info)
        }
        catch (err: KeyNotFoundError) { }
        catch (err: IndexOutOfBoundsError) { }
    }

    // Declare error structs for each throw type of the called function
    mut idx := 0
    for throw_type: ValueType in throw_types {
        switch throw_type {
        case ValueType.TCustom(type_name):
            output.push_str(indent_str)
            output.push_str(til_name(type_name))
            output.push_str(" _err")
            output.push_str(I64.to_str(idx))
            output.push_str("_")
            output.push_str(temp_suffix)
            output.push_str(" = {};\n")
        case:
        }
        idx.inc()
    }

    // Check for variadic function call
    variadic_info := detect_variadic_fcall(fcall, ctx)
    variadic_arr_var := hoist_variadic_args(variadic_info.elem_type, fcall.params, hoisted, variadic_info.regular_count, output, indent, ctx, context)
    mut has_variadic := true
    catch (err: KeyNotFoundError) {
        has_variadic = false
        variadic_arr_var = ""
    }

    // Generate the function call with output parameters
    output.push_str(indent_str)
    output.push_str("int _status_")
    output.push_str(temp_suffix)
    output.push_str(" = ")
    output.push_str(til_func_name(func_name))
    output.push_str("(")

    // First: return value pointer
    if needs_ret {
        output.push_str("&_ret_")
        output.push_str(temp_suffix)
    }

    // Then: error pointers
    mut err_idx := 0
    while err_idx.lt(throw_types.len()) {
        if needs_ret.or(err_idx.gt(0)) {
            output.push_str(", ")
        }
        output.push_str("&_err")
        output.push_str(I64.to_str(err_idx))
        output.push_str("_")
        output.push_str(temp_suffix)
        err_idx.inc()
    }

    // Emit arguments - handle variadic separately
    if has_variadic {
        mut arg_i := 0
        while arg_i.lt(variadic_info.regular_count) {
            if needs_ret.or(not(throw_types.is_empty())).or(arg_i.gt(0)) {
                output.push_str(", ")
            }
            arg := fcall.params.get(arg_i.add(1))
            by_ref := param_by_ref.get(arg_i)
            param_type := param_types.get(arg_i)
            emit_arg_with_param_type(arg, arg_i, hoisted, param_type, by_ref, output, ctx, context)
            arg_i.inc()
            catch (err: IndexOutOfBoundsError) { }
        }
        if needs_ret.or(not(throw_types.is_empty())).or(variadic_info.regular_count.gt(0)) {
            output.push_str(", ")
        }
        output.push_str("&")
        output.push_str(variadic_arr_var)
    } else {
        mut args_started := false
        mut arg_i := 0
        for arg: Expr in fcall.params {
            if arg_i.eq(0) {
                arg_i.inc()
                continue
            }
            if needs_ret.or(not(throw_types.is_empty())).or(args_started) {
                output.push_str(", ")
            }
            by_ref := param_by_ref.get(arg_i.sub(1))
            param_type := param_types.get(arg_i.sub(1))
            emit_arg_with_param_type(arg, arg_i.sub(1), hoisted, param_type, by_ref, output, ctx, context)
            args_started = true
            arg_i.inc()
            catch (err: IndexOutOfBoundsError) { }
        }
    }

    output.push_str(");\n")

    // Generate error propagation: if status != 0, copy error to caller's error param and return
    mut called_idx := 0
    for called_throw_type: ValueType in throw_types {
        switch called_throw_type {
        case ValueType.TCustom(called_type_name):
            // Find matching error type in current function's throw types
            mut current_idx := 0
            mut found := false
            for cur_vt: ValueType in ctx.current_throw_types {
                switch cur_vt {
                case ValueType.TCustom(name):
                    if name.eq(called_type_name) {
                        found = true
                        break
                    }
                case:
                }
                current_idx.inc()
            }
            if found {
                output.push_str(indent_str)
                output.push_str("if (_status_")
                output.push_str(temp_suffix)
                output.push_str(" == ")
                output.push_str(I64.to_str(called_idx.add(1)))
                output.push_str(") { *_err")
                output.push_str(I64.to_str(current_idx.add(1)))
                output.push_str(" = _err")
                output.push_str(I64.to_str(called_idx))
                output.push_str("_")
                output.push_str(temp_suffix)
                output.push_str("; return ")
                output.push_str(I64.to_str(current_idx.add(1)))
                output.push_str("; }\n")
            }
        case:
        }
        called_idx.inc()
    }

    // Success case: assign return value to target variable if needed
    if not(decl_name.eq("")).and(not(decl_name.eq("_"))) {
        output.push_str(indent_str)
        output.push_str(til_name(decl_name))
        output.push_str(" = _ret_")
        output.push_str(temp_suffix)
        output.push_str(";\n")
    } else if not(assign_name.eq("")) {
        output.push_str(indent_str)
        dot_pos := assign_name.find(".")
        if dot_pos.gteq(0) {
            base := assign_name.get_substr(0, dot_pos)
            rest := assign_name.get_substr(dot_pos.add(1), assign_name.len())
            if ctx.current_ref_params.contains(base) {
                output.push_str(til_name(base))
                output.push_str("->")
                output.push_str(rest)
            } else {
                output.push_str(til_name(assign_name))
            }
        } else if ctx.current_ref_params.contains(assign_name) {
            output.push_str("*")
            output.push_str(til_name(assign_name))
        } else {
            output.push_str(til_name(assign_name))
        }
        output.push_str(" = _ret_")
        output.push_str(temp_suffix)
        output.push_str(";\n")
    }

    catch (err: IndexOutOfBoundsError) {
        throw err.msg
    }
    catch (err: AllocError) {
        throw err.msg
    }
}

/// Emit a throwing function call that uses goto for error handling
/// Used when there are outer catch blocks registered in ctx.local_catch_labels
emit_throwing_call_with_goto := proc(fcall: Expr, throw_types: Vec, decl_name: Str, assign_name: Str, output: Str, indent: I64, ctx: CodegenContext, context: Context) throws Str {
    indent_str := indent_string(indent)

    // Get function name
    mut func_name := get_fcall_func_name(fcall)
    catch (err: KeyNotFoundError) {
        throw "emit_throwing_call_with_goto: could not get function name"
    }

    // Check if this is a call to a nested (hoisted) function - use mangled name
    mangled_name := ctx.nested_func_names.get(func_name)
    func_name = mangled_name
    catch (err: KeyNotFoundError) { }

    // Generate unique temp names for this call
    temp_suffix := next_mangled(ctx)

    // Determine if we need a return value temp variable
    fd := get_fcall_func_def(context, fcall)
    func_has_return := not(fd.return_types.is_empty())
    needs_ret := func_has_return.and(not(decl_name.eq("")).or(not(assign_name.eq(""))))
    catch (err: KeyNotFoundError) {
        func_has_return = false
        needs_ret = false
    }

    // Calculate param_types and param_by_ref
    mut param_types := Vec.new(ValueType)
    mut param_by_ref := Vec.new(Bool)
    mut arg_idx := 0
    for arg: Expr in fcall.params {
        if arg_idx.eq(0) {
            arg_idx.inc()
            continue
        }
        fd2 := get_fcall_func_def(context, fcall)
        param_arg := fd2.args.get(arg_idx.sub(1))
        param_types.push(param_arg.value_type.clone())
        param_by_ref.push(param_needs_by_ref(param_arg))
        arg_idx.inc()
        catch (err: KeyNotFoundError) {
            param_types.push(ValueType.TCustom(INFER_TYPE))
            param_by_ref.push(false)
        }
        catch (err: IndexOutOfBoundsError) {
            param_types.push(ValueType.TCustom(INFER_TYPE))
            param_by_ref.push(false)
        }
    }

    // Hoist any throwing function calls in arguments BEFORE emitting this call
    mut hoisted := Map.new(I64, Str)
    if fcall.params.len().gt(1) {
        hoisted_vec := hoist_throwing_args(fcall.params, 1, output, indent, ctx, context)
        for h: HoistedArg in hoisted_vec {
            hoisted.set(h.index, h.temp_var)
        }
    }

    // Hoist non-lvalue args when param type is Dynamic
    if fcall.params.len().gt(1) {
        dynamic_hoisted := hoist_for_dynamic_params(fcall.params, param_types, hoisted, output, indent, ctx, context)
        for h: HoistedArg in dynamic_hoisted {
            hoisted.set(h.index, h.temp_var)
        }
    }

    // Hoist struct-returning FCall args when param is by-ref
    if fcall.params.len().gt(1) {
        ref_hoisted := hoist_for_ref_params(fcall.params, param_by_ref, hoisted, output, indent, ctx, context)
        for h: HoistedArg in ref_hoisted {
            hoisted.set(h.index, h.temp_var)
        }
    }

    // Look up the actual return type
    mut ret_type := "int"
    if needs_ret {
        fd3 := get_fcall_func_def(context, fcall)
        if not(fd3.return_types.is_empty()) {
            first_ret := fd3.return_types.get(0)
            ret_type = til_type_to_c(first_ret)
            catch (err: Str) { ret_type = "int" }
            catch (err: KeyNotFoundError) { ret_type = "int" }
        }
        catch (err: KeyNotFoundError) { }
        catch (err: IndexOutOfBoundsError) { }
    }

    // Declare temp for return value if needed
    if needs_ret {
        output.push_str(indent_str)
        output.push_str(ret_type)
        output.push_str(" _ret_")
        output.push_str(temp_suffix)
        output.push_str(";\n")
    }

    // For declarations: declare the variable BEFORE the if block so it's visible after
    if not(decl_name.eq("")).and(not(decl_name.eq("_"))) {
        output.push_str(indent_str)
        output.push_str(ret_type)
        output.push_str(" ")
        output.push_str(til_name(decl_name))
        output.push_str(";\n")
        ctx.declared_vars.insert(til_name(decl_name))
        fd4 := get_fcall_func_def(context, fcall)
        if not(fd4.return_types.is_empty()) {
            first_type := fd4.return_types.get(0)
            mut sym_info := SymbolInfo()
            sym_info.value_type = first_type.clone()
            sym_info.is_mut = true
            sym_info.is_copy = false
            sym_info.is_own = false
            sym_info.is_comptime_const = false
            context.scope_stack.declare_symbol(decl_name.clone(), sym_info)
        }
        catch (err: KeyNotFoundError) { }
        catch (err: IndexOutOfBoundsError) { }
    }

    // Declare error structs for each throw type of the called function
    mut idx := 0
    for throw_type: ValueType in throw_types {
        switch throw_type {
        case ValueType.TCustom(type_name):
            output.push_str(indent_str)
            output.push_str(til_name(type_name))
            output.push_str(" _err")
            output.push_str(I64.to_str(idx))
            output.push_str("_")
            output.push_str(temp_suffix)
            output.push_str(" = {};\n")
        case:
        }
        idx.inc()
    }

    // Check for variadic function call
    variadic_info := detect_variadic_fcall(fcall, ctx)
    variadic_arr_var := hoist_variadic_args(variadic_info.elem_type, fcall.params, hoisted, variadic_info.regular_count, output, indent, ctx, context)
    mut has_variadic := true
    catch (err: KeyNotFoundError) {
        has_variadic = false
        variadic_arr_var = ""
    }

    // Generate the function call with output parameters
    output.push_str(indent_str)
    output.push_str("int _status_")
    output.push_str(temp_suffix)
    output.push_str(" = ")
    output.push_str(til_func_name(func_name))
    output.push_str("(")

    // First: return value pointer
    if needs_ret {
        output.push_str("&_ret_")
        output.push_str(temp_suffix)
    }

    // Then: error pointers
    mut err_idx := 0
    while err_idx.lt(throw_types.len()) {
        if needs_ret.or(err_idx.gt(0)) {
            output.push_str(", ")
        }
        output.push_str("&_err")
        output.push_str(I64.to_str(err_idx))
        output.push_str("_")
        output.push_str(temp_suffix)
        err_idx.inc()
    }

    // Emit arguments
    mut args_started := false
    regular_arg_count := variadic_info.regular_count
    mut arg_i := 0
    for arg: Expr in fcall.params {
        if arg_i.eq(0) {
            arg_i.inc()
            continue
        }
        if arg_i.sub(1).gteq(regular_arg_count) {
            break
        }
        if needs_ret.or(not(throw_types.is_empty())).or(args_started) {
            output.push_str(", ")
        }
        by_ref := param_by_ref.get(arg_i.sub(1))
        param_type := param_types.get(arg_i.sub(1))
        emit_arg_with_param_type(arg, arg_i.sub(1), hoisted, param_type, by_ref, output, ctx, context)
        args_started = true
        arg_i.inc()
        catch (err: IndexOutOfBoundsError) { }
    }

    // Add variadic array if present
    if has_variadic {
        if needs_ret.or(not(throw_types.is_empty())).or(args_started) {
            output.push_str(", ")
        }
        output.push_str("&")
        output.push_str(variadic_arr_var)
    }

    output.push_str(");\n")

    // Bug #39 fix: Use simple status checks with goto, not if-block nesting
    mut goto_err_idx := 0
    for goto_throw_type: ValueType in throw_types {
        switch goto_throw_type {
        case ValueType.TCustom(type_name):
            // Check if there's a local catch for this error type
            catch_info := ctx.local_catch_labels.get(type_name)
            // Jump to local catch: copy error to temp var, then goto label
            output.push_str(indent_str)
            output.push_str("if (_status_")
            output.push_str(temp_suffix)
            output.push_str(" == ")
            output.push_str(I64.to_str(goto_err_idx.add(1)))
            output.push_str(") { ")
            output.push_str(catch_info.temp_var)
            output.push_str(" = _err")
            output.push_str(I64.to_str(goto_err_idx))
            output.push_str("_")
            output.push_str(temp_suffix)
            output.push_str("; goto ")
            output.push_str(catch_info.label)
            output.push_str("; }\n")
            catch (err: KeyNotFoundError) {
                // Check if this error type is in current_throw_types (propagate to caller)
                mut prop_idx := 0
                mut found_prop := false
                for prop_vt: ValueType in ctx.current_throw_types {
                    switch prop_vt {
                    case ValueType.TCustom(n):
                        if n.eq(type_name) {
                            found_prop = true
                            break
                        }
                    case:
                    }
                    prop_idx.inc()
                }
                if found_prop {
                    output.push_str(indent_str)
                    output.push_str("if (_status_")
                    output.push_str(temp_suffix)
                    output.push_str(" == ")
                    output.push_str(I64.to_str(goto_err_idx.add(1)))
                    output.push_str(") { *_err")
                    output.push_str(I64.to_str(prop_idx.add(1)))
                    output.push_str(" = _err")
                    output.push_str(I64.to_str(goto_err_idx))
                    output.push_str("_")
                    output.push_str(temp_suffix)
                    output.push_str("; return ")
                    output.push_str(I64.to_str(prop_idx.add(1)))
                    output.push_str("; }\n")
                }
            }
        case:
        }
        goto_err_idx.inc()
    }

    // Success case: assign return value to target variable if needed
    if not(decl_name.eq("")).and(not(decl_name.eq("_"))) {
        output.push_str(indent_str)
        output.push_str(til_name(decl_name))
        output.push_str(" = _ret_")
        output.push_str(temp_suffix)
        output.push_str(";\n")
    } else if not(assign_name.eq("")) {
        output.push_str(indent_str)
        dot_pos := assign_name.find(".")
        if dot_pos.gteq(0) {
            base := assign_name.get_substr(0, dot_pos)
            rest := assign_name.get_substr(dot_pos.add(1), assign_name.len())
            if ctx.current_ref_params.contains(base) {
                output.push_str(til_name(base))
                output.push_str("->")
                output.push_str(rest)
            } else {
                output.push_str(til_name(assign_name))
            }
        } else if ctx.current_ref_params.contains(assign_name) {
            output.push_str("*")
            output.push_str(til_name(assign_name))
        } else {
            output.push_str(til_name(assign_name))
        }
        output.push_str(" = _ret_")
        output.push_str(temp_suffix)
        output.push_str(";\n")
    }

    catch (err: IndexOutOfBoundsError) {
        throw err.msg
    }
    catch (err: AllocError) {
        throw err.msg
    }
}

emit_declaration := proc(decl: SDeclaration, expr: Expr, mut output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) throws Str {
    // Skip inline ext_func/ext_proc declarations - they're just declaring external functions exist
    if not(expr.params.is_empty()) {
        first_param := expr.params.get(0)
        switch first_param.node_type {
        case NodeType.FuncDef(func_def):
            if func_def.is_ext() {
                return
            }

            // Handle nested function declarations - hoist to top level with mangled name
            if not(ctx.current_function_name.is_empty()) {
                parent_name := ctx.current_function_name.clone()
                mangled_name := parent_name.clone()
                mangled_name.push_str("_")
                mangled_name.push_str(decl.name)

                // Register the name mapping so function calls can find it
                ctx.nested_func_names.set(decl.name.clone(), mangled_name.clone())

                // Register function definition so get_value_type can resolve return types
                context.scope_stack.declare_func(decl.name.clone(), func_def.clone())

                // Generate the hoisted function prototype
                mut proto_output := ""
                emit_func_signature(til_name(mangled_name), func_def, proto_output)
                proto_output.push_str(";\n")
                ctx.hoisted_prototypes.push(proto_output)

                // Generate the hoisted function definition
                mut func_output := ""

                // Save and set context for nested function
                prev_throw_types := ctx.current_throw_types.clone()
                prev_return_types := ctx.current_return_types.clone()
                prev_declared_vars := ctx.declared_vars.clone()
                prev_mut_params := ctx.current_ref_params.clone()
                prev_variadic_params := ctx.current_variadic_params.clone()
                prev_function_name := ctx.current_function_name.clone()
                prev_mangling_counter := ctx.mangling_counter

                ctx.current_throw_types = func_def.throw_types.clone()
                ctx.current_return_types = func_def.return_types.clone()
                ctx.current_function_name = mangled_name.clone()
                ctx.mangling_counter = 0  // Reset counter per-function for determinism

                // Bug #60: Track ref and variadic params - all non-copy args are by pointer
                for arg: SArg in func_def.args {
                    if not(arg.is_copy) {
                        ctx.current_ref_params.add(arg.name.clone())
                    }
                    switch arg.value_type {
                    case ValueType.TMulti(elem_type):
                        ctx.current_variadic_params.set(arg.name.clone(), til_name(elem_type))
                    case:
                        // REM: not variadic
                    }
                }

                // Push scope frame for function
                mut function_frame := ScopeFrame()
                function_frame.scope_type = ScopeType.Function
                for arg: SArg in func_def.args {
                    mut value_type := arg.value_type.clone()
                    switch arg.value_type {
                    case ValueType.TMulti(_):
                        value_type = ValueType.TCustom("Array")
                    case:
                        // REM: keep original type
                    }
                    mut symbol_info := SymbolInfo()
                    symbol_info.value_type = value_type
                    symbol_info.is_mut = arg.is_mut
                    symbol_info.is_copy = arg.is_copy
                    symbol_info.is_own = arg.is_own
                    symbol_info.is_comptime_const = false
                    function_frame.symbols.set(arg.name.clone(), symbol_info)
                }
                context.scope_stack.frames.push(function_frame)

                // Emit the function
                emit_func_signature(til_name(mangled_name), func_def, func_output)
                func_output.push_str(" {\n")
                emit_stmts(func_def.body, func_output, 1, ctx, context)
                if not(func_def.throw_types.is_empty()).and(func_def.return_types.is_empty()) {
                    func_output.push_str("    return 0;\n")
                }
                func_output.push_str("}\n\n")

                // Pop scope frame
                _ := context.scope_stack.frames.pop()

                // Restore context
                ctx.current_throw_types = prev_throw_types
                ctx.current_return_types = prev_return_types
                ctx.declared_vars = prev_declared_vars
                ctx.current_ref_params = prev_mut_params
                ctx.current_variadic_params = prev_variadic_params
                ctx.current_function_name = prev_function_name
                ctx.mangling_counter = prev_mangling_counter

                ctx.hoisted_functions.push(func_output)

                // Don't emit anything at the declaration site - the function is hoisted
                return
            }
        case:
            // REM: not a FuncDef
        }
    }

    indent_str := indent_string(indent)

    // Hoist any throwing function calls in the RHS expression before emitting the declaration
    // This ensures throwing calls are properly handled with error checking
    if not(expr.params.is_empty()) {
        first_param := expr.params.get(0)
        hoist_throwing_expr(first_param, output, indent, ctx, context)
    }

    // Bug #35: For underscore declarations, just emit the expression (discard result)
    // This avoids C redeclaration errors and matches the semantics of discarding
    if decl.name.eq("_") {
        if not(expr.params.is_empty()) {
            output.push_str(indent_str)
            // FCall at statement level (indent > 0) adds ";\n" itself
            // Other expressions need semicolon added explicitly
            first_param := expr.params.get(0)
            switch first_param.node_type {
            case NodeType.FCall:
                emit_expr(first_param, output, indent, ctx, context)
            case:
                emit_expr(first_param, output, 0, ctx, context)
                output.push_str(";\n")
            }
        }
        return
    }

    name := decl.name.clone()
    is_mut := decl.is_mut

    // Check if this is a struct construction (TypeName())
    mut struct_type := ""
    mut has_struct_type := false
    if not(expr.params.is_empty()) {
        first_param := expr.params.get(0)
        struct_type = get_struct_construction_type(first_param, context)
        if not(struct_type.is_empty()) {
            has_struct_type = true
        }
    }

    // Check if this is an enum construction (Type.Variant or Type.Variant(value))
    mut enum_type := ""
    mut has_enum_type := false
    if not(expr.params.is_empty()) {
        first_param := expr.params.get(0)
        enum_type = get_enum_construction_type(first_param, context)
        if not(enum_type.is_empty()) {
            has_enum_type = true
        }
    }

    // Track variable type for method mangling
    // Prioritize struct/enum constructor types, then use get_value_type for other cases
    mut var_type := decl.value_type.clone()
    if has_struct_type {
        var_type = ValueType.TCustom(struct_type.clone())
    } else if has_enum_type {
        var_type = ValueType.TCustom(enum_type.clone())
    } else if not(expr.params.is_empty()) {
        first_param := expr.params.get(0)
        var_type = get_value_type(context, first_param)
        catch (err: Str) {
            var_type = decl.value_type.clone()
        }
    }
    // Add to scope_stack so get_value_type can find it
    mut symbol_info := SymbolInfo()
    symbol_info.value_type = var_type.clone()
    symbol_info.is_mut = decl.is_mut
    symbol_info.is_copy = false
    symbol_info.is_own = false
    symbol_info.is_comptime_const = false
    context.scope_stack.declare_symbol(name.clone(), symbol_info)

    // Check if variable already declared in this function (avoid C redefinition errors)
    // Use til_name() since that's what hoisting code uses when inserting into declared_vars
    already_declared := ctx.declared_vars.contains(til_name(name))

    if has_struct_type {
        // Struct variable declaration with values (defaults or named args)

        // Build map of named arg values from struct literal
        mut named_values := Map.new(Str, Expr)
        if not(expr.params.is_empty()) {
            first_param := expr.params.get(0)
            mut arg_idx := 1
            while arg_idx.lt(first_param.params.len()) {
                arg := first_param.params.get(arg_idx)
                switch arg.node_type {
                case NodeType.NamedArg(field_name):
                    if not(arg.params.is_empty()) {
                        value_expr := arg.params.get(0)
                        named_values.set(field_name.clone(), value_expr.clone())
                    }
                case:
                    // REM: not a named arg
                }
                arg_idx.inc()
            }
        }

        // Look up struct definition to get default values
        // Clone to avoid borrow issues with emit_expr
        struct_def := context.scope_stack.lookup_struct(struct_type)
        if struct_def.members.is_empty() {
            // Empty struct - use empty initializer
            output.push_str(indent_str)
            if not(already_declared) {
                if not(is_mut) {
                    output.push_str("const ")
                }
                output.push_str(til_name(struct_type))
                output.push_str(" ")
                ctx.declared_vars.add(til_name(name))
            }
            output.push_str(til_name(name))
            emit_struct_literal_assign(output, struct_type, already_declared, "{}")
        } else if struct_def.default_values.is_empty().and(named_values.is_empty()) {
            // No defaults and no named args - zero initialize
            output.push_str(indent_str)
            if not(already_declared) {
                if not(is_mut) {
                    output.push_str("const ")
                }
                output.push_str(til_name(struct_type))
                output.push_str(" ")
                ctx.declared_vars.add(til_name(name))
            }
            output.push_str(til_name(name))
            emit_struct_literal_assign(output, struct_type, already_declared, "{0}")
        } else {
            // Has default values or named args - emit designated initializer
            // First check if any default value is a throwing function call
            mut throwing_defaults := Vec.new(ThrowingDefault)
            for member: SMember in struct_def.members {
                if not(member.is_mut) {
                    continue
                }
                if named_values.contains_key(member.name) {
                    continue // Named arg overrides default
                }
                default_expr := struct_def.default_values.get(member.name)
                if is_throwing_fcall(default_expr, context) {
                    mut td := ThrowingDefault()
                    td.member_name = member.name.clone()
                    td.default_expr = default_expr.clone()
                    throwing_defaults.push(td)
                }
                catch (err: KeyNotFoundError) {
                    // REM: no default for this member
                }
            }

            // If we have throwing defaults, emit them as separate statements first
            for td: ThrowingDefault in throwing_defaults {
                // Get the function's throw types
                mut throw_types := Vec.new(Str)
                if not(td.default_expr.params.is_empty()) {
                    first_param := td.default_expr.params.get(0)
                    lookup_name := get_til_func_name_string(first_param)
                    if not(lookup_name.is_empty()) {
                        fd := context.scope_stack.lookup_func(lookup_name)
                        throw_types = fd.throw_types.clone()
                        catch (err: KeyNotFoundError) {
                            // REM: function not found
                        }
                    }
                }

                temp_name := "_default_"
                temp_name.push_str(td.member_name)
                temp_name.push_str("_")
                temp_name.push_str(next_mangled(ctx))
                // Emit the function call with out-param using emit_throwing_call_propagate
                emit_throwing_call_propagate(td.default_expr, throw_types, temp_name, "", output, indent, ctx, context)
                // Record that this struct default was hoisted using "struct_name:member_name" key
                key := struct_type.clone()
                key.push_str(":")
                key.push_str(td.member_name)
                ctx.hoisted_struct_defaults.set(key, temp_name)
            }

            output.push_str(indent_str)
            if not(already_declared) {
                if not(is_mut) {
                    output.push_str("const ")
                }
                output.push_str(til_name(struct_type))
                output.push_str(" ")
                ctx.declared_vars.add(til_name(name))
            }
            output.push_str(til_name(name))
            emit_struct_literal_start(output, struct_type, already_declared)
            output.push_str("{")
            mut first := true
            for member: SMember in struct_def.members {
                // Only include mut fields (actual struct data members)
                // Skip functions, constants, and non-mut fields
                if not(member.is_mut) {
                    continue
                }
                if not(first) {
                    output.push_str(", ")
                }
                first = false
                output.push_str(".")
                output.push_str(member.name)
                output.push_str(" = ")
                // Use named arg value if provided, otherwise use default
                if named_values.contains_key(member.name) {
                    value_expr := named_values.get(member.name)
                    emit_expr(value_expr, output, 0, ctx, context)
                } else {
                    default_expr := struct_def.default_values.get(member.name)
                    // Check if this was a throwing default - use temp var instead
                    key := struct_type.clone()
                    key.push_str(":")
                    key.push_str(member.name)
                    if ctx.hoisted_struct_defaults.contains(key) {
                        temp_name := ctx.hoisted_struct_defaults.get(key)
                        output.push_str(temp_name)
                    } else if is_throwing_fcall(default_expr, context) {
                        // Shouldn't happen - throwing defaults should be hoisted
                        output.push_str("/* ERROR: unhoisted throwing default */")
                    } else {
                        emit_expr(default_expr, output, 0, ctx, context)
                    }
                    catch (err: KeyNotFoundError) {
                        // No default - use zero
                        output.push_str("0")
                    }
                }
            }
            output.push_str("};\n")
        }

        catch (err: KeyNotFoundError) {
            // Struct not found - fall back to zero init
            output.push_str(indent_str)
            if not(already_declared) {
                if not(is_mut) {
                    output.push_str("const ")
                }
                output.push_str(til_name(struct_type))
                output.push_str(" ")
                ctx.declared_vars.add(til_name(name))
            }
            output.push_str(til_name(name))
            emit_struct_literal_assign(output, struct_type, already_declared, "{0}")
        }
    } else if has_enum_type {
        // Enum variable declaration
        output.push_str(indent_str)
        if not(already_declared) {
            if not(is_mut) {
                output.push_str("const ")
            }
            output.push_str(til_name(enum_type))
            output.push_str(" ")
            ctx.declared_vars.add(til_name(name))
        }
        output.push_str(til_name(name))
        output.push_str(" = ")
        first_param := expr.params.get(0)
        emit_expr(first_param, output, 0, ctx, context)
        output.push_str(";\n")
    } else if is_mut {
        // Hoist Dynamic and ref params in RHS if it's an FCall
        if not(expr.params.is_empty()) {
            first_param := expr.params.get(0)
            switch first_param.node_type {
            case NodeType.FCall:
                rhs_fcall := first_param
                if rhs_fcall.params.len().gt(1) {
                    fd := get_fcall_func_def(context, rhs_fcall)
                    mut param_types := Vec.new(ValueType)
                    mut param_by_ref := Vec.new(Bool)
                    for arg: SArg in fd.args {
                        param_types.push(arg.value_type.clone())
                        param_by_ref.push(param_needs_by_ref(arg))
                    }
                    mut args := Vec.new(Expr)
                    mut ai := 1
                    while ai.lt(rhs_fcall.params.len()) {
                        arg := rhs_fcall.params.get(ai)
                        args.push(arg.clone())
                        ai.inc()
                    }
                    empty_hoisted := Map.new(I64, Str)
                    dynamic_hoisted := hoist_for_dynamic_params(args, param_types, empty_hoisted, output, indent, ctx, context)
                    for h: HoistedArg in dynamic_hoisted {
                        if h.index.lt(args.len()) {
                            arg := args.get(h.index)
                            expr_key := arg.id
                            mut hoisted_val := "&"
                            hoisted_val.push_str(h.temp_var)
                            ctx.hoisted_exprs.set(expr_key, hoisted_val)
                        }
                    }
                    mut hoisted_map := Map.new(I64, Str)
                    for h: HoistedArg in dynamic_hoisted {
                        hoisted_map.set(h.index, h.temp_var.clone())
                    }
                    ref_hoisted := hoist_for_ref_params(args, param_by_ref, hoisted_map, output, indent, ctx, context)
                    for h: HoistedArg in ref_hoisted {
                        if h.index.lt(args.len()) {
                            arg := args.get(h.index)
                            expr_key := arg.id
                            mut hoisted_val := "&"
                            hoisted_val.push_str(h.temp_var)
                            ctx.hoisted_exprs.set(expr_key, hoisted_val)
                        }
                        hoisted_map.set(h.index, h.temp_var.clone())
                    }
                    catch (err: KeyNotFoundError) {
                        // REM: function not found, skip hoisting
                    }
                }
            case:
                // REM: not FCall
            }
        }
        output.push_str(indent_str)
        if not(already_declared) {
            // Determine C type - use explicit type annotation if present, otherwise infer
            mut is_infer := false
            switch decl.value_type {
            case ValueType.TCustom(s):
                if s.eq(INFER_TYPE) {
                    is_infer = true
                }
            case:
                // REM: not TCustom
            }
            mut c_type := ""
            if is_infer {
                // INFER_TYPE - infer from RHS
                first_param := expr.params.get(0)
                vt := get_value_type(context, first_param)
                c_type = til_type_to_c(vt)
                catch (err: Str) {
                    throw expr.lang_error(context.path, "ccodegen", err)
                }
            } else {
                // Explicit type annotation - use it
                c_type = til_type_to_c(decl.value_type)
                catch (err: Str) {
                    throw expr.lang_error(context.path, "ccodegen", err)
                }
            }
            output.push_str(c_type)
            output.push_str(" ")
            ctx.declared_vars.add(til_name(name))
        }
        output.push_str(til_name(name))
        if not(expr.params.is_empty()) {
            output.push_str(" = ")
            first_param := expr.params.get(0)
            emit_expr(first_param, output, 0, ctx, context)
        }
        output.push_str(";\n")
    } else {
        // const declaration
        // Hoist Dynamic and ref params in RHS if it's an FCall
        if not(expr.params.is_empty()) {
            first_param := expr.params.get(0)
            switch first_param.node_type {
            case NodeType.FCall:
                rhs_fcall := first_param
                if rhs_fcall.params.len().gt(1) {
                    fd := get_fcall_func_def(context, rhs_fcall)
                    mut param_types := Vec.new(ValueType)
                    mut param_by_ref := Vec.new(Bool)
                    for arg: SArg in fd.args {
                        param_types.push(arg.value_type.clone())
                        param_by_ref.push(param_needs_by_ref(arg))
                    }
                    mut args := Vec.new(Expr)
                    mut ai := 1
                    while ai.lt(rhs_fcall.params.len()) {
                        arg := rhs_fcall.params.get(ai)
                        args.push(arg.clone())
                        ai.inc()
                    }
                    empty_hoisted := Map.new(I64, Str)
                    dynamic_hoisted := hoist_for_dynamic_params(args, param_types, empty_hoisted, output, indent, ctx, context)
                    for h: HoistedArg in dynamic_hoisted {
                        if h.index.lt(args.len()) {
                            arg := args.get(h.index)
                            expr_key := arg.id
                            mut hoisted_val := "&"
                            hoisted_val.push_str(h.temp_var)
                            ctx.hoisted_exprs.set(expr_key, hoisted_val)
                        }
                    }
                    mut hoisted_map := Map.new(I64, Str)
                    for h: HoistedArg in dynamic_hoisted {
                        hoisted_map.set(h.index, h.temp_var.clone())
                    }
                    ref_hoisted := hoist_for_ref_params(args, param_by_ref, hoisted_map, output, indent, ctx, context)
                    for h: HoistedArg in ref_hoisted {
                        if h.index.lt(args.len()) {
                            arg := args.get(h.index)
                            expr_key := arg.id
                            mut hoisted_val := "&"
                            hoisted_val.push_str(h.temp_var)
                            ctx.hoisted_exprs.set(expr_key, hoisted_val)
                        }
                        hoisted_map.set(h.index, h.temp_var.clone())
                    }
                    catch (err: KeyNotFoundError) {
                        // REM: function not found, skip hoisting
                    }
                }
            case:
                // REM: not FCall
            }
        }
        output.push_str(indent_str)
        if not(already_declared) {
            // Determine C type - use explicit type annotation if present, otherwise infer
            mut is_infer := false
            switch decl.value_type {
            case ValueType.TCustom(s):
                if s.eq(INFER_TYPE) {
                    is_infer = true
                }
            case:
                // REM: not TCustom
            }
            mut c_type := ""
            if is_infer {
                // INFER_TYPE - infer from RHS
                first_param := expr.params.get(0)
                vt := get_value_type(context, first_param)
                c_type = til_type_to_c(vt)
                catch (err: Str) {
                    throw expr.lang_error(context.path, "ccodegen", err)
                }
            } else {
                // Explicit type annotation - use it
                c_type = til_type_to_c(decl.value_type)
                catch (err: Str) {
                    throw expr.lang_error(context.path, "ccodegen", err)
                }
            }
            output.push_str("const ")
            output.push_str(c_type)
            output.push_str(" ")
            ctx.declared_vars.add(til_name(name))
        }
        output.push_str(til_name(name))
        if not(expr.params.is_empty()) {
            output.push_str(" = ")
            first_param := expr.params.get(0)
            emit_expr(first_param, output, 0, ctx, context)
        }
        output.push_str(";\n")
    }

    catch (err: IndexOutOfBoundsError) {
        throw err.msg
    }
    catch (err: AllocError) {
        throw err.msg
    }
}

// Check if an expression is a struct construction call (TypeName() or TypeName(x=1, y=2))
// Returns the type name if it is, empty string otherwise
get_struct_construction_type := func(expr: Expr, context: Context) returns Str {
    switch expr.node_type {
    case NodeType.FCall:
        if not(expr.params.is_empty()) {
            mut first_param := Expr()
            expr.params.get(0, first_param)
            switch first_param.node_type {
            case NodeType.Identifier(name):
                // Use lookup_struct to check if this is a known struct type
                if first_param.params.is_empty() {
                    _ := context.scope_stack.lookup_struct(name)
                    catch (err: KeyNotFoundError) {
                        return ""
                    }
                    mut only_named_args := expr.params.len().eq(1)
                    if not(only_named_args) {
                        only_named_args = true
                        mut i := 1
                        while i.lt(expr.params.len()) {
                            mut arg := Expr()
                            expr.params.get(i, arg)
                            switch arg.node_type {
                            case NodeType.NamedArg(_):
                                // ok
                            case:
                                only_named_args = false
                            }
                            i.inc()
                        }
                    }
                    if only_named_args {
                        return name.clone()
                    }
                }
            case:
            }
        }
    case:
    }
    return ""

    catch (err: IndexOutOfBoundsError) {
        return ""
    }
}

// Check if an expression is an enum construction (Type.Variant or Type.Variant(value))
// Returns the type name if it is, empty string otherwise
// AST structure for Color.Red(42): FCall -> [Identifier("Color") -> [Identifier("Red")], Literal(42)]
// AST structure for Color.Unknown: Identifier("Color") -> [Identifier("Unknown")]
get_enum_construction_type := func(expr: Expr, context: Context) returns Str {
    // Check FCall case: Type.Variant(value) or Type.Variant()
    switch expr.node_type {
    case NodeType.FCall:
        if not(expr.params.is_empty()) {
            mut first_param := Expr()
            expr.params.get(0, first_param)
            switch first_param.node_type {
            case NodeType.Identifier(type_name):
                // Use lookup_enum to check if this is a known enum type
                enum_def := context.scope_stack.lookup_enum(type_name)
                catch (err: KeyNotFoundError) {
                    // REM: Not an enum - continue to check Identifier case below
                }
                // Check if there's a nested identifier (the variant)
                if not(first_param.params.is_empty()) {
                    mut variant_param := Expr()
                    first_param.params.get(0, variant_param)
                    switch variant_param.node_type {
                    case NodeType.Identifier(variant_name):
                        // Verify the variant exists in the enum
                        if enum_def.contains_key(variant_name) {
                            return type_name.clone()
                        }
                    case:
                    }
                }
            case:
            }
        }
    case NodeType.Identifier(type_name):
        // Check Identifier case: Type.Variant (no parentheses, no payload)
        // Use lookup_enum to check if this is a known enum type
        enum_def := context.scope_stack.lookup_enum(type_name)
        catch (err: KeyNotFoundError) {
            return ""
        }
        if not(expr.params.is_empty()) {
            mut variant_param := Expr()
            expr.params.get(0, variant_param)
            switch variant_param.node_type {
            case NodeType.Identifier(variant_name):
                // Verify the variant exists in the enum
                if enum_def.contains_key(variant_name) {
                    return type_name.clone()
                }
            case:
            }
        }
    case:
    }
    return ""

    catch (err: IndexOutOfBoundsError) {
        return ""
    }
}

emit_funcdef := proc(_func_def: SFuncDef, expr: Expr, mut output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) throws Str {
    // For now, just inline the function body (we're inside main anyway)
    // TODO: proper function generation with prototypes
    indent_str := indent_string(indent)
    output.push_str(indent_str)
    output.push_str("{\n")
    for stmt: Expr in expr.params {
        emit_expr(stmt, output, indent.add(1), ctx, context)
    }
    output.push_str(indent_str)
    output.push_str("}\n")
}

emit_assignment := proc(name: Str, expr: Expr, mut output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) throws Str {
    indent_str := indent_string(indent)

    if not(expr.params.is_empty()) {
        mut rhs_expr := Expr()
        expr.params.get(0, rhs_expr)

        // Check if RHS is a call to a throwing function
        switch rhs_expr.node_type {
        case NodeType.FCall:
            fd := get_fcall_func_def(context, rhs_expr)
            catch (err: KeyNotFoundError) {
                // REM: no func def found, continue with normal assignment
            }
            if not(fd.throw_types.is_empty()) {
                // RHS is a throwing function call - emit with error propagation
                // (typer should ensure non-throwing context doesn't call throwing functions without catch)
                // Pass raw name so function can properly handle field access on mut params
                emit_throwing_call_propagate(rhs_expr, fd.throw_types, "", name, output, indent, ctx, context)
                return
            }
        case:
        }

        // Hoist any throwing function calls nested in the RHS expression
        _ := hoist_throwing_expr(rhs_expr, output, indent, ctx, context)
        catch (err: KeyNotFoundError) { }
    }

    // Regular assignment
    output.push_str(indent_str)
    // Check if assignment target is a field access on a mut param (self.field)
    // If so, emit with -> instead of .
    dot_pos := name.find(".")
    if dot_pos.gteq(0) {
        base := name.get_substr(0, dot_pos)
        rest := name.get_substr(dot_pos.add(1), name.len())
        if ctx.current_ref_params.contains(base) {
            // Mut param field access: til_self->field
            output.push_str(til_name(base))
            output.push_str("->")
            output.push_str(rest)
        } else {
            output.push_str(til_name(name))
        }
    } else if ctx.current_ref_params.contains(name) {
        // Direct assignment to mut param: *til_self = value
        output.push_str("*")
        output.push_str(til_name(name))
    } else {
        output.push_str(til_name(name))
    }
    output.push_str(" = ")
    if not(expr.params.is_empty()) {
        mut first_param := Expr()
        expr.params.get(0, first_param)
        emit_expr(first_param, output, 0, ctx, context)
    }
    output.push_str(";\n")

    catch (err: IndexOutOfBoundsError) {
        throw err.msg
    }
    catch (err: AllocError) {
        throw err.msg
    }
}

emit_return := proc(expr: Expr, mut output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) throws Str {
    indent_str := indent_string(indent)
    is_throwing := not(ctx.current_throw_types.is_empty())

    if is_throwing {
        // Throwing function: store value through _ret pointer and return 0 (success)
        if not(expr.params.is_empty()) {
            mut return_expr := Expr()
            expr.params.get(0, return_expr)

            // Check if return expression is a call to a throwing function
            switch return_expr.node_type {
            case NodeType.FCall:
                fd := get_fcall_func_def(context, return_expr)
                catch (err: KeyNotFoundError) {
                    // REM: no func def found
                }
                if not(fd.throw_types.is_empty()) {
                    // Return expression is a throwing function call - emit with error propagation
                    // The result will be stored via the assign_name "*_ret"
                    emit_throwing_call_propagate(return_expr, fd.throw_types, "", "*_ret", output, indent, ctx, context)
                    output.push_str(indent_str)
                    output.push_str("return 0;\n")
                    return
                }
            case:
            }

            // Hoist any throwing function calls nested in the return expression
            _ := hoist_throwing_expr(return_expr, output, indent, ctx, context)

            // Regular return value - just emit it
            output.push_str(indent_str)
            output.push_str("*_ret = ")
            mut first_param := Expr()
            expr.params.get(0, first_param)
            emit_expr(first_param, output, 0, ctx, context)
            output.push_str(";\n")
        }
        output.push_str(indent_str)
        output.push_str("return 0;\n")
    } else {
        // Non-throwing function: normal return
        // Check if return expression is a variadic function call
        if not(expr.params.is_empty()) {
            mut return_expr := Expr()
            expr.params.get(0, return_expr)
            switch return_expr.node_type {
            case NodeType.FCall:
                variadic_fcall_info := detect_variadic_fcall(return_expr, ctx)
                catch (err: KeyNotFoundError) {
                    // REM: not variadic
                }
                // Variadic call in return - need to hoist it
                // First, hoist any nested throwing/variadic calls in the arguments
                mut all_args := Vec.new(Expr)
                mut arg_idx := 1
                while arg_idx.lt(return_expr.params.len()) {
                    mut arg := Expr()
                    return_expr.params.get(arg_idx, arg)
                    all_args.push(arg)
                    arg_idx.inc()
                }
                hoisted_vec := hoist_throwing_args(all_args.clone(), output, indent, ctx, context)
                mut hoisted := Map.new(I64, Str)
                for h: HoistedArg in hoisted_vec {
                    hoisted.set(h.index, h.temp_var)
                }
                mut variadic_args := Vec.new(Expr)
                mut va_idx := variadic_fcall_info.regular_count
                while va_idx.lt(all_args.len()) {
                    mut arg := Expr()
                    all_args.get(va_idx, arg)
                    variadic_args.push(arg)
                    va_idx.inc()
                }
                arr_var := hoist_variadic_args(variadic_fcall_info.elem_type, variadic_args, hoisted, variadic_fcall_info.regular_count, output, indent, ctx, context)

                // Emit the function call storing result
                temp_var := next_mangled(ctx)
                ret_type_vt := get_value_type(context, return_expr)
                catch (err: Str) {
                    throw return_expr.lang_error(context.path, "ccodegen", err)
                }
                ret_type := til_type_to_c(ret_type_vt)
                catch (err2: Str) {
                    throw return_expr.lang_error(context.path, "ccodegen", err2)
                }
                output.push_str(indent_str)
                output.push_str(ret_type)
                output.push_str(" ")
                output.push_str(temp_var)
                output.push_str(" = ")
                // Emit the function call
                mut first_param := Expr()
                return_expr.params.get(0, first_param)
                switch first_param.node_type {
                case NodeType.Identifier(func_name):
                    output.push_str(til_func_name(func_name))
                case:
                }
                output.push_str("(")
                // Emit regular args
                mut args := Vec.new(Expr)
                mut arg_idx2 := 1
                while arg_idx2.lt(return_expr.params.len()) {
                    mut arg := Expr()
                    return_expr.params.get(arg_idx2, arg)
                    args.push(arg)
                    arg_idx2.inc()
                }
                mut i := 0
                while i.lt(variadic_fcall_info.regular_count) {
                    if i.gt(0) {
                        output.push_str(", ")
                    }
                    mut arg := Expr()
                    args.get(i, arg)
                    emit_expr(arg, output, 0, ctx, context)
                    i.inc()
                }
                // Emit variadic array pointer
                if variadic_fcall_info.regular_count.gt(0) {
                    output.push_str(", ")
                }
                output.push_str("&")
                output.push_str(arr_var)
                output.push_str(");\n")

                // Delete array
                output.push_str(indent_str)
                output.push_str(TIL_PREFIX)
                output.push_str("Array_delete(&")
                output.push_str(arr_var)
                output.push_str(");\n")

                // Return the result
                output.push_str(indent_str)
                output.push_str("return ")
                output.push_str(temp_var)
                output.push_str(";\n")
                return
            case:
            }
        }

        // Hoist any throwing function calls nested in the return expression
        if not(expr.params.is_empty()) {
            mut first_param := Expr()
            expr.params.get(0, first_param)
            _ := hoist_throwing_expr(first_param, output, indent, ctx, context)
        }

        // Regular non-variadic return
        output.push_str(indent_str)
        output.push_str("return")
        if not(expr.params.is_empty()) {
            output.push_str(" ")
            mut first_param := Expr()
            expr.params.get(0, first_param)
            emit_expr(first_param, output, 0, ctx, context)
        }
        output.push_str(";\n")
    }

    catch (err: IndexOutOfBoundsError) {
        throw err.msg
    }
    catch (err: AllocError) {
        throw err.msg
    }
}

emit_throw := proc(expr: Expr, mut output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) throws Str {
    // Throw: params[0] = the value to throw (typically a struct constructor like DivideByZero())
    if expr.params.is_empty() {
        throw "ccodegen: throw requires a value"
    }

    indent_str := indent_string(indent)
    mut thrown_expr := Expr()
    expr.params.get(0, thrown_expr)

    // Check if the thrown expression itself is a throwing function call (e.g., throw format(...))
    // If so, we need to hoist it first to ensure proper error handling
    hoisted_temp := hoist_throwing_expr(thrown_expr, output, indent, ctx, context)
    if not(hoisted_temp.eq("")) {
        // The thrown expression was hoisted to a temp variable
        // Now we just need to throw the temp variable value
        // Get the type of the hoisted value
        fd := get_fcall_func_def(context, thrown_expr)
        catch (err: KeyNotFoundError) {
            throw "ccodegen: could not find throwing function definition"
        }
        if fd.return_types.is_empty() {
            throw "ccodegen: throwing function has no return type"
        }
        mut ret_type := ValueType.TCustom("")
        fd.return_types.get(0, ret_type)
        mut thrown_type_name := ""
        switch ret_type {
        case ValueType.TCustom(type_name):
            thrown_type_name = type_name.clone()
        case:
            thrown_type_name = value_type_to_str(ret_type)
        }

        // Check if this type is locally caught
        if ctx.local_catch_labels.contains_key(thrown_type_name.clone()) {
            mut catch_info := CatchLabelInfo()
            ctx.local_catch_labels.get(thrown_type_name.clone(), catch_info)
            label := catch_info.label.clone()
            temp_var := catch_info.temp_var.clone()
            output.push_str(indent_str)
            output.push_str(temp_var)
            output.push_str(" = ")
            output.push_str(hoisted_temp)
            output.push_str(";\n")
            output.push_str(indent_str)
            output.push_str("goto ")
            output.push_str(label)
            output.push_str(";\n")
            return
        }

        // Find the index of this type in current_throw_types
        mut error_index := sub(0, 1)
        mut i := 0
        while i.lt(ctx.current_throw_types.len()) {
            mut vt := ValueType.TCustom("")
            ctx.current_throw_types.get(i, vt)
            switch vt {
            case ValueType.TCustom(name):
                if name.eq(thrown_type_name) {
                    error_index = i
                    break
                }
            case:
            }
            i.inc()
        }

        if error_index.gteq(0) {
            output.push_str(indent_str)
            output.push_str("*_err")
            output.push_str(I64.to_str(error_index.add(1)))
            output.push_str(" = ")
            output.push_str(hoisted_temp)
            output.push_str(";\n")
            output.push_str(indent_str)
            output.push_str("return ")
            output.push_str(I64.to_str(error_index.add(1)))
            output.push_str(";\n")
            return
        } else {
            throw concat("ccodegen: thrown type '", thrown_type_name, "' not found in function's throw types")
        }
    }

    // Get the thrown type name from the expression
    // For FCall, we need to determine if it's:
    // 1. A constructor like DivideByZero() - use the type name
    // 2. A function that returns an error type like format() - use the return type
    mut thrown_type_name := ""
    switch thrown_expr.node_type {
    case NodeType.FCall:
        if not(thrown_expr.params.is_empty()) {
            mut first_param := Expr()
            thrown_expr.params.get(0, first_param)
            switch first_param.node_type {
            case NodeType.Identifier(name):
                // Check if this is a constructor (struct/enum) or a function call
                // If it's a function that returns a type, use the return type
                fd := get_fcall_func_def(context, thrown_expr)
                catch (err: KeyNotFoundError) {
                    // Not a function, assume it's a constructor
                    thrown_type_name = name.clone()
                }
                // It's a function - use its return type as the thrown type
                if not(fd.return_types.is_empty()) {
                    mut ret_type := ValueType.TCustom("")
                    fd.return_types.get(0, ret_type)
                    switch ret_type {
                    case ValueType.TCustom(type_name):
                        thrown_type_name = type_name.clone()
                    case:
                        // Return type is a primitive (like Str) - convert to name
                        thrown_type_name = value_type_to_str(ret_type)
                    }
                } else {
                    // No return type, assume it's a constructor
                    thrown_type_name = name.clone()
                }
            case:
                throw "ccodegen: throw FCall must have identifier as first param"
            }
        } else {
            throw "ccodegen: throw FCall has no params"
        }
    case NodeType.Identifier(name):
        // Look up the type of the identifier (could be a variable or type name)
        value_type := get_value_type(context, thrown_expr)
        catch (err: Str) {
            // Fallback: assume identifier is a type name (for struct constructors without args)
            thrown_type_name = name.clone()
        }
        switch value_type {
        case ValueType.TCustom(type_name):
            thrown_type_name = type_name.clone()
        case:
            thrown_type_name = value_type_to_str(value_type)
        }
    case NodeType.LLiteral(lit):
        // Literal values - determine their type
        switch lit {
        case Literal.Str(_):
            thrown_type_name = "Str"
        case Literal.Number(_):
            thrown_type_name = "I64"
        case Literal.List(_):
            throw "ccodegen: cannot throw a list literal"
        }
    case:
        throw "ccodegen: throw expression must be a constructor"
    }

    // Check if this type is locally caught (has a catch block at function level)
    if ctx.local_catch_labels.contains_key(thrown_type_name.clone()) {
        mut catch_info := CatchLabelInfo()
        ctx.local_catch_labels.get(thrown_type_name.clone(), catch_info)
        label := catch_info.label.clone()
        temp_var := catch_info.temp_var.clone()
        // Hoist any throwing function calls in the thrown expression
        mut hoisted := Map.new(I64, Str)
        switch thrown_expr.node_type {
        case NodeType.FCall:
            if thrown_expr.params.len().gt(1) {
                mut args := Vec.new(Expr)
                mut arg_idx := 1
                while arg_idx.lt(thrown_expr.params.len()) {
                    mut arg := Expr()
                    thrown_expr.params.get(arg_idx, arg)
                    args.push(arg)
                    arg_idx.inc()
                }
                hoisted_vec := hoist_throwing_args(args, output, indent, ctx, context)
                for h: HoistedArg in hoisted_vec {
                    hoisted.set(h.index, h.temp_var)
                }
            }
        case:
        }

        // Store the error value in temp variable
        output.push_str(indent_str)
        output.push_str(temp_var)
        output.push_str(" = ")

        // Emit the thrown expression
        switch thrown_expr.node_type {
        case NodeType.FCall:
            emit_fcall_with_hoisted(thrown_expr, hoisted, output, ctx, context)
        case:
            emit_expr(thrown_expr, output, 0, ctx, context)
        }
        output.push_str(";\n")

        // Jump to the catch block
        output.push_str(indent_str)
        output.push_str("goto ")
        output.push_str(label)
        output.push_str(";\n")
        return
    }

    // Find the index of this type in current_throw_types
    // Note: Str is represented as TCustom("Str") in the type system
    mut error_index := sub(0, 1)
    mut i := 0
    while i.lt(ctx.current_throw_types.len()) {
        mut vt := ValueType.TCustom("")
        ctx.current_throw_types.get(i, vt)
        switch vt {
        case ValueType.TCustom(name):
            if name.eq(thrown_type_name) {
                error_index = i
                break
            }
        case:
        }
        i.inc()
    }

    if error_index.gteq(0) {
        // Hoist throwing function calls from arguments of the thrown expression
        // E.g., throw Error.new(format(...)) needs to hoist format() first
        mut hoisted := Map.new(I64, Str)
        switch thrown_expr.node_type {
        case NodeType.FCall:
            if thrown_expr.params.len().gt(1) {
                mut args := Vec.new(Expr)
                mut arg_idx := 1
                while arg_idx.lt(thrown_expr.params.len()) {
                    mut arg := Expr()
                    thrown_expr.params.get(arg_idx, arg)
                    args.push(arg)
                    arg_idx.inc()
                }
                hoisted_vec := hoist_throwing_args(args, output, indent, ctx, context)
                for h: HoistedArg in hoisted_vec {
                    hoisted.set(h.index, h.temp_var)
                }
            }
        case:
        }

        // Store the error value in the appropriate error pointer
        // Note: error params are 1-based (_err1, _err2, etc.)
        output.push_str(indent_str)
        output.push_str("*_err")
        output.push_str(I64.to_str(error_index.add(1)))
        output.push_str(" = ")

        // Emit the thrown expression, using hoisted temp vars for arguments
        switch thrown_expr.node_type {
        case NodeType.FCall:
            emit_fcall_with_hoisted(thrown_expr, hoisted, output, ctx, context)
        case:
            emit_expr(thrown_expr, output, 0, ctx, context)
        }
        output.push_str(";\n")

        // Return the error index (1-based, since 0 = success)
        output.push_str(indent_str)
        output.push_str("return ")
        output.push_str(I64.to_str(error_index.add(1)))
        output.push_str(";\n")
    } else {
        throw concat("ccodegen: thrown type '", thrown_type_name, "' not found in function's throw types")
    }

    catch (err: IndexOutOfBoundsError) {
        throw err.msg
    }
    catch (err: AllocError) {
        throw err.msg
    }
}

// Emit an FCall expression using pre-hoisted temp vars for arguments
emit_fcall_with_hoisted := proc(expr: Expr, hoisted: Map, mut output: Str, mut ctx: CodegenContext, mut context: Context) throws Str {
    if expr.params.is_empty() {
        throw "emit_fcall_with_hoisted: FCall with no params"
    }

    // Get function name (handles both nested identifiers and precomp'd "Type.method" strings)
    mut first_param := Expr()
    expr.params.get(0, first_param)
    func_name := get_func_name_string(first_param)
    if func_name.eq("") {
        throw "emit_fcall_with_hoisted: FCall first param not Identifier"
    }

    // Check for struct construction: TypeName() or TypeName(x=10, y=20)
    // Use lookup_struct to verify this is a known struct type
    mut has_named_args := false
    mut arg_idx := 1
    while arg_idx.lt(expr.params.len()) {
        mut arg := Expr()
        expr.params.get(arg_idx, arg)
        switch arg.node_type {
        case NodeType.NamedArg(_):
            has_named_args = true
            break
        case:
        }
        arg_idx.inc()
    }
    struct_def := context.scope_stack.lookup_struct(func_name)
    catch (err: KeyNotFoundError) {
        // REM: not a struct, continue to function call
    }
    if expr.params.len().eq(1).or(has_named_args) {
        output.push_str("(")
        output.push_str(TIL_PREFIX)
        output.push_str(func_name)
        output.push_str(")")

        // Use the struct definition to get default values
        if struct_def.members.is_empty().or(struct_def.default_values.is_empty()) {
            output.push_str("{}")
        } else {
            // Build map of named arg values
            mut named_values := Map.new(Str, Expr)
            mut na_idx := 1
            while na_idx.lt(expr.params.len()) {
                mut arg := Expr()
                expr.params.get(na_idx, arg)
                switch arg.node_type {
                case NodeType.NamedArg(field_name):
                    if not(arg.params.is_empty()) {
                        mut value_expr := Expr()
                        arg.params.get(0, value_expr)
                        named_values.set(field_name.clone(), value_expr)
                    }
                case:
                }
                na_idx.inc()
            }

            // Check if any default value is a throwing function call
            mut throwing_defaults := Vec.new(ThrowingDefault)
            for member: StructMember in struct_def.members {
                if not(member.is_mut) {
                    continue
                }
                if named_values.contains_key(member.name.clone()) {
                    continue  // Named arg overrides default
                }
                default_expr := struct_def.default_values.get(member.name.clone())
                catch (err: KeyNotFoundError) {
                    continue
                }
                if is_throwing_fcall(default_expr.clone(), context) {
                    mut td := ThrowingDefault()
                    td.member_name = member.name.clone()
                    td.default_expr = default_expr.clone()
                    throwing_defaults.push(td)
                }
            }

            // If we have throwing defaults, emit them as separate statements first
            for td: ThrowingDefault in throwing_defaults {
                // Get the function's throw types
                mut throw_types := Vec.new(ValueType)
                if not(td.default_expr.params.is_empty()) {
                    mut fp := Expr()
                    td.default_expr.params.get(0, fp)
                    lookup_name := get_til_func_name_string(fp)
                    if not(lookup_name.eq("")) {
                        fd := context.scope_stack.lookup_func(lookup_name)
                        throw_types = fd.throw_types.clone()
                        catch (err: KeyNotFoundError) {
                            // REM: no func found
                        }
                    }
                }

                mut temp_name := "_default_"
                temp_name.push_str(td.member_name)
                temp_name.push_str("_")
                temp_name.push_str(next_mangled(ctx))
                emit_throwing_call_propagate(td.default_expr, throw_types, temp_name.clone(), "", output, 0, ctx, context)
                mut key := func_name.clone()
                key.push_str(":")
                key.push_str(td.member_name)
                ctx.hoisted_struct_defaults.set(key, temp_name)
            }

            // Build set of throwing default member names for quick lookup
            mut throwing_default_names := Set.new(Str)
            for td: ThrowingDefault in throwing_defaults {
                throwing_default_names.insert(td.member_name.clone())
            }

            output.push_str("{")
            mut first := true
            for member: StructMember in struct_def.members {
                if not(member.is_mut) {
                    continue
                }
                if not(first) {
                    output.push_str(", ")
                }
                first = false
                output.push_str(".")
                output.push_str(member.name)
                output.push_str(" = ")
                // Use named arg value if provided, otherwise use default
                value_expr := named_values.get(member.name.clone())
                catch (err: KeyNotFoundError) {
                    default_expr := struct_def.default_values.get(member.name.clone())
                    catch (err2: KeyNotFoundError) {
                        output.push_str("0")
                        continue
                    }
                    // Check if this was a throwing default - use temp var instead
                    mut key := func_name.clone()
                    key.push_str(":")
                    key.push_str(member.name)
                    temp_name := ctx.hoisted_struct_defaults.get(key)
                    catch (err3: KeyNotFoundError) {
                        if throwing_default_names.contains(member.name.clone()).or(is_throwing_fcall(default_expr.clone(), context)) {
                            output.push_str("/* ERROR: unhoisted throwing default */")
                        } else {
                            emit_expr(default_expr, output, 0, ctx, context)
                        }
                        continue
                    }
                    output.push_str(temp_name)
                    continue
                }
                emit_expr(value_expr, output, 0, ctx, context)
            }
            output.push_str("}")
        }
        return
    }

    // Function call: til_func_name(args...)
    // func_name already has underscores (from get_func_name_string), just add prefix
    output.push_str(TIL_PREFIX)
    output.push_str(func_name)
    output.push_str("(")

    // Look up param types for by-ref handling
    mut param_info := Vec.new(ParamTypeInfo)
    fd := get_fcall_func_def(context, expr)
    catch (err: KeyNotFoundError) {
        // REM: no func def found
    }
    for arg: FuncArg in fd.args {
        mut pi := ParamTypeInfo()
        pi.value_type = arg.value_type.clone()
        pi.value_type_set = true
        pi.by_ref = param_needs_by_ref(arg)
        param_info.push(pi)
    }

    mut i := 0
    mut arg_idx2 := 1
    while arg_idx2.lt(expr.params.len()) {
        if i.gt(0) {
            output.push_str(", ")
        }
        mut arg := Expr()
        expr.params.get(arg_idx2, arg)
        // Check if arg is a type identifier - emit as string literal
        type_name := get_type_arg_name(arg, context)
        if not(type_name.eq("")) {
            output.push_str("\"")
            output.push_str(type_name)
            output.push_str("\"")
        } else if not(param_info.is_empty()) {
            mut pi := ParamTypeInfo()
            param_info.get(i, pi)
            catch (err: IndexOutOfBoundsError) {
                emit_arg_or_hoisted(arg, i, hoisted, output, ctx, context)
                i.inc()
                arg_idx2.inc()
                continue
            }
            emit_arg_with_param_type(arg, i, hoisted, pi.value_type, pi.by_ref, output, ctx, context)
        } else {
            emit_arg_or_hoisted(arg, i, hoisted, output, ctx, context)
        }
        i.inc()
        arg_idx2.inc()
    }
    output.push_str(")")

    catch (err: IndexOutOfBoundsError) {
        throw err.msg
    }
    catch (err: AllocError) {
        throw err.msg
    }
}

emit_if := proc(expr: Expr, mut output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) throws Str {
    // If: params[0] = condition, params[1] = then-body, params[2] = else-body (optional)
    if expr.params.len().lt(2) {
        throw "ccodegen: If requires condition and body"
    }

    indent_str := indent_string(indent)

    // Hoist declarations from both branches to before the if statement
    // (TIL has function-level scoping, not block-level scoping)
    mut then_body := Expr()
    expr.params.get(1, then_body)
    then_decls := collect_declarations_in_body(then_body, context)
    for decl: CollectedDeclaration in then_decls {
        c_var_name := til_name(decl.name)
        if not(ctx.declared_vars.contains_key(c_var_name.clone())) {
            c_type := til_type_to_c(decl.value_type.clone())
            catch (err: Str) {
                continue
            }
            output.push_str(indent_str)
            output.push_str(c_type)
            output.push_str(" ")
            output.push_str(c_var_name.clone())
            output.push_str(";\n")
            ctx.declared_vars.set(c_var_name.clone(), true)
            // Also register in scope_stack so get_value_type can find it
            mut sym_info := SymbolInfo()
            sym_info.value_type = decl.value_type.clone()
            sym_info.is_mut = false
            sym_info.is_copy = false
            sym_info.is_own = false
            sym_info.is_comptime_const = false
            context.scope_stack.declare_symbol(decl.name.clone(), sym_info)
        }
    }
    if expr.params.len().gt(2) {
        mut else_body := Expr()
        expr.params.get(2, else_body)
        else_decls := collect_declarations_in_body(else_body, context)
        for decl: CollectedDeclaration in else_decls {
            c_var_name := til_name(decl.name)
            if not(ctx.declared_vars.contains_key(c_var_name.clone())) {
                c_type := til_type_to_c(decl.value_type.clone())
                catch (err: Str) {
                    continue
                }
                output.push_str(indent_str)
                output.push_str(c_type)
                output.push_str(" ")
                output.push_str(c_var_name.clone())
                output.push_str(";\n")
                ctx.declared_vars.set(c_var_name.clone(), true)
                // Also register in scope_stack so get_value_type can find it
                mut sym_info := SymbolInfo()
                sym_info.value_type = decl.value_type.clone()
                sym_info.is_mut = false
                sym_info.is_copy = false
                sym_info.is_own = false
                sym_info.is_comptime_const = false
                context.scope_stack.declare_symbol(decl.name.clone(), sym_info)
            }
        }
    }

    // Hoist any throwing function calls in the condition
    mut condition := Expr()
    expr.params.get(0, condition)
    _ := hoist_throwing_expr(condition, output, indent, ctx, context)
    output.push_str(indent_str)
    output.push_str("if (")
    emit_expr(condition, output, 0, ctx, context)
    // Bool is a struct with .data field - extract for C truthiness
    vt := get_value_type(context, condition)
    catch (err: Str) {
        // REM: couldn't get value type
    }
    switch vt {
    case ValueType.TCustom(type_name):
        if type_name.eq("Bool") {
            output.push_str(".data")
        }
    case:
    }
    output.push_str(") {\n")

    // Don't save/restore declared_vars - TIL has function-level scoping
    // Variables declared in if branches stay declared for the rest of the function
    emit_body(then_body, output, indent.add(1), ctx, context)

    output.push_str(indent_str)
    output.push_str("}")

    // Else branch (optional)
    if expr.params.len().gt(2) {
        mut else_body := Expr()
        expr.params.get(2, else_body)
        // Check if it's an else-if (nested If) or else block
        switch else_body.node_type {
        case NodeType.If:
            // Always wrap else-if in braces to ensure hoisted temp vars
            // from nested if conditions have proper scope
            output.push_str(" else {\n")
            emit_if(else_body, output, indent.add(1), ctx, context)
            output.push_str(indent_str)
            output.push_str("}\n")
        case:
            output.push_str(" else {\n")
            emit_body(else_body, output, indent.add(1), ctx, context)
            output.push_str(indent_str)
            output.push_str("}\n")
        }
    } else {
        output.push_str("\n")
    }

    catch (err: IndexOutOfBoundsError) {
        throw err.msg
    }
}

emit_while := proc(expr: Expr, mut output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) throws Str {
    // While: params[0] = condition, params[1] = body
    if expr.params.len().lt(2) {
        throw "ccodegen: While requires condition and body"
    }

    indent_str := indent_string(indent)

    // Hoist declarations from loop body to before the while statement
    // (TIL has function-level scoping, not block-level scoping)
    mut body := Expr()
    expr.params.get(1, body)
    body_decls := collect_declarations_in_body(body, context)
    for decl: CollectedDeclaration in body_decls {
        c_var_name := til_name(decl.name)
        if not(ctx.declared_vars.contains_key(c_var_name.clone())) {
            c_type := til_type_to_c(decl.value_type.clone())
            catch (err: Str) {
                continue
            }
            output.push_str(indent_str)
            output.push_str(c_type)
            output.push_str(" ")
            output.push_str(c_var_name.clone())
            output.push_str(";\n")
            ctx.declared_vars.set(c_var_name.clone(), true)
            // Also register in scope_stack so get_value_type can find it
            mut sym_info := SymbolInfo()
            sym_info.value_type = decl.value_type.clone()
            sym_info.is_mut = false
            sym_info.is_copy = false
            sym_info.is_own = false
            sym_info.is_comptime_const = false
            context.scope_stack.declare_symbol(decl.name.clone(), sym_info)
        }
    }

    // Check if condition contains throwing calls by trying to hoist to a temp buffer
    mut hoist_output := ""
    mut condition := Expr()
    expr.params.get(0, condition)
    _ := hoist_throwing_expr(condition, hoist_output, indent.add(1), ctx, context)

    if hoist_output.is_empty() {
        // No throwing calls in condition - use simple while
        output.push_str(indent_str)
        output.push_str("while (")
        emit_expr(condition, output, 0, ctx, context)
        // Bool is a struct with .data field - extract for C truthiness
        vt := get_value_type(context, condition)
        catch (err: Str) {
            // REM: couldn't get value type
        }
        switch vt {
        case ValueType.TCustom(type_name):
            if type_name.eq("Bool") {
                output.push_str(".data")
            }
        case:
        }
        output.push_str(") {\n")
    } else {
        // Throwing calls in condition - transform to while(1) with hoisted calls and break
        output.push_str(indent_str)
        output.push_str("while (1) {\n")

        // Emit hoisted throwing calls inside the loop
        output.push_str(hoist_output)

        // Emit condition check with break
        inner_indent_str := indent_string(indent.add(1))
        output.push_str(inner_indent_str)
        output.push_str("if (!(")
        emit_expr(condition, output, 0, ctx, context)
        // Bool is a struct with .data field - extract for C truthiness
        vt := get_value_type(context, condition)
        catch (err: Str) {
            // REM: couldn't get value type
        }
        switch vt {
        case ValueType.TCustom(type_name):
            if type_name.eq("Bool") {
                output.push_str(".data")
            }
        case:
        }
        output.push_str(")) break;\n")
    }

    // Don't save/restore declared_vars - TIL has function-level scoping
    // Variables declared in loops stay declared for the rest of the function
    emit_body(body, output, indent.add(1), ctx, context)

    output.push_str(indent_str)
    output.push_str("}\n")

    catch (err: IndexOutOfBoundsError) {
        throw err.msg
    }
}

emit_break := proc(_expr: Expr, mut output: Str, indent: I64) {
    indent_str := indent_string(indent)
    output.push_str(indent_str)
    output.push_str("break;\n")
}

emit_continue := proc(_expr: Expr, mut output: Str, indent: I64) {
    indent_str := indent_string(indent)
    output.push_str(indent_str)
    output.push_str("continue;\n")
}

// Info about a variadic parameter
VariadicParamInfo := struct {
    mut elem_type: Str = ""
    mut regular_count: I64 = 0
}

// Info about a catch label for local throw/catch
CatchLabelInfo := struct {
    label: Str = ""
    temp_var: Str = ""
}

// Result type for hoisted arguments
HoistedArg := struct {
    index: I64 = 0
    temp_var: Str = ""
}

// Result struct for variant info extraction
VariantInfo := struct {
    type_name: Str = ""
    variant_name: Str = ""
}

// Info about a catch block for code generation
// Note: TIL version also has catch_block field, but Rust accesses stmts[i] directly
CatchLabelInfoEntry := struct {
    stmt_index: I64 = 0        // Index in stmts array
    type_name: Str = ""        // Error type being caught (e.g., "Str", "IndexOutOfBoundsError")
    label: Str = ""            // Goto label for this catch
    temp_var: Str = ""         // Temp variable holding the thrown value
}

// Info about a function parameter's type and whether passed by reference
ParamTypeInfo := struct {
    value_type: ValueType = ValueType.TI64
    has_value_type: Bool = false
    by_ref: Bool = false
}

// Info about a struct member with a throwing default value
ThrowingDefault := struct {
    member_name: Str = ""
    default_expr: Expr = Expr()
}

// Info about a collected variable declaration for hoisting
CollectedDeclaration := struct {
    name: Str = ""
    value_type: ValueType = ValueType.TI64
}

// Info about a variadic function call
VariadicFCallInfo := struct {
    elem_type: Str = ""
    regular_count: I64 = 0
}

// Extract enum type and variant names from a case pattern expression
// For FCall: Type.Variant -> VariantInfo { type_name: "Type", variant_name: "Variant" }
get_case_variant_info := proc(expr: Expr) returns VariantInfo {
    switch expr.node_type {
    case NodeType.FCall:
        // FCall for Type.Variant (without payload extraction)
        if not(expr.params.is_empty()) {
            first_param := expr.params.get(0)
            switch first_param.node_type {
            case NodeType.Identifier(type_name):
                if not(first_param.params.is_empty()) {
                    nested_param := first_param.params.get(0)
                    switch nested_param.node_type {
                    case NodeType.Identifier(variant_name):
                        mut result := VariantInfo()
                        result.type_name = type_name.clone()
                        result.variant_name = variant_name.clone()
                        return result
                    case:
                        // REM: not identifier
                    }
                }
            case:
                // REM: not identifier
            }
        }
        return VariantInfo()
    case NodeType.Identifier(name):
        // Identifier with nested params: Type.Variant
        if not(expr.params.is_empty()) {
            first_param := expr.params.get(0)
            switch first_param.node_type {
            case NodeType.Identifier(variant_name):
                mut result := VariantInfo()
                result.type_name = name.clone()
                result.variant_name = variant_name.clone()
                return result
            case:
                // REM: not identifier
            }
        }
        // Plain identifier without nested params - NOT an enum variant pattern
        // Return empty to let emit_switch fall through to regular value comparison
        return VariantInfo()
    case:
        return VariantInfo()
    }

    catch (err: IndexOutOfBoundsError) {
        return VariantInfo()
    }
}

// Extract type name and variant name from a Pattern's variant_name (e.g., "Color.Green")
parse_pattern_variant_name := proc(variant_name: Str) returns VariantInfo {
    dot_pos := variant_name.rfind(".")
    if dot_pos.gteq(0) {
        type_name := variant_name.get_substr(0, dot_pos)
        var_name := variant_name.get_substr(dot_pos.add(1), variant_name.len())
        mut result := VariantInfo()
        result.type_name = type_name
        result.variant_name = var_name
        return result
    } else {
        // No dot - just variant name (shouldn't happen in practice)
        mut result := VariantInfo()
        result.variant_name = variant_name.clone()
        return result
    }
}

/// Collect all variable declarations in a body (recursively) for hoisting
/// Returns Vec of CollectedDeclaration structs
collect_declarations_in_body := proc(body: Expr, mut context: Context) returns Vec {
    mut decls := Vec.new(CollectedDeclaration)
    // Track collected declarations for lookups during type inference
    mut collected := Map.new(Str, ValueType)

    switch body.node_type {
    case NodeType.Body:
        for stmt: Expr in body.params {
            collect_declarations_recursive(stmt, decls, collected, context)
        }
    case:
        collect_declarations_recursive(body, decls, collected, context)
    }

    return decls

    catch (err: AllocError) {
        return decls
    }
}

collect_declarations_recursive := proc(expr: Expr, mut decls: Vec, mut collected: Map, mut context: Context) {
    switch expr.node_type {
    case NodeType.Declaration(decl):
        // Add this declaration
        // If type is INFER_TYPE, try to infer from the expression
        mut value_type := decl.value_type.clone()
        switch decl.value_type {
        case ValueType.TCustom(name):
            if name.eq(INFER_TYPE).and(not(expr.params.is_empty())) {
                first_param := expr.params.get(0)
                inferred := get_value_type(context, first_param)
                value_type = inferred
                catch (err: Str) {
                    value_type = decl.value_type.clone()
                }
            }
        case:
            // REM: not TCustom
        }
        // Track this declaration for future lookups (both in collected and scope_stack)
        collected.set(decl.name.clone(), value_type.clone())
        catch (err: AllocError) {
            // REM: ignore alloc error
        }
        // Also add to scope_stack so get_value_type can find it for subsequent declarations
        mut symbol_info := SymbolInfo()
        symbol_info.value_type = value_type.clone()
        symbol_info.is_mut = decl.is_mut
        symbol_info.is_copy = false
        symbol_info.is_own = false
        symbol_info.is_comptime_const = false
        context.scope_stack.declare_symbol(decl.name.clone(), symbol_info)
        mut collected_decl := CollectedDeclaration()
        collected_decl.name = decl.name.clone()
        collected_decl.value_type = value_type.clone()
        decls.push(collected_decl)
    case NodeType.Body:
        for stmt: Expr in expr.params {
            collect_declarations_recursive(stmt, decls, collected, context)
        }
    case NodeType.If:
        // Recurse into if branches
        if expr.params.len().gteq(2) {
            second_param := expr.params.get(1)
            collect_declarations_recursive(second_param, decls, collected, context)
        }
        if expr.params.len().gteq(3) {
            third_param := expr.params.get(2)
            collect_declarations_recursive(third_param, decls, collected, context)
        }
    case NodeType.Switch:
        // Recurse into switch case bodies
        mut i := 1
        while i.add(1).lt(expr.params.len()) {
            body_param := expr.params.get(i.add(1))
            collect_declarations_recursive(body_param, decls, collected, context)
            i = i.add(2)
        }
    case NodeType.While:
        // Recurse into while body
        if expr.params.len().gteq(2) {
            second_param := expr.params.get(1)
            collect_declarations_recursive(second_param, decls, collected, context)
        }
    case NodeType.Catch:
        // Recurse into catch body (params[2] is catch body)
        // params[0] = err_var, params[1] = err_type, params[2] = body
        if expr.params.len().gteq(3) {
            third_param := expr.params.get(2)
            collect_declarations_recursive(third_param, decls, collected, context)
        }
    case:
        // REM: other node types don't contain declarations
    }

    catch (err: IndexOutOfBoundsError) {
        // REM: ignore
    }
    catch (err: AllocError) {
        // REM: ignore
    }
}

emit_switch := proc(expr: Expr, mut output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) throws Str {
    // Switch: params[0] = switch expression
    // params[1..] = alternating (case_pattern, body) pairs
    // We emit if/else chains instead of C switch because:
    // 1. C switch only works with integer types, not Str
    // 2. Avoids GCC-specific case range extension
    // 3. More portable and easier to handle all TIL switch patterns
    if expr.params.is_empty() {
        throw "ccodegen: Switch requires expression"
    }

    indent_str := indent_string(indent)
    body_indent := indent_string(indent.add(1))

    // Get the switch expression
    switch_expr := expr.params.get(0)

    // Hoist any throwing function calls in the switch expression
    hoist_throwing_expr(switch_expr, output, indent, ctx, context)

    // Determine the type of the switch expression for proper comparison
    // Type inference may fail - that's okay, we'll use default
    mut switch_type := ValueType.TVoid
    mut has_switch_type := false
    switch_type = get_value_type(context, switch_expr)
    has_switch_type = true
    catch (err: Str) {
        // REM: type inference failed, that's ok
    }

    mut is_str_switch := false
    if has_switch_type {
        switch switch_type {
        case ValueType.TCustom(t):
            if t.eq("Str") {
                is_str_switch = true
            }
        case:
            // REM: not a custom type
        }
    }

    // Determine if this is an enum switch and if it has payloads
    mut is_enum_switch := false
    mut enum_has_payloads_flag := false
    mut i := 1
    while i.lt(expr.params.len()) {
        case_pattern := expr.params.get(i)
        switch case_pattern.node_type {
        case NodeType.Pattern(_):
            is_enum_switch = true
        case NodeType.Identifier(name):
            if not(case_pattern.params.is_empty()).or(name.contains(".")) {
                is_enum_switch = true
            }
        case NodeType.FCall:
            is_enum_switch = true
            // FCall with arguments (e.g., ValueType.TType(TTypeDef.TEnumDef))
            // implies the enum has payloads
            if case_pattern.params.len().gt(1) {
                enum_has_payloads_flag = true
            }
        case:
            // REM: not an enum case
        }
        i = i.add(2)
    }
    if is_enum_switch {
        if has_switch_type {
            switch switch_type {
            case ValueType.TCustom(enum_name):
                enum_def := context.scope_stack.lookup_enum(enum_name)
                enum_has_payloads_flag = enum_has_payloads(enum_def)
                catch (err: KeyNotFoundError) {
                    // REM: enum not found
                }
            case:
                // REM: not a custom type
            }
        }
    }

    // Store switch expression in a temp variable to avoid re-evaluation
    switch_var := next_mangled(ctx)
    output.push_str(indent_str)
    output.push_str("__auto_type ")
    output.push_str(switch_var)
    output.push_str(" = ")
    emit_expr(switch_expr, output, 0, ctx, context)
    output.push_str(";\n")

    // Hoist declarations from all case bodies to before the if/else chain
    // This ensures variables declared in switch cases are visible after the switch
    // (TIL has function-level scoping, not block-level scoping)
    mut j := 1
    while j.add(1).lt(expr.params.len()) {
        body := expr.params.get(j.add(1))
        decls := collect_declarations_in_body(body, context)
        for decl: CollectedDeclaration in decls {
            c_var_name := til_name(decl.name)
            if not(ctx.declared_vars.contains(c_var_name)) {
                c_type := til_type_to_c(decl.value_type)
                output.push_str(indent_str)
                output.push_str(c_type)
                output.push_str(" ")
                output.push_str(c_var_name)
                output.push_str(";\n")
                ctx.declared_vars.add(c_var_name)
                // Also register in scope_stack so get_value_type can find it
                mut symbol_info := SymbolInfo()
                symbol_info.value_type = decl.value_type.clone()
                symbol_info.is_mut = false
                symbol_info.is_copy = false
                symbol_info.is_own = false
                symbol_info.is_comptime_const = false
                context.scope_stack.declare_symbol(decl.name.clone(), symbol_info)
                catch (err: Str) {
                    // REM: til_type_to_c failed, skip this decl
                }
            }
        }
        j = j.add(2)
    }

    // Emit if/else if chain for cases
    // Default case (if present) is guaranteed to be last by parser
    mut k := 1
    mut first_case := true
    mut has_cases := false
    while k.add(1).lt(expr.params.len()) {
        case_pattern := expr.params.get(k)
        case_body := expr.params.get(k.add(1))
        has_cases = true

        // Check if this is the default case (guaranteed last by parser)
        switch case_pattern.node_type {
        case NodeType.DefaultCase:
            // Default case - emit as else block
            if not(first_case) {
                output.push_str(indent_str)
                output.push_str("} else {\n")
            }
            saved_declared_vars := ctx.declared_vars.clone()
            emit_body(case_body, output, indent.add(1), ctx, context)
            ctx.declared_vars = saved_declared_vars
            k = k.add(2)
            continue
        case:
            // REM: not default case, continue below
        }

        // Regular case - emit if/else if
        output.push_str(indent_str)
        if first_case {
            output.push_str("if (")
            first_case = false
        } else {
            output.push_str("} else if (")
        }

        // Emit the condition based on pattern type
        switch case_pattern.node_type {
        case NodeType.Pattern(pattern_info):
            // Enum pattern with payload binding: compare tag
            mut info := parse_pattern_variant_name(pattern_info.variant_name)
            // If no type prefix (shorthand syntax), use the switch expression's type
            if info.type_name.is_empty() {
                if has_switch_type {
                    switch switch_type {
                    case ValueType.TCustom(enum_name):
                        info.type_name = enum_name.clone()
                    case:
                        // REM: not a custom type
                    }
                }
            }
            output.push_str(switch_var)
            output.push_str(".tag == til_")
            output.push_str(info.type_name)
            output.push_str("_")
            output.push_str(info.variant_name)
            output.push_str(") {\n")

            // Extract payload into binding variable
            output.push_str(body_indent)
            output.push_str("__auto_type ")
            output.push_str(til_name(pattern_info.binding_var))
            output.push_str(" = ")
            output.push_str(switch_var)
            output.push_str(".payload.")
            output.push_str(info.variant_name)
            output.push_str(";\n")
        case NodeType.Range:
            // Range: low <= val && val <= high
            if case_pattern.params.len().lt(2) {
                throw "ccodegen: Range requires start and end values"
            }
            if is_str_switch {
                // For string ranges, use strcmp: strcmp(low, val) <= 0 && strcmp(val, high) <= 0
                output.push_str("strcmp((char*)")
                range_start := case_pattern.params.get(0)
                emit_expr(range_start, output, 0, ctx, context)
                output.push_str(".c_string, (char*)")
                output.push_str(switch_var)
                output.push_str(".c_string) <= 0 && strcmp((char*)")
                output.push_str(switch_var)
                output.push_str(".c_string, (char*)")
                range_end := case_pattern.params.get(1)
                emit_expr(range_end, output, 0, ctx, context)
                output.push_str(".c_string) <= 0")
            } else {
                range_start := case_pattern.params.get(0)
                emit_expr(range_start, output, 0, ctx, context)
                output.push_str(" <= ")
                output.push_str(switch_var)
                output.push_str(" && ")
                output.push_str(switch_var)
                output.push_str(" <= ")
                range_end := case_pattern.params.get(1)
                emit_expr(range_end, output, 0, ctx, context)
            }
            output.push_str(") {\n")
        case NodeType.Identifier(_name):
            info := get_case_variant_info(case_pattern)
            if not(info.variant_name.is_empty()) {
                // Enum variant without payload
                output.push_str(switch_var)
                if enum_has_payloads_flag {
                    output.push_str(".tag")
                }
                output.push_str(" == ")
                if not(info.type_name.is_empty()) {
                    output.push_str("til_")
                    output.push_str(info.type_name)
                    output.push_str("_")
                }
                output.push_str(info.variant_name)
            } else if is_str_switch {
                // String comparison - pass by pointer
                output.push_str("til_Str_eq(&")
                output.push_str(switch_var)
                output.push_str(", &")
                emit_expr(case_pattern, output, 0, ctx, context)
                output.push_str(").data")
            } else {
                // Regular value comparison
                output.push_str(switch_var)
                output.push_str(" == ")
                emit_expr(case_pattern, output, 0, ctx, context)
            }
            output.push_str(") {\n")
        case NodeType.LLiteral(_):
            if is_str_switch {
                // String literal comparison - pass by pointer
                output.push_str("til_Str_eq(&")
                output.push_str(switch_var)
                output.push_str(", &")
                emit_expr(case_pattern, output, 0, ctx, context)
                output.push_str(").data")
            } else {
                // Regular literal comparison
                output.push_str(switch_var)
                output.push_str(" == ")
                emit_expr(case_pattern, output, 0, ctx, context)
            }
            output.push_str(") {\n")
        case NodeType.FCall:
            // FCall pattern: Type.Variant(payload) - enum variant with payload argument
            // For example: ValueType.TType(TTypeDef.TEnumDef)
            info := get_case_variant_info(case_pattern)
            if not(info.variant_name.is_empty()).and(enum_has_payloads_flag) {
                // Compare tag first
                output.push_str(switch_var)
                output.push_str(".tag == til_")
                output.push_str(info.type_name)
                output.push_str("_")
                output.push_str(info.variant_name)

                // Check if there's a payload argument to compare
                // params[0] is Type.Variant identifier, params[1+] are arguments
                if case_pattern.params.len().gt(1) {
                    payload_expr := case_pattern.params.get(1)
                    // Check if payload is also an enum variant (Type.Variant pattern)
                    payload_info := get_case_variant_info(payload_expr)
                    if not(payload_info.variant_name.is_empty()) {
                        // Payload is an enum variant - compare payload field
                        output.push_str(" && ")
                        output.push_str(switch_var)
                        output.push_str(".payload.")
                        output.push_str(info.variant_name)
                        output.push_str(" == til_")
                        output.push_str(payload_info.type_name)
                        output.push_str("_")
                        output.push_str(payload_info.variant_name)
                    }
                    // TODO: handle non-enum payloads if needed
                }
                output.push_str(") {\n")
            } else if not(info.variant_name.is_empty()) {
                // Enum without payloads - simple tag comparison
                output.push_str(switch_var)
                output.push_str(" == til_")
                output.push_str(info.type_name)
                output.push_str("_")
                output.push_str(info.variant_name)
                output.push_str(") {\n")
            } else {
                // Not an enum pattern - fall through to generic comparison
                output.push_str(switch_var)
                output.push_str(" == ")
                emit_expr(case_pattern, output, 0, ctx, context)
                output.push_str(") {\n")
            }
        case:
            // Generic case: emit equality comparison
            // TODO: Should emit proper FCall through normal path instead of hardcoding
            if is_str_switch {
                output.push_str("til_Str_eq(&")
                output.push_str(switch_var)
                output.push_str(", &")
                emit_expr(case_pattern, output, 0, ctx, context)
                output.push_str(").data")
            } else {
                output.push_str(switch_var)
                output.push_str(" == ")
                emit_expr(case_pattern, output, 0, ctx, context)
            }
            output.push_str(") {\n")
        }

        // Emit case body
        // For pattern matching with binding variables, we need to add the binding to scope
        // so that get_value_type can resolve it during body emission
        // Save declared_vars for block scope - variables declared in this case body
        // should not affect other case bodies
        saved_declared_vars := ctx.declared_vars.clone()

        switch case_pattern.node_type {
        case NodeType.Pattern(pattern_info):
            // Get the payload type from the enum definition
            info := parse_pattern_variant_name(pattern_info.variant_name)
            mut payload_type_opt := ValueType.TVoid
            mut has_payload_type := false
            if has_switch_type {
                switch switch_type {
                case ValueType.TCustom(enum_name):
                    enum_def := context.scope_stack.lookup_enum(enum_name)
                    payload_type_opt = enum_def.get(info.variant_name)
                    has_payload_type = true
                    catch (err: KeyNotFoundError) {
                        // REM: variant not found
                    }
                case:
                    // REM: not a custom type
                }
            }

            if has_payload_type {
                // Push scope and declare binding variable
                context.scope_stack.push(ScopeType.Block)
                mut symbol_info := SymbolInfo()
                symbol_info.value_type = payload_type_opt.clone()
                symbol_info.is_mut = false
                symbol_info.is_copy = false
                symbol_info.is_own = false
                symbol_info.is_comptime_const = false
                context.scope_stack.declare_symbol(pattern_info.binding_var.clone(), symbol_info)
                emit_body(case_body, output, indent.add(1), ctx, context)
                _ := context.scope_stack.pop()
                catch (err: Str) {
                    // REM: pop failed
                }
            } else {
                emit_body(case_body, output, indent.add(1), ctx, context)
            }
        case:
            emit_body(case_body, output, indent.add(1), ctx, context)
        }

        // Restore declared_vars after case body
        ctx.declared_vars = saved_declared_vars
        k = k.add(2)
    }

    // Close the if/else chain
    if has_cases {
        output.push_str(indent_str)
        output.push_str("}\n")
    }

    catch (err: IndexOutOfBoundsError) {
        throw err.msg
    }
    catch (err: AllocError) {
        throw err.msg
    }
}

emit_fcall := proc(expr: Expr, mut output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) throws Str {
    // First param is the function name (or Type.method for type-qualified calls)
    if expr.params.is_empty() {
        throw "ccodegen: FCall with no params"
    }

    // Get function name (handles both nested identifiers and precomp'd "Type.method" strings)
    first_param := expr.params.get(0)
    mut func_name := get_func_name_string(first_param)
    if func_name.is_empty() {
        throw "ccodegen: FCall first param not Identifier"
    }

    // For builtins, we need the original name with dots
    orig_func_name := get_til_func_name_string(first_param)

    // Check if this is a call to a nested (hoisted) function - use mangled name
    if ctx.nested_func_names.contains(orig_func_name) {
        func_name = ctx.nested_func_names.get(orig_func_name)
    }

    // Named args are already reordered by precomp, so no reordering needed here

    // Check for struct literal: TypeName(field=value, ...) -> (til_TypeName){.field = value, ...}
    // Uses lookup_struct to properly identify struct types (like interpreter.rs)
    mut has_named_args := false
    mut arg_idx := 1
    while arg_idx.lt(expr.params.len()) {
        arg := expr.params.get(arg_idx)
        switch arg.node_type {
        case NodeType.NamedArg(_):
            has_named_args = true
        case:
            // REM: not a named arg
        }
        arg_idx.inc()
    }

    if has_named_args {
        struct_def := context.scope_stack.lookup_struct(func_name)
        // Emit C compound literal: (til_TypeName){.field1 = val1, .field2 = val2, ...}
        output.push_str("(")
        output.push_str(TIL_PREFIX)
        output.push_str(func_name)
        output.push_str(")")

        // Build map of named arg values
        mut named_values := Map.new(Str, Expr)
        mut na_idx := 1
        while na_idx.lt(expr.params.len()) {
            arg := expr.params.get(na_idx)
            switch arg.node_type {
            case NodeType.NamedArg(field_name):
                if not(arg.params.is_empty()) {
                    value_expr := arg.params.get(0)
                    named_values.set(field_name.clone(), value_expr.clone())
                }
            case:
                // REM: not a named arg
            }
            na_idx.inc()
        }

        if struct_def.members.is_empty().or(struct_def.default_values.is_empty()) {
            output.push_str("{}")
        } else {
            output.push_str("{")
            mut first := true
            for member: SMember in struct_def.members {
                if not(member.is_mut) {
                    continue
                }
                if not(first) {
                    output.push_str(", ")
                }
                first = false
                output.push_str(".")
                output.push_str(member.name)
                output.push_str(" = ")
                // Use named arg value if provided, otherwise use default
                if named_values.contains_key(member.name) {
                    value_expr := named_values.get(member.name)
                    emit_expr(value_expr, output, 0, ctx, context)
                } else {
                    default_expr := struct_def.default_values.get(member.name)
                    // Check if this was a throwing default - use temp var instead
                    key := func_name.clone()
                    key.push_str(":")
                    key.push_str(member.name)
                    if ctx.hoisted_struct_defaults.contains(key) {
                        temp_name := ctx.hoisted_struct_defaults.get(key)
                        output.push_str(temp_name)
                    } else if is_throwing_fcall(default_expr, context) {
                        // Shouldn't happen - throwing defaults should be hoisted
                        output.push_str("/* ERROR: unhoisted throwing default */")
                    } else {
                        emit_expr(default_expr, output, 0, ctx, context)
                    }
                    catch (err: KeyNotFoundError) {
                        output.push_str("0")
                    }
                }
            }
            output.push_str("}")
        }
        return
        catch (err: KeyNotFoundError) {
            // REM: not a struct, continue below
        }
    }

    indent_str := indent_string(indent)

    // Statement level vs expression level:
    // - indent > 0: statement level (can emit hoisting statements, adds semicolon)
    // - indent == 0: expression level (nested in larger expression, no hoisting, no semicolons)
    is_stmt_level := indent.gt(0)

    // Hoist throwing function calls from arguments (only at statement level)
    mut hoisted := Map.new(I64, Str)
    if is_stmt_level.and(expr.params.len().gt(1)) {
        mut args := Vec.new(Expr)
        mut hi := 1
        while hi.lt(expr.params.len()) {
            arg := expr.params.get(hi)
            args.push(arg.clone())
            hi.inc()
        }
        hoisted_vec := hoist_throwing_args(args, output, indent, ctx, context)
        for h: HoistedArg in hoisted_vec {
            hoisted.set(h.index, h.temp_var.clone())
        }
    }

    // Hoist non-lvalue args when param type is Dynamic (only at statement level)
    // Need to look up param types first
    if is_stmt_level.and(expr.params.len().gt(1)) {
        fd := get_fcall_func_def(context, expr)
        mut param_types := Vec.new(ValueType)
        mut param_by_ref := Vec.new(Bool)
        for arg: SArg in fd.args {
            param_types.push(arg.value_type.clone())
            param_by_ref.push(param_needs_by_ref(arg))
        }
        mut args := Vec.new(Expr)
        mut ai := 1
        while ai.lt(expr.params.len()) {
            arg := expr.params.get(ai)
            args.push(arg.clone())
            ai.inc()
        }
        dynamic_hoisted := hoist_for_dynamic_params(args, param_types, hoisted, output, indent, ctx, context)
        for h: HoistedArg in dynamic_hoisted {
            hoisted.set(h.index, h.temp_var.clone())
        }
        // Hoist struct-returning FCall args when param is by-ref
        ref_hoisted := hoist_for_ref_params(args, param_by_ref, hoisted, output, indent, ctx, context)
        for h: HoistedArg in ref_hoisted {
            hoisted.set(h.index, h.temp_var.clone())
        }
        catch (err: KeyNotFoundError) {
            // REM: function not found
        }
    }

    // Hardcoded builtins (compile-time intrinsics that can't be implemented in TIL)
    if orig_func_name.eq("type_as_str") {
        // type_as_str(T) - get type name as string
        if expr.params.len().lt(2) {
            throw "ccodegen: type_as_str requires 1 argument"
        }
        type_arg := expr.params.get(1)
        switch type_arg.node_type {
        case NodeType.Identifier(type_name):
            // Check if this is a Type variable or a literal type name
            mut is_type_var := false
            sym := context.scope_stack.lookup_symbol(type_name)
            switch sym.value_type {
            case ValueType.TCustom(t):
                if t.eq("Type") {
                    is_type_var = true
                }
            case:
                // REM: not Type
            }
            catch (err: KeyNotFoundError) {
                // REM: symbol not found
            }
            if is_type_var {
                // Type variable - already a const char*, wrap in Str struct literal
                output.push_str("((")
                output.push_str(TIL_PREFIX)
                output.push_str("Str){(")
                output.push_str(TIL_PREFIX)
                output.push_str("I64)")
                output.push_str(TIL_PREFIX)
                output.push_str(type_name)
                output.push_str(", strlen(")
                output.push_str(TIL_PREFIX)
                output.push_str(type_name)
                output.push_str(")})")
            } else {
                // Literal type name - create Str compound literal
                _ := context.scope_stack.lookup_struct("Str")
                emit_str_literal(type_name, output)
                catch (err: KeyNotFoundError) {
                    output.push_str("\"")
                    output.push_str(type_name)
                    output.push_str("\"")
                }
            }
        case:
            emit_str_literal("unknown", output)
        }
        return
    } else if orig_func_name.eq("enum_to_str") {
        // enum_to_str(e) - get enum variant name as string
        if expr.params.len().lt(2) {
            throw "ccodegen: enum_to_str requires 1 argument"
        }
        arg := expr.params.get(1)
        value_type := get_value_type(context, arg)
        switch value_type {
        case ValueType.TCustom(enum_type_name):
            // Verify it's an enum type
            _ := context.scope_stack.lookup_enum(enum_type_name)
            output.push_str(til_name(enum_type_name))
            output.push_str("_to_str(")
            // Check if the argument was already hoisted
            expr_key := arg.id
            if ctx.hoisted_exprs.contains(expr_key) {
                emit_expr(arg, output, 0, ctx, context)
            } else {
                output.push_str("&")
                emit_expr(arg, output, 0, ctx, context)
            }
            output.push_str(")")
            return
            catch (err: KeyNotFoundError) {
                mut err_msg := "ccodegen: enum_to_str argument '"
                err_msg.push_str(enum_type_name)
                err_msg.push_str("' is not an enum type")
                throw err_msg
            }
        case:
            throw "ccodegen: enum_to_str argument has non-custom type"
        }
    } else if orig_func_name.eq("size_of") {
        // size_of(T) - runtime type size lookup via til_size_of function
        if expr.params.len().lt(2) {
            throw "ccodegen: size_of requires 1 argument"
        }
        output.push_str(TIL_PREFIX)
        output.push_str("size_of(&")
        type_arg := expr.params.get(1)
        switch type_arg.node_type {
        case NodeType.Identifier(type_name):
            // Check if this is a Type variable or a literal type name
            mut is_type_var := false
            sym := context.scope_stack.lookup_symbol(type_name)
            switch sym.value_type {
            case ValueType.TCustom(t):
                if t.eq("Type") {
                    is_type_var = true
                }
            case:
                // REM: not Type
            }
            catch (err: KeyNotFoundError) {
                // REM: symbol not found
            }
            if is_type_var {
                // Type variable - already a const char*, wrap in Str struct literal
                output.push_str("((")
                output.push_str(TIL_PREFIX)
                output.push_str("Str){(")
                output.push_str(TIL_PREFIX)
                output.push_str("I64)")
                output.push_str(TIL_PREFIX)
                output.push_str(type_name)
                output.push_str(", strlen(")
                output.push_str(TIL_PREFIX)
                output.push_str(type_name)
                output.push_str(")})")
            } else {
                // Literal type name - create Str compound literal
                emit_str_literal(type_name, output)
            }
        case:
            // Not an identifier - emit as expression
            output.push_str("((")
            output.push_str(TIL_PREFIX)
            output.push_str("Str){(")
            output.push_str(TIL_PREFIX)
            output.push_str("I64)")
            emit_expr(type_arg, output, 0, ctx, context)
            output.push_str(", strlen(")
            emit_expr(type_arg, output, 0, ctx, context)
            output.push_str(")})")
        }
        output.push_str(")")
        return
    } else if orig_func_name.eq("to_ptr") {
        // to_ptr(var) - get address of variable
        if expr.params.len().lt(2) {
            throw "ccodegen: to_ptr requires 1 argument"
        }
        arg := expr.params.get(1)
        // Check if arg is a Dynamic parameter (void*) - just use the pointer directly
        mut is_dynamic_param := false
        switch arg.node_type {
        case NodeType.Identifier(name):
            if arg.params.is_empty() {
                sym := context.scope_stack.lookup_symbol(name)
                switch sym.value_type {
                case ValueType.TCustom(t):
                    if t.eq("Dynamic") {
                        is_dynamic_param = true
                    }
                case:
                    // REM: not Dynamic
                }
                catch (err: KeyNotFoundError) {
                    // REM: symbol not found
                }
            }
        case:
            // REM: not identifier
        }
        output.push_str("(")
        output.push_str(TIL_PREFIX)
        output.push_str("I64)")
        if is_dynamic_param {
            // Dynamic param: just output the variable name (it's already a pointer)
            switch arg.node_type {
            case NodeType.Identifier(name):
                output.push_str(til_name(name))
            case:
                // REM: should not happen
            }
        } else {
            // Check if arg was hoisted with & prefix already
            expr_key := arg.id
            if ctx.hoisted_exprs.contains(expr_key) {
                temp_var := ctx.hoisted_exprs.get(expr_key)
                if temp_var.starts_with("&") {
                    output.push_str(temp_var)
                } else {
                    output.push_str("&")
                    output.push_str(temp_var)
                }
            } else {
                output.push_str("&")
                emit_arg_or_hoisted(arg, 0, hoisted, output, ctx, context)
            }
        }
        return
    }

    // User-defined function call
    output.push_str(indent_str)

    // Check for type-qualified call on FCall result: func_name(fcall_result, args...)
    if first_param.params.is_empty().and(expr.params.len().gteq(2)) {
        second_param := expr.params.get(1)
        switch second_param.node_type {
        case NodeType.FCall:
            fcall_ret_type := get_value_type(context, second_param)
            switch fcall_ret_type {
            case ValueType.TCustom(type_name):
                candidate := type_name.clone()
                candidate.push_str(".")
                candidate.push_str(orig_func_name)
                _ := context.scope_stack.lookup_func(candidate)
                // Emit as Type_method(fcall_result, args...)
                output.push_str(TIL_PREFIX)
                output.push_str(type_name)
                output.push_str("_")
                output.push_str(orig_func_name)
                output.push_str("(")
                emit_expr(second_param, output, 0, ctx, context)
                mut ri := 2
                while ri.lt(expr.params.len()) {
                    output.push_str(", ")
                    remaining_arg := expr.params.get(ri)
                    emit_expr(remaining_arg, output, 0, ctx, context)
                    ri.inc()
                }
                output.push_str(")")
                if is_stmt_level {
                    output.push_str(";\n")
                }
                return
                catch (err: KeyNotFoundError) {
                    // REM: function not found, continue below
                }
            case:
                // REM: not TCustom
            }
            catch (err: Str) {
                // REM: get_value_type failed, continue below
            }
        case:
            // REM: not FCall
        }
    }

    // Check for struct construction: TypeName() -> (til_TypeName){defaults...}
    mut struct_def_opt_exists := false
    struct_def_opt := context.scope_stack.lookup_struct(func_name)
    struct_def_opt_exists = true
    catch (err: KeyNotFoundError) {
        // REM: not a struct
    }

    if struct_def_opt_exists.and(expr.params.len().eq(1)) {
        // Use the struct definition to get default values
        if struct_def_opt.members.is_empty().or(struct_def_opt.default_values.is_empty()) {
            output.push_str("(")
            output.push_str(TIL_PREFIX)
            output.push_str(func_name)
            output.push_str("){}")
        } else {
            // Check if any default value is a throwing function call
            mut throwing_defaults := Vec.new(ThrowingDefault)
            for member: SMember in struct_def_opt.members {
                if not(member.is_mut) {
                    continue
                }
                default_expr := struct_def_opt.default_values.get(member.name)
                if is_throwing_fcall(default_expr, context) {
                    mut td := ThrowingDefault()
                    td.member_name = member.name.clone()
                    td.default_expr = default_expr.clone()
                    throwing_defaults.push(td)
                }
                catch (err: KeyNotFoundError) {
                    // REM: no default for this member
                }
            }

            // If we have throwing defaults, emit them as separate statements first
            if is_stmt_level {
                for td: ThrowingDefault in throwing_defaults {
                    mut throw_types := Vec.new(Str)
                    if not(td.default_expr.params.is_empty()) {
                        td_first_param := td.default_expr.params.get(0)
                        lookup_name := get_til_func_name_string(td_first_param)
                        if not(lookup_name.is_empty()) {
                            fd := context.scope_stack.lookup_func(lookup_name)
                            throw_types = fd.throw_types.clone()
                            catch (err: KeyNotFoundError) {
                                // REM: function not found
                            }
                        }
                    }

                    temp_name := "_default_"
                    temp_name.push_str(td.member_name)
                    temp_name.push_str("_")
                    temp_name.push_str(next_mangled(ctx))
                    emit_throwing_call_propagate(td.default_expr, throw_types, temp_name, "", output, indent, ctx, context)
                    key := func_name.clone()
                    key.push_str(":")
                    key.push_str(td.member_name)
                    ctx.hoisted_struct_defaults.set(key, temp_name)
                }
            }

            output.push_str(indent_str)
            output.push_str("(")
            output.push_str(TIL_PREFIX)
            output.push_str(func_name)
            output.push_str("){")
            mut first := true
            for member: SMember in struct_def_opt.members {
                if not(member.is_mut) {
                    continue
                }
                if not(first) {
                    output.push_str(", ")
                }
                first = false
                output.push_str(".")
                output.push_str(member.name)
                output.push_str(" = ")
                default_expr := struct_def_opt.default_values.get(member.name)
                key := func_name.clone()
                key.push_str(":")
                key.push_str(member.name)
                if ctx.hoisted_struct_defaults.contains(key) {
                    temp_name := ctx.hoisted_struct_defaults.get(key)
                    output.push_str(temp_name)
                } else if is_throwing_fcall(default_expr, context) {
                    output.push_str("/* ERROR: unhoisted throwing default */")
                } else {
                    emit_expr(default_expr, output, 0, ctx, context)
                }
                catch (err: KeyNotFoundError) {
                    output.push_str("0")
                }
            }
            output.push_str("}")
        }

        if is_stmt_level {
            output.push_str(";\n")
        }
        return
    }

    // Regular function call
    // Detect and construct variadic array if needed (only at statement level)
    mut variadic_arr_var := ""
    mut has_variadic_arr := false
    if is_stmt_level {
        if ctx.func_variadic_args.contains(orig_func_name) {
            variadic_info := ctx.func_variadic_args.get(orig_func_name)
            elem_type := variadic_info.elem_type.clone()
            regular_count := variadic_info.regular_count
            mut variadic_args := Vec.new(Expr)
            mut vi := regular_count.add(1)
            while vi.lt(expr.params.len()) {
                varg := expr.params.get(vi)
                variadic_args.push(varg.clone())
                vi.inc()
            }
            variadic_arr_var = hoist_variadic_args(elem_type, variadic_args, hoisted, regular_count, output, indent, ctx, context)
            has_variadic_arr = true
        }
    }

    // Check if this is an enum variant constructor (Type.Variant or Type_Variant)
    mut is_enum_constructor := false
    if func_name.contains("_") {
        parts := func_name.split("_")
        if parts.len().gteq(2) {
            type_name_part := parts.get(0)
            variant_name_part := parts.get(1)
            enum_def := context.scope_stack.lookup_enum(type_name_part)
            if enum_def.contains_key(variant_name_part) {
                is_enum_constructor = true
            }
            catch (err: KeyNotFoundError) {
                // REM: not an enum
            }
        }
    }

    output.push_str(TIL_PREFIX)
    if is_enum_constructor {
        parts := func_name.split("_")
        type_name_part := parts.get(0)
        variant_name_part := parts.get(1)
        output.push_str(type_name_part)
        output.push_str("_make_")
        output.push_str(variant_name_part)
    } else {
        output.push_str(func_name)
    }
    output.push_str("(")

    // Check if this is a variadic function call
    if ctx.func_variadic_args.contains(orig_func_name) {
        variadic_info := ctx.func_variadic_args.get(orig_func_name)
        regular_count := variadic_info.regular_count

        // Look up function param info for proper by-ref handling of regular args
        mut param_info := Vec.new(ParamTypeInfo)
        fd := get_fcall_func_def(context, expr)
        for arg: SArg in fd.args {
            mut pi := ParamTypeInfo()
            pi.value_type = arg.value_type.clone()
            pi.has_value_type = true
            pi.by_ref = param_needs_by_ref(arg)
            param_info.push(pi)
        }
        catch (err: KeyNotFoundError) {
            fd2 := context.scope_stack.lookup_func(orig_func_name)
            for arg: SArg in fd2.args {
                mut pi := ParamTypeInfo()
                pi.value_type = arg.value_type.clone()
                pi.has_value_type = true
                pi.by_ref = param_needs_by_ref(arg)
                param_info.push(pi)
            }
            catch (err2: KeyNotFoundError) {
                // REM: function not found
            }
        }

        // Emit regular args first
        mut arg_i := 0
        while arg_i.lt(regular_count) {
            if arg_i.gt(0) {
                output.push_str(", ")
            }
            arg := expr.params.get(arg_i.add(1))
            mut param_type := ValueType.TVoid
            mut has_param_type := false
            mut by_ref := true
            if arg_i.lt(param_info.len()) {
                pi := param_info.get(arg_i)
                param_type = pi.value_type.clone()
                has_param_type = pi.has_value_type
                by_ref = pi.by_ref
            }
            emit_arg_with_param_type(arg, arg_i, hoisted, param_type, has_param_type, by_ref, output, ctx, context)
            arg_i.inc()
        }

        // Emit variadic array pointer
        if has_variadic_arr {
            if regular_count.gt(0) {
                output.push_str(", ")
            }
            output.push_str("&")
            output.push_str(variadic_arr_var)
        }
    } else {
        // Regular non-variadic function call
        mut param_info := Vec.new(ParamTypeInfo)
        fd := get_fcall_func_def(context, expr)
        for arg: SArg in fd.args {
            mut pi := ParamTypeInfo()
            pi.value_type = arg.value_type.clone()
            pi.has_value_type = true
            pi.by_ref = param_needs_by_ref(arg)
            param_info.push(pi)
        }
        catch (err: KeyNotFoundError) {
            // REM: function not found
        }

        mut first_arg := true
        mut arg_i := 0
        mut expr_i := 1
        while expr_i.lt(expr.params.len()) {
            if not(first_arg) {
                output.push_str(", ")
            }
            first_arg = false
            arg := expr.params.get(expr_i)
            // Check if arg is a type identifier - emit as string literal
            type_arg_name := get_type_arg_name(arg, context)
            if not(type_arg_name.is_empty()) {
                output.push_str("\"")
                output.push_str(type_arg_name)
                output.push_str("\"")
            } else if not(param_info.is_empty()) {
                mut param_type := ValueType.TVoid
                mut has_param_type := false
                mut by_ref := false
                if arg_i.lt(param_info.len()) {
                    pi := param_info.get(arg_i)
                    param_type = pi.value_type.clone()
                    has_param_type = pi.has_value_type
                    by_ref = pi.by_ref
                }
                emit_arg_with_param_type(arg, arg_i, hoisted, param_type, has_param_type, by_ref, output, ctx, context)
            } else {
                emit_arg_or_hoisted(arg, arg_i, hoisted, output, ctx, context)
            }
            arg_i.inc()
            expr_i.inc()
        }
    }

    output.push_str(")")
    if is_stmt_level {
        output.push_str(";\n")
    }

    // Emit Array.delete if variadic array was constructed
    if has_variadic_arr {
        output.push_str(indent_str)
        output.push_str(TIL_PREFIX)
        output.push_str("Array_delete(&")
        output.push_str(variadic_arr_var)
        output.push_str(");\n")
    }

    catch (err: IndexOutOfBoundsError) {
        throw err.msg
    }
    catch (err: AllocError) {
        throw err.msg
    }
}

// Helper to emit a Str compound literal: ((til_Str){(til_I64)"...", len})
// This is valid in both constant initializers and expressions (unlike til_Str_from_literal)
emit_str_literal := proc(s: Str, mut output: Str) {
    output.push_str("((")
    output.push_str(TIL_PREFIX)
    output.push_str("Str){(")
    output.push_str(TIL_PREFIX)
    output.push_str("I64)\"")
    // Escape special characters for C string literals
    mut i := 0
    while i.lt(s.len()) {
        c := s.char_at(i)
        if c.eq("\n") {
            output.push_str("\\n")
        } else if c.eq("\r") {
            output.push_str("\\r")
        } else if c.eq("\t") {
            output.push_str("\\t")
        } else if c.eq("\\") {
            output.push_str("\\\\")
        } else if c.eq("\"") {
            output.push_str("\\\"")
        } else {
            output.push_str(c)
        }
        i.inc()
    }
    output.push_str("\", ")
    output.push_str(s.len().to_str())
    output.push_str("})")
}

emit_literal := proc(lit: Literal, mut output: Str, context: Context) throws Str {
    switch lit {
    case Literal.Str(s):
        has_str := context.scope_stack.lookup_struct("Str")
        emit_str_literal(s, output)
        catch (err: KeyNotFoundError) {
            // No Str struct - emit raw string
            output.push_str("\"")
            // Escape special characters for C string literals
            mut j := 0
            while j.lt(s.len()) {
                c := s.char_at(j)
                if c.eq("\n") {
                    output.push_str("\\n")
                } else if c.eq("\r") {
                    output.push_str("\\r")
                } else if c.eq("\t") {
                    output.push_str("\\t")
                } else if c.eq("\\") {
                    output.push_str("\\\\")
                } else if c.eq("\"") {
                    output.push_str("\\\"")
                } else {
                    output.push_str(c)
                }
                j.inc()
            }
            output.push_str("\"")
        }
    case Literal.Number(n):
        output.push_str(n)
    case Literal.List(_):
        throw "ccodegen: List literals not yet supported"
    }
}
