mode liba

// C code generator for TIL
// Translates TIL AST to C source code

import("self.parser")  // Expr, NodeType, Literal
import("self.init")    // Context, get_value_type, ScopeFrame, SymbolInfo, ScopeType

// Prefix for all TIL-generated C names (structs, functions, etc.)
TIL_PREFIX := "til_"

// Info about a variadic parameter
VariadicParamInfo := struct {
    mut elem_type: Str = ""
    mut regular_count: I64 = 0
}

// Info about a catch label for local throw/catch
CatchLabelInfo := struct {
    mut label: Str = ""
    mut temp_var: Str = ""
}

// Info about a catch block for code generation
// Note: TIL has extra catch_block field - removing it causes til_compiled to hang (Bug #80)
CatchLabelInfoEntry := struct {
    mut stmt_index: I64 = 0      // Index in stmts array
    mut type_name: Str = ""      // Error type being caught (e.g., "Str", "IndexOutOfBoundsError")
    mut label: Str = ""          // Goto label for this catch
    mut temp_var: Str = ""       // Temp variable holding the thrown value
    mut catch_block: Expr = Expr()  // DO NOT REMOVE - causes hangs in til_compiled
}

// Info about a function parameter's type and whether passed by reference
ParamTypeInfo := struct {
    mut value_type: Ptr = Ptr()  // Option<ValueType> - pointer to ValueType or null
    mut by_ref: Bool = false
}

// Info about a struct member with a throwing default value
ThrowingDefault := struct {
    mut member_name: Str = ""
    mut default_expr: Expr = Expr()
}

// Info about a collected variable declaration for hoisting
CollectedDeclaration := struct {
    mut name: Str = ""
    mut value_type: ValueType = ValueType.TCustom("")
}


// Codegen context for tracking function info during code generation
CodegenContext := struct {
    // Counter for generating unique temp variable names (deterministic per-compilation)
    mut mangling_counter: I64 = 0
    // Map function name -> variadic arg info (Map<Str, VariadicParamInfo>)
    mut func_variadic_args: Map = Map.new(Str, VariadicParamInfo)
    // Currently generating function's throw types (if any)
    mut current_throw_types: Vec = Vec.new(ValueType)
    // Currently generating function's return types (if any)
    mut current_return_types: Vec = Vec.new(ValueType)
    // Set of declared variable names in current function (to avoid redefinition)
    mut declared_vars: Set = Set.new(Str)
    // Set of by-ref param names in current function - for using -> instead of . for field access
    mut current_ref_params: Vec = Vec.new(Str)
    // Map of variadic param names to their element type (e.g., "args" -> "til_Bool")
    // Passed as til_Array* so need dereference, and need type info for Array.get casting
    mut current_variadic_params: Map = Map.new(Str, Str)
    // All known type names for generating til_size_of function
    mut known_types: Vec = Vec.new(Str)
    // Map of hoisted expression addresses to their temp variable names
    // Used to track deeply nested variadic/throwing calls that have been hoisted
    // Key is "line:col" string, value is temp var name (Str)
    mut hoisted_exprs: Map = Map.new(I64, Str)
    // Map of locally-caught error types to (catch label, temp variable name)
    // Used for throw statements and throwing calls when there's an outer catch block
    // Key is error type name, value is struct with label and temp_var
    mut local_catch_labels: Map = Map.new(Str, CatchLabelInfo)
    // Current function name for nested function name mangling (empty at top-level)
    mut current_function_name: Str = ""
    // C code for hoisted nested function definitions
    mut hoisted_functions: Vec = Vec.new(Str)
    // C code for hoisted nested function prototypes
    mut hoisted_prototypes: Vec = Vec.new(Str)
    // Map original function name -> mangled name for nested functions
    mut nested_func_names: Map = Map.new(Str, Str)
    // Map of hoisted struct default expressions ((struct_name, member_name) -> temp var name)
    // Used to track which struct literal defaults were hoisted as temp vars
    // Key is "struct_name:member_name" string, value is temp var name
    mut hoisted_struct_defaults: Map = Map.new(Str, Str)
}

next_mangled := proc(mut ctx: CodegenContext) returns Str {
    n := ctx.mangling_counter
    ctx.mangling_counter.inc()
    func_name := ctx.current_function_name
    if func_name.len().gt(0) {
        return concat("_tmp_", func_name).concat("_").concat(n.to_str())
    }
    return concat("_tmp_", n.to_str())
}

// Returns the C name for a TIL identifier - adds TIL_PREFIX
// Exceptions: C keywords (true, false), pointer deref (*name), and generated names (_ prefix)
til_name := func(name: Str) returns Str {
    // "true" | "false" => name.to_string()
    if name.eq("true").or(name.eq("false")) { return name }
    // _ if name.starts_with('*') || name.starts_with('_') => name.to_string()
    if name.starts_with("*").or(name.starts_with("_")) { return name }
    return concat(TIL_PREFIX, name)
}

// Emit a struct literal, using compound literal syntax if already_declared
// e.g., " = {0}" vs " = (til_TypeName){0}"
emit_struct_literal_assign := proc(mut output: Str, type_name: Str, already_declared: Bool, literal_content: Str) returns Str {
    output.push_str(" = ")
    if already_declared {
        output.push_str("(")
        output.push_str(til_name(type_name))
        output.push_str(")")
    }
    output.push_str(literal_content)
    output.push_str(";\n")
    return output
}

// Start a struct literal assignment - caller will emit content and closing brace
// e.g., " = " vs " = (til_TypeName)"
emit_struct_literal_start := proc(mut output: Str, type_name: Str, already_declared: Bool) returns Str {
    output.push_str(" = ")
    if already_declared {
        output.push_str("(")
        output.push_str(til_name(type_name))
        output.push_str(")")
    }
    return output
}

// Returns the C name for a TIL function - adds TIL_PREFIX and converts dots to underscores
// Used for function names like Array.len -> til_Array_len
til_func_name := func(name: Str) returns Str {
    return concat(TIL_PREFIX, name.replace(".", "_"))
}

// Get function name from FCall's first param, handling both AST patterns:
// - Identifier("func") with params = [] -> "func"
// - Identifier("I64.inc") with params = [] -> "I64_inc" (from precomp)
// - Identifier("Vec") with params = [Identifier("new")] -> "Vec_new" (from parser)
// Returns the C-ready name with underscores (not dots)
get_func_name_string := func(first_param: Expr) returns Str throws Str, KeyNotFoundError {
    switch first_param.node_type {
    case NodeType.Identifier(name):
        if first_param.params.len().eq(0) {
            return name.replace(".", "_")
        } else {
            mut parts := name
            for p: Expr in first_param.params {
                switch p.node_type {
                case NodeType.Identifier(part):
                    parts = parts.concat("_").concat(part)
                case:
                }
            }
            return parts
        }
    case:
    }

    catch (err: AllocError) { throw err.msg }
    throw KeyNotFoundError(msg="first param not Identifier")
}

// Get TIL function name from FCall's first param (for scope lookups)
// Returns the dot-separated name like "Vec.new" or "I64.inc"
// This avoids the fragile .replacen('_', ".", 1) hack which breaks on type names with underscores
get_til_func_name_string := func(first_param: Expr) returns Str throws KeyNotFoundError {
    switch first_param.node_type {
    case NodeType.Identifier(name):
        if first_param.params.len().eq(0) {
            // Already has dots (from precomp) or is a simple name
            return name
        } else {
            // Build dotted name from nested identifiers
            mut parts := name
            for p: Expr in first_param.params {
                switch p.node_type {
                case NodeType.Identifier(part):
                    parts = parts.concat(".").concat(part)
                case:
                }
            }
            return parts
        }
    case:
    }

    throw KeyNotFoundError(msg="first param not Identifier")
}

// Check if an expression is a type identifier (a Type parameter at call site)
// Returns the type name if it is, so it can be emitted as a string literal
// Matches interpreter.rs behavior (line 1703-1713)
// Only matches STANDALONE identifiers - not field access like Vec.INIT_CAP
get_type_arg_name := func(expr: Expr, context: Context) returns Str throws KeyNotFoundError {
    switch expr.node_type {
    case NodeType.Identifier(name):
        // Only match standalone identifiers (no field access)
        if expr.params.len().gt(0) {
            throw KeyNotFoundError(msg="field access, not type arg")
        }
        // Check if this identifier is a type name (struct, enum, or builtin type)
        mut symbol := context.scope_stack.lookup_symbol(name)
        switch symbol.value_type {
        case ValueType.TType(type_name):
            return name
        case:
            // Not a type
        }
    case:
        // Not an identifier
    }
    throw KeyNotFoundError(msg="not a type identifier")
}
// Check if an expression is a function call that uses out-params (i.e., the function throws)
// Such calls cannot be used inline in struct initializers - they need separate statements
is_throwing_fcall := proc(expr: Expr, context: Context) returns Bool {
    switch expr.node_type {
    case NodeType.FCall:
        mut got_fd := false
        mut fd := SFuncDef()
        fd = get_fcall_func_def(context, expr)
        got_fd = true
        catch (err: KeyNotFoundError) { /* REM None case in Rust */ }
        if got_fd {
            return fd.throw_types.len().gt(0)
        }
    case:
    }
    return false
}

// Convert TIL type to C type. Returns Err if the type can't be represented in C (e.g., functions, infer)
til_type_to_c := func(til_type: ValueType) returns Str throws Str {
    switch til_type {
    case ValueType.TCustom(name):
        if name.eq(INFER_TYPE) {
            throw "Cannot convert inferred type to C"
        } else {
            // All types get TIL_PREFIX
            return concat(TIL_PREFIX, name)
        }
    case ValueType.TFunction:
        throw "Cannot convert function type to C"
    case ValueType.TType:
        throw "Cannot convert Type to C"
    case ValueType.TMulti:
        throw "Cannot convert multi-type to C"
    }
}

// Get function definition for an FCall expression
// Uses get_func_def_for_fcall from typer.til which handles all cases correctly
// (regular functions, struct methods, UFCS - though UFCS is already resolved by precomp)
// Throws KeyNotFoundError for struct/enum constructors (Rust returns None)
get_fcall_func_def := proc(context: Context, fcall_expr: Expr) returns SFuncDef throws KeyNotFoundError {
    mut fd := get_func_def_for_fcall(context, fcall_expr)

    catch (err: Str) { throw KeyNotFoundError(msg=err) }
    catch (err: KeyNotFoundError) { throw err }

    return fd
}

// Bug #60: Determine if a parameter should be passed by reference (pointer)
// - copy params: pass by value (copy is made)
// - Type params: already pointers (const char*), pass by value
// - All other const/own/mut params (including Dynamic): pass by pointer
param_needs_by_ref := func(param: Declaration) returns Bool {
    if param.is_copy {
        return false
    }
    // Type is already a pointer type in C (const char*), no extra indirection needed
    switch param.value_type {
    case ValueType.TCustom(name):
        if name.eq("Type") {
            return false
        }
    case:
    }
    return true
}

// Helper to get C type name for a ValueType (same as til_type_to_c but for error struct definitions)
value_type_to_c_name := func(vt: ValueType) returns Str throws Str {
    switch vt {
    case ValueType.TCustom(name):
        // All types get TIL_PREFIX
        return concat(TIL_PREFIX, name)
    case:
        throw concat("Cannot convert ", value_type_to_str(vt)).concat(" to C type name")
    }
}

// Result type for detect_variadic_fcall
VariadicFCallInfo := struct {
    mut elem_type: Str = ""
    mut regular_count: I64 = 0
}

// Detect if an expression is a variadic function call
// Returns VariadicFCallInfo if it's a variadic call
detect_variadic_fcall := proc(expr: Expr, ctx: CodegenContext) returns VariadicFCallInfo throws KeyNotFoundError {
    if expr.params.len().eq(0) {
        throw KeyNotFoundError(msg="not variadic")
    }

    // Get original TIL function name (with dots) for variadic map lookup
    mut first := Expr()
    expr.params.get(0, first)
    catch (err: IndexOutOfBoundsError) { throw KeyNotFoundError(msg="index out of bounds") }
    mut orig_func_name := get_til_func_name_string(first)

    mut info := VariadicParamInfo()
    ctx.func_variadic_args.get(orig_func_name, info)

    return VariadicFCallInfo(elem_type=info.elem_type, regular_count=info.regular_count)
}

// Result type for hoisted arguments
HoistedArg := struct {
    mut index: I64 = 0
    mut temp_var: Str = ""
}

// Hoist arguments that need to be passed by reference but are rvalues
// Returns a Vec of HoistedArg with the hoisted temp variables
// Note: output is mut and modified in-place (same pattern as hoist_for_dynamic_params)
hoist_for_ref_params := proc(args: Vec, param_by_ref: Vec, already_hoisted: Map, mut output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) returns Vec throws Str {
    mut hoisted := Vec.new(HoistedArg)
    mut indent_str := ""
    for _ in 0..indent {
        indent_str = indent_str.concat("    ")
    }

    for idx in 0..args.len() {
        mut arg := Expr()
        args.get(idx, arg)

        // Skip if already hoisted
        mut idx_str := idx.to_str()
        if already_hoisted.contains_key(idx_str) {
            continue
        }

        // Check if param is by-ref
        mut is_by_ref := false
        if idx.lt(param_by_ref.len()) {
            param_by_ref.get(idx, is_by_ref)
        }
        if not(is_by_ref) {
            continue
        }

        // Check if arg needs hoisting (rvalues that can't have & taken)
        mut needs_hoisting := false
        switch arg.node_type {
        case NodeType.Identifier(name):
            // Simple identifier is lvalue - doesn't need hoisting
            // But type-qualified calls like I64.to_str need hoisting
            if arg.params.len().eq(0) {
                needs_hoisting = false
            } else {
                // Check for UFCS placeholder "_" which is a chained expression
                if name.eq("_") {
                    // UFCS placeholder - this is a chained expression like get_foo().bar
                    // The result is an lvalue, doesn't need hoisting
                    needs_hoisting = false
                } else {
                    mut is_variable := false
                    mut is_function := false
                    mut sym := context.scope_stack.lookup_symbol(name)
                    is_variable = true
                    catch (err: KeyNotFoundError) {
                        // Not a variable
                    }
                    mut fd := context.scope_stack.lookup_func(name)
                    is_function = true
                    catch (err: KeyNotFoundError) {
                        // Not a function
                    }
                    // Only needs hoisting if it's not a variable AND not a function call
                    // (i.e., it's a Type.method call like I64.to_str)
                    needs_hoisting = not(is_variable).and(not(is_function))
                }
            }
        case NodeType.LLiteral(lit):
            switch lit {
            case Literal.Str(s):
                needs_hoisting = false  // Compound literal, can use &
            case Literal.Number(n):
                needs_hoisting = true   // Can't take address of integer literal
            case:
                needs_hoisting = false
            }
        case NodeType.FCall:
            // Check if this is an enum or struct constructor - those become compound literals
            // which CAN have & taken directly, so no hoisting needed
            if arg.params.len().gt(0) {
                mut first := Expr()
                arg.params.get(0, first)
                mut combined := get_combined_name(context.path, first)
                needs_hoisting = not(context.scope_stack.is_type_constructor(combined))
            } else {
                needs_hoisting = true
            }
        case:
            needs_hoisting = false
        }

        if not(needs_hoisting) {
            continue
        }

        // Determine the C type based on what the arg evaluates to
        mut c_type := ""
        switch arg.node_type {
        case NodeType.LLiteral(lit):
            switch lit {
            case Literal.Number(n):
                c_type = concat(TIL_PREFIX, "I64")
            case:
                throw arg.lang_error(context.path, "ccodegen", "Cannot determine type for ref param hoisting")
            }
        case NodeType.FCall:
            // Look up the function's return type
            mut fd := get_fcall_func_def(context, arg)
            if fd.return_types.len().gt(0) {
                mut ret_type := ValueType.TCustom("")
                fd.return_types.get(0, ret_type)
                c_type = til_type_to_c(ret_type)
            } else {
                throw arg.lang_error(context.path, "ccodegen", "Function has no return type")
            }
            catch (err: KeyNotFoundError) {
                throw arg.lang_error(context.path, "ccodegen", "Cannot find function definition")
            }
        case NodeType.Identifier(var_name):
            if arg.params.len().gt(0) {
                // Type-qualified method call like I64.to_str(x) - look up return type
                mut first_param := Expr()
                arg.params.get(0, first_param)
                switch first_param.node_type {
                case NodeType.Identifier(method_name):
                    // Look up Type.method function
                    func_name := var_name.concat(".").concat(method_name)
                    mut fd := context.scope_stack.lookup_func(func_name)
                    if fd.return_types.len().gt(0) {
                        mut ret_type := ValueType.TCustom("")
                        fd.return_types.get(0, ret_type)
                        c_type = til_type_to_c(ret_type)
                    } else {
                        throw arg.lang_error(context.path, "ccodegen", concat("Function ", func_name).concat(" has no return type"))
                    }
                    catch (err: KeyNotFoundError) {
                        throw arg.lang_error(context.path, "ccodegen", concat("Cannot find function: ", func_name))
                    }
                case:
                    throw arg.lang_error(context.path, "ccodegen", "Expected method name")
                }
            } else {
                throw arg.lang_error(context.path, "ccodegen", "Cannot determine type for ref param hoisting")
            }
        case:
            throw arg.lang_error(context.path, "ccodegen", "Cannot determine type for ref param hoisting")
        }

        mut temp_var := next_mangled(ctx)

        // Emit: Type _tmpXX = <expression>;
        output = output.concat(indent_str)
        output = output.concat(c_type)
        output = output.concat(" ")
        output = output.concat(temp_var)
        output = output.concat(" = ")
        output = emit_expr(arg, output, 0, ctx, context)
        output = output.concat(";\n")

        mut hoisted_arg := HoistedArg()
        hoisted_arg.index = idx
        hoisted_arg.temp_var = temp_var
        hoisted.push(hoisted_arg)
    }

    catch (err: IndexOutOfBoundsError) { throw err.msg }
    catch (err: AllocError) { throw err.msg }

    return hoisted
}

// Forward declaration - get_fcall_func_name is defined later in the file
// Hoist a single expression if it's a throwing call, or recursively hoist throwing calls within it.
// Returns the output string with hoisted code prepended.
hoist_throwing_expr := proc(expr: Expr, output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) returns Str throws Str {
    mut result := output
    mut indent_str := ""
    for _ in 0..indent {
        indent_str = indent_str.concat("    ")
    }

    // Check if this expression itself is a throwing call
    switch expr.node_type {
    case NodeType.FCall:
        mut got_throwing_fd := false
        mut throwing_fd := SFuncDef()
        throwing_fd = get_fcall_func_def(context, expr)
        got_throwing_fd = true
        catch (err: KeyNotFoundError) { /* REM None case in Rust */ }
        if got_throwing_fd {
        if throwing_fd.throw_types.len().gt(0) {
        // First, recursively hoist any throwing calls in this call's arguments
        mut nested_hoisted := Map.new(I64, Str)
        if expr.params.len().gt(1) {
            mut nested_vec := hoist_throwing_args(expr.params, result, indent, ctx, context)
            // Convert Vec<HoistedArg> to Map<I64, Str>
            for nhi in 0..nested_vec.len() {
                mut entry := HoistedArg()
                nested_vec.get(nhi, entry)
                nested_hoisted.set(entry.index, entry.temp_var)
                catch (err: IndexOutOfBoundsError) { throw err.msg }
            }
            catch (err: AllocError) { throw err.msg }
        }

        mut temp_var := next_mangled(ctx)

        // Determine the C type for the temp variable
        mut c_type := ""
        if throwing_fd.return_types.len().gt(0) {
            mut first_ret := ValueType.TCustom("")
            throwing_fd.return_types.get(0, first_ret)
            c_type = til_type_to_c(first_ret)
            catch (err: IndexOutOfBoundsError) { throw err.msg }
        } else {
            throw "Cannot hoist throwing call with no return type"
        }

        // Declare temp variable
        result = result.concat(indent_str).concat(c_type).concat(" ").concat(temp_var).concat(";\n")

        // Declare error variables for each throw type
        mut temp_suffix := next_mangled(ctx)
        for err_idx in 0..throwing_fd.throw_types.len() {
            mut throw_type := ValueType.TCustom("")
            throwing_fd.throw_types.get(err_idx, throw_type)
            switch throw_type {
            case ValueType.TCustom(type_name):
                result = result.concat(indent_str).concat(til_name(type_name)).concat(" _err").concat(err_idx.to_str()).concat("_").concat(temp_suffix).concat(";\n")
            case:
            }
            catch (err: IndexOutOfBoundsError) { throw err.msg }
        }

        // Detect and construct variadic array if needed
        mut variadic_arr_var := ""
        mut has_variadic_arr := false
        mut variadic_info := detect_variadic_fcall(expr, ctx)
        mut variadic_args := Vec.new(Expr)
        for vi in variadic_info.regular_count.add(1)..expr.params.len() {
            mut varg := Expr()
            expr.params.get(vi, varg)
            variadic_args.push(varg)
            catch (err: IndexOutOfBoundsError) { throw err.msg }
            catch (err: AllocError) { throw err.msg }
        }
        variadic_arr_var = hoist_variadic_args(variadic_info.elem_type, variadic_args, nested_hoisted, variadic_info.regular_count, result, indent, ctx, context)
        has_variadic_arr = true
        catch (err: KeyNotFoundError) {
            // Not a variadic function - use defaults
        }

        // Emit the function call with output pointers
        result = result.concat(indent_str).concat("int _status_").concat(temp_suffix).concat(" = ")

        // Emit the function name and args (using nested hoisted temps)
        result = emit_fcall_name_and_args_for_throwing(expr, temp_var, temp_suffix, throwing_fd.throw_types, nested_hoisted, variadic_arr_var, result, ctx, context)

        result = result.concat(";\n")

        // Emit error checking - propagate or goto based on context
        // Check if there are local catch labels for these error types
        mut has_local_catch := false
        for throw_check_idx in 0..throwing_fd.throw_types.len() {
            mut throw_type := ValueType.TCustom("")
            throwing_fd.throw_types.get(throw_check_idx, throw_type)
            switch throw_type {
            case ValueType.TCustom(type_name):
                if ctx.local_catch_labels.contains_key(type_name) {
                    has_local_catch = true
                    break
                }
            case:
            }
            catch (err: IndexOutOfBoundsError) { throw err.msg }
        }

        if has_local_catch {
            // Use goto for local catches
            for err_idx in 0..throwing_fd.throw_types.len() {
                mut throw_type := ValueType.TCustom("")
                throwing_fd.throw_types.get(err_idx, throw_type)
                switch throw_type {
                case ValueType.TCustom(type_name):
                    if ctx.local_catch_labels.contains_key(type_name) {
                        mut catch_info := CatchLabelInfo()
                        ctx.local_catch_labels.get(type_name, catch_info)
                        result = result.concat(indent_str).concat("if (_status_").concat(temp_suffix).concat(" == ").concat(err_idx.add(1).to_str()).concat(") { ")
                        result = result.concat(catch_info.temp_var).concat(" = _err").concat(err_idx.to_str()).concat("_").concat(temp_suffix)
                        result = result.concat("; goto ").concat(catch_info.label).concat("; }\n")
                    }
                case:
                }
                catch (err: IndexOutOfBoundsError) { throw err.msg }
                catch (err: KeyNotFoundError) { throw err.msg }
            }
        } else {
            // Propagate errors
            result = result.concat(indent_str).concat("if (_status_").concat(temp_suffix).concat(" != 0) {\n")

            for err_idx in 0..throwing_fd.throw_types.len() {
                mut throw_type := ValueType.TCustom("")
                throwing_fd.throw_types.get(err_idx, throw_type)
                switch throw_type {
                case ValueType.TCustom(type_name):
                    for curr_idx in 0..ctx.current_throw_types.len() {
                        mut curr_throw := ValueType.TCustom("")
                        ctx.current_throw_types.get(curr_idx, curr_throw)
                        switch curr_throw {
                        case ValueType.TCustom(curr_type_name):
                            if curr_type_name.eq(type_name) {
                                result = result.concat(indent_str).concat("    if (_status_").concat(temp_suffix).concat(" == ").concat(err_idx.add(1).to_str())
                                result = result.concat(") { *_err").concat(curr_idx.add(1).to_str()).concat(" = _err").concat(err_idx.to_str()).concat("_").concat(temp_suffix)
                                result = result.concat("; return ").concat(curr_idx.add(1).to_str()).concat("; }\n")
                                break
                            }
                        case:
                        }
                        catch (err: IndexOutOfBoundsError) { throw err.msg }
                    }
                case:
                }
                catch (err: IndexOutOfBoundsError) { throw err.msg }
            }

            result = result.concat(indent_str).concat("}\n")
        }

        // Emit Array.delete if variadic array was constructed
        if has_variadic_arr {
            result = result.concat(indent_str).concat(TIL_PREFIX).concat("Array_delete(&").concat(variadic_arr_var).concat(");\n")
        }

        // Record in hoisted_exprs map using expression address
        expr_addr := to_ptr(expr)
        ctx.hoisted_exprs.set(expr_addr, temp_var)

        return result
    }}
    case:
    }

    // Check if this is a non-throwing variadic call - also needs hoisting
    mut handled_variadic := false
    switch expr.node_type {
    case NodeType.FCall:
        mut variadic_info := detect_variadic_fcall(expr, ctx)
        // Recursively hoist any throwing/variadic calls in this call's arguments first
        mut nested_hoisted := Map.new(I64, Str)
        if expr.params.len().gt(1) {
            mut nested_vec := hoist_throwing_args(expr.params, result, indent, ctx, context)
            // Convert Vec<HoistedArg> to Map<I64, Str>
            for nhi in 0..nested_vec.len() {
                mut entry := HoistedArg()
                nested_vec.get(nhi, entry)
                nested_hoisted.set(entry.index, entry.temp_var)
                catch (err: IndexOutOfBoundsError) { throw err.msg }
                catch (err: AllocError) { throw err.msg }
            }
        }

        mut temp_var := next_mangled(ctx)

        // Determine return type from function - use if true block to scope catch
        mut func_name := get_fcall_func_name(expr)
        if true {
            mut fd := get_fcall_func_def(context, expr)
            catch (err: KeyNotFoundError) {
                throw expr.lang_error(context.path, "ccodegen", concat("Function not found: ", func_name))
            }
        if fd.return_types.len().eq(0) {
            throw expr.lang_error(context.path, "ccodegen", "Function has no return type")
        }
        mut ret_type := ValueType.TCustom("")
        fd.return_types.get(0, ret_type)
        catch (err: IndexOutOfBoundsError) { throw err.msg }
        mut c_type := til_type_to_c(ret_type)

        // Declare temp variable
        result = result.concat(indent_str).concat(c_type).concat(" ").concat(temp_var).concat(";\n")

        // Construct variadic array
        mut variadic_args := Vec.new(Expr)
        for vi in variadic_info.regular_count.add(1)..expr.params.len() {
            mut varg := Expr()
            expr.params.get(vi, varg)
            variadic_args.push(varg)
            catch (err: IndexOutOfBoundsError) { throw err.msg }
            catch (err: AllocError) { throw err.msg }
        }
        mut variadic_arr_var := hoist_variadic_args(variadic_info.elem_type, variadic_args, nested_hoisted, variadic_info.regular_count, result, indent, ctx, context)

        // Emit the function call (non-throwing, so direct assignment)
        result = result.concat(indent_str).concat(temp_var).concat(" = ")

        // Emit function name
        if func_name.len().gt(0) {
            result = result.concat(til_func_name(func_name))
        }
        result = result.concat("(")

        // Emit regular args (using nested hoisted temps)
        mut first := true
        for ri in 0..variadic_info.regular_count {
            if not(first) {
                result = result.concat(", ")
            }
            first = false
            // Check if this arg was hoisted
            mut found_hoisted := false
            mut hoisted_temp := ""
            nested_hoisted.get(ri, hoisted_temp)
            found_hoisted = true
            catch (err: KeyNotFoundError) { }
            if found_hoisted {
                result = result.concat(hoisted_temp)
            } else {
                mut param := Expr()
                expr.params.get(ri.add(1), param)
                catch (err: IndexOutOfBoundsError) { throw err.msg }
                result = emit_expr(param, result, 0, ctx, context)
            }
        }

        // Emit variadic array pointer
        if not(first) {
            result = result.concat(", ")
        }
        result = result.concat("&").concat(variadic_arr_var).concat(");\n")

        // Emit Array.delete for the variadic array
        result = result.concat(indent_str).concat(TIL_PREFIX).concat("Array_delete(&").concat(variadic_arr_var).concat(");\n")

        // Record in hoisted_exprs map using expression address
        expr_addr := to_ptr(expr)
        ctx.hoisted_exprs.set(expr_addr, temp_var)

        handled_variadic = true
        } // end if true (scopes get_fcall_func_def catch)
        catch (err: KeyNotFoundError) { /* REM None case in Rust - not variadic */ }
    case:
    }
    if handled_variadic {
        return result
    }

    // Check if this is a struct literal with throwing default values
    switch expr.node_type {
    case NodeType.FCall:
        if expr.params.len().gt(0) {
            mut first_param := Expr()
            expr.params.get(0, first_param)
            catch (err: IndexOutOfBoundsError) { throw err.msg }
            mut got_func_name := false
            mut func_name := ""
            func_name = get_func_name_string(first_param)
            got_func_name = true
            catch (err: KeyNotFoundError) { /* REM None case in Rust */ }
            if got_func_name {
                mut has_named_args := false
                for nai in 1..expr.params.len() {
                    mut narg := Expr()
                    expr.params.get(nai, narg)
                    switch narg.node_type {
                    case NodeType.NamedArg(field_name):
                        has_named_args = true
                    case:
                    }
                    catch (err: IndexOutOfBoundsError) { throw err.msg }
                }

                // Check if func_name is a known struct
                mut found_struct := false
                mut struct_def := context.scope_stack.lookup_struct(func_name)
                found_struct = true
                catch (err: KeyNotFoundError) { }

                if found_struct {
                    if or(eq(expr.params.len(), 1), has_named_args) {
                        // This is a struct literal - check for throwing defaults
                        // Build set of named arg names that are provided
                        mut provided_names := Set.new(Str)
                        for pai in 1..expr.params.len() {
                            mut parg := Expr()
                            expr.params.get(pai, parg)
                            switch parg.node_type {
                            case NodeType.NamedArg(field_name):
                                _ := provided_names.insert(field_name)
                                catch (err: AllocError) { throw err.msg }
                            case:
                            }
                            catch (err: IndexOutOfBoundsError) { throw err.msg }
                        }

                        // For each member with a default that's a throwing call and not overridden, emit temp var
                        for member: Declaration in struct_def.members {
                            if not(member.is_mut) {
                                continue
                            }
                            if provided_names.contains(member.name) {
                                continue  // Named arg overrides default
                            }
                            // Check if there's a default value for this member
                            mut default_expr := Expr()
                            mut has_default := false
                            struct_def.default_values.get(member.name, default_expr)
                            has_default = true
                            catch (err: KeyNotFoundError) { }

                            if has_default {
                                if is_throwing_fcall(default_expr, context) {
                                    // Get the function's throw types
                                    mut throw_types := Vec.new(ValueType)
                                    if default_expr.params.len().gt(0) {
                                        mut def_first := Expr()
                                        default_expr.params.get(0, def_first)
                                        catch (err: IndexOutOfBoundsError) { throw err.msg }
                                        mut lookup_name := get_til_func_name_string(def_first)
                                        mut fd := context.scope_stack.lookup_func(lookup_name)
                                        throw_types = fd.throw_types
                                        catch (err: KeyNotFoundError) { /* REM None case in Rust */ }
                                    }

                                    mut temp_name := next_mangled(ctx)
                                    // Emit the function call with out-param using emit_throwing_call_propagate
                                    result = emit_throwing_call_propagate(default_expr, throw_types, temp_name, "", result, indent, ctx, context)
                                    // Record that this struct default was hoisted using "struct_name:member_name" key
                                    mut key := func_name.concat(":").concat(member.name)
                                    ctx.hoisted_struct_defaults.set(key, temp_name)
                                    catch (err: AllocError) { throw err.msg }
                                } else {
                                    // Also recursively hoist any throwing expressions in the default
                                    // This handles nested struct constructors with throwing defaults
                                    result = hoist_throwing_expr(default_expr, result, indent, ctx, context)
                                }
                            }
                        }
                    }
                }
            }
        }
    case:
    }

    // Not a throwing call or variadic call - recursively check sub-expressions
    // For FCall, check all arguments
    switch expr.node_type {
    case NodeType.FCall:
        if expr.params.len().gt(1) {
            for arg_idx in 1..expr.params.len() {
                mut arg := Expr()
                expr.params.get(arg_idx, arg)
                catch (err: IndexOutOfBoundsError) { throw err.msg }
                result = hoist_throwing_expr(arg, result, indent, ctx, context)
            }
        }
    case:
        // For other expression types with sub-expressions, recurse into them
        for param_idx in 0..expr.params.len() {
            mut param := Expr()
            expr.params.get(param_idx, param)
            catch (err: IndexOutOfBoundsError) { throw err.msg }
            result = hoist_throwing_expr(param, result, indent, ctx, context)
        }
    }

    catch (err: AllocError) { throw err.msg }
    return result
}

// Collect function info (throw types, return types, names) from AST into context
// Handles both top-level functions and struct methods
collect_func_info := proc(expr: Expr, mut ctx: CodegenContext) throws Str {
    switch expr.node_type {
    case NodeType.Declaration(decl):
        if expr.params.len().gt(0) {
            mut first := Expr()
            expr.params.get(0, first)
            switch first.node_type {
            case NodeType.FuncDef(func_def):
                // Top-level function - check for variadic args (TMulti)
                for idx in 0..func_def.args.len() {
                    mut arg := Declaration()
                    func_def.args.get(idx, arg)
                    switch arg.value_type {
                    case ValueType.TMulti(elem_type):
                        mut info := VariadicParamInfo()
                        info.elem_type = elem_type
                        info.regular_count = idx
                        ctx.func_variadic_args.set(decl.name, info)
                        break  // Only one variadic arg per function
                    case:
                    }
                }
                catch (err: IndexOutOfBoundsError) { throw err.msg }
            case NodeType.StructDef(struct_def):
                // Struct methods - use mangled names (StructName_methodName)
                struct_name := decl.name
                for member_name: Str in struct_def.default_values.keys {
                    mut default_expr := Expr()
                    struct_def.default_values.get(member_name, default_expr)
                    switch default_expr.node_type {
                    case NodeType.FuncDef(func_def):
                        mangled_name := format(struct_name, "_", member_name)
                        // Check for variadic args (TMulti) in struct methods
                        for idx in 0..func_def.args.len() {
                            mut arg := Declaration()
                            func_def.args.get(idx, arg)
                            switch arg.value_type {
                            case ValueType.TMulti(elem_type):
                                mut info := VariadicParamInfo()
                                info.elem_type = elem_type
                                info.regular_count = idx
                                ctx.func_variadic_args.set(mangled_name, info)
                                break
                            case:
                            }
                        }
                        catch (err: IndexOutOfBoundsError) { throw err.msg }
                    case:
                    }
                    catch (err: KeyNotFoundError) { /* skip */ }
                    catch (err: AllocError) { throw err.msg }
                    catch (err: I64_OverflowError) { throw err.msg }
                }
            case:
            }
            catch (err: IndexOutOfBoundsError) { throw err.msg }
            catch (err: AllocError) { throw err.msg }
        }
    case:
    }
}

// Collect nested function info (for hoisting): scan function bodies for nested FuncDef declarations
// This populates ctx.nested_func_names and ctx.hoisted_prototypes before we emit function bodies
collect_nested_func_info := proc(expr: Expr, mut ctx: CodegenContext, parent_func_name: Str) throws Str {
    switch expr.node_type {
    case NodeType.Declaration(decl):
        if expr.params.len().gt(0) {
            mut first := Expr()
            expr.params.get(0, first)
            switch first.node_type {
            case NodeType.FuncDef(func_def):
                if parent_func_name.len().gt(0) {
                    // This is a nested function - record it for hoisting
                    mangled_name := parent_func_name.concat("_").concat(decl.name)
                    ctx.nested_func_names.set(decl.name, mangled_name)

                    // Generate prototype using emit_func_signature (handles throwing functions properly)
                    mut proto := ""
                    proto = emit_func_signature(til_name(mangled_name), func_def, proto)
                    proto = proto.concat(";\n")
                    ctx.hoisted_prototypes.push(proto)

                    // Recursively check for nested functions within this nested function
                    for body_expr: Expr in func_def.body {
                        collect_nested_func_info(body_expr, ctx, mangled_name)
                    }
                } else {
                    // Top-level function - scan its body for nested functions
                    for body_expr: Expr in func_def.body {
                        collect_nested_func_info(body_expr, ctx, decl.name)
                    }
                }
            case:
                // Not a FuncDef
            }
        }
    case NodeType.Body:
        for child: Expr in expr.params {
            collect_nested_func_info(child, ctx, parent_func_name)
        }
    case NodeType.If:
        // Check then and else branches (params[0] = condition, params[1] = then, params[2] = else)
        if expr.params.len().gt(1) {
            mut then_branch := Expr()
            expr.params.get(1, then_branch)
            collect_nested_func_info(then_branch, ctx, parent_func_name)
        }
        if expr.params.len().gt(2) {
            mut else_branch := Expr()
            expr.params.get(2, else_branch)
            collect_nested_func_info(else_branch, ctx, parent_func_name)
        }
    case NodeType.While:
        // Check loop body (params[0] = condition, params[1] = body)
        if expr.params.len().gt(1) {
            mut body := Expr()
            expr.params.get(1, body)
            collect_nested_func_info(body, ctx, parent_func_name)
        }
    case NodeType.Switch:
        // Check all case bodies (params[0] = value, rest are cases)
        for i in 1..expr.params.len() {
            mut case_expr := Expr()
            expr.params.get(i, case_expr)
            collect_nested_func_info(case_expr, ctx, parent_func_name)
        }
    case:
        // Recursively check all params for other node types
        for child: Expr in expr.params {
            collect_nested_func_info(child, ctx, parent_func_name)
        }
    }

    catch (err: IndexOutOfBoundsError) { throw err.msg }
    catch (err: AllocError) { throw err.msg }
}

// Emit function signature (used by both prototype and definition)
// For throwing functions:
//   int func_name(RetType* _ret, Error1* _err1, Error2* _err2, args...)
// For non-throwing:
//   RetType func_name(args...)
emit_func_signature := proc(func_name: Str, func_def: SFuncDef, output: Str) returns Str throws Str {
    mut result := output
    mut is_throwing := func_def.throw_types.len().gt(0)

    if is_throwing {
        // Throwing function returns int status code
        result = result.concat("int ")
    } else {
        // Non-throwing function returns its actual type
        if func_def.return_types.len().eq(0) {
            result = result.concat("void ")
        } else {
            mut ret_type := ValueType.TCustom("")
            func_def.return_types.get(0, ret_type)
            result = result.concat(til_type_to_c(ret_type))
            result = result.concat(" ")
            catch (err: IndexOutOfBoundsError) { throw err.msg }
        }
    }

    result = result.concat(func_name).concat("(")

    mut param_count := 0

    if is_throwing {
        // Output params first: return value pointer, then error pointers
        if func_def.return_types.len().gt(0) {
            mut ret_type := ValueType.TCustom("")
            func_def.return_types.get(0, ret_type)
            result = result.concat(til_type_to_c(ret_type)).concat("* _ret")
            param_count = param_count.add(1)
            catch (err: IndexOutOfBoundsError) { throw err.msg }
        }

        // Error output parameters
        for i in 0..func_def.throw_types.len() {
            if param_count.gt(0) {
                result = result.concat(", ")
            }
            mut throw_type := ValueType.TCustom("")
            func_def.throw_types.get(i, throw_type)
            mut err_type := value_type_to_c_name(throw_type)
            result = result.concat(err_type).concat("* _err").concat(i.add(1).to_str())
            param_count = param_count.add(1)
            catch (err: IndexOutOfBoundsError) { throw err.msg }
        }
    }

    // Input parameters
    for j in 0..func_def.args.len() {
        if param_count.gt(0) {
            result = result.concat(", ")
        }
        mut arg := Declaration()
        func_def.args.get(j, arg)

        // Check for variadic arg (TMulti)
        switch arg.value_type {
        case ValueType.TMulti(elem_type_name):
            // Variadic args are passed as til_Array*
            result = result.concat(TIL_PREFIX).concat("Array* ").concat(TIL_PREFIX).concat(arg.name)
            param_count = param_count.add(1)
            // Variadic must be last, so break
            break
        case:
            // Regular arg - Bug #60: use param_needs_by_ref for pointer determination
            // Type is already const char*, don't add extra indirection
            mut is_type_param := false
            switch arg.value_type {
            case ValueType.TCustom(name):
                if name.eq("Type") {
                    is_type_param = true
                }
            case:
            }
            if arg.is_mut {
                // mut: pass by pointer so mutations are visible to caller
                result = result.concat(til_type_to_c(arg.value_type)).concat("* ")
            } else if arg.is_own {
                // own: pass by pointer, caller transfers ownership
                // Type is already a pointer, so pass by value
                if is_type_param {
                    result = result.concat(til_type_to_c(arg.value_type)).concat(" ")
                } else {
                    result = result.concat(til_type_to_c(arg.value_type)).concat("* ")
                }
            } else if arg.is_copy {
                // copy: pass by value, caller's copy is made
                result = result.concat(til_type_to_c(arg.value_type)).concat(" ")
            } else {
                // const (default): pass by const pointer, read-only
                // Type is already a pointer, so pass by value
                if is_type_param {
                    result = result.concat(til_type_to_c(arg.value_type)).concat(" ")
                } else {
                    result = result.concat("const ").concat(til_type_to_c(arg.value_type)).concat("* ")
                }
            }
            result = result.concat(TIL_PREFIX).concat(arg.name)
            param_count = param_count.add(1)
        }
        catch (err: IndexOutOfBoundsError) { throw err.msg }
    }

    if param_count.eq(0) {
        result = result.concat("void")
    }

    result = result.concat(")")
    return result
}

/// Get the function name from an FCall expression (returns underscore format like Type_method)
/// This returns the name WITHOUT til_ prefix.
/// For C output, use til_name() on the result.
get_fcall_func_name := func(expr: Expr) returns Str throws Str {
    if expr.params.len().eq(0) {
        return ""
    }

    mut first := Expr()
    expr.params.get(0, first)

    // get_func_name_string already handles both nested identifiers (Type.method)
    // and precomp'd strings ("Type.method"), returning "Type_method" format
    mut result := get_func_name_string(first)

    catch (err: IndexOutOfBoundsError) { throw err.msg }
    catch (err: KeyNotFoundError) { return "" }

    return result
}

// Check if an expression is a struct declaration (Name := struct {...})
is_struct_declaration := func(expr: Expr) returns Bool {
    switch expr.node_type {
    case NodeType.Declaration(_):
        if not(expr.params.is_empty()) {
            mut first := Expr()
            expr.params.get(0, first)
            catch (err: IndexOutOfBoundsError) { return false }
            switch first.node_type {
            case NodeType.StructDef(_):
                return true
            case:
            }
        }
    case:
    }
    return false
}

// Get struct name from a struct declaration expression
// Returns empty string if not a struct declaration
get_struct_name := func(expr: Expr) returns Str {
    switch expr.node_type {
    case NodeType.Declaration(decl):
        if expr.params.len().gt(0) {
            mut first := Expr()
            expr.params.get(0, first)
            catch (err: IndexOutOfBoundsError) { return "" }
            switch first.node_type {
            case NodeType.StructDef(_):
                return decl.name
            case:
                return ""
            }
        }
        return ""
    case:
        return ""
    }
}

// Extract struct field type dependencies for topological sorting
// Returns the type name if it's a custom type that needs to be defined first
// Returns empty string if no dependency (like Rust's Option::None)
get_field_type_dependency := func(value_type: ValueType) returns Str {
    switch value_type {
    case ValueType.TCustom(name):
        // I64 and U8 are primitives, not struct dependencies
        if name.eq("I64").or(name.eq("U8")).or(name.eq(INFER_TYPE)) {
            return ""
        }
        return name
    case:
        return ""
    }
}

// Get struct dependencies (other struct types used as fields)
get_struct_dependencies := func(expr: Expr) returns Vec throws Str {
    mut deps := Vec.new(Str)
    switch expr.node_type {
    case NodeType.Declaration(decl):
        if expr.params.len().gt(0) {
            mut first := Expr()
            expr.params.get(0, first)
            catch (err: IndexOutOfBoundsError) { return deps }
            switch first.node_type {
            case NodeType.StructDef(struct_def):
                for member: Declaration in struct_def.members {
                    if member.is_mut {
                        dep := get_field_type_dependency(member.value_type)
                        if dep.len().gt(0) {
                            deps.push(dep)
                        }
                    }
                }
            case:
            }
        }
    case:
    }
    catch (err: AllocError) { throw err.msg }
    return deps
}

// Get enum dependencies (types used in payloads) as a Vec
get_enum_dependencies := func(expr: Expr) returns Vec throws Str {
    mut deps := Vec.new(Str)
    switch expr.node_type {
    case NodeType.Declaration(_):
        if expr.params.len().gt(0) {
            mut first := Expr()
            expr.params.get(0, first)
            catch (err: IndexOutOfBoundsError) { return deps }
            switch first.node_type {
            case NodeType.EnumDef(enum_def):
                for ev: EnumVariant in enum_def.variants {
                    payload_ptr := ev.payload_type
                    // Check if payload exists (not NULL)
                    if not(NULL.eq(payload_ptr.data)) {
                        mut payload_type := ValueType.TCustom("")
                        memcpy(to_ptr(payload_type), payload_ptr.data, size_of(ValueType))
                        switch payload_type {
                        case ValueType.TCustom(type_name):
                            // Skip primitives (but NOT Str or Bool - they're structs)
                            if not(type_name.eq("I64")) {
                                if not(type_name.eq("U8")) {
                                    if not(type_name.eq("Dynamic")) {
                                        if not(type_name.eq("Type")) {
                                            deps.push(type_name)
                                        }
                                    }
                                }
                            }
                        case:
                        }
                    }
                }
            case:
            }
        }
    case:
    }
    catch (err: AllocError) { throw err.msg }
    return deps
}

// Get enum name from an enum-with-payloads declaration expression
get_enum_name := func(expr: Expr) returns Str {
    switch expr.node_type {
    case NodeType.Declaration(decl):
        if expr.params.len().gt(0) {
            mut first := Expr()
            expr.params.get(0, first)
            catch (err: IndexOutOfBoundsError) { return "" }
            switch first.node_type {
            case NodeType.EnumDef(_):
                return decl.name
            case:
                return ""
            }
        }
        return ""
    case:
        return ""
    }
}

// Get dependencies for a type (struct or enum-with-payloads)
get_type_dependencies := func(expr: Expr) returns Vec throws Str {
    if is_struct_declaration(expr) {
        return get_struct_dependencies(expr)
    }
    if is_enum_declaration(expr).and(is_enum_with_payloads(expr)) {
        return get_enum_dependencies(expr)
    }
    return Vec.new(Str)
}

// Get name for a type (struct or enum-with-payloads)
get_type_name := func(expr: Expr) returns Str {
    if is_struct_declaration(expr) {
        return get_struct_name(expr)
    }
    if is_enum_declaration(expr).and(is_enum_with_payloads(expr)) {
        return get_enum_name(expr)
    }
    return ""
}

// Topologically sort type declarations (structs and enums-with-payloads) by their dependencies
// Returns indices into the original vector in sorted order
topological_sort_types := func(types: Vec) returns Vec throws Str {
    mut result := Vec.new(I64)
    mut n := types.len()

    // Build name -> index map
    mut name_to_idx := Map.new(Str, I64)
    for idx in 0..n {
        mut expr := Expr()
        types.get(idx, expr)
        mut name := get_type_name(expr)
        if name.len().gt(0) {
            name_to_idx.set(name, idx)
        }
        catch (err: IndexOutOfBoundsError) { throw err.msg }
        catch (err: AllocError) { throw err.msg }
    }

    // Build adjacency list (dependencies)
    mut deps := Vec.new(Vec)
    for idx in 0..n {
        deps.push(Vec.new(I64))
        catch (err: AllocError) { throw err.msg }
    }
    for idx in 0..n {
        mut expr := Expr()
        types.get(idx, expr)
        mut type_deps := get_type_dependencies(expr)
        for dep_idx in 0..type_deps.len() {
            mut dep_name := ""
            type_deps.get(dep_idx, dep_name)
            if name_to_idx.contains_key(dep_name) {
                mut dep_type_idx := 0
                name_to_idx.get(dep_name, dep_type_idx)
                catch (err: KeyNotFoundError) { throw err.msg }
                if not(dep_type_idx.eq(idx)) {
                    mut idx_deps := Vec.new(I64)
                    deps.get(idx, idx_deps)
                    idx_deps.push(dep_type_idx)
                    deps.set(idx, idx_deps)
                }
            }
            catch (err: IndexOutOfBoundsError) { throw err.msg }
            catch (err: AllocError) { throw err.msg }
        }
        catch (err: IndexOutOfBoundsError) { throw err.msg }
    }

    // Kahn's algorithm for topological sort
    // If A depends on B, B must come first, so reverse the edges
    mut reverse_deps := Vec.new(Vec)
    for idx in 0..n {
        reverse_deps.push(Vec.new(I64))
        catch (err: AllocError) { throw err.msg }
    }
    for idx in 0..n {
        mut dep_list := Vec.new(I64)
        deps.get(idx, dep_list)
        for dep_idx in 0..dep_list.len() {
            mut dep := 0
            dep_list.get(dep_idx, dep)
            mut rev_deps := Vec.new(I64)
            reverse_deps.get(dep, rev_deps)
            rev_deps.push(idx)
            reverse_deps.set(dep, rev_deps)
            catch (err: IndexOutOfBoundsError) { throw err.msg }
            catch (err: AllocError) { throw err.msg }
        }
        catch (err: IndexOutOfBoundsError) { throw err.msg }
    }

    // Calculate in-degree for reversed graph
    mut in_degree := Vec.new(I64)
    for idx in 0..n {
        in_degree.push(0)
        catch (err: AllocError) { throw err.msg }
    }
    for idx in 0..n {
        mut dep_list := Vec.new(I64)
        reverse_deps.get(idx, dep_list)
        for dep_idx in 0..dep_list.len() {
            mut dep := 0
            dep_list.get(dep_idx, dep)
            mut degree := 0
            in_degree.get(dep, degree)
            in_degree.set(dep, degree.add(1))
            catch (err: IndexOutOfBoundsError) { throw err.msg }
        }
        catch (err: IndexOutOfBoundsError) { throw err.msg }
    }

    // Start with nodes that have in-degree 0
    mut queue := Vec.new(I64)
    for idx in 0..n {
        mut degree := 0
        in_degree.get(idx, degree)
        if degree.eq(0) {
            queue.push(idx)
        }
        catch (err: IndexOutOfBoundsError) { throw err.msg }
        catch (err: AllocError) { throw err.msg }
    }

    // Process queue
    while queue.len().gt(0) {
        mut current := 0
        queue.pop(current)
        result.push(current)

        // For each node that current points to, decrement in-degree
        mut edges := Vec.new(I64)
        reverse_deps.get(current, edges)
        for edge_idx in 0..edges.len() {
            mut next := 0
            edges.get(edge_idx, next)
            mut degree := 0
            in_degree.get(next, degree)
            in_degree.set(next, degree.sub(1))
            if degree.sub(1).eq(0) {
                queue.push(next)
            }
            catch (err: IndexOutOfBoundsError) { throw err.msg }
            catch (err: AllocError) { throw err.msg }
        }
        catch (err: IndexOutOfBoundsError) { throw err.msg }
        catch (err: AllocError) { throw err.msg }
    }

    // If we couldn't sort all (cycle), append remaining in original order
    if result.len().lt(n) {
        for idx in 0..n {
            mut found := false
            for i in 0..result.len() {
                mut val := 0
                result.get(i, val)
                if val.eq(idx) {
                    found = true
                }
                catch (err: IndexOutOfBoundsError) { throw err.msg }
            }
            if not(found) {
                result.push(idx)
            }
            catch (err: AllocError) { throw err.msg }
        }
    }

    return result
}

// Check if an expression is an enum declaration (Name := enum {...})
is_enum_declaration := func(expr: Expr) returns Bool {
    switch expr.node_type {
    case NodeType.Declaration(_):
        if not(expr.params.is_empty()) {
            mut first := Expr()
            expr.params.get(0, first)
            catch (err: IndexOutOfBoundsError) { return false }
            switch first.node_type {
            case NodeType.EnumDef(_):
                return true
            case:
            }
        }
    case:
    }
    return false
}

// Check if an expression is an enum declaration with payloads
is_enum_with_payloads := func(expr: Expr) returns Bool {
    switch expr.node_type {
    case NodeType.Declaration(_):
        if expr.params.len().gt(0) {
            mut first := Expr()
            expr.params.get(0, first)
            catch (err: IndexOutOfBoundsError) { return false }
            switch first.node_type {
            case NodeType.EnumDef(enum_def):
                return enum_has_payloads(enum_def)
            case:
            }
        }
    case:
    }
    return false
}

// Check if an expression is a top-level constant declaration (name := literal)
// Constants are non-mut declarations with literal values (numbers, strings, bools)
is_constant_declaration := func(expr: Expr) returns Bool {
    switch expr.node_type {
    case NodeType.Declaration(decl):
        // Must not be mutable
        if decl.is_mut {
            return false
        }
        if expr.params.len().gt(0) {
            mut first := Expr()
            expr.params.get(0, first)
            catch (err: IndexOutOfBoundsError) { return false }
            switch first.node_type {
            // Literal values are constants
            case NodeType.LLiteral(_):
                return true
            // Bool identifiers (true/false) are constants
            case NodeType.Identifier(name):
                if name.eq("true").or(name.eq("false")) {
                    return true
                }
            // Skip struct, enum, and function definitions
            case NodeType.StructDef(_):
            case NodeType.EnumDef(_):
            case NodeType.FuncDef(_):
            case:
            }
        }
    case:
    }
    return false
}

// Check if an expression is a non-constant top-level declaration that needs to be a global
// These are declarations that are NOT: functions, structs, enums, or literal constants
// but are still non-mut and could be referenced from function bodies
is_global_declaration := func(expr: Expr) returns Bool {
    switch expr.node_type {
    case NodeType.Declaration(decl):
        // Skip true/false declarations - they're handled specially
        if decl.name.eq("true").or(decl.name.eq("false")) {
            return false
        }
        if expr.params.len().gt(0) {
            mut first := Expr()
            expr.params.get(0, first)
            catch (err: IndexOutOfBoundsError) { return false }
            switch first.node_type {
            // Skip these - they have their own handling
            case NodeType.StructDef(_):
            case NodeType.EnumDef(_):
            case NodeType.FuncDef(_):
                return false
            // Skip literal constants - they're handled by is_constant_declaration
            // (only non-mut literals are constants)
            case NodeType.LLiteral(_):
                if not(decl.is_mut) {
                    return false
                }
                return true
            // Skip true/false RHS - they're handled by is_constant_declaration
            // (only non-mut booleans are constants)
            case NodeType.Identifier(name):
                if name.eq("true").or(name.eq("false")) {
                    if not(decl.is_mut) {
                        return false
                    }
                }
                // Everything else is a global
                return true
            case:
                // Everything else (function calls like EvalArena.new(), etc.) is a global
                return true
            }
        }
    case:
    }
    return false
}

// Emit a global declaration as a static variable at file scope (type only, no initializer)
// The initializer will be emitted in main()
emit_global_declaration := proc(expr: Expr, output: Str, mut ctx: CodegenContext, context: Context) returns Str throws Str {
    mut result := output
    switch expr.node_type {
    case NodeType.Declaration(decl):
        if expr.params.len().gt(0) {
            // Bug #35: Use unique name for "_" declarations to avoid C redefinition errors
            mut var_name := ""
            if decl.name.eq("_") {
                var_name = next_mangled(ctx)
            } else {
                var_name = til_name(decl.name)
            }

            mut first := Expr()
            expr.params.get(0, first)
            // Determine the type from the initializer expression using get_value_type
            mut c_type := "int"
            mut vt := get_value_type(context, first)
            c_type = til_type_to_c(vt)
            catch (err: Str) {
                // Either get_value_type or til_type_to_c failed - try decl.value_type
                c_type = til_type_to_c(decl.value_type)
                catch (err: Str) {
                    c_type = "int"
                }
            }

            // Emit static declaration (no initializer - will be set in main)
            result = result.concat("static ")
            result = result.concat(c_type)
            result = result.concat(" ")
            result = result.concat(var_name)
            result = result.concat(";\n")

            // Track that this variable has been declared globally
            _ := ctx.declared_vars.insert(var_name)
        }
    case:
        result = result
    }

    catch (err: IndexOutOfBoundsError) { throw err.msg }
    catch (err: AllocError) { throw err.msg }

    return result
}

// Emit a top-level constant declaration at file scope
emit_constant_declaration := proc(expr: Expr, output: Str, mut ctx: CodegenContext, context: Context) returns Str throws Str {
    mut result := output
    switch expr.node_type {
    case NodeType.Declaration(decl):
        if expr.params.len().gt(0) {
            // Bug #35: Use unique name for "_" declarations to avoid C redefinition errors
            mut var_name := ""
            if decl.name.eq("_") {
                var_name = next_mangled(ctx)
            } else {
                var_name = til_name(decl.name)
            }

            mut first := Expr()
            expr.params.get(0, first)
            // Handle literal constants (numbers, strings)
            switch first.node_type {
            case NodeType.LLiteral(lit):
                mut has_str := false
                _ := context.scope_stack.lookup_struct("Str")
                has_str = true
                catch (err: KeyNotFoundError) { }
                mut c_type := ""
                switch lit {
                case Literal.Number(n):
                    c_type = format(TIL_PREFIX, "I64")
                case Literal.Str(s):
                    if has_str {
                        c_type = format(TIL_PREFIX, "Str")
                    } else {
                        c_type = "const char*"
                    }
                case Literal.List(l):
                    return result // Skip list literals for now
                }
                // Non-mut declarations are constants
                if not(decl.is_mut) {
                    result = result.concat("const ")
                }
                result = result.concat(c_type)
                result = result.concat(" ")
                result = result.concat(var_name)
                result = result.concat(" = ")
                result = emit_literal(lit, result, context)
                result = result.concat(";\n")
            // Handle Bool constants (true/false identifiers)
            case NodeType.Identifier(name):
                if name.eq("true").or(name.eq("false")) {
                    if not(decl.is_mut) {
                        result = result.concat("const ")
                    }
                    result = result.concat(format(TIL_PREFIX, "Bool "))
                    result = result.concat(var_name)
                    result = result.concat(" = ")
                    // Use struct literal instead of macro (valid constant initializer)
                    if name.eq("true") {
                        result = result.concat(format("((", TIL_PREFIX, "Bool){1})"))
                    } else {
                        result = result.concat(format("((", TIL_PREFIX, "Bool){0})"))
                    }
                    result = result.concat(";\n")
                }
            case:
                result = result
            }
        }
    case:
        result = result
    }

    catch (err: I64_OverflowError) { throw err.msg }
    catch (err: IndexOutOfBoundsError) { throw err.msg }
    catch (err: AllocError) { throw err.msg }

    return result
}

// Emit a struct declaration as a C struct (only mut fields become struct fields)
// Forward declarations are emitted separately, so we use "struct Name { ... };" here
emit_struct_declaration := proc(expr: Expr, output: Str) returns Str throws Str {
    mut result := output
    switch expr.node_type {
    case NodeType.Declaration(decl):
        // Skip I64, U8, Bool, Dynamic, Type - these are primitive typedefs defined in boilerplate
        if decl.name.eq("I64").or(decl.name.eq("U8")).or(decl.name.eq("Bool")).or(decl.name.eq("Dynamic")).or(decl.name.eq("Type")) {
            return result
        }
        if expr.params.len().gt(0) {
            mut first := Expr()
            expr.params.get(0, first)
            switch first.node_type {
            case NodeType.StructDef(struct_def):
                result = result.concat("struct ").concat(til_name(decl.name)).concat(" {\n")
                for member: Declaration in struct_def.members {
                    // Only emit mut fields as struct members
                    // Skip functions and non-mut fields (constants)
                    if member.is_mut {
                        result = result.concat("    ").concat(til_type_to_c(member.value_type)).concat(" ").concat(member.name).concat(";\n")
                    }
                }
                result = result.concat("};\n\n")
                return result
            case:
                throw "emit_struct_declaration: not a struct"
            }
        }
        throw "emit_struct_declaration: empty params"
    case:
        throw "emit_struct_declaration: not a declaration"
    }
    catch (err: IndexOutOfBoundsError) { throw err.msg }
}

// Emit struct constants (non-mut, non-function fields) with mangled names: StructName_constant
// Also emits size_of constant: til_size_of_StructName = sizeof(til_StructName)
emit_struct_constants := proc(expr: Expr, output: Str, mut ctx: CodegenContext, mut context: Context) returns Str throws Str {
    mut result := output
    switch expr.node_type {
    case NodeType.Declaration(decl):
        if expr.params.len().gt(0) {
            mut first := Expr()
            expr.params.get(0, first)
            switch first.node_type {
            case NodeType.StructDef(struct_def):
                mut struct_name := til_name(decl.name)
                for member: Declaration in struct_def.members {
                    // Only emit non-mut, non-function fields as constants
                    if not(member.is_mut) {
                        mut c_type := til_type_to_c(member.value_type)
                        // Get the default value
                        mut default_val := Expr()
                        struct_def.default_values.get(member.name, default_val)
                        result = result.concat("const ").concat(c_type).concat(" ").concat(struct_name).concat("_").concat(member.name).concat(" = ")
                        result = emit_expr(default_val, result, 0, ctx, context).concat(";\n")
                        catch (err: Str) { /* til_type_to_c failed, skip */ }
                        catch (err: KeyNotFoundError) { /* member has no default value, skip */ }
                    }
                }
                // Emit size_of constant for this struct
                result = result.concat("const ").concat(TIL_PREFIX).concat("I64 ").concat(TIL_PREFIX).concat("size_of_").concat(decl.name).concat(" = sizeof(").concat(struct_name).concat(");\n")
                // Track this type for til_size_of function generation
                ctx.known_types.push(decl.name)
                return result
            case:
                return result
            }
        }
        return result
    case:
        return result
    }
    catch (err: IndexOutOfBoundsError) { throw err.msg }
    catch (err: AllocError) { throw err.msg }
}

// Emit size_of constant for an enum: til_size_of_EnumName = sizeof(til_EnumName)
emit_enum_size_of_constant := proc(expr: Expr, output: Str, mut ctx: CodegenContext) returns Str throws Str {
    mut result := output
    switch expr.node_type {
    case NodeType.Declaration(decl):
        if expr.params.len().gt(0) {
            mut first := Expr()
            expr.params.get(0, first)
            switch first.node_type {
            case NodeType.EnumDef(enum_def):
                mut enum_name := til_name(decl.name)
                result = result.concat("const ")
                result = result.concat(TIL_PREFIX)
                result = result.concat("I64 ")
                result = result.concat(TIL_PREFIX)
                result = result.concat("size_of_")
                result = result.concat(decl.name)
                result = result.concat(" = sizeof(")
                result = result.concat(enum_name)
                result = result.concat(");\n")
                // Track this type for til_size_of function generation
                ctx.known_types.push(decl.name)
            case:
            }
        }
    case:
    }

    catch (err: IndexOutOfBoundsError) { throw err.msg }
    catch (err: AllocError) { throw err.msg }

    return result
}

// Emit til_size_of function for runtime type size lookup
emit_size_of_function := proc(output: Str, ctx: CodegenContext) returns Str {
    mut result := output
    result = result.concat("\nstatic inline ").concat(TIL_PREFIX).concat("I64 ").concat(TIL_PREFIX).concat("size_of(const ").concat(TIL_PREFIX).concat("Str* type_name) {\n")

    // All known types from structs and enums
    for type_name: Str in ctx.known_types {
        result = result.concat("    if (strcmp((char*)type_name->c_string, \"").concat(type_name).concat("\") == 0) return ").concat(TIL_PREFIX).concat("size_of_").concat(type_name).concat(";\n")
    }

    result = result.concat("    fprintf(stderr, \"size_of: unknown type %s\\n\", (char*)type_name->c_string);\n").concat("    exit(1);\n}\n")

    return result
}

// Check if an enum has any payloads
enum_has_payloads := func(enum_def: SEnumDef) returns Bool {
    for ev: EnumVariant in enum_def.variants {
        if not(NULL.eq(ev.payload_type.data)) {
            return true
        }
    }
    return false
}

// Emit an enum with payloads as a tagged union
emit_enum_with_payloads := proc(enum_name: Str, enum_def: SEnumDef, output: Str) returns Str throws Str {
    mut result := output

    // 1. Emit tag enum: typedef enum { Color_Unknown = 0, ... } Color_Tag;
    result = result.concat("typedef enum {\n")
    for i in 0..enum_def.variants.len() {
        mut ev := EnumVariant()
        enum_def.variants.get(i, ev)
        result = result.concat("    ").concat(enum_name).concat("_").concat(ev.name).concat(" = ").concat(i.to_str()).concat(",\n")
    }
    catch (err: IndexOutOfBoundsError) { throw err.msg }
    result = result.concat("} ").concat(enum_name).concat("_Tag;\n\n")

    // 2. Emit payload union (only for variants that have payloads)
    // typedef union { unsigned char Green; long long Number; } Color_Payload;
    mut has_any_payload := false
    for i in 0..enum_def.variants.len() {
        mut ev := EnumVariant()
        enum_def.variants.get(i, ev)
        if not(NULL.eq(ev.payload_type.data)) {
            has_any_payload = true
        }
    }
    catch (err: IndexOutOfBoundsError) { throw err.msg }

    if has_any_payload {
        result = result.concat("typedef union {\n")
        for i in 0..enum_def.variants.len() {
            mut ev := EnumVariant()
            enum_def.variants.get(i, ev)
            if not(NULL.eq(ev.payload_type.data)) {
                mut payload_type := ValueType.TCustom("")
                memcpy(to_ptr(payload_type), ev.payload_type.data, size_of(ValueType))
                result = result.concat("    ").concat(til_type_to_c(payload_type)).concat(" ").concat(ev.name).concat(";\n")
            }
        }
        catch (err: IndexOutOfBoundsError) { throw err.msg }
        result = result.concat("} ").concat(enum_name).concat("_Payload;\n\n")
    }

    // 3. Emit wrapper struct: struct Color { Color_Tag tag; Color_Payload payload; };
    // Note: typedef is already forward-declared, so we just define the struct body
    result = result.concat("struct ").concat(enum_name).concat(" {\n")
    result = result.concat("    ").concat(enum_name).concat("_Tag tag;\n")
    if has_any_payload {
        result = result.concat("    ").concat(enum_name).concat("_Payload payload;\n")
    }
    result = result.concat("};\n\n")

    // 4. Emit constructor functions for ALL variants (including no-payload ones)
    // This ensures consistent calling convention: Color_make_Red(42), Color_make_Unknown()
    for i in 0..enum_def.variants.len() {
        mut ev := EnumVariant()
        enum_def.variants.get(i, ev)
        payload_ptr := ev.payload_type

        result = result.concat("static inline ").concat(enum_name).concat(" ").concat(enum_name).concat("_make_").concat(ev.name).concat("(")

        // Parameter for payload (if any)
        if not(NULL.eq(payload_ptr.data)) {
            mut payload_type := ValueType.TCustom("")
            memcpy(to_ptr(payload_type), payload_ptr.data, size_of(ValueType))
            result = result.concat(til_type_to_c(payload_type)).concat(" value) {\n")
        } else {
            result = result.concat("void) {\n")
        }

        // Constructor body
        result = result.concat("    ").concat(enum_name).concat(" result = { .tag = ").concat(enum_name).concat("_").concat(ev.name).concat(" };\n")

        // Set payload if present
        if not(NULL.eq(payload_ptr.data)) {
            result = result.concat("    result.payload.").concat(ev.name).concat(" = value;\n")
        }

        result = result.concat("    return result;\n}\n\n")
    }
    catch (err: IndexOutOfBoundsError) { throw err.msg }

    return result
}

// Emit an enum declaration as a C typedef enum (for simple enums without payloads)
// or as a tagged union struct (for enums with payloads)
emit_enum_declaration := proc(expr: Expr, output: Str) returns Str throws Str {
    mut result := output
    switch expr.node_type {
    case NodeType.Declaration(decl):
        if expr.params.len().gt(0) {
            mut first := Expr()
            expr.params.get(0, first)
            switch first.node_type {
            case NodeType.EnumDef(enum_def):
                mut enum_name := til_name(decl.name)

                if enum_has_payloads(enum_def) {
                    // Phase 2: Enums with payloads - tagged union
                    return emit_enum_with_payloads(enum_name, enum_def, result)
                }

                // Phase 1: Simple enum without payloads
                // typedef enum { EnumName_Variant1 = 0, ... } EnumName;
                result = result.concat("typedef enum {\n")

                for i in 0..enum_def.variants.len() {
                    mut ev := EnumVariant()
                    enum_def.variants.get(i, ev)
                    result = result.concat("    ").concat(enum_name).concat("_").concat(ev.name).concat(" = ").concat(i.to_str()).concat(",\n")
                }

                result = result.concat("} ").concat(enum_name).concat(";\n\n")

                // Generate constructor functions for consistency with payload enums
                // static inline EnumName EnumName_make_Variant(void) { return EnumName_Variant; }
                for i in 0..enum_def.variants.len() {
                    mut ev := EnumVariant()
                    enum_def.variants.get(i, ev)
                    result = result.concat("static inline ").concat(enum_name).concat(" ").concat(enum_name).concat("_make_").concat(ev.name).concat("(void) { return ").concat(enum_name).concat("_").concat(ev.name).concat("; }\n")
                }
                result = result.concat("\n")

                return result
            case:
                throw "emit_enum_declaration: not an enum"
            }
        }
        throw "emit_enum_declaration: empty params"
    case:
        throw "emit_enum_declaration: not a declaration"
    }
    catch (err: IndexOutOfBoundsError) { throw err.msg }
}

// Emit a _to_str function for an enum type
// Takes a pointer because enum_to_str(e: Dynamic) passes by reference
// For simple enums: til_Str til_EnumName_to_str(til_EnumName* e)
// For enums with payloads: til_Str til_EnumName_to_str(til_EnumName* e)
emit_enum_to_str_function := proc(enum_name: Str, enum_def: SEnumDef, output: Str) returns Str throws Str {
    mut result := output
    has_payloads := enum_has_payloads(enum_def)

    // Function signature - takes pointer since Dynamic params are passed by reference
    result = result.concat("static inline til_Str ").concat(enum_name).concat("_to_str(").concat(enum_name).concat("* e) {\n    switch(")
    if has_payloads {
        result = result.concat("e->tag")
    } else {
        result = result.concat("*e")
    }
    result = result.concat(") {\n")

    // Cases - need the original (non-prefixed) enum name for the string
    mut original_name := enum_name
    if enum_name.starts_with(TIL_PREFIX) {
        original_name = get_substr(enum_name, TIL_PREFIX.len(), enum_name.len())
    }

    for i in 0..enum_def.variants.len() {
        mut ev := EnumVariant()
        enum_def.variants.get(i, ev)
        full_name := original_name.concat(".").concat(ev.name)
        result = result.concat("        case ").concat(enum_name).concat("_").concat(ev.name).concat(": return (til_Str){(til_I64)\"").concat(full_name).concat("\", ").concat(full_name.len().to_str()).concat("};\n")
    }

    // Default case (shouldn't happen but good for safety)
    unknown_name := original_name.concat(".?")
    result = result.concat("    }\n    return (til_Str){(til_I64)\"").concat(unknown_name).concat("\", ").concat(unknown_name.len().to_str()).concat("};\n}\n\n")

    catch (err: IndexOutOfBoundsError) { throw err.msg }
    catch (err: AllocError) { throw err.msg }

    return result
}

// Emit _to_str function for an enum declaration node
emit_enum_to_str_for_declaration := proc(expr: Expr, output: Str, context: Context) returns Str throws Str {
    mut result := output
    switch expr.node_type {
    case NodeType.Declaration(decl):
        enum_name := decl.name
        enum_def := context.scope_stack.lookup_enum(enum_name)
        c_enum_name := til_name(enum_name)
        result = emit_enum_to_str_function(c_enum_name, enum_def, result)

        catch (err: KeyNotFoundError) {
            throw "emit_enum_to_str_for_declaration: enum not found in context"
        }
        return result
    case:
        throw "emit_enum_to_str_for_declaration: not an enum declaration"
    }
}

// Emit struct function prototypes with mangled names: StructName_funcname
emit_struct_func_prototypes := proc(expr: Expr, output: Str, ctx: CodegenContext) returns Str throws Str {
    mut result := output
    switch expr.node_type {
    case NodeType.Declaration(decl):
        if expr.params.len().gt(0) {
            mut first := Expr()
            expr.params.get(0, first)
            switch first.node_type {
            case NodeType.StructDef(struct_def):
                mut struct_name := til_name(decl.name)
                for member: Declaration in struct_def.members {
                    // Check if default_value is a function definition
                    mut func_expr := Expr()
                    struct_def.default_values.get(member.name, func_expr)
                    switch func_expr.node_type {
                    case NodeType.FuncDef(func_def):
                        mut mangled_name := struct_name.concat("_").concat(member.name)
                        result = emit_func_signature(mangled_name, func_def, result)
                        result = result.concat(";\n")
                    case:
                        // Not a function
                    }
                    catch (err: KeyNotFoundError) { /* member has no default value, skip */ }
                }
                return result
            case:
                return result
            }
        }
        return result
    case:
        return result
    }
    catch (err: IndexOutOfBoundsError) { throw err.msg }
}

// Emit a struct function body with mangled name: StructName_funcname
emit_struct_func_body := proc(struct_name: Str, member: Declaration, func_def: SFuncDef, output: Str, mut ctx: CodegenContext, mut context: Context) returns Str throws Str {
    // Skip external functions
    if func_def.is_ext() {
        return output
    }

    mut result := output

    // Set current function context
    ctx.current_throw_types = func_def.throw_types
    ctx.current_return_types = func_def.return_types
    // Track mut params for pointer dereference (-> vs .)
    ctx.current_ref_params = Vec.new(Str)
    // Track variadic params - they're passed as til_Array* so need dereference
    ctx.current_variadic_params = Map.new(Str, Str)
    for arg: Declaration in func_def.args {
        // Bug #60: All non-copy args are passed by pointer (mut, own, and const/default)
        if not(arg.is_copy) {
            ctx.current_ref_params.push(arg.name)
        }
        switch arg.value_type {
        case ValueType.TMulti(elem_type_name):
            // elem_type is the type name string like "Bool"
            ctx.current_variadic_params.set(arg.name, til_name(elem_type_name))
        case:
        }
        catch (err: AllocError) { throw err.msg }
    }

    // Push a new scope frame for this function (like interpreter does)
    context.scope_stack.push(ScopeType.Function)
    // Register function parameters in the frame
    for arg: Declaration in func_def.args {
        mut value_type := arg.value_type
        switch arg.value_type {
        case ValueType.TMulti(elem_type):
            value_type = ValueType.TCustom("Array")
        case:
        }
        mut sym := SymbolInfo(value_type=value_type, is_mut=arg.is_mut, is_copy=arg.is_copy, is_own=arg.is_own)
        context.scope_stack.declare_symbol(arg.name, sym)
    }

    // Clear declared_vars and hoisted_struct_defaults for new function scope
    ctx.declared_vars = Set.new(Str)
    ctx.hoisted_struct_defaults = Map.new(Str, Str)

    mangled_name := struct_name.concat("_").concat(member.name)

    // Save and set current function name for deterministic temp naming (Bug #42 fix)
    mut prev_function_name := ctx.current_function_name
    mut prev_mangling_counter := ctx.mangling_counter
    ctx.current_function_name = mangled_name
    ctx.mangling_counter = 0

    result = emit_func_signature(mangled_name, func_def, result)
    result = result.concat(" {\n")

    // Emit function body with catch pattern detection
    result = emit_stmts(func_def.body, result, 1, ctx, context)

    // For throwing void functions, add implicit return 0 at end
    if func_def.throw_types.len().gt(0) {
        if func_def.return_types.len().eq(0) {
            result = result.concat("    return 0;\n")
        }
    }

    result = result.concat("}\n\n")

    // Restore previous function name and counter
    ctx.current_function_name = prev_function_name
    ctx.mangling_counter = prev_mangling_counter

    // Pop the function scope frame
    _ := context.scope_stack.pop()

    // Clear current function context
    ctx.current_throw_types = Vec.new(ValueType)
    ctx.current_return_types = Vec.new(ValueType)
    ctx.current_ref_params = Vec.new(Str)

    catch (err: IndexOutOfBoundsError) { throw err.msg }

    return result
}

// Emit struct function bodies for all functions in a struct
emit_struct_func_bodies := proc(expr: Expr, output: Str, mut ctx: CodegenContext, mut context: Context) returns Str throws Str {
    mut result := output
    switch expr.node_type {
    case NodeType.Declaration(decl):
        if expr.params.len().gt(0) {
            mut first := Expr()
            expr.params.get(0, first)
            switch first.node_type {
            case NodeType.StructDef(struct_def):
                struct_name := til_name(decl.name)
                for member: Declaration in struct_def.members {
                    // Check if default_value is a function definition
                    mut func_expr := Expr()
                    struct_def.default_values.get(member.name, func_expr)
                    switch func_expr.node_type {
                    case NodeType.FuncDef(func_def):
                        result = emit_struct_func_body(struct_name, member, func_def, result, ctx, context)
                    case:
                    }
                    catch (err: KeyNotFoundError) { /* skip member with no default */ }
                }
                return result
            case:
                return result
            }
        }
        return result
    case:
        return result
    }
    catch (err: IndexOutOfBoundsError) { throw err.msg }
}

// Emit C code from AST (multi-pass architecture)
emit := proc(ast: Expr, mut context: Context) returns Str throws Str {
    mut output := ""
    mut ctx := CodegenContext()

    // C boilerplate
    output = output.concat("#include <stdio.h>\n#include <stdlib.h>\n#include <string.h>\n\n")
    output = format(output, "typedef unsigned char ", TIL_PREFIX, "U8;\n")
    output = format(output, "typedef long long ", TIL_PREFIX, "I64;\n")
    output = format(output, "typedef struct ", TIL_PREFIX, "Bool { ", TIL_PREFIX, "U8 data; } ", TIL_PREFIX, "Bool;\n")
    // Dynamic and Type are special placeholder types
    output = format(output, "typedef void* ", TIL_PREFIX, "Dynamic;\n")
    output = format(output, "typedef const char* ", TIL_PREFIX, "Type;\n\n")

    // Pass 0: collect function info (throw types, return types) for call-site generation
    switch ast.node_type {
    case NodeType.Body:
        for child: Expr in ast.params {
            collect_func_info(child, ctx)
        }
    case:
        // Not a body, skip collect pass
    }

    // Pass 0a: collect nested function info for hoisting (populates hoisted_prototypes and nested_func_names)
    switch ast.node_type {
    case NodeType.Body:
        for child: Expr in ast.params {
            collect_nested_func_info(child, ctx, "")
        }
    case:
        // Not a body, skip nested func collect pass
    }

    // Pass 0b: emit forward declarations for all structs and enums-with-payloads
    // (enums with payloads are implemented as structs in C, so they need forward declarations too)
    // Skip I64, U8, Bool, Dynamic, Type - these are primitive typedefs defined in boilerplate
    switch ast.node_type {
    case NodeType.Body:
        for child: Expr in ast.params {
            if is_struct_declaration(child) {
                switch child.node_type {
                case NodeType.Declaration(decl):
                    if decl.name.eq("I64").or(decl.name.eq("U8")).or(decl.name.eq("Bool")).or(decl.name.eq("Dynamic")).or(decl.name.eq("Type")) {
                        continue  // Skip - these are primitive typedefs
                    }
                    mut struct_name := til_name(decl.name)
                    output = output.concat("typedef struct ").concat(struct_name).concat(" ").concat(struct_name).concat(";\n")
                case:
                }
            }
            // Also forward-declare enums with payloads (they're implemented as structs)
            if is_enum_declaration(child).and(is_enum_with_payloads(child)) {
                switch child.node_type {
                case NodeType.Declaration(decl):
                    mut enum_name := til_name(decl.name)
                    output = output.concat("typedef struct ").concat(enum_name).concat(" ").concat(enum_name).concat(";\n")
                case:
                }
            }
        }
        output = output.concat("\n")
    case:
        // Not a body, skip forward declarations
    }

    // Pass 1a: emit simple enums (no payloads) - safe to emit early, structs may use them as fields
    switch ast.node_type {
    case NodeType.Body:
        for child: Expr in ast.params {
            if is_enum_declaration(child) {
                if not(is_enum_with_payloads(child)) {
                    output = emit_enum_declaration(child, output)
                }
            }
        }
    case:
        // Not a body, skip enum pass
    }

    // Pass 1b: emit all structs and enums-with-payloads in topologically sorted order
    // Both are "complex types" that can depend on each other
    switch ast.node_type {
    case NodeType.Body:
        // Collect all struct declarations and enums-with-payloads
        mut type_decls := Vec.new(Expr)
        for child: Expr in ast.params {
            if is_struct_declaration(child) {
                type_decls.push(child)
            } else if is_enum_declaration(child) {
                if is_enum_with_payloads(child) {
                    type_decls.push(child)
                }
            }
        }

        // Sort by dependencies and emit in order
        mut sorted_indices := topological_sort_types(type_decls)
        for idx: I64 in sorted_indices {
            mut type_expr := Expr()
            type_decls.get(idx, type_expr)
            if is_struct_declaration(type_expr) {
                output = emit_struct_declaration(type_expr, output)
            } else {
                output = emit_enum_declaration(type_expr, output)
            }
        }
        catch (err: IndexOutOfBoundsError) { throw err.msg }
        catch (err: AllocError) { throw "AllocError in type pass" }
    case:
        // Not a body, skip type pass
    }

    // Pass 2: emit function prototypes (forward declarations)
    // 2a: top-level functions
    switch ast.node_type {
    case NodeType.Body:
        for child: Expr in ast.params {
            if is_func_declaration(child) {
                output = emit_func_prototype(child, output)
            }
        }
    case:
        // Not a body, skip prototype pass
    }
    // 2b: struct functions (with mangled names)
    switch ast.node_type {
    case NodeType.Body:
        for child: Expr in ast.params {
            if is_struct_declaration(child) {
                output = emit_struct_func_prototypes(child, output, ctx)
            }
        }
    case:
        // Not a body, skip struct func prototype pass
    }
    // 2c: hoisted nested function prototypes (collected in Pass 0a)
    if ctx.hoisted_prototypes.len().gt(0) {
        output = output.concat("\n// Nested function prototypes (hoisted)\n")
        for proto: Str in ctx.hoisted_prototypes {
            output = output.concat(proto)
        }
    }
    output = output.concat("\n")

    // Pass 3: include external C interface (after structs and forward decls)
    // Use angle brackets to search only -I paths (src/), not the current directory
    // This avoids including generated c/self/ext.c instead of the hand-written src/ext.c
    output = output.concat("#include <ext.c>\n\n")

    // Pass 4: emit struct constants (non-mut, non-function fields with mangled names)
    // Also emits size_of constants for each struct
    switch ast.node_type {
    case NodeType.Body:
        for child: Expr in ast.params {
            if is_struct_declaration(child) {
                output = emit_struct_constants(child, output, ctx, context)
            }
        }
    case:
        // Not a body, skip constants pass
    }

    // Pass 4a: emit size_of constants for enums
    switch ast.node_type {
    case NodeType.Body:
        for child: Expr in ast.params {
            if is_enum_declaration(child) {
                output = emit_enum_size_of_constant(child, output, ctx)
            }
        }
    case:
        // Not a body, skip enum size_of pass
    }

    // Pass 4a2: emit enum_to_str functions for all enums
    // This must come after structs are defined (Str is needed for return type)
    switch ast.node_type {
    case NodeType.Body:
        for child: Expr in ast.params {
            if is_enum_declaration(child) {
                output = emit_enum_to_str_for_declaration(child, output, context)
            }
        }
    case:
        // Not a body, skip enum_to_str pass
    }

    // Pass 4b: emit top-level constants (non-mut declarations with literal values)
    switch ast.node_type {
    case NodeType.Body:
        for child: Expr in ast.params {
            if is_constant_declaration(child) {
                output = emit_constant_declaration(child, output, ctx, context)
            }
        }
    case:
        // Not a body, skip top-level constants pass
    }

    // Pass 4c: emit global declarations (non-constant, non-func/struct/enum declarations)
    // These need to be file-scope statics so functions can access them
    switch ast.node_type {
    case NodeType.Body:
        for child: Expr in ast.params {
            if is_global_declaration(child) {
                output = emit_global_declaration(child, output, ctx, context)
            }
        }
    case:
        // Not a body, skip global declarations pass
    }

    // Pass 4d: emit til_size_of function (runtime type size lookup)
    output = emit_size_of_function(output, ctx)

    output = output.concat("\n")

    // Pass 5: emit function definitions
    // 5a: top-level functions
    switch ast.node_type {
    case NodeType.Body:
        for child: Expr in ast.params {
            if is_func_declaration(child) {
                output = emit_func_declaration(child, output, ctx, context)
            }
        }
    case:
    }
    // 5b: struct functions (with mangled names)
    switch ast.node_type {
    case NodeType.Body:
        for child: Expr in ast.params {
            if is_struct_declaration(child) {
                output = emit_struct_func_bodies(child, output, ctx, context)
            }
        }
    case:
    }

    // 5c: hoisted nested function definitions
    // These were collected during emit_func_declaration when encountering nested functions
    // (prototypes were already emitted in Pass 2c)
    if ctx.hoisted_functions.len().gt(0) {
        output = output.concat("\n// Hoisted nested function definitions\n")
        for func_code: Str in ctx.hoisted_functions {
            output = output.concat(func_code)
        }
    }

    // Main function
    output = output.concat("int main(int argc, char** argv) {\n")

    // Clear hoisted_exprs to avoid cross-contamination from function passes
    ctx.hoisted_exprs = Map.new(I64, Str)

    // Re-populate declared_vars with global declarations (functions clear declared_vars)
    // This ensures global declarations emit only assignments in main(), not redeclarations
    switch ast.node_type {
    case NodeType.Body:
        for child: Expr in ast.params {
            if is_global_declaration(child) {
                switch child.node_type {
                case NodeType.Declaration(decl):
                    _ := ctx.declared_vars.insert(til_name(decl.name))
                    catch (err: AllocError) { throw err.msg }
                case:
                }
            }
        }
    case:
    }

    // Pass 6: emit non-struct, non-function, non-enum, non-constant statements
    // Collect them into a Vec and use emit_stmts for proper variadic/throwing call handling
    // Note: global declarations are still included but will emit only assignments since they're already declared
    switch ast.node_type {
    case NodeType.Body:
        mut main_stmts := Vec.new(Expr)
        for child: Expr in ast.params {
            // Skip true/false declarations - they're now #defines
            mut skip_true_false := false
            switch child.node_type {
            case NodeType.Declaration(decl):
                if decl.name.eq("true") { skip_true_false = true }
                if decl.name.eq("false") { skip_true_false = true }
            case:
            }
            if not(skip_true_false) {
                if not(is_func_declaration(child)) {
                    if not(is_struct_declaration(child)) {
                        if not(is_enum_declaration(child)) {
                            if not(is_constant_declaration(child)) {
                                main_stmts.push(child)
                            }
                        }
                    }
                }
            }
        }
        output = emit_stmts(main_stmts, output, 1, ctx, context)
    case:
    }

    // Call til_main() for modes that require a main proc (like cli)
    if context.mode_def.needs_main_proc {
        // Check if main has variadic args by looking up the function
        // Variadic params have ValueType.TMulti as their type
        mut main_has_variadic := false
        mut main_func := context.scope_stack.lookup_func("main")
        for arg: Declaration in main_func.args {
            switch arg.value_type {
            case ValueType.TMulti(_):
                main_has_variadic = true
            case:
            }
        }
        catch (err: KeyNotFoundError) { }

        if main_has_variadic {
            // Convert argc/argv to til_Array and pass to til_main
            // Skip argv[0] (exe path) to match interpreter behavior
            output = output.concat("    til_Array _main_args;\n")
            output = output.concat("    til_AllocError _main_args_err;\n")
            output = output.concat("    til_Array_new(&_main_args, &_main_args_err, \"Str\", &(til_I64){argc - 1});\n")
            output = output.concat("    for (int i = 1; i < argc; i++) {\n")
            output = output.concat("        til_Str _arg = {(til_I64)argv[i], strlen(argv[i])};\n")
            output = output.concat("        til_IndexOutOfBoundsError _set_err;\n")
            output = output.concat("        til_Array_set(&_set_err, &_main_args, &(til_I64){i - 1}, &_arg);\n")
            output = output.concat("    }\n")
            output = output.concat("    til_main(&_main_args);\n")
        } else {
            output = output.concat("    til_main();\n")
        }
    }

    output = output.concat("    return 0;\n}\n")

    catch (err: I64_OverflowError) { throw err.msg }
    catch (err: IndexOutOfBoundsError) { throw err.msg }
    catch (err: AllocError) { throw err.msg }

    return output
}

// Check if an expression is a function declaration (name := proc/func)
is_func_declaration := func(expr: Expr) returns Bool {
    switch expr.node_type {
    case NodeType.Declaration(_):
        if expr.params.len().gt(0) {
            mut first := Expr()
            expr.params.get(0, first)
            catch (err: IndexOutOfBoundsError) { return false }
            switch first.node_type {
            case NodeType.FuncDef(_):
                return true
            case:
            }
        }
    case:
    }
    return false
}

// Emit a function prototype (forward declaration)
emit_func_prototype := proc(expr: Expr, output: Str) returns Str throws Str {
    mut result := output
    switch expr.node_type {
    case NodeType.Declaration(decl):
        if expr.params.len().gt(0) {
            mut first := Expr()
            expr.params.get(0, first)
            catch (err: IndexOutOfBoundsError) { throw err.msg }
            switch first.node_type {
            case NodeType.FuncDef(func_def):
                // Skip external functions
                if func_def.is_ext() {
                    return output
                }

                // Use emit_func_signature for both throwing and non-throwing functions
                mut func_name := til_name(decl.name)
                result = emit_func_signature(func_name, func_def, result)
                result = result.concat(";\n")
                return result
            case:
                throw "emit_func_prototype: not a function"
            }
        }
        throw "emit_func_prototype: empty params"
    case:
        throw "emit_func_prototype: not a declaration"
    }
}

// Emit a function declaration as a C function
emit_func_declaration := proc(expr: Expr, output: Str, mut ctx: CodegenContext, mut context: Context) returns Str throws Str {
    mut result := output
    switch expr.node_type {
    case NodeType.Declaration(decl):
        if expr.params.len().gt(0) {
            mut first := Expr()
            expr.params.get(0, first)
            catch (err: IndexOutOfBoundsError) { throw err.msg }
            switch first.node_type {
            case NodeType.FuncDef(func_def):
                // Skip external functions (ext_proc/ext_func) - they're just declarations
                if func_def.is_ext() {
                    return output
                }

                // Set current function context for return/throw generation
                ctx.current_throw_types = func_def.throw_types
                ctx.current_return_types = func_def.return_types
                // Track ref params for pointer dereference (-> vs .)
                ctx.current_ref_params = Vec.new(Str)
                // Track variadic params - they're passed as til_Array* so need dereference
                ctx.current_variadic_params = Map.new(Str, Str)
                for arg: Declaration in func_def.args {
                    // Bug #60: All non-copy args are passed by pointer (mut, own, and const/default)
                    if not(arg.is_copy) {
                        ctx.current_ref_params.push(arg.name)
                    }
                    switch arg.value_type {
                    case ValueType.TMulti(elem_type):
                        // elem_type is the type name string like "Bool"
                        ctx.current_variadic_params.set(arg.name, til_name(elem_type))
                    case:
                    }
                    catch (err: AllocError) { throw err.msg }
                }

                // Push a new scope frame for this function (like interpreter does)
                context.scope_stack.push(ScopeType.Function)
                // Register function parameters in the frame
                // For variadic params (TMulti), register as Array type
                for arg: Declaration in func_def.args {
                    mut value_type := arg.value_type
                    switch arg.value_type {
                    case ValueType.TMulti(_):
                        value_type = ValueType.TCustom("Array")
                    case:
                    }
                    mut sym := SymbolInfo(value_type=value_type, is_mut=arg.is_mut, is_copy=arg.is_copy, is_own=arg.is_own)
                    context.scope_stack.declare_symbol(arg.name, sym)
                }

                // Clear declared_vars and hoisted_struct_defaults for new function scope
                ctx.declared_vars = Set.new(Str)
                ctx.hoisted_struct_defaults = Map.new(Str, Str)

                mut func_name := til_name(decl.name)

                // Save and set current function name for nested function mangling
                mut prev_function_name := ctx.current_function_name
                mut prev_mangling_counter := ctx.mangling_counter
                ctx.current_function_name = decl.name
                ctx.mangling_counter = 0  // Reset counter per-function for determinism

                result = emit_func_signature(func_name, func_def, result)
                result = result.concat(" {\n")

                // Emit function body with catch pattern detection
                result = emit_stmts(func_def.body, result, 1, ctx, context)

                // For throwing void functions, add implicit return 0 at end
                if func_def.throw_types.len().gt(0).and(func_def.return_types.len().eq(0)) {
                    result = result.concat("    return 0;\n")
                }

                result = result.concat("}\n\n")

                // Pop the function scope frame
                _ := context.scope_stack.pop()
                catch (err: IndexOutOfBoundsError) { throw err.msg }

                // Restore previous function name and counter
                ctx.current_function_name = prev_function_name
                ctx.mangling_counter = prev_mangling_counter

                // Clear current function context
                ctx.current_throw_types = Vec.new(ValueType)
                ctx.current_return_types = Vec.new(ValueType)

                return result
            case:
            }
        }
    case:
    }
    throw "emit_func_declaration: not a function declaration"
}

/// Emit an argument, using hoisted temp var if available
/// Also handles Type arguments by emitting them as string literals
emit_arg_or_hoisted := proc(arg: Expr, arg_idx: I64, hoisted: Map, output: Str, mut ctx: CodegenContext, mut context: Context) returns Str throws Str {
    mut no_param_type := ValueType.TCustom("")  // Empty TCustom represents None
    return emit_arg_with_param_type(arg, arg_idx, hoisted, no_param_type, false, false, output, ctx, context)
}

/// Emit an argument with knowledge of expected parameter type and by-ref flag
/// Handles: Type args as string literals, Dynamic args with &, by-ref args with &
emit_arg_with_param_type := proc(arg: Expr, arg_idx: I64, hoisted: Map, param_type: ValueType, has_param_type: Bool, param_by_ref: Bool, output: Str, mut ctx: CodegenContext, mut context: Context) returns Str throws Str {
    mut result := output

    // Check if hoisted temp var exists for this arg index
    mut temp_var := ""
    mut found_hoisted := false
    hoisted.get(arg_idx, temp_var)
    found_hoisted = true
    catch (err: KeyNotFoundError) { }

    if found_hoisted {
        // Hoisted temp var is an lvalue - add & if param is Dynamic or by-ref
        if param_by_ref {
            result = result.concat("&")
        } else if has_param_type {
            switch param_type {
            case ValueType.TCustom(param_type_name):
                if param_type_name.eq("Dynamic") {
                    result = result.concat("&")
                }
            case:
            }
        }
        result = result.concat(temp_var)
        return result
    }

    // Also check ctx.hoisted_exprs for expressions hoisted via other mechanisms
    arg_addr := to_ptr(arg)
    if ctx.hoisted_exprs.contains_key(arg_addr) {
        mut temp_expr := ""
        ctx.hoisted_exprs.get(arg_addr, temp_expr)
        // hoisted_exprs may store just the temp var name or with & prefix
        // Add & if param is by-ref and temp_expr doesn't already start with &
        if param_by_ref.and(not(temp_expr.starts_with("&"))) {
            result = result.concat("&")
        }
        result = result.concat(temp_expr)
        catch (err: KeyNotFoundError) { }
        return result
    }

    // Check if arg is a type identifier - emit as string literal (matches interpreter.rs)
    mut type_arg_handled := false
    {
        mut type_arg := get_type_arg_name(arg, context)
        result = result.concat("\"").concat(type_arg).concat("\"")
        type_arg_handled = true
        catch (err: KeyNotFoundError) { }
    }
    if type_arg_handled {
        return result
    }

    // Check if parameter type is Dynamic (including mut Dynamic)
    // Must check BEFORE the general mut check since Dynamic needs casting
    mut is_dynamic := false
    if has_param_type {
        switch param_type {
        case ValueType.TCustom(name):
            if name.eq("Dynamic") {
                is_dynamic = true
            }
        case:
        }
    }
    if is_dynamic {
        // Check if arg is a simple identifier (can take address directly)
        switch arg.node_type {
        case NodeType.Identifier(name):
            if arg.params.len().eq(0) {
                // Check if this identifier is already a pointer:
                // - mut params are passed as pointers
                // - variadic params are passed as Array pointers
                // - Dynamic params are already void* (no need to take address)
                mut is_already_pointer := ctx.current_ref_params.contains(name).or(Map.contains_key(ctx.current_variadic_params, name))
                // Also check if the symbol is a Dynamic-typed parameter
                if not(is_already_pointer) {
                    mut sym := context.scope_stack.lookup_symbol(name)
                    switch sym.value_type {
                    case ValueType.TCustom(type_name):
                        if type_name.eq("Dynamic") {
                            is_already_pointer = true
                        }
                    case:
                    }
                    catch (err: KeyNotFoundError) { }
                }
                if is_already_pointer {
                    // Already a pointer - just cast without &
                    result = result.concat("(").concat(TIL_PREFIX).concat("Dynamic*)").concat(til_name(name))
                } else {
                    // Simple variable - cast to til_Dynamic* (void**) and take address
                    result = result.concat("(").concat(TIL_PREFIX).concat("Dynamic*)&").concat(til_name(name))
                }
                return result
            } else {
                // Check if this is an enum constructor (Type.Variant) - need temp variable
                if arg.params.len().gt(0) {
                    mut first_param := Expr()
                    arg.params.get(0, first_param)
                    switch first_param.node_type {
                    case NodeType.Identifier(field_name):
                        combined := name.concat(".").concat(field_name)
                        if context.scope_stack.is_enum_constructor(combined) {
                            // Enum constructor passed as Dynamic - need temp variable
                            c_type := TIL_PREFIX.concat(name)
                            temp_var := next_mangled(ctx)
                            result = result.concat("(").concat(TIL_PREFIX).concat("Dynamic*)({ ")
                            result = result.concat(c_type).concat(" ").concat(temp_var).concat(" = ")
                            result = emit_expr(arg, result, 0, ctx, context)
                            result = result.concat("; &").concat(temp_var).concat("; })")
                            return result
                        }
                    case:
                    }
                    catch (err: IndexOutOfBoundsError) { }
                }
                // Field access like member.name - need (til_Dynamic*)&(var.field)
                // Check if base is a mut param (pointer) or regular variable
                mut base_is_pointer := ctx.current_ref_params.contains(name)
                result = result.concat("(").concat(TIL_PREFIX).concat("Dynamic*)&").concat(til_name(name))
                mut param_idx := 0
                for param: Expr in arg.params {
                    switch param.node_type {
                    case NodeType.Identifier(field):
                        if base_is_pointer.and(param_idx.eq(0)) {
                            result = result.concat("->")
                        } else {
                            result = result.concat(".")
                        }
                        // Field names don't get til_ prefix
                        result = result.concat(field)
                    case:
                    }
                    param_idx.inc()
                }
                return result
            }
        case:
        }
        // For non-identifier args (literals, compound literals), emit with (til_Dynamic*)&
        // But first check if already hoisted (hoisted_exprs already includes & prefix)
        dyn_arg_addr := to_ptr(arg)
        if ctx.hoisted_exprs.contains_key(dyn_arg_addr) {
            // Already hoisted with & prefix - just emit it
            result = emit_expr(arg, result, 0, ctx, context)
        } else {
            switch arg.node_type {
            case NodeType.LLiteral(_):
                // Compound literals are lvalues in C99+, so &((til_Str){...}) works
                result = result.concat("(").concat(TIL_PREFIX).concat("Dynamic*)&")
                result = emit_expr(arg, result, 0, ctx, context)
            case:
                // Other non-identifier args (function calls, etc.) - emit as-is
                result = emit_expr(arg, result, 0, ctx, context)
            }
        }
        return result
    }

    // Check if param is mut (but not Dynamic) - emit &arg for pointer
    if param_by_ref {
        switch arg.node_type {
        case NodeType.Identifier(name):
            if arg.params.len().eq(0) {
                // Check if this identifier is already a mut param (already a pointer)
                // or a variadic param (also a pointer) - don't add & again
                mut is_already_pointer := ctx.current_ref_params.contains(name).or(Map.contains_key(ctx.current_variadic_params, name))
                if is_already_pointer {
                    // Already a pointer - just emit the name
                    result = result.concat(til_name(name))
                } else {
                    // Simple variable - emit &var
                    result = result.concat("&").concat(til_name(name))
                }
                return result
            } else {
                // Check if this is a struct constant access (Type.CONSTANT) vs field access (var.field)
                if arg.params.len().gt(0) {
                    mut first_param := Expr()
                    arg.params.get(0, first_param)
                    switch first_param.node_type {
                    case NodeType.Identifier(field_name):
                        // Check if 'name' is a struct type and 'field_name' is a constant
                        mut struct_def := context.scope_stack.lookup_struct(name)
                        // Check if this field is a constant (in default_values and not mut)
                        if struct_def.default_values.contains_key(field_name) {
                            // Struct constant - emit &til_StructName_constant
                            result = result.concat("&").concat(til_name(name)).concat("_").concat(field_name)
                            return result
                        }
                        catch (err: KeyNotFoundError) { }
                        // Bug #60: Check if this is an enum constructor (Type.Variant)
                        // Enum constructors need a temp variable to take address
                        combined := name.concat(".").concat(field_name)
                        if context.scope_stack.is_enum_constructor(combined) {
                            // Get the enum type for C type name
                            c_type := TIL_PREFIX.concat(name)
                            temp_var := next_mangled(ctx)
                            result = result.concat("({ ").concat(c_type).concat(" ").concat(temp_var).concat(" = ")
                            result = emit_expr(arg, result, 0, ctx, context)
                            result = result.concat("; &").concat(temp_var).concat("; })")
                            return result
                        }
                    case:
                    }
                    catch (err: IndexOutOfBoundsError) { }
                }
                // Handle UFCS placeholder "_" - chained expression like get_foo().bar
                // The base expression (params[0]) may be an rvalue (function call), so use temp var
                if name.eq("_").and(arg.params.len().gt(0)) {
                    // Get the type of the full expression for the temp var
                    mut c_type := "int"
                    mut vt := get_value_type(context, arg)
                    c_type = til_type_to_c(vt)
                    catch (err: Str) { c_type = "int" }
                    temp_var := next_mangled(ctx)
                    result = result.concat("({ ").concat(c_type).concat(" ").concat(temp_var).concat(" = ")
                    result = emit_expr(arg, result, 0, ctx, context)
                    result = result.concat("; &").concat(temp_var).concat("; })")
                    return result
                }
                // Field access like self.type_names - need &(self->field) or &(var.field)
                // Check if base is a mut param (pointer) or regular variable
                mut base_is_pointer := ctx.current_ref_params.contains(name)
                result = result.concat("&").concat(til_name(name))
                mut i := 0
                for param: Expr in arg.params {
                    switch param.node_type {
                    case NodeType.Identifier(field):
                        if base_is_pointer.and(i.eq(0)) {
                            result = result.concat("->")
                        } else {
                            result = result.concat(".")
                        }
                        result = result.concat(field)
                    case:
                    }
                    i = i.add(1)
                }
                return result
            }
        case:
        }
        // For non-identifier args that need by-ref passing:
        // - Str literals become compound literals, can use &((til_Str){...})
        // - FCall results are rvalues, use compound literal: &(Type){fcall()}
        // - I64 literals use compound literal: &(til_I64){42}
        switch arg.node_type {
        case NodeType.LLiteral(lit):
            switch lit {
            case Literal.Str(_):
                result = result.concat("&")
                result = emit_expr(arg, result, 0, ctx, context)
            case:
                result = emit_expr(arg, result, 0, ctx, context)
            }
        case NodeType.FCall:
            // Bug #60: FCall result is rvalue, can't take address directly
            // For simple typedefs like I64: use compound literal &(Type){fcall()}
            // For struct types: use GCC statement expression ({ Type _t = fcall(); &_t; })
            mut got_fd := false
            mut fd := SFuncDef()
            fd = get_fcall_func_def(context, arg)
            got_fd = true
            catch (err: KeyNotFoundError) { /* REM None case in Rust */ }
            if got_fd {
                if fd.return_types.len().gt(0) {
                    mut ret_type := ValueType.TCustom("")
                    fd.return_types.get(0, ret_type)
                    c_type := til_type_to_c(ret_type)
                    // Check if return type is a simple typedef (I64) vs struct
                    // I64 is the only simple typedef - all others are structs
                    mut is_simple_type := false
                    switch ret_type {
                    case ValueType.TCustom(name):
                        if name.eq("I64") {
                            is_simple_type = true
                        }
                    case:
                    }
                    if is_simple_type {
                        result = result.concat("&(").concat(c_type).concat("){")
                        result = emit_expr(arg, result, 0, ctx, context)
                        result = result.concat("}")
                    } else {
                        temp_var := next_mangled(ctx)
                        result = result.concat("({ ").concat(c_type).concat(" ").concat(temp_var).concat(" = ")
                        result = emit_expr(arg, result, 0, ctx, context)
                        result = result.concat("; &").concat(temp_var).concat("; })")
                    }
                    catch (err: Str) {
                        result = emit_expr(arg, result, 0, ctx, context)
                    }
                    catch (err: IndexOutOfBoundsError) {
                        result = emit_expr(arg, result, 0, ctx, context)
                    }
                } else {
                    result = emit_expr(arg, result, 0, ctx, context)
                }
            } else {
                result = emit_expr(arg, result, 0, ctx, context)
            }
        case:
            result = emit_expr(arg, result, 0, ctx, context)
        }
        return result
    }

    // Check if arg is a variadic param (which is til_Array*) but expected param is NOT mut
    // In this case we need to dereference: (*til_args) to get til_Array by value
    switch arg.node_type {
    case NodeType.Identifier(name):
        if arg.params.len().eq(0) {
            if ctx.current_variadic_params.contains_key(name) {
                result = result.concat("(*").concat(til_name(name)).concat(")")
                return result
            }
        }
    case:
    }

    result = emit_expr(arg, result, 0, ctx, context)

    return result
}

/// Emit an FCall expression using pre-hoisted temp vars for arguments
emit_fcall_with_hoisted := proc(expr: Expr, hoisted: Map, output: Str, mut ctx: CodegenContext, mut context: Context) returns Str throws Str {
    mut result := output
    if expr.params.len().eq(0) {
        throw "emit_fcall_with_hoisted: FCall with no params"
    }

    // Get function name (handles both nested identifiers and precomp'd "Type.method" strings)
    mut first := Expr()
    expr.params.get(0, first)
    mut func_name := get_func_name_string(first)
    catch (err: KeyNotFoundError) { throw first.lang_error(context.path, "ccodegen", "FCall first param not Identifier") }

    // Check for struct construction: TypeName() or TypeName(x=10, y=20)
    // Use lookup_struct to verify this is a known struct type
    mut has_named_args := false
    mut nai := 1
    while nai.lt(expr.params.len()) {
        mut narg := Expr()
        expr.params.get(nai, narg)
        switch narg.node_type {
        case NodeType.NamedArg(_):
            has_named_args = true
        case:
        }
        nai = nai.add(1)
        catch (err: IndexOutOfBoundsError) { throw err.msg }
    }

    mut struct_def_opt_found := false
    mut struct_def := SStructDef()
    struct_def = context.scope_stack.lookup_struct(func_name)
    struct_def_opt_found = true
    catch (err: KeyNotFoundError) { }

    if struct_def_opt_found {
        if or(expr.params.len().eq(1), has_named_args) {
            // No args OR only named args (struct literal)
            result = result.concat("(").concat(TIL_PREFIX).concat(func_name).concat(")")

            if struct_def.members.len().eq(0).or(struct_def.default_values.len().eq(0)) {
                result = result.concat("{}")
            } else {
                // Build map of named arg values
                mut named_values := Map.new(Str, Expr)
                mut pai := 1
                while pai.lt(expr.params.len()) {
                    mut parg := Expr()
                    expr.params.get(pai, parg)
                    switch parg.node_type {
                    case NodeType.NamedArg(field_name):
                        if parg.params.len().gt(0) {
                            mut value_expr := Expr()
                            parg.params.get(0, value_expr)
                            named_values.set(field_name, value_expr)
                            catch (err: IndexOutOfBoundsError) { throw err.msg }
                        }
                    case:
                    }
                    pai = pai.add(1)
                    catch (err: IndexOutOfBoundsError) { throw err.msg }
                }
                catch (err: AllocError) { throw err.msg }

                result = result.concat("{")
                mut is_first := true
                for member: Declaration in struct_def.members {
                    if not(member.is_mut) {
                        continue
                    }
                    if not(is_first) {
                        result = result.concat(", ")
                    }
                    is_first = false
                    result = result.concat(".").concat(member.name).concat(" = ")

                    // Use named arg value if provided, otherwise use default
                    mut named_found := false
                    mut named_expr := Expr()
                    named_values.get(member.name, named_expr)
                    named_found = true
                    catch (err: KeyNotFoundError) { }

                    if named_found {
                        result = emit_expr(named_expr, result, 0, ctx, context)
                    } else {
                        mut default_found := false
                        mut default_expr := Expr()
                        struct_def.default_values.get(member.name, default_expr)
                        default_found = true
                        catch (err: KeyNotFoundError) { }

                        if default_found {
                            // Check if this was a throwing default - use hoisted temp var
                            mut key := func_name.concat(":").concat(member.name)
                            mut hoisted_temp := ""
                            mut hoisted_found := false
                            ctx.hoisted_struct_defaults.get(key, hoisted_temp)
                            hoisted_found = true
                            catch (err: KeyNotFoundError) { }

                            if hoisted_found {
                                result = result.concat(hoisted_temp)
                            } else if is_throwing_fcall(default_expr, context) {
                                // Shouldn't happen - throwing defaults should be hoisted
                                result = result.concat("/* ERROR: unhoisted throwing default */")
                            } else {
                                result = emit_expr(default_expr, result, 0, ctx, context)
                            }
                        } else {
                            result = result.concat("0")
                        }
                    }
                }
                result = result.concat("}")
            }
            return result
        }
    }

    // Function call: til_func_name(args...)
    // func_name already has underscores (from get_func_name_string), just add prefix
    result = result.concat(TIL_PREFIX).concat(func_name).concat("(")

    // Look up param types for mut handling
    mut got_func_lookup := false
    mut func_lookup := SFuncDef()
    func_lookup = get_fcall_func_def(context, expr)
    got_func_lookup = true
    catch (err: KeyNotFoundError) { /* REM None case in Rust */ }

    mut i := 0
    mut params_len := expr.params.len()
    while i.lt(params_len.sub(1)) {
        if i.gt(0) {
            result = result.concat(", ")
        }
        mut arg := Expr()
        expr.params.get(i.add(1), arg)

        // Check if arg is a type identifier - emit as string literal
        mut type_arg_handled := false
        {
            mut type_arg_name := get_type_arg_name(arg, context)
            result = result.concat("\"").concat(type_arg_name).concat("\"")
            type_arg_handled = true
            catch (err: KeyNotFoundError) { }
        }
        if not(type_arg_handled) {
            if got_func_lookup {
                mut param_type := ValueType.TCustom("")  // Empty TCustom represents None
                mut has_param_type := false
                mut by_ref := false
                if i.lt(func_lookup.args.len()) {
                    mut arg_def := Declaration()
                    func_lookup.args.get(i, arg_def)
                    param_type = arg_def.value_type
                    has_param_type = true
                    by_ref = arg_def.is_mut
                }
                result = emit_arg_with_param_type(arg, i, hoisted, param_type, has_param_type, by_ref, result, ctx, context)
            } else {
                result = emit_arg_or_hoisted(arg, i, hoisted, result, ctx, context)
            }
        }
        i = i.add(1)
    }
    result = result.concat(")")

    catch (err: IndexOutOfBoundsError) { throw err.msg }
    return result
}

emit_if := proc(expr: Expr, output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) returns Str throws Str {
    // If: params[0] = condition, params[1] = then-body, params[2] = else-body (optional)
    if expr.params.len().lt(2) {
        throw "ccodegen: If requires condition and body"
    }

    mut result := output
    mut indent_str := ""
    for _ in 0..indent {
        indent_str = indent_str.concat("    ")
    }

    // Hoist declarations from both branches to before the if statement
    // (TIL has function-level scoping, not block-level scoping)
    mut then_body := Expr()
    expr.params.get(1, then_body)
    then_decls := collect_declarations_in_body(then_body, context)
    for decl: CollectedDeclaration in then_decls {
        c_var_name := til_name(decl.name)
        if not(ctx.declared_vars.contains(c_var_name)) {
            c_type := til_type_to_c(decl.value_type)
            if c_type.len().gt(0) {
                result = result.concat(indent_str)
                result = result.concat(c_type)
                result = result.concat(" ")
                result = result.concat(c_var_name)
                result = result.concat(";\n")
                _ := ctx.declared_vars.insert(c_var_name)
                // Also register in scope_stack so get_value_type can find it
                context.scope_stack.declare_symbol(decl.name, SymbolInfo(value_type=decl.value_type))
            }
        }
    }
    if expr.params.len().gt(2) {
        mut else_body := Expr()
        expr.params.get(2, else_body)
        else_decls := collect_declarations_in_body(else_body, context)
        for decl: CollectedDeclaration in else_decls {
            c_var_name := til_name(decl.name)
            if not(ctx.declared_vars.contains(c_var_name)) {
                c_type := til_type_to_c(decl.value_type)
                if c_type.len().gt(0) {
                    result = result.concat(indent_str)
                    result = result.concat(c_type)
                    result = result.concat(" ")
                    result = result.concat(c_var_name)
                    result = result.concat(";\n")
                    _ := ctx.declared_vars.insert(c_var_name)
                    // Also register in scope_stack so get_value_type can find it
                    context.scope_stack.declare_symbol(decl.name, SymbolInfo(value_type=decl.value_type))
                }
            }
        }
    }

    // Hoist any throwing function calls in the condition
    mut cond := Expr()
    expr.params.get(0, cond)
    result = hoist_throwing_expr(cond, result, indent, ctx, context)

    result = result.concat(indent_str)
    result = result.concat("if (")
    result = emit_expr(cond, result, 0, ctx, context)
    // Bool is a struct with .data field - extract for C truthiness
    mut cond_type := get_value_type(context, cond)
    switch cond_type {
    case ValueType.TCustom(type_name):
        if type_name.eq("Bool") {
            result = result.concat(".data")
        }
    case:
    }
    catch (err: Str) { /* get_value_type failed, skip */ }
    result = result.concat(") {\n")

    // Don't save/restore declared_vars - TIL has function-level scoping
    // Variables declared in if branches stay declared for the rest of the function
    result = emit_body(then_body, result, indent.add(1), ctx, context)

    result = result.concat(indent_str)
    result = result.concat("}")

    // Else branch (optional)
    if expr.params.len().gt(2) {
        mut else_part := Expr()
        expr.params.get(2, else_part)
        switch else_part.node_type {
        case NodeType.If:
            // Always wrap else-if in braces to ensure hoisted temp vars
            // from nested if conditions have proper scope
            result = result.concat(" else {\n")
            result = emit_if(else_part, result, indent.add(1), ctx, context)
            result = result.concat(indent_str)
            result = result.concat("}\n")
        case:
            // else block
            result = result.concat(" else {\n")
            result = emit_body(else_part, result, indent.add(1), ctx, context)
            result = result.concat(indent_str)
            result = result.concat("}\n")
        }
    } else {
        result = result.concat("\n")
    }

    catch (err: IndexOutOfBoundsError) { throw err.msg }
    catch (err: AllocError) { throw err.msg }
    return result
}

emit_while := proc(expr: Expr, output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) returns Str throws Str {
    // While: params[0] = condition, params[1] = body
    if expr.params.len().lt(2) {
        throw "ccodegen: While requires condition and body"
    }

    mut result := output
    mut indent_str := ""
    for _ in 0..indent {
        indent_str = indent_str.concat("    ")
    }

    // Hoist declarations from loop body to before the while statement
    // (TIL has function-level scoping, not block-level scoping)
    mut body := Expr()
    expr.params.get(1, body)
    body_decls := collect_declarations_in_body(body, context)
    for decl: CollectedDeclaration in body_decls {
        c_var_name := til_name(decl.name)
        if not(ctx.declared_vars.contains(c_var_name)) {
            c_type := til_type_to_c(decl.value_type)
            if c_type.len().gt(0) {
                result = result.concat(indent_str)
                result = result.concat(c_type)
                result = result.concat(" ")
                result = result.concat(c_var_name)
                result = result.concat(";\n")
                _ := ctx.declared_vars.insert(c_var_name)
                // Also register in scope_stack so get_value_type can find it
                context.scope_stack.declare_symbol(decl.name, SymbolInfo(value_type=decl.value_type))
            }
        }
    }

    // Check if condition contains throwing calls by trying to hoist to a temp buffer
    mut cond := Expr()
    expr.params.get(0, cond)
    mut hoist_output := hoist_throwing_expr(cond, "", indent.add(1), ctx, context)

    if hoist_output.len().eq(0) {
        // No throwing calls in condition - use simple while
        result = result.concat(indent_str)
        result = result.concat("while (")
        result = emit_expr(cond, result, 0, ctx, context)
        // Bool is a struct with .data field - extract for C truthiness
        mut cond_type := get_value_type(context, cond)
        switch cond_type {
        case ValueType.TCustom(type_name):
            if type_name.eq("Bool") {
                result = result.concat(".data")
            }
        case:
        }
        catch (err: Str) { /* get_value_type failed, skip */ }
        result = result.concat(") {\n")
    } else {
        // Throwing calls in condition - transform to while(1) with hoisted calls and break
        result = result.concat(indent_str)
        result = result.concat("while (1) {\n")

        // Emit hoisted throwing calls inside the loop
        result = result.concat(hoist_output)

        // Emit condition check with break
        mut inner_indent_str := ""
        for _ in 0..indent.add(1) {
            inner_indent_str = inner_indent_str.concat("    ")
        }
        result = result.concat(inner_indent_str)
        result = result.concat("if (!(")
        result = emit_expr(cond, result, 0, ctx, context)
        // Bool is a struct with .data field - extract for C truthiness
        mut cond_type := get_value_type(context, cond)
        switch cond_type {
        case ValueType.TCustom(type_name):
            if type_name.eq("Bool") {
                result = result.concat(".data")
            }
        case:
        }
        catch (err: Str) { /* get_value_type failed, skip */ }
        result = result.concat(")) break;\n")
    }

    // Don't save/restore declared_vars - TIL has function-level scoping
    // Variables declared in loops stay declared for the rest of the function
    result = emit_body(body, result, indent.add(1), ctx, context)

    result = result.concat(indent_str)
    result = result.concat("}\n")

    catch (err: IndexOutOfBoundsError) { throw err.msg }
    catch (err: AllocError) { throw err.msg }
    return result
}

emit_break := proc(expr: Expr, output: Str, indent: I64) returns Str {
    mut result := output
    for _ in 0..indent {
        result = result.concat("    ")
    }
    result = result.concat("break;\n")
    return result
}

emit_continue := proc(expr: Expr, output: Str, indent: I64) returns Str {
    mut result := output
    for _ in 0..indent {
        result = result.concat("    ")
    }
    result = result.concat("continue;\n")
    return result
}

// Result struct for variant info extraction
VariantInfo := struct {
    mut type_name: Str = ""
    mut variant_name: Str = ""
}

// Extract enum type and variant names from a case pattern expression
// For FCall: Type.Variant -> VariantInfo { type_name: "Type", variant_name: "Variant" }
get_case_variant_info := func(expr: Expr) returns VariantInfo {
    mut result := VariantInfo()
    switch expr.node_type {
    case NodeType.FCall:
        // FCall for Type.Variant (without payload extraction)
        if expr.params.len().gt(0) {
            mut first := Expr()
            expr.params.get(0, first)
            catch (err: IndexOutOfBoundsError) { return result }
            switch first.node_type {
            case NodeType.Identifier(type_name):
                if first.params.len().gt(0) {
                    mut nested := Expr()
                    first.params.get(0, nested)
                    catch (err: IndexOutOfBoundsError) { return result }
                    switch nested.node_type {
                    case NodeType.Identifier(variant_name):
                        result.type_name = type_name
                        result.variant_name = variant_name
                        return result
                    case:
                        return result
                    }
                }
                return result
            case:
                return result
            }
        }
        return result
    case NodeType.Identifier(name):
        // Identifier with nested params: Type.Variant
        if expr.params.len().gt(0) {
            mut nested := Expr()
            expr.params.get(0, nested)
            catch (err: IndexOutOfBoundsError) { return result }
            switch nested.node_type {
            case NodeType.Identifier(variant_name):
                result.type_name = name
                result.variant_name = variant_name
                return result
            case:
            }
        }
        // Plain identifier without nested params - NOT an enum variant pattern
        // Return empty to let emit_switch fall through to regular value comparison
        return result
    case:
        return result
    }
}

// Extract type name and variant name from a Pattern's variant_name (e.g., "Color.Green")
parse_pattern_variant_name := func(variant_name: Str) returns VariantInfo {
    mut result := VariantInfo()
    mut dot_pos := variant_name.rfind(".")
    if dot_pos.gteq(0) {
        result.type_name = variant_name.get_substr(0, dot_pos)
        result.variant_name = variant_name.get_substr(dot_pos.add(1), variant_name.len())
        catch (err: IndexOutOfBoundsError) {
            result.variant_name = variant_name
        }
        catch (err: AllocError) {
            result.variant_name = variant_name
        }
        return result
    }
    // No dot - just variant name (shouldn't happen in practice)
    result.variant_name = variant_name
    return result
}

/// Collect all variable declarations in a body (recursively) for hoisting
/// Returns Vec of CollectedDeclaration structs
collect_declarations_in_body := proc(body: Expr, mut context: Context) returns Vec throws Str {
    mut decls := Vec.new(CollectedDeclaration)
    // Track collected declarations for lookups during type inference
    mut collected := Map.new(Str, ValueType)

    switch body.node_type {
    case NodeType.Body:
        for stmt: Expr in body.params {
            collect_declarations_recursive(stmt, decls, collected, context)
        }
    case:
        collect_declarations_recursive(body, decls, collected, context)
    }

    return decls
}

collect_declarations_recursive := proc(expr: Expr, mut decls: Vec, mut collected: Map, mut context: Context) throws Str {
    switch expr.node_type {
    case NodeType.Declaration(decl):
        // Add this declaration
        // If type is INFER_TYPE, try to infer from the expression
        mut value_type := decl.value_type
        switch decl.value_type {
        case ValueType.TCustom(name):
            if name.eq(INFER_TYPE).and(expr.params.len().gt(0)) {
                mut first := Expr()
                expr.params.get(0, first)
                catch (err: IndexOutOfBoundsError) { throw err.msg }
                value_type = get_value_type(context, first)
                catch (err: Str) {
                    value_type = decl.value_type
                }
            }
        case:
        }
        // Track this declaration for future lookups (both in collected and scope_stack)
        collected.set(decl.name, value_type)
        // Also add to scope_stack so get_value_type can find it for subsequent declarations
        context.scope_stack.declare_symbol(decl.name, SymbolInfo(value_type=value_type, is_mut=decl.is_mut))
        mut collected_decl := CollectedDeclaration()
        collected_decl.name = decl.name
        collected_decl.value_type = value_type
        decls.push(collected_decl)
    case NodeType.Body:
        for stmt: Expr in expr.params {
            collect_declarations_recursive(stmt, decls, collected, context)
        }
    case NodeType.If:
        // Recurse into if branches
        if expr.params.len().gteq(2) {
            mut then_branch := Expr()
            expr.params.get(1, then_branch)
            catch (err: IndexOutOfBoundsError) { throw err.msg }
            collect_declarations_recursive(then_branch, decls, collected, context)
        }
        if expr.params.len().gteq(3) {
            mut else_branch := Expr()
            expr.params.get(2, else_branch)
            catch (err: IndexOutOfBoundsError) { throw err.msg }
            collect_declarations_recursive(else_branch, decls, collected, context)
        }
    case NodeType.Switch:
        // Recurse into switch case bodies
        mut i := 1
        while i.add(1).lt(expr.params.len()) {
            mut case_body := Expr()
            expr.params.get(i.add(1), case_body)
            catch (err: IndexOutOfBoundsError) { throw err.msg }
            collect_declarations_recursive(case_body, decls, collected, context)
            i = i.add(2)
        }
    case NodeType.While:
        // Recurse into while body
        if expr.params.len().gteq(2) {
            mut while_body := Expr()
            expr.params.get(1, while_body)
            catch (err: IndexOutOfBoundsError) { throw err.msg }
            collect_declarations_recursive(while_body, decls, collected, context)
        }
    case NodeType.Catch:
        // Recurse into catch body (params[2] is catch body)
        // params[0] = err_var, params[1] = err_type, params[2] = body
        if expr.params.len().gteq(3) {
            mut catch_body := Expr()
            expr.params.get(2, catch_body)
            catch (err: IndexOutOfBoundsError) { throw err.msg }
            collect_declarations_recursive(catch_body, decls, collected, context)
        }
    case:
    }
    catch (err: AllocError) { throw err.msg }
}

emit_switch := proc(expr: Expr, output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) returns Str throws Str {
    // Switch: params[0] = switch expression
    // params[1..] = alternating (case_pattern, body) pairs
    // We emit if/else chains instead of C switch because:
    // 1. C switch only works with integer types, not Str
    // 2. Avoids GCC-specific case range extension
    // 3. More portable and easier to handle all TIL switch patterns
    if expr.params.len().eq(0) {
        throw "ccodegen: Switch requires expression"
    }

    mut result := output

    // Indentation strings
    mut indent_str := ""
    for _ in 0..indent {
        indent_str = indent_str.concat("    ")
    }

    mut body_indent := ""
    for _ in 0..indent.add(1) {
        body_indent = body_indent.concat("    ")
    }

    // Get the switch expression
    mut switch_expr := Expr()
    expr.params.get(0, switch_expr)
    catch (err: IndexOutOfBoundsError) { throw err.msg }

    // Hoist any throwing function calls in the switch expression
    result = hoist_throwing_expr(switch_expr, result, indent, ctx, context)

    // Determine the type of the switch expression for proper comparison
    mut switch_type := ValueType.TCustom("")
    switch_type = get_value_type(context, switch_expr)
    // Type inference may fail - that's okay, we'll use default empty type
    catch (err: Str) { }
    mut is_str_switch := false
    switch switch_type {
    case ValueType.TCustom(type_name):
        is_str_switch = type_name.eq("Str")
    case:
    }

    // Determine if this is an enum switch and if it has payloads
    mut is_enum_switch := false
    mut enum_has_payloads_flag := false
    mut scan_i := 1
    while scan_i.lt(expr.params.len()) {
        mut case_pattern := Expr()
        expr.params.get(scan_i, case_pattern)
        catch (err: IndexOutOfBoundsError) { throw err.msg }

        switch case_pattern.node_type {
        case NodeType.Pattern(pattern_info):
            is_enum_switch = true
        case NodeType.Identifier(name):
            if case_pattern.params.len().gt(0) {
                is_enum_switch = true
            } else if name.contains(".") {
                is_enum_switch = true
            }
        case NodeType.FCall:
            is_enum_switch = true
            // FCall with arguments (e.g., ValueType.TType(TTypeDef.TEnumDef))
            // implies the enum has payloads
            if case_pattern.params.len().gt(1) {
                enum_has_payloads_flag = true
            }
        case:
        }
        scan_i = scan_i.add(2)
    }
    if is_enum_switch {
        switch switch_type {
        case ValueType.TCustom(enum_name):
            mut enum_def := context.scope_stack.lookup_enum(enum_name)
            enum_has_payloads_flag = enum_has_payloads(enum_def)
            catch (err: KeyNotFoundError) { /* not an enum or not found */ }
        case:
        }
    }

    // Store switch expression in a temp variable to avoid re-evaluation
    switch_var := next_mangled(ctx)
    result = result.concat(indent_str)
    result = result.concat("__auto_type ")
    result = result.concat(switch_var)
    result = result.concat(" = ")
    result = emit_expr(switch_expr, result, 0, ctx, context)
    result = result.concat(";\n")

    // Hoist declarations from all case bodies to before the if/else chain
    // This ensures variables declared in switch cases are visible after the switch
    // (TIL has function-level scoping, not block-level scoping)
    mut i := 1
    while i.add(1).lt(expr.params.len()) {
        mut body := Expr()
        expr.params.get(i.add(1), body)
        catch (err: IndexOutOfBoundsError) { throw err.msg }
        decls := collect_declarations_in_body(body, context)
        for decl: CollectedDeclaration in decls {
            c_var_name := til_name(decl.name)
            if not(ctx.declared_vars.contains(c_var_name)) {
                c_type := til_type_to_c(decl.value_type)
                if c_type.len().gt(0) {
                    result = result.concat(indent_str)
                    result = result.concat(c_type)
                    result = result.concat(" ")
                    result = result.concat(c_var_name)
                    result = result.concat(";\n")
                    _ := ctx.declared_vars.insert(c_var_name)
                    // Also register in scope_stack so get_value_type can find it
                    context.scope_stack.declare_symbol(decl.name, SymbolInfo(value_type=decl.value_type))
                }
            }
        }
        catch (err: AllocError) { throw err.msg }
        i = i.add(2)
    }

    // Emit if/else if chain for cases
    // Default case (if present) is guaranteed to be last by parser
    i = 1
    mut first_case := true
    mut has_cases := false
    while i.add(1).lt(expr.params.len()) {
        mut case_pattern := Expr()
        expr.params.get(i, case_pattern)
        catch (err: IndexOutOfBoundsError) { throw err.msg }
        mut case_body := Expr()
        expr.params.get(i.add(1), case_body)
        catch (err: IndexOutOfBoundsError) { throw err.msg }
        has_cases = true

        // Check if this is the default case (guaranteed last by parser)
        switch case_pattern.node_type {
        case NodeType.DefaultCase:
            // Default case - emit as else block
            if not(first_case) {
                result = result.concat(indent_str)
                result = result.concat("} else {\n")
            }
            saved_declared_vars := ctx.declared_vars.clone()
            catch (err: AllocError) { throw err.msg }
            result = emit_body(case_body, result, indent.add(1), ctx, context)
            ctx.declared_vars = saved_declared_vars
            i = i.add(2)
            continue
        case:
        }

        // Regular case - emit if/else if
        result = result.concat(indent_str)
        if first_case {
            result = result.concat("if (")
            first_case = false
        } else {
            result = result.concat("} else if (")
        }

        // Emit the condition based on pattern type
        switch case_pattern.node_type {
        case NodeType.Pattern(pattern_info):
            // Enum pattern with payload binding: compare tag
            info := parse_pattern_variant_name(pattern_info.variant_name)
            result = result.concat(switch_var)
            result = result.concat(".tag == til_")
            result = result.concat(info.type_name)
            result = result.concat("_")
            result = result.concat(info.variant_name)
            result = result.concat(") {\n")

            // Extract payload into binding variable
            result = result.concat(body_indent)
            result = result.concat("__auto_type ")
            result = result.concat(til_name(pattern_info.binding_var))
            result = result.concat(" = ")
            result = result.concat(switch_var)
            result = result.concat(".payload.")
            result = result.concat(info.variant_name)
            result = result.concat(";\n")
        case NodeType.Range:
            // Range: low <= val && val <= high
            if case_pattern.params.len().lt(2) {
                throw "ccodegen: Range requires start and end values"
            }
            mut range_start := Expr()
            case_pattern.params.get(0, range_start)
            catch (err: IndexOutOfBoundsError) { throw err.msg }
            mut range_end := Expr()
            case_pattern.params.get(1, range_end)
            catch (err: IndexOutOfBoundsError) { throw err.msg }

            if is_str_switch {
                // For string ranges, use strcmp: strcmp(low, val) <= 0 && strcmp(val, high) <= 0
                result = result.concat("strcmp((char*)")
                result = emit_expr(range_start, result, 0, ctx, context)
                result = result.concat(".c_string, (char*)")
                result = result.concat(switch_var)
                result = result.concat(".c_string) <= 0 && strcmp((char*)")
                result = result.concat(switch_var)
                result = result.concat(".c_string, (char*)")
                result = emit_expr(range_end, result, 0, ctx, context)
                result = result.concat(".c_string) <= 0")
            } else {
                result = emit_expr(range_start, result, 0, ctx, context)
                result = result.concat(" <= ")
                result = result.concat(switch_var)
                result = result.concat(" && ")
                result = result.concat(switch_var)
                result = result.concat(" <= ")
                result = emit_expr(range_end, result, 0, ctx, context)
            }
            result = result.concat(") {\n")
        case NodeType.Identifier(name):
            info := get_case_variant_info(case_pattern)
            if info.variant_name.len().gt(0) {
                // Enum variant without payload
                result = result.concat(switch_var)
                if enum_has_payloads_flag {
                    result = result.concat(".tag")
                }
                result = result.concat(" == ")
                if info.type_name.len().gt(0) {
                    result = result.concat("til_")
                    result = result.concat(info.type_name)
                    result = result.concat("_")
                }
                result = result.concat(info.variant_name)
            } else if is_str_switch {
                // String comparison
                result = result.concat("til_Str_eq(")
                result = result.concat(switch_var)
                result = result.concat(", ")
                result = emit_expr(case_pattern, result, 0, ctx, context)
                result = result.concat(").data")
            } else {
                // Regular value comparison
                result = result.concat(switch_var)
                result = result.concat(" == ")
                result = emit_expr(case_pattern, result, 0, ctx, context)
            }
            result = result.concat(") {\n")
        case NodeType.LLiteral(lit):
            if is_str_switch {
                // String literal comparison
                result = result.concat("til_Str_eq(")
                result = result.concat(switch_var)
                result = result.concat(", ")
                result = emit_expr(case_pattern, result, 0, ctx, context)
                result = result.concat(").data")
            } else {
                // Regular literal comparison
                result = result.concat(switch_var)
                result = result.concat(" == ")
                result = emit_expr(case_pattern, result, 0, ctx, context)
            }
            result = result.concat(") {\n")
        case NodeType.FCall:
            // FCall pattern: Type.Variant(payload) - enum variant with payload argument
            // For example: ValueType.TType(TTypeDef.TEnumDef)
            info := get_case_variant_info(case_pattern)
            if info.variant_name.len().gt(0).and(enum_has_payloads_flag) {
                // Compare tag first
                result = result.concat(switch_var)
                result = result.concat(".tag == til_")
                result = result.concat(info.type_name)
                result = result.concat("_")
                result = result.concat(info.variant_name)

                // Check if there's a payload argument to compare
                // params[0] is Type.Variant identifier, params[1+] are arguments
                if case_pattern.params.len().gt(1) {
                    mut payload_expr := Expr()
                    case_pattern.params.get(1, payload_expr)
                    catch (err: IndexOutOfBoundsError) { throw err.msg }
                    // Check if payload is also an enum variant (Type.Variant pattern)
                    payload_info := get_case_variant_info(payload_expr)
                    if payload_info.variant_name.len().gt(0) {
                        // Payload is an enum variant - compare payload field
                        result = result.concat(" && ")
                        result = result.concat(switch_var)
                        result = result.concat(".payload.")
                        result = result.concat(info.variant_name)
                        result = result.concat(" == til_")
                        result = result.concat(payload_info.type_name)
                        result = result.concat("_")
                        result = result.concat(payload_info.variant_name)
                    }
                    // TODO: handle non-enum payloads if needed
                }
                result = result.concat(") {\n")
            } else if info.variant_name.len().gt(0) {
                // Enum without payloads - simple tag comparison
                result = result.concat(switch_var)
                result = result.concat(" == til_")
                result = result.concat(info.type_name)
                result = result.concat("_")
                result = result.concat(info.variant_name)
                result = result.concat(") {\n")
            } else {
                // Not an enum pattern - fall through to generic comparison
                result = result.concat(switch_var)
                result = result.concat(" == ")
                result = emit_expr(case_pattern, result, 0, ctx, context)
                result = result.concat(") {\n")
            }
        case:
            // Generic case: emit equality comparison
            if is_str_switch {
                result = result.concat("til_Str_eq(")
                result = result.concat(switch_var)
                result = result.concat(", ")
                result = emit_expr(case_pattern, result, 0, ctx, context)
                result = result.concat(").data")
            } else {
                result = result.concat(switch_var)
                result = result.concat(" == ")
                result = emit_expr(case_pattern, result, 0, ctx, context)
            }
            result = result.concat(") {\n")
        }

        // Emit case body - with scope for pattern bindings
        // Save declared_vars for block scope - variables declared in this case body
        // should not affect other case bodies
        saved_declared_vars := ctx.declared_vars.clone()
        catch (err: AllocError) { throw err.msg }

        switch case_pattern.node_type {
        case NodeType.Pattern(pattern_info):
            // For pattern bindings, we need to declare the binding variable in scope
            // so that get_value_type can find it during body emission
            info := parse_pattern_variant_name(pattern_info.variant_name)

            // Look up the payload type from the enum definition
            mut payload_type := ValueType.TCustom("")
            mut have_payload_type := false
            switch switch_type {
            case ValueType.TCustom(enum_name):
                mut found_enum := false
                enum_def := context.scope_stack.lookup_enum(enum_name)
                found_enum = true
                catch (err: KeyNotFoundError) { }
                if found_enum {
                    // Found the enum, look up the variant's payload type (Ptr to ValueType, NULL = None)
                    // Bug #38 fix: use helper method
                    mut payload_ptr := Ptr()
                    payload_ptr = enum_def.get(info.variant_name)
                    catch (err: KeyNotFoundError) { }
                    // Check if payload exists (not NULL)
                    if not(NULL.eq(payload_ptr.data)) {
                        memcpy(to_ptr(payload_type), payload_ptr.data, size_of(ValueType))
                        have_payload_type = true
                    }
                }
            case:
            }

            if have_payload_type {
                // Push a new scope and declare the binding variable
                context.scope_stack.push(ScopeType.Block)
                sym_info := SymbolInfo(
                    symbol_type = SymbolType.Variable,
                    value_type = payload_type,
                    is_mutable = false,
                )
                context.scope_stack.declare_symbol(pattern_info.binding_var, sym_info)
                result = emit_body(case_body, result, indent.add(1), ctx, context)
                _ := context.scope_stack.pop()
            } else {
                result = emit_body(case_body, result, indent.add(1), ctx, context)
            }
        case:
            result = emit_body(case_body, result, indent.add(1), ctx, context)
        }

        // Restore declared_vars after case body
        ctx.declared_vars = saved_declared_vars
        i = i.add(2)
    }

    // Close the if/else chain
    if has_cases {
        result = result.concat(indent_str)
        result = result.concat("}\n")
    }

    catch (err: IndexOutOfBoundsError) { throw err.msg }

    return result
}

emit_expr := proc(expr: Expr, output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) returns Str throws Str {
    // Check if this expression has been hoisted (for nested variadic/throwing calls)
    expr_addr := to_ptr(expr)
    if ctx.hoisted_exprs.contains_key(expr_addr) {
        mut temp_var := ""
        ctx.hoisted_exprs.get(expr_addr, temp_var)
        catch (err: KeyNotFoundError) { }
        return output.concat(temp_var)
    }

    switch expr.node_type {
    case NodeType.Body:
        return emit_body(expr, output, indent, ctx, context)
    case NodeType.FCall:
        return emit_fcall(expr, output, indent, ctx, context)
    case NodeType.LLiteral(lit):
        return emit_literal(lit, output, context)
    case NodeType.Declaration(decl):
        return emit_declaration(decl, expr, output, indent, ctx, context)
    case NodeType.Identifier(name):
        // Bug #32 fix: Handle field access on expression results
        // When name is "_", params[0] is an expression to emit first,
        // then params[1..] are the field chain
        if name.eq("_") {
            if expr.params.len().gt(0) {
                // Emit the base expression (params[0])
                mut result := emit_expr(expr.get(0), output, indent, ctx, context)
                // Then emit the field chain
                for i in 1..expr.params.len() {
                    field_expr := expr.get(i)
                    switch field_expr.node_type {
                    case NodeType.Identifier(field):
                        result = result.concat(".")
                        result = result.concat(field)
                    case:
                    }
                }
                return result
            }
        }

        // Check for type-qualified access (Type.field or Type.Variant)
        if expr.params.len().gt(0) {
            mut param := Expr()
            expr.params.get(0, param)
            switch param.node_type {
            case NodeType.Identifier(field):
                // Check if this is an enum variant by looking up in context
                mut is_enum_variant := false
                mut enum_def := context.scope_stack.lookup_enum(name)
                is_enum_variant = enum_def.contains_key(field)
                catch (err: KeyNotFoundError) { }

                if is_enum_variant {
                    // Enum variant: Type.Variant -> til_Type_make_Variant()
                    mut result := concat(output, TIL_PREFIX)
                    result = result.concat(name)
                    result = result.concat("_make_")
                    result = result.concat(field)
                    result = result.concat("()")
                    return result
                }
                // Check if this is a struct constant access
                mut is_struct := false
                _ := context.scope_stack.lookup_struct(name)
                is_struct = true
                catch (err: KeyNotFoundError) { }
                if is_struct {
                    // Struct constant: Type.constant -> til_Type_constant
                    mut result := concat(output, til_name(name))
                    result = result.concat("_")
                    result = result.concat(field)
                    return result
                }
            case:
            }
            catch (err: IndexOutOfBoundsError) { throw err.msg }
        }
        // Regular identifier or field access (b.val -> til_b.val)
        // For ref params (which are pointers in C), use -> for field access
        mut is_ref_param := ctx.current_ref_params.contains(name)
        if is_ref_param.and(expr.params.len().gt(0)) {
            // Ref param with field access: til_self->field1.field2.field3
            // First field uses -> (self is a pointer), rest use . (struct values)
            mut result := concat(output, til_name(name))
            mut i := 0
            while i.lt(expr.params.len()) {
                mut param := Expr()
                expr.params.get(i, param)
                switch param.node_type {
                case NodeType.Identifier(field):
                    if i.eq(0) {
                        result = result.concat("->")
                    } else {
                        result = result.concat(".")
                    }
                    result = result.concat(field)
                case:
                }
                i = i.add(1)
            }
            catch (err: IndexOutOfBoundsError) { throw err.msg }
            return result
        } else if is_ref_param {
            // Ref param used as value: dereference with *
            mut result := concat(output, "(*")
            result = result.concat(til_name(name))
            result = result.concat(")")
            return result
        } else {
            // Regular identifier or field access
            mut result := concat(output, til_name(name))
            mut i := 0
            while i.lt(expr.params.len()) {
                mut param := Expr()
                expr.params.get(i, param)
                switch param.node_type {
                case NodeType.Identifier(field):
                    result = result.concat(".")
                    result = result.concat(field)  // Field names stay as-is (C struct fields)
                case:
                }
                i = i.add(1)
            }
            catch (err: IndexOutOfBoundsError) { throw err.msg }
            return result
        }
    case NodeType.FuncDef(func_def):
        return emit_funcdef(func_def, expr, output, indent, ctx, context)
    case NodeType.Assignment(name):
        return emit_assignment(name, expr, output, indent, ctx, context)
    case NodeType.Return:
        return emit_return(expr, output, indent, ctx, context)
    case NodeType.If:
        return emit_if(expr, output, indent, ctx, context)
    case NodeType.While:
        return emit_while(expr, output, indent, ctx, context)
    case NodeType.Break:
        return emit_break(expr, output, indent)
    case NodeType.Continue:
        return emit_continue(expr, output, indent)
    case NodeType.Catch:
        // Catch blocks handled at call site
        return output
    case NodeType.Throw:
        return emit_throw(expr, output, indent, ctx, context)
    case NodeType.StructDef(s):
        throw "ccodegen: StructDef should be handled at top level, not in emit_expr"
    case NodeType.EnumDef(e):
        throw "ccodegen: EnumDef should be handled at top level, not in emit_expr"
    case NodeType.Switch:
        return emit_switch(expr, output, indent, ctx, context)
    case NodeType.DefaultCase:
        throw "ccodegen: DefaultCase should be handled inside emit_switch"
    case NodeType.Range:
        throw "ccodegen: Range not yet supported"
    case NodeType.Pattern(p):
        throw "ccodegen: Pattern should be handled inside emit_switch"
    case NodeType.NamedArg(_):
        throw expr.error(context.path, "ccodegen", "NamedArg should be reordered before reaching emit_expr")
    case NodeType.ForIn(_):
        throw expr.lang_error(context.path, "ccodegen", "ForIn should be desugared in precomp before reaching ccodegen")
    }
}

emit_body := proc(expr: Expr, output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) returns Str throws Str {
    // Bug #44 fix: Save and restore local_catch_labels around nested blocks
    // Without this, nested blocks clear the outer catches when processing their
    // own throwing calls, causing missing status checks for outer throwing calls.
    mut saved_catch_labels := ctx.local_catch_labels.clone()
    catch (err: AllocError) { throw "AllocError in emit_body" }
    mut result := emit_stmts(expr.params, output, indent, ctx, context)
    ctx.local_catch_labels = saved_catch_labels
    return result
}

prescan_func_level_catches := proc(stmts: Vec) returns Vec throws AllocError {
    mut catches := Vec.new(Expr)

    // Collect ALL catch blocks - we'll register them all for local throw handling
    // The emit_throwing_call handles immediate catch chains for function calls,
    // but we need labels for throw statements that jump to these catches
    for stmt: Expr in stmts {
        switch stmt.node_type {
        case NodeType.Catch:
            catches.push(stmt)
        case:
        }
    }

    return catches
}

// Emit a sequence of statements with catch pattern detection
// This is the core logic for handling throw/catch at call sites
emit_stmts := proc(stmts: Vec, output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) returns Str throws Str {
    mut result := output
    mut i := 0

    // Generate indentation string
    mut indent_str := ""
    for _ in 0..indent {
        indent_str = indent_str.concat("    ")
    }

    // Pre-scan for function-level catches (at the end of the block)
    mut func_level_catches := prescan_func_level_catches(stmts)
    catch (err: AllocError) { throw err.msg }

    // Hoist declarations from catch blocks to function level
    // (TIL has function-level scoping, not block-level scoping)
    for catch_block: Expr in func_level_catches {
        if gteq(catch_block.params.len(), 3) {
            // Temporarily add the catch error variable to scope for type inference
            // catch (err: ErrType) { body } -> err_var=params[0], err_type=params[1], body=params[2]
            mut err_var_expr := Expr()
            catch_block.params.get(0, err_var_expr)
            switch err_var_expr.node_type {
            case NodeType.Identifier(err_var_name):
                mut err_type_expr := Expr()
                catch_block.params.get(1, err_type_expr)
                switch err_type_expr.node_type {
                case NodeType.Identifier(err_type_name):
                    context.scope_stack.declare_symbol(
                        err_var_name,
                        SymbolInfo(value_type=ValueType.TCustom(err_type_name))
                    )
                case:
                }
            case:
            }

            mut catch_body := Expr()
            catch_block.params.get(2, catch_body)
            decls := collect_declarations_in_body(catch_body, context)
            for decl: CollectedDeclaration in decls {
                c_var_name := til_name(decl.name)
                if not(ctx.declared_vars.contains(c_var_name)) {
                    c_type := til_type_to_c(decl.value_type)
                    if c_type.len().gt(0) {
                        result = result.concat(indent_str)
                        result = result.concat(c_type)
                        result = result.concat(" ")
                        result = result.concat(c_var_name)
                        result = result.concat(";\n")
                        _ := ctx.declared_vars.insert(c_var_name)
                        // Also register in scope_stack so get_value_type can find it
                        context.scope_stack.declare_symbol(decl.name, SymbolInfo(value_type=decl.value_type))
                    }
                }
            }
        }

        catch (err: IndexOutOfBoundsError) { throw err.msg }
        catch (err: AllocError) { throw err.msg }
    }

    // Bug #39 fix: Give each catch block its own unique label using the global counter
    // Store catch info with statement index so we can match calls to their NEXT catch
    mut all_catch_info := Vec.new(CatchLabelInfoEntry)
    mut scan_idx := 0
    while scan_idx.lt(stmts.len()) {
        mut scan_stmt := Expr()
        stmts.get(scan_idx, scan_stmt)
        switch scan_stmt.node_type {
        case NodeType.Catch:
            if scan_stmt.params.len().gteq(3) {
                mut err_type_expr := Expr()
                scan_stmt.params.get(1, err_type_expr)
                switch err_type_expr.node_type {
                case NodeType.Identifier(err_type_name):
                    catch_suffix := next_mangled(ctx)  // Unique suffix for EACH catch
                    label := format("_catch_", err_type_name, "_", catch_suffix)
                    temp_var := format("_thrown_", err_type_name, "_", catch_suffix)
                    mut entry := CatchLabelInfoEntry()
                    entry.stmt_index = scan_idx
                    entry.type_name = err_type_name
                    entry.label = label
                    entry.temp_var = temp_var
                    all_catch_info.push(entry)
                case:
                }
            }
        case:
        }
        scan_idx = scan_idx.add(1)
    }
    catch (err: IndexOutOfBoundsError) { throw err.msg }
    catch (err: AllocError) { throw err.msg }
    catch (err: I64_OverflowError) { throw err.msg }

    // Declare temp variables for all catches at the start
    for catch_entry: CatchLabelInfoEntry in all_catch_info {
        result = result.concat(indent_str)
        result = result.concat(til_name(catch_entry.type_name))
        result = result.concat(" ")
        result = result.concat(catch_entry.temp_var)
        result = result.concat(";\n")
    }

    // For backward compatibility, populate local_catch_labels with first catch of each type
    mut registered_types := Set.new(Str)
    for catch_entry: CatchLabelInfoEntry in all_catch_info {
        if not(registered_types.contains(catch_entry.type_name)) {
            _ := registered_types.insert(catch_entry.type_name)
            mut info := CatchLabelInfo()
            info.label = catch_entry.label
            info.temp_var = catch_entry.temp_var
            ctx.local_catch_labels.set(catch_entry.type_name, info)
        }
        catch (err: AllocError) { throw err.msg }
    }

    // Track which catch labels have been emitted to avoid duplicates
    mut emitted_catch_labels := Set.new(Str)

    // Process all statements
    while i.lt(stmts.len()) {
        mut stmt := Expr()
        stmts.get(i, stmt)

        // Bug #39 fix: Handle catch blocks inline with if(0) { label: ... }
        // This keeps catches where they appear in source, and execution falls through after
        switch stmt.node_type {
        case NodeType.Catch:
            // Get the error type this catch handles
            if stmt.params.len().gteq(3) {
                mut err_type_expr := Expr()
                stmt.params.get(1, err_type_expr)
                switch err_type_expr.node_type {
                case NodeType.Identifier(err_type_name):
                    // Look up this specific catch's label from all_catch_info by statement index
                    mut found_entry := false
                    mut catch_entry := CatchLabelInfoEntry()
                    mut find_idx := 0
                    while find_idx.lt(all_catch_info.len()) {
                        mut entry := CatchLabelInfoEntry()
                        all_catch_info.get(find_idx, entry)
                        if entry.stmt_index.eq(i) {
                            catch_entry = entry
                            found_entry = true
                            break
                        }
                        find_idx = find_idx.add(1)
                    }
                    catch (err: IndexOutOfBoundsError) { throw err.msg }

                    if found_entry {
                        // Only emit if we haven't already emitted this label
                        if not(emitted_catch_labels.contains(catch_entry.label)) {
                            _ := emitted_catch_labels.insert(catch_entry.label)

                            result = result.concat(indent_str)
                            result = result.concat("if (0) { ")
                            result = result.concat(catch_entry.label)
                            result = result.concat(":\n")

                            // Bind error variable
                            mut err_var_expr := Expr()
                            stmt.params.get(0, err_var_expr)
                            switch err_var_expr.node_type {
                            case NodeType.Identifier(err_var_name):
                                mut inner_indent := ""
                                for _ in 0..indent.add(1) {
                                    inner_indent = inner_indent.concat("    ")
                                }
                                result = result.concat(inner_indent)
                                result = result.concat(til_name(err_type_name))
                                result = result.concat(" ")
                                result = result.concat(til_name(err_var_name))
                                result = result.concat(" = ")
                                result = result.concat(catch_entry.temp_var)
                                result = result.concat(";\n")

                                // Add error variable to scope for type resolution in catch body
                                context.scope_stack.declare_symbol(err_var_name, SymbolInfo(value_type=ValueType.TCustom(err_type_name)))
                            case:
                            }
                            catch (err: IndexOutOfBoundsError) { throw err.msg }

                            // Emit catch body
                            // IMPORTANT: Temporarily clear local_catch_labels so throwing calls inside
                            // the catch body don't try to jump to catches
                            mut saved_catch_labels := ctx.local_catch_labels
                            ctx.local_catch_labels = Map.new(Str, CatchLabelInfo)
                            catch (err: AllocError) { throw err.msg }
                            mut catch_body := Expr()
                            stmt.params.get(2, catch_body)
                            result = emit_expr(catch_body, result, indent.add(1), ctx, context)
                            ctx.local_catch_labels = saved_catch_labels
                            catch (err: IndexOutOfBoundsError) { throw err.msg }

                            // Close the if(0) block
                            result = result.concat(indent_str)
                            result = result.concat("}\n")

                            // Bug #46 fix: Remove this catch from local_catch_labels
                            // Catches should only handle throws from BEFORE them, not after.
                            // This mirrors the interpreter's behavior where pending_throw is
                            // set by statements and only subsequent catches can handle it.
                            ctx.local_catch_labels.remove(err_type_name)
                        }
                    }
                case:
                }
            }
            i = i.add(1)
            continue
        case:
        }

        // Check if this statement is followed by catch blocks
        // And if it's a call to a throwing function (FCall, Declaration with FCall, or Assignment with FCall)
        mut maybe_fcall := Expr()
        mut has_fcall := false
        mut maybe_decl_name := ""
        mut has_decl := false
        mut maybe_assign_name := ""
        mut has_assign := false

        switch stmt.node_type {
        case NodeType.FCall:
            maybe_fcall = stmt
            has_fcall = true
        case NodeType.Declaration(decl):
            // Check if declaration has an FCall as initializer
            if stmt.params.len().gt(0) {
                mut first := Expr()
                stmt.params.get(0, first)
                switch first.node_type {
                case NodeType.FCall:
                    maybe_fcall = first
                    has_fcall = true
                    maybe_decl_name = decl.name
                    has_decl = true
                case:
                    // Not an FCall
                }
                catch (err: IndexOutOfBoundsError) { /* no initializer */ }
            }
        case NodeType.Assignment(name):
            // Check if assignment has an FCall as RHS
            if stmt.params.len().gt(0) {
                mut first := Expr()
                stmt.params.get(0, first)
                switch first.node_type {
                case NodeType.FCall:
                    maybe_fcall = first
                    has_fcall = true
                    maybe_assign_name = name
                    has_assign = true
                case:
                    // Not an FCall
                }
                catch (err: IndexOutOfBoundsError) { /* no RHS */ }
            }
        case:
            // Not FCall, Declaration, or Assignment
        }

        if has_fcall {
            // eval functions from std.meta are only available in interpret mode
            mut got_fn_name := false
            mut fn_name := ""
            fn_name = get_til_func_name_string(maybe_fcall.get(0))
            got_fn_name = true
            catch (err: KeyNotFoundError) { /* REM None case in Rust */ }
            if got_fn_name {
                if or(or(fn_name.eq("eval_file"), fn_name.eq("eval_to_str")), or(fn_name.eq("eval_to_ast_str"), fn_name.eq("eval_to_expr"))) {
                    throw maybe_fcall.error(context.path, "ccodegen", format(fn_name, "() is only available in interpret mode, not in build/run"))
                }
            }

            // Check if this function is a throwing function
            mut got_lookup := false
            mut lookup_fd := SFuncDef()
            lookup_fd = get_fcall_func_def(context, maybe_fcall)
            got_lookup = true
            catch (err: KeyNotFoundError) { /* REM None case in Rust */ }
            if got_lookup {
                if lookup_fd.throw_types.len().gt(0) {
                    mut throw_types := lookup_fd.throw_types

                    // Collect subsequent catch blocks
                    mut catch_blocks := Vec.new(Expr)
                    mut j := i.add(1)
                    while j.lt(stmts.len()) {
                        mut next_stmt := Expr()
                        stmts.get(j, next_stmt)
                        switch next_stmt.node_type {
                        case NodeType.Catch:
                            catch_blocks.push(next_stmt)
                            catch (err: AllocError) { throw err.msg }
                            j = j.add(1)
                        case:
                            break
                        }
                        catch (err: IndexOutOfBoundsError) { throw next_stmt.lang_error(context.path, "ccodegen", err.msg) }
                    }

                    if and(catch_blocks.len().gt(0), eq(func_level_catches.len(), 0)) {
                        // Immediate catches AND no function-level catches - handle inline
                        if has_decl {
                            result = emit_throwing_call(maybe_fcall, throw_types, catch_blocks, maybe_decl_name, "", result, indent, ctx, context)
                        } else if has_assign {
                            result = emit_throwing_call(maybe_fcall, throw_types, catch_blocks, "", maybe_assign_name, result, indent, ctx, context)
                        } else {
                            result = emit_throwing_call(maybe_fcall, throw_types, catch_blocks, "", "", result, indent, ctx, context)
                        }
                        i = j  // Skip past catch blocks
                        continue
                    } else if and(and(ctx.current_throw_types.len().gt(0), eq(ctx.local_catch_labels.len(), 0)), eq(func_level_catches.len(), 0)) {
                        // Bug #68 fix: Also check func_level_catches.is_empty()
                        // Without this check, we might propagate even when there are catches in the block
                        // just because local_catch_labels was cleared after processing an earlier catch
                        // No catch blocks, we're inside a throwing function, and no catches in this block
                        // Emit error propagation pattern
                        if has_decl {
                            result = emit_throwing_call_propagate(maybe_fcall, throw_types, maybe_decl_name, "", result, indent, ctx, context)
                        } else if has_assign {
                            result = emit_throwing_call_propagate(maybe_fcall, throw_types, "", maybe_assign_name, result, indent, ctx, context)
                        } else {
                            result = emit_throwing_call_propagate(maybe_fcall, throw_types, "", "", result, indent, ctx, context)
                        }
                        i = i.add(1)
                        continue
                    } else if or(func_level_catches.len().gt(0), ctx.local_catch_labels.len().gt(0)) {
                        // Has function-level catches or outer catches registered
                        // Bug #39 fix: Update local_catch_labels to point to NEXT catch for each type
                        // This ensures each call jumps to the correct catch, not always the first one
                        // Build map with only catches AFTER current position
                        mut next_catches := Map.new(Str, CatchLabelInfo)
                        mut next_idx := 0
                        while next_idx.lt(all_catch_info.len()) {
                            mut catch_entry := CatchLabelInfoEntry()
                            all_catch_info.get(next_idx, catch_entry)
                            if catch_entry.stmt_index.gt(i) {
                                // First catch of this type after current position
                                if not(next_catches.contains_key(catch_entry.type_name)) {
                                    mut info := CatchLabelInfo()
                                    info.label = catch_entry.label
                                    info.temp_var = catch_entry.temp_var
                                    next_catches.set(catch_entry.type_name, info)
                                }
                            }
                            next_idx = next_idx.add(1)
                        }
                        catch (err: IndexOutOfBoundsError) { throw err.msg }
                        catch (err: AllocError) { throw err.msg }
                        // Replace local_catch_labels with only the next catches
                        // This removes entries for types that have no more catches after this point
                        ctx.local_catch_labels.clear()
                        for type_name: Str in next_catches.keys {
                            mut info := CatchLabelInfo()
                            next_catches.get(type_name, info)
                            ctx.local_catch_labels.set(type_name, info)
                        }
                        catch (err: KeyNotFoundError) { throw err.msg }
                        catch (err: AllocError) { throw err.msg }

                        if has_decl {
                            result = emit_throwing_call_with_goto(maybe_fcall, throw_types, maybe_decl_name, "", result, indent, ctx, context)
                        } else if has_assign {
                            result = emit_throwing_call_with_goto(maybe_fcall, throw_types, "", maybe_assign_name, result, indent, ctx, context)
                        } else {
                            result = emit_throwing_call_with_goto(maybe_fcall, throw_types, "", "", result, indent, ctx, context)
                        }
                        i = i.add(1)
                        continue
                    } else {
                        // No catches - typer should have caught this if we're in non-throwing context
                        // Just use propagate (will silently succeed on error if we're not throwing)
                        if has_decl {
                            result = emit_throwing_call_propagate(maybe_fcall, throw_types, maybe_decl_name, "", result, indent, ctx, context)
                        } else if has_assign {
                            result = emit_throwing_call_propagate(maybe_fcall, throw_types, "", maybe_assign_name, result, indent, ctx, context)
                        } else {
                            result = emit_throwing_call_propagate(maybe_fcall, throw_types, "", "", result, indent, ctx, context)
                        }
                        i = i.add(1)
                        continue
                    }
                }
            }
        }

        // Check for non-throwing variadic calls in declarations/assignments
        if has_fcall {
            mut is_variadic := false
            mut variadic_info := VariadicFCallInfo()
            variadic_info = detect_variadic_fcall(maybe_fcall, ctx)
            is_variadic = true
            catch (err: KeyNotFoundError) {
                // REM: Not a variadic function
            }
            if is_variadic {
                // Check that this is NOT a throwing function (those are handled above)
                mut is_throwing := false
                mut lookup_fd := get_fcall_func_def(context, maybe_fcall)
                is_throwing = lookup_fd.throw_types.len().gt(0)
                catch (err: KeyNotFoundError) { /* REM None case in Rust */ }

                if not(is_throwing) {
                    if has_decl {
                        result = emit_variadic_call(maybe_fcall, variadic_info.elem_type, variadic_info.regular_count, maybe_decl_name, "", result, indent, ctx, context)
                    } else if has_assign {
                        result = emit_variadic_call(maybe_fcall, variadic_info.elem_type, variadic_info.regular_count, "", maybe_assign_name, result, indent, ctx, context)
                    } else {
                        result = emit_variadic_call(maybe_fcall, variadic_info.elem_type, variadic_info.regular_count, "", "", result, indent, ctx, context)
                    }
                    i = i.add(1)
                    continue
                }
            }
        }

        // Regular statement handling
        result = emit_expr(stmt, result, indent, ctx, context)
        i = i.add(1)
        catch (err: IndexOutOfBoundsError) { throw err.msg }
    }

    // Clean up local_catch_labels for this block
    for catch_entry: CatchLabelInfoEntry in all_catch_info {
        ctx.local_catch_labels.remove(catch_entry.type_name)
    }

    return result
}

/// Emit a non-throwing variadic function call at statement level
/// Handles constructing the variadic array and cleaning it up after the call
emit_variadic_call := proc(fcall: Expr, elem_type: Str, regular_count: I64, decl_name: Str, assign_name: Str, output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) returns Str throws Str {
    mut result := output

    // Generate indentation string
    mut indent_str := ""
    for _ in 0..indent {
        indent_str = indent_str.concat("    ")
    }

    // Get function name
    mut func_name := get_fcall_func_name(fcall)
    if func_name.len().eq(0) {
        throw "emit_variadic_call: could not get function name"
    }

    // Calculate param info for mut param handling
    mut param_by_ref := Vec.new(Bool)
    mut got_lookup := false
    mut lookup_fd := SFuncDef()
    lookup_fd = get_fcall_func_def(context, fcall)
    got_lookup = true
    catch (err: KeyNotFoundError) { /* REM None case in Rust */ }
    mut pi := 0
    while pi.lt(fcall.params.len().sub(1)) {
        mut by_ref := false
        if got_lookup {
            if pi.lt(lookup_fd.args.len()) {
                mut arg_decl := Declaration()
                lookup_fd.args.get(pi, arg_decl)
                by_ref = arg_decl.is_mut
            }
        }
        param_by_ref.push(by_ref)
        pi = pi.add(1)
    }

    // Calculate param_types for emit_arg_with_param_type
    mut param_types := Vec.new(ValueType)
    pi = 0
    while pi.lt(fcall.params.len().sub(1)) {
        if got_lookup {
            if pi.lt(lookup_fd.args.len()) {
                mut arg_decl := Declaration()
                lookup_fd.args.get(pi, arg_decl)
                param_types.push(arg_decl.value_type)
            } else {
                param_types.push(ValueType.TCustom(""))
            }
        } else {
            param_types.push(ValueType.TCustom(""))
        }
        pi = pi.add(1)
    }

    // Hoist any throwing function calls in arguments BEFORE constructing variadic array
    mut hoisted := Map.new(I64, Str)
    if fcall.params.len().gt(1) {
        mut args := Vec.new(Expr)
        mut ai := 1
        while ai.lt(fcall.params.len()) {
            mut arg := Expr()
            fcall.params.get(ai, arg)
            args.push(arg)
            ai = ai.add(1)
        }
        mut hoisted_vec := hoist_throwing_args(args, result, indent, ctx, context)
        // Convert Vec<HoistedArg> to Map<I64, Str>
        for entry: HoistedArg in hoisted_vec {
            hoisted.set(entry.index, entry.temp_var)
            catch (err: AllocError) { throw err.msg }
        }
    }

    // Construct variadic array
    mut variadic_args := Vec.new(Expr)
    mut vai := regular_count.add(1)
    while vai.lt(fcall.params.len()) {
        mut arg := Expr()
        fcall.params.get(vai, arg)
        variadic_args.push(arg)
        vai = vai.add(1)
    }
    mut variadic_arr_var := hoist_variadic_args(elem_type, variadic_args, hoisted, regular_count, result, indent, ctx, context)

    // Determine return type if we need to declare a variable
    mut ret_type := "int"
    if decl_name.len().gt(0).or(assign_name.len().gt(0)) {
        if got_lookup {
            if lookup_fd.return_types.len().gt(0) {
                mut first_ret := ValueType.TCustom("")
                lookup_fd.return_types.get(0, first_ret)
                mut c_type := til_type_to_c(first_ret)
                if c_type.len().gt(0) {
                    ret_type = c_type
                }
            }
        }
    }

    // For declarations: declare the variable (skip for "_" - Bug #35)
    if decl_name.len().gt(0) {
        if decl_name.eq("_") {
            // Bug #35: For underscore, just call the function (discard result)
            result = result.concat(indent_str)
            result = result.concat(TIL_PREFIX)
            result = result.concat(func_name.replace(".", "_"))
            result = result.concat("(")

            mut rai := 0
            while rai.lt(regular_count) {
                if rai.gt(0) {
                    result = result.concat(", ")
                }
                mut arg := Expr()
                fcall.params.get(rai.add(1), arg)
                mut param_type := ValueType.TCustom("")
                mut has_param_type := false
                param_types.get(rai, param_type)
                has_param_type = true
                catch (err: IndexOutOfBoundsError) { }
                mut by_ref := false
                param_by_ref.get(rai, by_ref)
                catch (err: IndexOutOfBoundsError) { throw err.msg }
                result = emit_arg_with_param_type(arg, rai, hoisted, param_type, has_param_type, by_ref, result, ctx, context)
                rai = rai.add(1)
            }

            if regular_count.gt(0) {
                result = result.concat(", ")
            }
            result = result.concat("&")
            result = result.concat(variadic_arr_var)
            result = result.concat(");\n")
        } else {
            result = result.concat(indent_str)
            result = result.concat(ret_type)
            result = result.concat(" ")
            result = result.concat(til_name(decl_name))
            result = result.concat(" = ")

            // Emit function call
            result = result.concat(TIL_PREFIX)
            result = result.concat(func_name.replace(".", "_"))
            result = result.concat("(")

            // Emit regular args first
            mut rai := 0
            while rai.lt(regular_count) {
                if rai.gt(0) {
                    result = result.concat(", ")
                }
                mut arg := Expr()
                fcall.params.get(rai.add(1), arg)
                mut param_type := ValueType.TCustom("")
                mut has_param_type := false
                param_types.get(rai, param_type)
                has_param_type = true
                catch (err: IndexOutOfBoundsError) { }
                mut by_ref := false
                param_by_ref.get(rai, by_ref)
                catch (err: IndexOutOfBoundsError) { throw err.msg }
                result = emit_arg_with_param_type(arg, rai, hoisted, param_type, has_param_type, by_ref, result, ctx, context)
                rai = rai.add(1)
            }

            // Emit variadic array pointer
            if regular_count.gt(0) {
                result = result.concat(", ")
            }
            result = result.concat("&")
            result = result.concat(variadic_arr_var)
            result = result.concat(");\n")

            // Add variable to scope
            if got_lookup {
                if lookup_fd.return_types.len().gt(0) {
                    mut first_type := ValueType.TCustom("")
                    lookup_fd.return_types.get(0, first_type)
                    context.scope_stack.declare_symbol(decl_name, SymbolInfo(value_type=first_type, is_mut=true))
                }
            }
            _ := ctx.declared_vars.insert(til_name(decl_name))
        }
    } else {
        if assign_name.len().gt(0) {
            // Assignment
            result = result.concat(indent_str)
            result = result.concat(til_name(assign_name))
            result = result.concat(" = ")

            // Emit function call
            result = result.concat(TIL_PREFIX)
            result = result.concat(func_name.replace(".", "_"))
            result = result.concat("(")

            // Emit regular args first
            mut rai := 0
            while rai.lt(regular_count) {
                if rai.gt(0) {
                    result = result.concat(", ")
                }
                mut arg := Expr()
                fcall.params.get(rai.add(1), arg)
                mut param_type := ValueType.TCustom("")
                mut has_param_type := false
                param_types.get(rai, param_type)
                has_param_type = true
                catch (err: IndexOutOfBoundsError) { }
                mut by_ref := false
                param_by_ref.get(rai, by_ref)
                catch (err: IndexOutOfBoundsError) { throw err.msg }
                result = emit_arg_with_param_type(arg, rai, hoisted, param_type, has_param_type, by_ref, result, ctx, context)
                rai = rai.add(1)
            }

            // Emit variadic array pointer
            if regular_count.gt(0) {
                result = result.concat(", ")
            }
            result = result.concat("&")
            result = result.concat(variadic_arr_var)
            result = result.concat(");\n")
        } else {
            // Standalone variadic call (no return value used)
            result = result.concat(indent_str)
            result = result.concat(TIL_PREFIX)
            result = result.concat(func_name.replace(".", "_"))
            result = result.concat("(")

            // Emit regular args first
            mut rai := 0
            while rai.lt(regular_count) {
                if rai.gt(0) {
                    result = result.concat(", ")
                }
                mut arg := Expr()
                fcall.params.get(rai.add(1), arg)
                mut param_type := ValueType.TCustom("")
                mut has_param_type := false
                param_types.get(rai, param_type)
                has_param_type = true
                catch (err: IndexOutOfBoundsError) { }
                mut by_ref := false
                param_by_ref.get(rai, by_ref)
                catch (err: IndexOutOfBoundsError) { throw err.msg }
                result = emit_arg_with_param_type(arg, rai, hoisted, param_type, has_param_type, by_ref, result, ctx, context)
                rai = rai.add(1)
            }

            // Emit variadic array pointer
            if regular_count.gt(0) {
                result = result.concat(", ")
            }
            result = result.concat("&")
            result = result.concat(variadic_arr_var)
            result = result.concat(");\n")
        }
    }

    // Clean up variadic array
    result = result.concat(indent_str)
    result = result.concat(TIL_PREFIX)
    result = result.concat("Array_delete(&")
    result = result.concat(variadic_arr_var)
    result = result.concat(");\n")

    catch (err: IndexOutOfBoundsError) { throw err.msg }
    catch (err: AllocError) { throw err.msg }

    return result
}

// Emit a call to a throwing function with catch handling
// Generates: declare temps, call func, if/else chain for error handling
emit_throwing_call := proc(fcall: Expr, throw_types: Vec, catch_blocks: Vec, decl_name: Str, assign_name: Str, output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) returns Str throws Str {
    mut result := output

    // Generate indentation string
    mut indent_str := ""
    for _ in 0..indent {
        indent_str = indent_str.concat("    ")
    }

    // Get function name
    mut func_name := get_fcall_func_name(fcall)
    if func_name.len().eq(0) {
        throw "emit_throwing_call: could not get function name"
    }

    // Check if this is a call to a nested (hoisted) function - use mangled name
    mut mangled_name := ""
    ctx.nested_func_names.get(func_name, mangled_name)
    catch (err: KeyNotFoundError) { }
    if mangled_name.len().gt(0) {
        func_name = mangled_name
    }

    // Generate unique temp name for this call
    mut temp_suffix := next_mangled(ctx)

    // Determine if we need a return value temp variable
    // Only if function actually returns something AND we're capturing it
    mut func_has_return := false
    mut func_def_found := false
    mut func_def := context.scope_stack.lookup_func(func_name)
    func_def_found = true
    catch (err: KeyNotFoundError) { }
    if func_def_found {
        func_has_return = func_def.return_types.len().gt(0)
    }
    mut needs_ret := false
    if func_has_return {
        if decl_name.len().gt(0) { needs_ret = true }
        if assign_name.len().gt(0) { needs_ret = true }
    }

    // Calculate param_types early for Dynamic hoisting
    mut param_types := Vec.new(ValueType)
    mut func_def_lookup := context.scope_stack.lookup_func(func_name)
    mut arg_i := 0
    while arg_i.lt(fcall.params.len().sub(1)) {
        if arg_i.lt(func_def_lookup.args.len()) {
            mut arg_decl := Declaration()
            func_def_lookup.args.get(arg_i, arg_decl)
            param_types.push(arg_decl.value_type)
        } else {
            param_types.push(ValueType.TCustom(""))
        }
        arg_i = arg_i.add(1)
    }
    catch (err: KeyNotFoundError) { }
    catch (err: IndexOutOfBoundsError) { throw err.msg }
    catch (err: AllocError) { throw "emit_throwing_call: AllocError in param_types" }

    // Calculate param_by_ref for pointer passing
    mut param_by_ref := Vec.new(Bool)
    func_def_lookup = context.scope_stack.lookup_func(func_name)
    arg_i = 0
    while arg_i.lt(fcall.params.len().sub(1)) {
        if arg_i.lt(func_def_lookup.args.len()) {
            mut arg_decl := Declaration()
            func_def_lookup.args.get(arg_i, arg_decl)
            param_by_ref.push(param_needs_by_ref(arg_decl))
        } else {
            param_by_ref.push(false)
        }
        arg_i = arg_i.add(1)
    }
    catch (err: KeyNotFoundError) { }
    catch (err: IndexOutOfBoundsError) { throw err.msg }
    catch (err: AllocError) { throw "emit_throwing_call: AllocError in param_by_ref" }

    // Hoist any throwing function calls in arguments BEFORE emitting this call
    mut hoisted := Map.new(I64, Str)
    if fcall.params.len().gt(1) {
        mut hoisted_vec := hoist_throwing_args(fcall.params, result, indent, ctx, context)
        // Convert Vec<HoistedArg> to Map<I64, Str>
        for entry: HoistedArg in hoisted_vec {
            hoisted.set(entry.index, entry.temp_var)
            catch (err: AllocError) { throw "emit_throwing_call: AllocError in hoist_throwing_args" }
        }
    }

    // Hoist non-lvalue args (string literals, function calls) when param type is Dynamic
    if fcall.params.len().gt(1) {
        mut dynamic_hoisted := hoist_for_dynamic_params(fcall.params, param_types, hoisted, result, indent, ctx, context)
        // Merge dynamic_hoisted into hoisted
        for dh: HoistedArg in dynamic_hoisted {
            hoisted.set(dh.index, dh.temp_var)
            catch (err: AllocError) { throw "emit_throwing_call: AllocError in hoist_for_dynamic_params" }
        }
    }

    // Hoist struct-returning FCall args when param is by-ref (can't use compound literal for structs)
    if fcall.params.len().gt(1) {
        ref_hoisted := hoist_for_ref_params(fcall.params, param_by_ref, hoisted, result, indent, ctx, context)
        // Merge ref_hoisted into hoisted
        for rh: HoistedArg in ref_hoisted {
            hoisted.set(rh.index, rh.temp_var)
            catch (err: AllocError) { throw err.msg }
        }
    }

    // Declare local variables for return value and errors
    // Look up the actual return type from scope_stack
    mut ret_type := "int"
    if needs_ret {
        mut fd := context.scope_stack.lookup_func(func_name)
        if fd.return_types.len().gt(0) {
            mut first_ret := ValueType.TCustom("")
            fd.return_types.get(0, first_ret)
            ret_type = til_type_to_c(first_ret)
        }
        catch (err: KeyNotFoundError) { /* use default int */ }
        catch (err: IndexOutOfBoundsError) { /* use default int */ }
        result = result.concat(indent_str)
        result = result.concat(ret_type)
        result = result.concat(" _ret_")
        result = result.concat(temp_suffix)
        result = result.concat(";\n")
    }

    // For declarations: declare the variable BEFORE the if block so it's visible after
    // Skip for underscore _ which is just a discard
    if decl_name.len().gt(0) {
        if not(decl_name.eq("_")) {
            result = result.concat(indent_str)
            result = result.concat(ret_type)
            result = result.concat(" ")
            result = result.concat(decl_name)
            result = result.concat(";\n")
            // Add to scope_stack for type resolution
            mut fd := context.scope_stack.lookup_func(func_name)
            if fd.return_types.len().gt(0) {
                mut first_type := ValueType.TCustom("")
                fd.return_types.get(0, first_type)
                context.scope_stack.declare_symbol(decl_name, SymbolInfo(value_type=first_type, is_mut=true))
            }
            catch (err: KeyNotFoundError) { }
            catch (err: IndexOutOfBoundsError) { throw err.msg }
        }
    }

    // Declare error structs for each throw type
    // Use = {} to avoid warning for empty structs
    mut i := 0
    while i.lt(throw_types.len()) {
        mut throw_type := ValueType.TCustom("")
        throw_types.get(i, throw_type)
        switch throw_type {
        case ValueType.TCustom(type_name):
            result = result.concat(indent_str)
            result = result.concat(type_name)
            result = result.concat(" _err")
            result = result.concat(i.to_str())
            result = result.concat("_")
            result = result.concat(temp_suffix)
            result = result.concat(" = {};\n")
        case:
            // Non-custom type, use int
            result = result.concat(indent_str)
            result = result.concat("int _err")
            result = result.concat(i.to_str())
            result = result.concat("_")
            result = result.concat(temp_suffix)
            result = result.concat(" = 0;\n")
        }
        i = i.add(1)
    }
    catch (err: IndexOutOfBoundsError) { throw err.msg }

    // Check if this is a variadic function call and construct array BEFORE the call
    mut variadic_arr_var := ""
    mut regular_count := 0
    mut variadic_info := detect_variadic_fcall(fcall, ctx)
    regular_count = variadic_info.regular_count
    mut variadic_args := Vec.new(Expr)
    mut va_idx := variadic_info.regular_count.add(1)
    while va_idx.lt(fcall.params.len()) {
        mut va_arg := Expr()
        fcall.params.get(va_idx, va_arg)
        variadic_args.push(va_arg)
        va_idx = va_idx.add(1)
        catch (err: IndexOutOfBoundsError) { throw err.msg }
        catch (err: AllocError) { throw "emit_throwing_call: AllocError in variadic handling" }
    }
    variadic_arr_var = hoist_variadic_args(variadic_info.elem_type, variadic_args, hoisted, variadic_info.regular_count, result, indent, ctx, context)
    catch (err: KeyNotFoundError) {
        // Not a variadic function - defaults stay (variadic_arr_var = "", regular_count = 0)
    }

    // Generate the function call with output parameters
    result = result.concat(indent_str)
    result = result.concat("int _status_")
    result = result.concat(temp_suffix)
    result = result.concat(" = ")
    result = result.concat(til_func_name(func_name))
    result = result.concat("(")

    // First: return value pointer (if function returns something)
    mut param_count := 0
    if needs_ret {
        result = result.concat("&_ret_")
        result = result.concat(temp_suffix)
        param_count = param_count.add(1)
    }

    // Then: error pointers
    i = 0
    while i.lt(throw_types.len()) {
        if param_count.gt(0) {
            result = result.concat(", ")
        }
        result = result.concat("&_err")
        result = result.concat(i.to_str())
        result = result.concat("_")
        result = result.concat(temp_suffix)
        param_count = param_count.add(1)
        i = i.add(1)
    }

    // Emit arguments - handle variadic separately from regular args
    if variadic_arr_var.len().gt(0) {
        // Variadic call: emit regular args first, then variadic array pointer
        i = 1
        while i.lt(regular_count.add(1)) {
            if param_count.gt(0) {
                result = result.concat(", ")
            }
            mut arg := Expr()
            fcall.params.get(i, arg)
            mut arg_idx := i.sub(1)

            // Check if this arg was hoisted (Map lookup)
            mut found_hoisted := false
            mut hoisted_temp := ""
            hoisted.get(arg_idx, hoisted_temp)
            found_hoisted = true
            catch (err: KeyNotFoundError) { }

            // Get param type and mutability for this argument
            mut is_dynamic_param := false
            mut by_ref := false
            if arg_idx.lt(param_types.len()) {
                mut pt := ValueType.TCustom("")
                param_types.get(arg_idx, pt)
                switch pt {
                case ValueType.TCustom(type_name):
                    if type_name.eq("Dynamic") {
                        is_dynamic_param = true
                    }
                case:
                }
            }
            if arg_idx.lt(param_by_ref.len()) {
                param_by_ref.get(arg_idx, by_ref)
            }

            if found_hoisted {
                result = result.concat(hoisted_temp)
            } else if is_dynamic_param {
                result = result.concat("(").concat(TIL_PREFIX).concat("Dynamic*)&")
                result = emit_expr(arg, result, 0, ctx, context)
            } else if by_ref {
                result = result.concat("&")
                result = emit_expr(arg, result, 0, ctx, context)
            } else {
                result = emit_expr(arg, result, 0, ctx, context)
            }
            param_count = param_count.add(1)
            i = i.add(1)
        }

        // Emit variadic array pointer
        if variadic_arr_var.len().gt(0) {
            if param_count.gt(0) {
                result = result.concat(", ")
            }
            result = result.concat("&")
            result = result.concat(variadic_arr_var)
        }
    } else {
        // Non-variadic: emit all args directly
        mut args_started := false
        i = 1
        while i.lt(fcall.params.len()) {
            if args_started {
                result = result.concat(", ")
            } else {
                if param_count.gt(0) {
                    result = result.concat(", ")
                }
            }
            mut arg := Expr()
            fcall.params.get(i, arg)
            mut arg_idx := i.sub(1)

            // Check if this arg was hoisted (Map lookup)
            mut found_hoisted := false
            mut hoisted_temp := ""
            hoisted.get(arg_idx, hoisted_temp)
            found_hoisted = true
            catch (err: KeyNotFoundError) { }

            // Get param type for this argument
            mut is_dynamic_param := false
            if arg_idx.lt(param_types.len()) {
                mut pt := ValueType.TCustom("")
                param_types.get(arg_idx, pt)
                switch pt {
                case ValueType.TCustom(type_name):
                    if type_name.eq("Dynamic") {
                        is_dynamic_param = true
                    }
                case:
                }
            }

            if found_hoisted {
                // Hoisted temp var is an lvalue - add & if param is Dynamic
                if is_dynamic_param {
                    result = result.concat("&")
                }
                result = result.concat(hoisted_temp)
            } else {
                // Check if arg is a type identifier - emit as string literal
                mut type_arg_handled := false
                {
                    mut type_arg_name := get_type_arg_name(arg, context)
                    result = result.concat("\"")
                    result = result.concat(type_arg_name)
                    result = result.concat("\"")
                    type_arg_handled = true
                    catch (err: KeyNotFoundError) { }
                }
                if not(type_arg_handled) {
                    // Check if param is Dynamic and arg is simple identifier
                    if is_dynamic_param {
                        switch arg.node_type {
                        case NodeType.Identifier(name):
                            if arg.params.len().eq(0) {
                                // Simple variable - emit &var
                                result = result.concat("&")
                                result = result.concat(til_name(name))
                            } else {
                                // Not simple - emit as is
                                result = emit_expr(arg, result, 0, ctx, context)
                            }
                        case:
                            result = emit_expr(arg, result, 0, ctx, context)
                        }
                    } else {
                        result = emit_expr(arg, result, 0, ctx, context)
                    }
                }
            }
            args_started = true
            param_count = param_count.add(1)
            i = i.add(1)
        }
    }
    catch (err: IndexOutOfBoundsError) { throw err.msg }

    result = result.concat(");\n")

    // Generate if/else chain for error handling
    result = result.concat(indent_str)
    result = result.concat("if (_status_")
    result = result.concat(temp_suffix)
    result = result.concat(" == 0) {\n")

    // Success case: assign return value to target variable
    // Skip for underscore _ which is just a discard
    if decl_name.len().gt(0) {
        if not(decl_name.eq("_")) {
            // Declaration: assign to newly declared variable (declared before if block)
            mut inner_indent := ""
            for _ in 0..indent.add(1) {
                inner_indent = inner_indent.concat("    ")
            }
            result = result.concat(inner_indent)
            result = result.concat(decl_name)
            result = result.concat(" = _ret_")
            result = result.concat(temp_suffix)
            result = result.concat(";\n")
        }
    } else {
        if assign_name.len().gt(0) {
            // Assignment: assign to existing variable
            mut inner_indent := ""
            for _ in 0..indent.add(1) {
                inner_indent = inner_indent.concat("    ")
            }
            result = result.concat(inner_indent)
            // Check if assignment target is a field access on a mut param (self.field)
            // If so, emit with -> instead of .
            mut dot_pos := assign_name.find(".")
            if dot_pos.gteq(0) {
                mut base := assign_name.get_substr(0, dot_pos)
                mut rest := assign_name.get_substr(dot_pos.add(1), assign_name.len())
                if ctx.current_ref_params.contains(base) {
                    // Mut param field access: til_self->field
                    result = result.concat(til_name(base))
                    result = result.concat("->")
                    result = result.concat(rest)
                } else {
                    result = result.concat(til_name(assign_name))
                }
            } else {
                if ctx.current_ref_params.contains(assign_name) {
                    // Direct assignment to mut param: *til_var = value
                    result = result.concat("*")
                    result = result.concat(til_name(assign_name))
                } else {
                    result = result.concat(til_name(assign_name))
                }
            }
            result = result.concat(" = _ret_")
            result = result.concat(temp_suffix)
            result = result.concat(";\n")
        }
    }

    result = result.concat(indent_str)
    result = result.concat("}")

    // Generate else-if branches for each catch block
    for catch_block: Expr in catch_blocks {
        // catch block params: [0]=var_name, [1]=type_name, [2]=body
        if gteq(catch_block.params.len(), 3) {
            // Get error type name from catch block
            mut err_type_name := ""
            mut err_type_expr := Expr()
            catch_block.params.get(1, err_type_expr)
            switch err_type_expr.node_type {
            case NodeType.Identifier(type_name):
                err_type_name = type_name
            case:
                err_type_name = ""
            }

            if err_type_name.len().gt(0) {
                // Find index of this error type in throw_types
                mut err_idx := sub(0, 1)  // -1 = not found
                mut j := 0
                while j.lt(throw_types.len()) {
                    mut vt := ValueType.TFunction(FunctionType.FTFunc)
                    throw_types.get(j, vt)
                    switch vt {
                    case ValueType.TCustom(type_name):
                        if type_name.eq(err_type_name) {
                            err_idx = j
                        }
                    case:
                        // Not a custom type
                    }
                    j = j.add(1)
                }

                if err_idx.gteq(0) {
                    result = result.concat(" else if (_status_")
                    result = result.concat(temp_suffix)
                    result = result.concat(" == ")
                    result = result.concat(err_idx.add(1).to_str())
                    result = result.concat(") {\n")

                    // Bind error variable and add to scope for type resolution
                    mut err_var_name := ""
                    mut err_var_expr := Expr()
                    catch_block.params.get(0, err_var_expr)
                    switch err_var_expr.node_type {
                    case NodeType.Identifier(var_name):
                        err_var_name = var_name

                        mut inner_indent := ""
                        for _ in 0..indent.add(1) {
                            inner_indent = inner_indent.concat("    ")
                        }
                        result = result.concat(inner_indent)
                        result = result.concat(til_name(err_type_name))
                        result = result.concat(" ")
                        result = result.concat(til_name(err_var_name))
                        result = result.concat(" = _err")
                        result = result.concat(err_idx.to_str())
                        result = result.concat("_")
                        result = result.concat(temp_suffix)
                        result = result.concat(";\n")

                        // Add error variable to scope for type resolution in catch body
                        context.scope_stack.declare_symbol(err_var_name, SymbolInfo(value_type=ValueType.TCustom(err_type_name)))
                    case:
                        // Not an identifier
                    }

                    // Emit catch body (params[2])
                    mut body_expr := Expr()
                    catch_block.params.get(2, body_expr)
                    result = emit_expr(body_expr, result, indent.add(1), ctx, context)

                    result = result.concat(indent_str)
                    result = result.concat("}")
                }
            }
        }
    }
    catch (err: IndexOutOfBoundsError) { throw err.msg }
    catch (err: AllocError) { throw err.msg }

    result = result.concat("\n")
    return result
}

// Emit a call to a throwing function WITHOUT catch blocks - propagate errors to caller
// Used when inside a throwing function that can forward the same error types
emit_throwing_call_propagate := proc(fcall: Expr, throw_types: Vec, decl_name: Str, assign_name: Str, output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) returns Str throws Str {
    mut result := output

    // Generate indentation string
    mut indent_str := ""
    for _ in 0..indent {
        indent_str = indent_str.concat("    ")
    }

    // Get function name
    mut func_name := get_fcall_func_name(fcall)
    if func_name.len().eq(0) {
        throw "emit_throwing_call_propagate: could not get function name"
    }

    // Check if this is a call to a nested (hoisted) function - use mangled name
    mut mangled_name := ""
    ctx.nested_func_names.get(func_name, mangled_name)
    catch (err: KeyNotFoundError) { }
    if mangled_name.len().gt(0) {
        func_name = mangled_name
    }

    // Generate unique temp name for this call
    mut temp_suffix := next_mangled(ctx)

    // Determine if we need a return value temp variable
    // Only if function actually returns something AND we're capturing it
    mut func_has_return := false
    mut func_def_found := false
    mut func_def := context.scope_stack.lookup_func(func_name)
    func_def_found = true
    catch (err: KeyNotFoundError) { }
    if func_def_found {
        func_has_return = func_def.return_types.len().gt(0)
    }
    mut needs_ret := false
    if func_has_return {
        if decl_name.len().gt(0) { needs_ret = true }
        if assign_name.len().gt(0) { needs_ret = true }
    }

    // Calculate param_types early for Dynamic hoisting
    mut param_types := Vec.new(ValueType)
    mut func_def_lookup := context.scope_stack.lookup_func(func_name)
    mut arg_i := 0
    while arg_i.lt(fcall.params.len().sub(1)) {
        if arg_i.lt(func_def_lookup.args.len()) {
            mut arg_decl := Declaration()
            func_def_lookup.args.get(arg_i, arg_decl)
            param_types.push(arg_decl.value_type)
        } else {
            param_types.push(ValueType.TCustom(""))
        }
        arg_i = arg_i.add(1)
    }
    catch (err: KeyNotFoundError) { }
    catch (err: IndexOutOfBoundsError) { throw err.msg }
    catch (err: AllocError) { throw "emit_throwing_call_propagate: AllocError in param_types" }

    // Calculate param_by_ref for pointer passing
    mut param_by_ref := Vec.new(Bool)
    func_def_lookup = context.scope_stack.lookup_func(func_name)
    arg_i = 0
    while arg_i.lt(fcall.params.len().sub(1)) {
        if arg_i.lt(func_def_lookup.args.len()) {
            mut arg_decl := Declaration()
            func_def_lookup.args.get(arg_i, arg_decl)
            param_by_ref.push(param_needs_by_ref(arg_decl))
        } else {
            param_by_ref.push(false)
        }
        arg_i = arg_i.add(1)
    }
    catch (err: KeyNotFoundError) { }
    catch (err: IndexOutOfBoundsError) { throw err.msg }
    catch (err: AllocError) { throw "emit_throwing_call_propagate: AllocError in param_by_ref" }

    // Hoist any throwing function calls in arguments BEFORE emitting this call
    mut hoisted := Map.new(I64, Str)
    if fcall.params.len().gt(1) {
        mut hoisted_vec := hoist_throwing_args(fcall.params, result, indent, ctx, context)
        // Convert Vec<HoistedArg> to Map<I64, Str>
        for entry: HoistedArg in hoisted_vec {
            hoisted.set(entry.index, entry.temp_var)
            catch (err: AllocError) { throw "emit_throwing_call_propagate: AllocError in hoist_throwing_args" }
        }
    }

    // Hoist non-lvalue args (string literals, function calls) when param type is Dynamic
    if fcall.params.len().gt(1) {
        mut dynamic_hoisted := hoist_for_dynamic_params(fcall.params, param_types, hoisted, result, indent, ctx, context)
        // Merge dynamic_hoisted into hoisted
        for dh: HoistedArg in dynamic_hoisted {
            hoisted.set(dh.index, dh.temp_var)
            catch (err: AllocError) { throw "emit_throwing_call_propagate: AllocError in hoist_for_dynamic_params" }
        }
    }

    // Hoist struct-returning FCall args when param is by-ref
    if fcall.params.len().gt(1) {
        ref_hoisted := hoist_for_ref_params(fcall.params, param_by_ref, hoisted, result, indent, ctx, context)
        // Merge ref_hoisted into hoisted
        for rh: HoistedArg in ref_hoisted {
            hoisted.set(rh.index, rh.temp_var)
            catch (err: AllocError) { throw err.msg }
        }
    }

    // Look up the actual return type from scope_stack
    mut ret_type := "int"
    if needs_ret {
        mut fd := context.scope_stack.lookup_func(func_name)
        if fd.return_types.len().gt(0) {
            mut first_ret := ValueType.TCustom("")
            fd.return_types.get(0, first_ret)
            ret_type = til_type_to_c(first_ret)
        }
        catch (err: KeyNotFoundError) { /* use default int */ }
        catch (err: IndexOutOfBoundsError) { /* use default int */ }
    }

    // Declare temp for return value if needed
    if needs_ret {
        result = result.concat(indent_str)
        result = result.concat(ret_type)
        result = result.concat(" _ret_")
        result = result.concat(temp_suffix)
        result = result.concat(";\n")
    }

    // For declarations: declare the variable BEFORE the if block so it's visible after
    // Skip for underscore _ which is just a discard
    if decl_name.len().gt(0) {
        if not(decl_name.eq("_")) {
            result = result.concat(indent_str)
            result = result.concat(ret_type)
            result = result.concat(" ")
            result = result.concat(decl_name)
            result = result.concat(";\n")
            // Add to scope_stack for type resolution
            mut fd := context.scope_stack.lookup_func(func_name)
            if fd.return_types.len().gt(0) {
                mut first_type := ValueType.TCustom("")
                fd.return_types.get(0, first_type)
                context.scope_stack.declare_symbol(decl_name, SymbolInfo(value_type=first_type, is_mut=true))
            }
            catch (err: KeyNotFoundError) { }
            catch (err: IndexOutOfBoundsError) { throw err.msg }
        }
    }

    // Declare error structs for each throw type of the called function
    mut i := 0
    while i.lt(throw_types.len()) {
        mut throw_type := ValueType.TCustom("")
        throw_types.get(i, throw_type)
        switch throw_type {
        case ValueType.TCustom(type_name):
            result = result.concat(indent_str)
            result = result.concat(type_name)
            result = result.concat(" _err")
            result = result.concat(i.to_str())
            result = result.concat("_")
            result = result.concat(temp_suffix)
            result = result.concat(" = {};\n")
        case:
            // Non-custom type, use int
            result = result.concat(indent_str)
            result = result.concat("int _err")
            result = result.concat(i.to_str())
            result = result.concat("_")
            result = result.concat(temp_suffix)
            result = result.concat(" = 0;\n")
        }
        i = i.add(1)
    }
    catch (err: IndexOutOfBoundsError) { throw err.msg }

    // Check if this is a variadic function call and construct array BEFORE the call
    mut variadic_arr_var := ""
    mut regular_count := 0
    mut variadic_info := detect_variadic_fcall(fcall, ctx)
    regular_count = variadic_info.regular_count
    mut variadic_args := Vec.new(Expr)
    mut va_idx := variadic_info.regular_count.add(1)
    while va_idx.lt(fcall.params.len()) {
        mut va_arg := Expr()
        fcall.params.get(va_idx, va_arg)
        variadic_args.push(va_arg)
        va_idx = va_idx.add(1)
        catch (err: IndexOutOfBoundsError) { throw err.msg }
        catch (err: AllocError) { throw "emit_throwing_call_propagate: AllocError in variadic handling" }
    }
    variadic_arr_var = hoist_variadic_args(variadic_info.elem_type, variadic_args, hoisted, variadic_info.regular_count, result, indent, ctx, context)
    catch (err: KeyNotFoundError) {
        // Not a variadic function - defaults stay
    }

    // Generate the function call with output parameters
    result = result.concat(indent_str)
    result = result.concat("int _status_")
    result = result.concat(temp_suffix)
    result = result.concat(" = ")
    result = result.concat(til_func_name(func_name))
    result = result.concat("(")

    // First: return value pointer (if function returns something)
    mut param_count := 0
    if needs_ret {
        result = result.concat("&_ret_")
        result = result.concat(temp_suffix)
        param_count = param_count.add(1)
    }

    // Then: error pointers
    i = 0
    while i.lt(throw_types.len()) {
        if param_count.gt(0) {
            result = result.concat(", ")
        }
        result = result.concat("&_err")
        result = result.concat(i.to_str())
        result = result.concat("_")
        result = result.concat(temp_suffix)
        param_count = param_count.add(1)
        i = i.add(1)
    }

    // Emit arguments - handle variadic separately from regular args
    if variadic_arr_var.len().gt(0) {
        // Variadic call: emit regular args first, then variadic array pointer
        mut arg_i := 1
        while arg_i.lt(regular_count.add(1)) {
            if param_count.gt(0) {
                result = result.concat(", ")
            }
            mut arg := Expr()
            fcall.params.get(arg_i, arg)
            mut arg_idx := arg_i.sub(1)

            // Check if this arg was hoisted (Map lookup)
            mut found_hoisted := false
            mut hoisted_temp := ""
            hoisted.get(arg_idx, hoisted_temp)
            found_hoisted = true
            catch (err: KeyNotFoundError) { }

            // Get param type for this argument
            mut is_dynamic_param := false
            if arg_idx.lt(param_types.len()) {
                mut pt := ValueType.TCustom("")
                param_types.get(arg_idx, pt)
                switch pt {
                case ValueType.TCustom(type_name):
                    if type_name.eq("Dynamic") {
                        is_dynamic_param = true
                    }
                case:
                }
            }

            if found_hoisted {
                if is_dynamic_param {
                    result = result.concat("&")
                }
                result = result.concat(hoisted_temp)
            } else {
                result = emit_expr(arg, result, 0, ctx, context)
            }
            param_count = param_count.add(1)
            arg_i = arg_i.add(1)
        }

        // Emit variadic array pointer
        if variadic_arr_var.len().gt(0) {
            if param_count.gt(0) {
                result = result.concat(", ")
            }
            result = result.concat("&")
            result = result.concat(variadic_arr_var)
        }
    } else {
        // Non-variadic: emit all args directly
        mut args_started := false
        i = 1
        while i.lt(fcall.params.len()) {
            if args_started {
                result = result.concat(", ")
            } else {
                if param_count.gt(0) {
                    result = result.concat(", ")
                }
            }
            mut arg := Expr()
            fcall.params.get(i, arg)
            mut arg_idx := i.sub(1)

            // Check if this arg was hoisted (Map lookup)
            mut found_hoisted := false
            mut hoisted_temp := ""
            hoisted.get(arg_idx, hoisted_temp)
            found_hoisted = true
            catch (err: KeyNotFoundError) { }

            // Get param type for this argument
            mut is_dynamic_param := false
            if arg_idx.lt(param_types.len()) {
                mut pt := ValueType.TCustom("")
                param_types.get(arg_idx, pt)
                switch pt {
                case ValueType.TCustom(type_name):
                    if type_name.eq("Dynamic") {
                        is_dynamic_param = true
                    }
                case:
                }
            }

            if found_hoisted {
                if is_dynamic_param {
                    result = result.concat("&")
                }
                result = result.concat(hoisted_temp)
            } else {
                // Check if arg is a type identifier - emit as string literal
                mut type_arg_handled := false
                {
                    mut type_arg_name := get_type_arg_name(arg, context)
                    result = result.concat("\"")
                    result = result.concat(type_arg_name)
                    result = result.concat("\"")
                    type_arg_handled = true
                    catch (err: KeyNotFoundError) { }
                }
                if not(type_arg_handled) {
                    // Check if param is Dynamic and arg is simple identifier
                    if is_dynamic_param {
                        switch arg.node_type {
                        case NodeType.Identifier(name):
                            if arg.params.len().eq(0) {
                                // Simple variable - emit &var
                                result = result.concat("&")
                                result = result.concat(til_name(name))
                            } else {
                                // Not simple - emit as is
                                result = emit_expr(arg, result, 0, ctx, context)
                            }
                        case:
                            result = emit_expr(arg, result, 0, ctx, context)
                        }
                    } else {
                        result = emit_expr(arg, result, 0, ctx, context)
                    }
                }
            }
            args_started = true
            param_count = param_count.add(1)
            i = i.add(1)
        }
    }
    catch (err: IndexOutOfBoundsError) { throw err.msg }

    result = result.concat(");\n")

    // Generate error propagation: if status != 0, copy error to caller's error param and return
    // Map error types from called function to current function's error params
    mut called_idx := 0
    while called_idx.lt(throw_types.len()) {
        mut called_throw_type := ValueType.TCustom("")
        throw_types.get(called_idx, called_throw_type)
        switch called_throw_type {
        case ValueType.TCustom(called_type_name):
            // Find matching error type in current function's throw types
            mut curr_idx := 0
            mut found := false
            while curr_idx.lt(ctx.current_throw_types.len()) {
                mut curr_throw := ValueType.TCustom("")
                ctx.current_throw_types.get(curr_idx, curr_throw)
                switch curr_throw {
                case ValueType.TCustom(curr_name):
                    if curr_name.eq(called_type_name) {
                        // Found matching error type, generate propagation
                        result = result.concat(indent_str)
                        result = result.concat("if (_status_")
                        result = result.concat(temp_suffix)
                        result = result.concat(" == ")
                        result = result.concat(called_idx.add(1).to_str())
                        result = result.concat(") { *_err")
                        result = result.concat(curr_idx.add(1).to_str())
                        result = result.concat(" = _err")
                        result = result.concat(called_idx.to_str())
                        result = result.concat("_")
                        result = result.concat(temp_suffix)
                        result = result.concat("; return ")
                        result = result.concat(curr_idx.add(1).to_str())
                        result = result.concat("; }\n")
                        found = true
                        curr_idx = ctx.current_throw_types.len()  // break
                    }
                case:
                    // Non-TCustom, skip
                }
                curr_idx = curr_idx.add(1)
            }
            catch (err: IndexOutOfBoundsError) { throw err.msg }
        case:
            // Non-custom throw type, skip
        }
        called_idx = called_idx.add(1)
    }
    catch (err: IndexOutOfBoundsError) { throw err.msg }

    // Success case: assign return value to target variable if needed
    // Skip for underscore _ which is just a discard
    if decl_name.len().gt(0) {
        if not(decl_name.eq("_")) {
            result = result.concat(indent_str)
            result = result.concat(decl_name)
            result = result.concat(" = _ret_")
            result = result.concat(temp_suffix)
            result = result.concat(";\n")
        }
    } else {
        if assign_name.len().gt(0) {
            result = result.concat(indent_str)
            // Check if assignment target is a field access on a mut param (self.field)
            // If so, emit with -> instead of .
            mut dot_pos := assign_name.find(".")
            if dot_pos.gteq(0) {
                mut base := assign_name.get_substr(0, dot_pos)
                mut rest := assign_name.get_substr(dot_pos.add(1), assign_name.len())
                if ctx.current_ref_params.contains(base) {
                    // Mut param field access: til_self->field
                    result = result.concat(til_name(base))
                    result = result.concat("->")
                    result = result.concat(rest)
                } else {
                    result = result.concat(til_name(assign_name))
                }
            } else {
                if ctx.current_ref_params.contains(assign_name) {
                    // Direct assignment to mut param: *til_var = value
                    result = result.concat("*")
                    result = result.concat(til_name(assign_name))
                } else {
                    result = result.concat(til_name(assign_name))
                }
            }
            result = result.concat(" = _ret_")
            result = result.concat(temp_suffix)
            result = result.concat(";\n")
        }
    }

    catch (err: IndexOutOfBoundsError) { throw err.msg }
    catch (err: AllocError) { throw err.msg }

    return result
}

// Emit a throwing function call that uses goto for error handling
// Used when there are outer catch blocks registered in ctx.local_catch_labels
emit_throwing_call_with_goto := proc(fcall: Expr, throw_types: Vec, decl_name: Str, assign_name: Str, output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) returns Str throws Str {
    mut result := output

    // Generate indentation string
    mut indent_str := ""
    for _ in 0..indent {
        indent_str = indent_str.concat("    ")
    }

    // Get function name
    mut func_name := get_fcall_func_name(fcall)
    if func_name.len().eq(0) {
        throw "emit_throwing_call_with_goto: could not get function name"
    }

    // Check if this is a call to a nested (hoisted) function - use mangled name
    mut mangled_name := ""
    ctx.nested_func_names.get(func_name, mangled_name)
    catch (err: KeyNotFoundError) { }
    if mangled_name.len().gt(0) {
        func_name = mangled_name
    }

    // Generate unique temp name for this call
    mut temp_suffix := next_mangled(ctx)

    // Determine if we need a return value temp variable
    // Only if function actually returns something AND we're capturing it
    mut func_has_return := false
    mut func_def_found := false
    mut func_def := context.scope_stack.lookup_func(func_name)
    func_def_found = true
    catch (err: KeyNotFoundError) { }
    if func_def_found {
        func_has_return = func_def.return_types.len().gt(0)
    }
    mut needs_ret := false
    if func_has_return {
        if decl_name.len().gt(0) { needs_ret = true }
        if assign_name.len().gt(0) { needs_ret = true }
    }

    // Calculate param_types early for Dynamic hoisting
    mut param_types := Vec.new(ValueType)
    mut func_def_lookup := context.scope_stack.lookup_func(func_name)
    mut arg_i := 0
    while arg_i.lt(fcall.params.len().sub(1)) {
        if arg_i.lt(func_def_lookup.args.len()) {
            mut arg_decl := Declaration()
            func_def_lookup.args.get(arg_i, arg_decl)
            param_types.push(arg_decl.value_type)
        } else {
            param_types.push(ValueType.TCustom(""))
        }
        arg_i = arg_i.add(1)
    }
    catch (err: KeyNotFoundError) { }
    catch (err: IndexOutOfBoundsError) { throw err.msg }
    catch (err: AllocError) { throw "emit_throwing_call_with_goto: AllocError in param_types" }

    // Calculate param_by_ref for pointer passing
    mut param_by_ref := Vec.new(Bool)
    func_def_lookup = context.scope_stack.lookup_func(func_name)
    arg_i = 0
    while arg_i.lt(fcall.params.len().sub(1)) {
        if arg_i.lt(func_def_lookup.args.len()) {
            mut arg_decl := Declaration()
            func_def_lookup.args.get(arg_i, arg_decl)
            param_by_ref.push(param_needs_by_ref(arg_decl))
        } else {
            param_by_ref.push(false)
        }
        arg_i = arg_i.add(1)
    }
    catch (err: KeyNotFoundError) { }
    catch (err: IndexOutOfBoundsError) { throw err.msg }
    catch (err: AllocError) { throw "emit_throwing_call_with_goto: AllocError in param_by_ref" }

    // Hoist any throwing function calls in arguments BEFORE emitting this call
    mut hoisted := Map.new(I64, Str)
    if fcall.params.len().gt(1) {
        mut hoisted_vec := hoist_throwing_args(fcall.params, result, indent, ctx, context)
        // Convert Vec<HoistedArg> to Map<I64, Str>
        for entry: HoistedArg in hoisted_vec {
            hoisted.set(entry.index, entry.temp_var)
            catch (err: AllocError) { throw "emit_throwing_call_with_goto: AllocError in hoist_throwing_args" }
        }
    }

    // Hoist non-lvalue args (string literals, function calls) when param type is Dynamic
    if fcall.params.len().gt(1) {
        mut dynamic_hoisted := hoist_for_dynamic_params(fcall.params, param_types, hoisted, result, indent, ctx, context)
        for dh: HoistedArg in dynamic_hoisted {
            hoisted.set(dh.index, dh.temp_var)
            catch (err: AllocError) { throw "emit_throwing_call_with_goto: AllocError in hoist_for_dynamic_params" }
        }
    }

    // Hoist struct-returning FCall args when param is by-ref
    if fcall.params.len().gt(1) {
        ref_hoisted := hoist_for_ref_params(fcall.params, param_by_ref, hoisted, result, indent, ctx, context)
        // Merge ref_hoisted into hoisted
        for rh: HoistedArg in ref_hoisted {
            hoisted.set(rh.index, rh.temp_var)
            catch (err: AllocError) { throw err.msg }
        }
    }

    // Look up the actual return type from scope_stack
    mut ret_type := "int"
    if needs_ret {
        mut fd := context.scope_stack.lookup_func(func_name)
        if fd.return_types.len().gt(0) {
            mut first_ret := ValueType.TCustom("")
            fd.return_types.get(0, first_ret)
            ret_type = til_type_to_c(first_ret)
        }
        catch (err: KeyNotFoundError) { /* use default int */ }
        catch (err: IndexOutOfBoundsError) { /* use default int */ }
    }

    // Declare temp for return value if needed
    if needs_ret {
        result = result.concat(indent_str)
        result = result.concat(ret_type)
        result = result.concat(" _ret_")
        result = result.concat(temp_suffix)
        result = result.concat(";\n")
    }

    // For declarations: declare the variable BEFORE
    // Skip for underscore _ which is just a discard
    if decl_name.len().gt(0) {
        if not(decl_name.eq("_")) {
            result = result.concat(indent_str)
            result = result.concat(ret_type)
            result = result.concat(" ")
            result = result.concat(til_name(decl_name))
            result = result.concat(";\n")
            // Add to scope_stack for type resolution
            mut fd := context.scope_stack.lookup_func(func_name)
            if fd.return_types.len().gt(0) {
                mut first_type := ValueType.TCustom("")
                fd.return_types.get(0, first_type)
                context.scope_stack.declare_symbol(decl_name, SymbolInfo(value_type=first_type, is_mut=true))
            }
            catch (err: KeyNotFoundError) { }
            catch (err: IndexOutOfBoundsError) { throw err.msg }
        }
    }

    // Declare error structs for each throw type of the called function
    mut i := 0
    while i.lt(throw_types.len()) {
        mut throw_type := ValueType.TCustom("")
        throw_types.get(i, throw_type)
        switch throw_type {
        case ValueType.TCustom(type_name):
            result = result.concat(indent_str)
            result = result.concat(til_name(type_name))
            result = result.concat(" _err")
            result = result.concat(i.to_str())
            result = result.concat("_")
            result = result.concat(temp_suffix)
            result = result.concat(" = {};\n")
        case:
            result = result.concat(indent_str)
            result = result.concat("int _err")
            result = result.concat(i.to_str())
            result = result.concat("_")
            result = result.concat(temp_suffix)
            result = result.concat(" = 0;\n")
        }
        i = i.add(1)
    }
    catch (err: IndexOutOfBoundsError) { throw err.msg }

    // Check if this is a variadic function call and construct array BEFORE the call
    mut variadic_arr_var := ""
    mut regular_count := 0
    mut variadic_info := detect_variadic_fcall(fcall, ctx)
    regular_count = variadic_info.regular_count
    mut variadic_args := Vec.new(Expr)
    mut va_idx := variadic_info.regular_count.add(1)
    while va_idx.lt(fcall.params.len()) {
        mut va_arg := Expr()
        fcall.params.get(va_idx, va_arg)
        variadic_args.push(va_arg)
        va_idx = va_idx.add(1)
        catch (err: IndexOutOfBoundsError) { throw err.msg }
        catch (err: AllocError) { throw "emit_throwing_call_with_goto: AllocError in variadic handling" }
    }
    variadic_arr_var = hoist_variadic_args(variadic_info.elem_type, variadic_args, hoisted, variadic_info.regular_count, result, indent, ctx, context)
    catch (err: KeyNotFoundError) {
        // Not a variadic function - defaults stay
    }

    // Generate the function call with output parameters
    result = result.concat(indent_str)
    result = result.concat("int _status_")
    result = result.concat(temp_suffix)
    result = result.concat(" = ")
    result = result.concat(til_func_name(func_name))
    result = result.concat("(")

    // First: return value pointer (if function returns something)
    mut param_count := 0
    if needs_ret {
        result = result.concat("&_ret_")
        result = result.concat(temp_suffix)
        param_count = param_count.add(1)
    }

    // Then: error pointers
    i = 0
    while i.lt(throw_types.len()) {
        if param_count.gt(0) {
            result = result.concat(", ")
        }
        result = result.concat("&_err")
        result = result.concat(i.to_str())
        result = result.concat("_")
        result = result.concat(temp_suffix)
        param_count = param_count.add(1)
        i = i.add(1)
    }

    // Emit arguments - handle variadic separately from regular args
    if variadic_arr_var.len().gt(0) {
        // Variadic call: emit regular args first, then variadic array pointer
        arg_i = 1
        while arg_i.lt(regular_count.add(1)) {
            if param_count.gt(0) {
                result = result.concat(", ")
            }
            mut arg := Expr()
            fcall.params.get(arg_i, arg)
            mut arg_idx := arg_i.sub(1)

            // Check if this arg was hoisted (Map lookup)
            mut found_hoisted := false
            mut hoisted_temp := ""
            hoisted.get(arg_idx, hoisted_temp)
            found_hoisted = true
            catch (err: KeyNotFoundError) { }

            // Get param type for this argument
            mut is_dynamic_param := false
            if arg_idx.lt(param_types.len()) {
                mut pt := ValueType.TCustom("")
                param_types.get(arg_idx, pt)
                switch pt {
                case ValueType.TCustom(type_name):
                    if type_name.eq("Dynamic") {
                        is_dynamic_param = true
                    }
                case:
                }
            }

            if found_hoisted {
                if is_dynamic_param {
                    result = result.concat("&")
                }
                result = result.concat(hoisted_temp)
            } else {
                result = emit_expr(arg, result, 0, ctx, context)
            }
            param_count = param_count.add(1)
            arg_i = arg_i.add(1)
        }

        // Emit variadic array pointer
        if variadic_arr_var.len().gt(0) {
            if param_count.gt(0) {
                result = result.concat(", ")
            }
            result = result.concat("&")
            result = result.concat(variadic_arr_var)
        }
    } else {
        // Non-variadic: emit all args directly
        mut args_started := false
        i = 1
        while i.lt(fcall.params.len()) {
            if args_started {
                result = result.concat(", ")
            } else {
                if param_count.gt(0) {
                    result = result.concat(", ")
                }
            }
            mut arg := Expr()
            fcall.params.get(i, arg)
            mut arg_idx := i.sub(1)

            // Check if this arg was hoisted (Map lookup)
            mut found_hoisted := false
            mut hoisted_temp := ""
            hoisted.get(arg_idx, hoisted_temp)
            found_hoisted = true
            catch (err: KeyNotFoundError) { }

            // Get param type for this argument
            mut is_dynamic_param := false
            if arg_idx.lt(param_types.len()) {
                mut pt := ValueType.TCustom("")
                param_types.get(arg_idx, pt)
                switch pt {
                case ValueType.TCustom(type_name):
                    if type_name.eq("Dynamic") {
                        is_dynamic_param = true
                    }
                case:
                }
            }

            if found_hoisted {
                if is_dynamic_param {
                    result = result.concat("&")
                }
                result = result.concat(hoisted_temp)
            } else {
                // Check if arg is a type identifier - emit as string literal
                mut type_arg_handled := false
                {
                    mut type_arg_name := get_type_arg_name(arg, context)
                    result = result.concat("\"")
                    result = result.concat(type_arg_name)
                    result = result.concat("\"")
                    type_arg_handled = true
                    catch (err: KeyNotFoundError) { }
                }
                if not(type_arg_handled) {
                    // Check if param is Dynamic and arg is simple identifier
                    if is_dynamic_param {
                        switch arg.node_type {
                        case NodeType.Identifier(name):
                            if arg.params.len().eq(0) {
                                // Simple variable - emit &var
                                result = result.concat("&")
                                result = result.concat(til_name(name))
                            } else {
                                // Not simple - emit as is
                                result = emit_expr(arg, result, 0, ctx, context)
                            }
                        case:
                            result = emit_expr(arg, result, 0, ctx, context)
                        }
                    } else {
                        result = emit_expr(arg, result, 0, ctx, context)
                    }
                }
            }
            args_started = true
            param_count = param_count.add(1)
            i = i.add(1)
        }
    }
    catch (err: IndexOutOfBoundsError) { throw err.msg }

    result = result.concat(");\n")

    // Generate goto for each error type that has a registered catch label
    mut called_idx := 0
    while called_idx.lt(throw_types.len()) {
        mut called_throw_type := ValueType.TCustom("")
        throw_types.get(called_idx, called_throw_type)
        switch called_throw_type {
        case ValueType.TCustom(called_type_name):
            // Check if this type has a registered catch label
            mut has_label := false
            mut label := ""
            mut temp_var := ""
            mut info := CatchLabelInfo()
            ctx.local_catch_labels.get(called_type_name, info)
            has_label = true
            label = info.label
            temp_var = info.temp_var
            catch (err: KeyNotFoundError) { }

            if has_label {
                result = result.concat(indent_str)
                result = result.concat("if (_status_")
                result = result.concat(temp_suffix)
                result = result.concat(" == ")
                result = result.concat(called_idx.add(1).to_str())
                result = result.concat(") { ")
                result = result.concat(temp_var)
                result = result.concat(" = _err")
                result = result.concat(called_idx.to_str())
                result = result.concat("_")
                result = result.concat(temp_suffix)
                result = result.concat("; goto ")
                result = result.concat(label)
                result = result.concat("; }\n")
            }
        case:
            // Non-custom throw type
        }
        called_idx = called_idx.add(1)
    }
    catch (err: IndexOutOfBoundsError) { throw err.msg }

    // Success case: assign return value to target variable if needed
    // Skip for underscore _ which is just a discard
    if decl_name.len().gt(0) {
        if not(decl_name.eq("_")) {
            result = result.concat(indent_str)
            result = result.concat(til_name(decl_name))
            result = result.concat(" = _ret_")
            result = result.concat(temp_suffix)
            result = result.concat(";\n")
        }
    } else {
        if assign_name.len().gt(0) {
            result = result.concat(indent_str)
            // Check if assignment target is a field access on a mut param (self.field)
            // If so, emit with -> instead of .
            mut dot_pos := assign_name.find(".")
            if dot_pos.gteq(0) {
                mut base := assign_name.get_substr(0, dot_pos)
                mut rest := assign_name.get_substr(dot_pos.add(1), assign_name.len())
                if ctx.current_ref_params.contains(base) {
                    // Mut param field access: til_self->field
                    result = result.concat(til_name(base))
                    result = result.concat("->")
                    result = result.concat(rest)
                } else {
                    result = result.concat(til_name(assign_name))
                }
            } else {
                if ctx.current_ref_params.contains(assign_name) {
                    // Direct assignment to mut param: *til_var = value
                    result = result.concat("*")
                    result = result.concat(til_name(assign_name))
                } else {
                    result = result.concat(til_name(assign_name))
                }
            }
            result = result.concat(" = _ret_")
            result = result.concat(temp_suffix)
            result = result.concat(";\n")
        }
    }

    catch (err: IndexOutOfBoundsError) { throw err.msg }
    catch (err: AllocError) { throw err.msg }

    return result
}

// Emit a throw statement
// For throwing functions: *_errN = value; return N;
// For local catches: temp_var = value; goto label;
emit_throw := proc(expr: Expr, output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) returns Str throws Str {
    // Throw: params[0] = the value to throw (typically a struct constructor like DivideByZero())
    if expr.params.is_empty() {
        throw "ccodegen: throw requires a value"
    }

    mut indent_str := ""
    for _ in 0..indent {
        indent_str = indent_str.concat("    ")
    }
    mut thrown_expr := Expr()
    expr.params.get(0, thrown_expr)

    // Check if the thrown expression itself is a throwing function call (e.g., throw format(...))
    // If so, we need to hoist it first to ensure proper error handling
    thrown_expr_addr := to_ptr(thrown_expr)
    mut result := output
    result = hoist_throwing_expr(thrown_expr, result, indent, ctx, context)
    // Check if thrown_expr was hoisted (its address is now in hoisted_exprs)
    if ctx.hoisted_exprs.contains_key(thrown_expr_addr) {
        // The thrown expression was hoisted to a temp variable
        // Now we just need to throw the temp variable value
        mut hoisted_temp := ""
        ctx.hoisted_exprs.get(thrown_expr_addr, hoisted_temp)
        catch (err: KeyNotFoundError) { throw "emit_throw: hoisted key not found" }

        // Get the type of the hoisted value
        mut thrown_type_name := ""
        mut lookup_fd := get_fcall_func_def(context, thrown_expr)
        catch (err: KeyNotFoundError) {
            throw thrown_expr.lang_error(context.path, "ccodegen", "could not find throwing function definition")
        }
        if lookup_fd.return_types.len().gt(0) {
            mut ret_type := ValueType.TCustom("")
            lookup_fd.return_types.get(0, ret_type)
            switch ret_type {
            case ValueType.TCustom(type_name):
                thrown_type_name = type_name
            case:
                thrown_type_name = value_type_to_str(ret_type)
            }
            catch (err: IndexOutOfBoundsError) { throw "emit_throw: index error getting return type" }
        } else {
            throw "ccodegen: throwing function has no return type"
        }

        // Check if this type is locally caught
        mut has_local_catch := false
        mut info := CatchLabelInfo()
        ctx.local_catch_labels.get(thrown_type_name, info)
        has_local_catch = true
        catch (err: KeyNotFoundError) { }

        if has_local_catch {
            result = result.concat(indent_str).concat(info.temp_var).concat(" = ").concat(hoisted_temp).concat(";\n")
            result = result.concat(indent_str).concat("goto ").concat(info.label).concat(";\n")
            return result
        }

        // Find the index of this type in current_throw_types
        mut error_index := sub(0, 1)  // -1 = not found
        mut i := 0
        while i.lt(ctx.current_throw_types.len()) {
            mut vt := ValueType.TCustom("")
            ctx.current_throw_types.get(i, vt)
            switch vt {
            case ValueType.TCustom(name):
                if name.eq(thrown_type_name) {
                    error_index = i
                }
            case:
            }
            i = i.add(1)
        }
        catch (err: IndexOutOfBoundsError) { throw "emit_throw: index error in throw types" }

        if error_index.lt(0) {
            throw format("ccodegen: thrown type '", thrown_type_name, "' not found in function's throw types")
        }

        result = result.concat(indent_str).concat("*_err").concat(error_index.add(1).to_str()).concat(" = ").concat(hoisted_temp).concat(";\n")
        result = result.concat(indent_str).concat("return ").concat(error_index.add(1).to_str()).concat(";\n")
        return result
    }

    // Get the thrown type name from the expression
    // For FCall, we need to determine if it's:
    // 1. A constructor like DivideByZero() - use the type name
    // 2. A function that returns an error type like format() - use the return type
    mut thrown_type_name := ""

    switch thrown_expr.node_type {
    case NodeType.FCall:
        if thrown_expr.params.len().gt(0) {
            mut first_param := Expr()
            thrown_expr.params.get(0, first_param)
            switch first_param.node_type {
            case NodeType.Identifier(name):
                // Check if this is a constructor (struct/enum) or a function call
                // If it's a function that returns a type, use the return type
                mut got_lookup := false
                mut lookup_fd := SFuncDef()
                lookup_fd = get_fcall_func_def(context, thrown_expr)
                got_lookup = true
                catch (err: KeyNotFoundError) { /* REM None case in Rust */ }
                if got_lookup {
                    // It's a function - use its return type as the thrown type
                    if lookup_fd.return_types.len().gt(0) {
                        mut ret_type := ValueType.TFunction(FunctionType.FTFunc)
                        lookup_fd.return_types.get(0, ret_type)
                        switch ret_type {
                        case ValueType.TCustom(type_name):
                            thrown_type_name = type_name
                        case:
                            thrown_type_name = value_type_to_str(ret_type)
                        }
                    } else {
                        // No return type, assume it's a constructor
                        thrown_type_name = name
                    }
                } else {
                    // Not a function, assume it's a constructor
                    thrown_type_name = name
                }
            case:
                throw "ccodegen: throw FCall must have identifier as first param"
            }
        } else {
            throw "ccodegen: throw FCall has no params"
        }
    case NodeType.Identifier(name):
        // Look up the type of the identifier (could be a variable or type name)
        mut value_type := get_value_type(context, thrown_expr)
        switch value_type {
        case ValueType.TCustom(type_name):
            thrown_type_name = type_name
        case:
            thrown_type_name = value_type_to_str(value_type)
        }
        catch (err: Str) {
            // Fallback: assume identifier is a type name (for struct constructors without args)
            thrown_type_name = name
        }
    case NodeType.LLiteral(lit):
        // Literal values - determine their type
        switch lit {
        case Literal.Str(s):
            thrown_type_name = "Str"
        case Literal.Number(n):
            thrown_type_name = "I64"
        case Literal.List(l):
            throw "ccodegen: cannot throw a list literal"
        }
    case:
        throw "ccodegen: throw expression must be a constructor, function call, identifier, or literal"
    }

    // Check if this type is locally caught (has a catch block at function level)
    mut has_local_catch := false
    mut label := ""
    mut temp_var := ""
    mut info := CatchLabelInfo()
    ctx.local_catch_labels.get(thrown_type_name, info)
    has_local_catch = true
    label = info.label
    temp_var = info.temp_var
    catch (err: KeyNotFoundError) { }

    if has_local_catch {
        // Hoist any throwing function calls in the thrown expression
        mut hoisted := Map.new(I64, Str)
        switch thrown_expr.node_type {
        case NodeType.FCall:
            if thrown_expr.params.len().gt(1) {
                // Create args Vec with elements starting from index 1 (skip function name)
                mut args := Vec.new(Expr)
                mut ai := 1
                while ai.lt(thrown_expr.params.len()) {
                    mut arg := Expr()
                    thrown_expr.params.get(ai, arg)
                    args.push(arg)
                    ai = ai.add(1)
                }
                mut hoisted_vec := hoist_throwing_args(args, result, indent, ctx, context)
                for h: HoistedArg in hoisted_vec {
                    hoisted.set(h.index, h.temp_var)
                    catch (err: AllocError) { throw err.msg }
                }
                catch (err: IndexOutOfBoundsError) { throw err.msg }
            }
        case:
        }

        // Store the error value in temp variable
        result = result.concat(indent_str)
        result = result.concat(temp_var)
        result = result.concat(" = ")

        // Emit the thrown expression
        switch thrown_expr.node_type {
        case NodeType.FCall:
            result = emit_fcall_with_hoisted(thrown_expr, hoisted, result, ctx, context)
        case:
            result = emit_expr(thrown_expr, result, 0, ctx, context)
        }
        result = result.concat(";\n")

        // Jump to the catch block
        result = result.concat(indent_str)
        result = result.concat("goto ")
        result = result.concat(label)
        result = result.concat(";\n")

        return result
    }

    // Find the index of this type in current_throw_types
    // Note: Str is represented as TCustom("Str") in the type system
    mut error_index := sub(0, 1)  // -1 = not found
    mut i := 0
    while i.lt(ctx.current_throw_types.len()) {
        mut vt := ValueType.TFunction(FunctionType.FTFunc)
        ctx.current_throw_types.get(i, vt)
        switch vt {
        case ValueType.TCustom(name):
            if name.eq(thrown_type_name) {
                error_index = i
            }
        case:
        }
        i = i.add(1)
    }

    if error_index.lt(0) {
        throw format("ccodegen: thrown type '", thrown_type_name, "' not found in function's throw types")
    }

    // Hoist throwing function calls from arguments of the thrown expression
    // E.g., throw Error.new(format(...)) needs to hoist format() first
    mut hoisted := Map.new(I64, Str)
    switch thrown_expr.node_type {
    case NodeType.FCall:
        if thrown_expr.params.len().gt(1) {
            // Create args Vec with elements starting from index 1 (skip function name)
            mut args := Vec.new(Expr)
            mut ai := 1
            while ai.lt(thrown_expr.params.len()) {
                mut arg := Expr()
                thrown_expr.params.get(ai, arg)
                args.push(arg)
                ai = ai.add(1)
            }
            mut hoisted_vec := hoist_throwing_args(args, result, indent, ctx, context)
            for h: HoistedArg in hoisted_vec {
                hoisted.set(h.index, h.temp_var)
                catch (err: AllocError) { throw err.msg }
            }
        }
    case:
    }

    // Store the error value in the appropriate error pointer
    // Note: error params are 1-based (_err1, _err2, etc.)
    result = format(result, indent_str, "*_err", error_index.add(1).to_str(), " = ")

    // Emit the thrown expression, using hoisted temp vars for arguments
    switch thrown_expr.node_type {
    case NodeType.FCall:
        result = emit_fcall_with_hoisted(thrown_expr, hoisted, result, ctx, context)
    case:
        result = emit_expr(thrown_expr, result, 0, ctx, context)
    }
    result = result.concat(";\n")

    // Return the error index (1-based, since 0 = success)
    result = format(result, indent_str, "return ", error_index.add(1).to_str(), ";\n")

    catch (err: IndexOutOfBoundsError) { throw err.msg }
    catch (err: I64_OverflowError) { throw err.msg }
    catch (err: AllocError) { throw err.msg }

    return result
}

// Check if an expression is a struct construction call (TypeName())
// Returns the type name if it is, empty string otherwise
get_struct_construction_type := func(expr: Expr, context: Context) returns Str {
    switch expr.node_type {
    case NodeType.FCall:
        if expr.params.len().gt(0) {
            mut first := Expr()
            expr.params.get(0, first)
            catch (err: IndexOutOfBoundsError) { return "" }
            switch first.node_type {
            case NodeType.Identifier(name):
                // Use lookup_struct to check if this is a known struct type
                if first.params.len().eq(0) {
                    mut struct_found := false
                    _ := context.scope_stack.lookup_struct(name)
                    struct_found = true
                    catch (err: KeyNotFoundError) { }
                    if struct_found {
                        mut only_named_args := true
                        if expr.params.len().gt(1) {
                            mut i := 1
                            while i.lt(expr.params.len()) {
                                mut arg := Expr()
                                expr.params.get(i, arg)
                                catch (err: IndexOutOfBoundsError) { only_named_args = false }
                                switch arg.node_type {
                                case NodeType.NamedArg(_):
                                    // OK
                                case:
                                    only_named_args = false
                                }
                                i.inc()
                            }
                        }
                        if only_named_args {
                            return name
                        }
                    }
                }
                return ""
            case:
                return ""
            }
        }
        return ""
    case:
        return ""
    }
}

// Check if an expression is an enum construction (Type.Variant or Type.Variant(value))
// Returns the type name if it is, None otherwise
// AST structure for Color.Red(42): FCall -> [Identifier("Color") -> [Identifier("Red")], Literal(42)]
// AST structure for Color.Unknown: Identifier("Color") -> [Identifier("Unknown")]
get_enum_construction_type := func(expr: Expr, context: Context) returns Str throws Str {
    // Check FCall case: Type.Variant(value) or Type.Variant()
    switch expr.node_type {
    case NodeType.FCall:
        if expr.params.len().gt(0) {
            mut first := Expr()
            expr.params.get(0, first)
            catch (err: IndexOutOfBoundsError) { return "" }
            switch first.node_type {
            case NodeType.Identifier(type_name):
                // Use lookup_enum to check if this is a known enum type
                mut enum_def := context.scope_stack.lookup_enum(type_name)
                // Check if there's a nested identifier (the variant)
                if first.params.len().gt(0) {
                    mut nested := Expr()
                    first.params.get(0, nested)
                    catch (err: IndexOutOfBoundsError) { return "" }
                    switch nested.node_type {
                    case NodeType.Identifier(variant_name):
                        // Verify the variant exists in the enum
                        if enum_def.contains_key(variant_name) {
                            return type_name
                        }
                    case:
                        return ""
                    }
                }
                return ""
            case:
                return ""
            }
            catch (err: KeyNotFoundError) { return "" }
        }
        return ""
    case NodeType.Identifier(type_name):
        // Check Identifier case: Type.Variant (no parentheses, no payload)
        // Use lookup_enum to check if this is a known enum type
        mut enum_def := context.scope_stack.lookup_enum(type_name)
        if expr.params.len().gt(0) {
            mut nested := Expr()
            expr.params.get(0, nested)
            catch (err: IndexOutOfBoundsError) { return "" }
            switch nested.node_type {
            case NodeType.Identifier(variant_name):
                // Verify the variant exists in the enum
                if enum_def.contains_key(variant_name) {
                    return type_name
                }
            case:
                return ""
            }
        }
        catch (err: KeyNotFoundError) { return "" }
        return ""
    case:
        return ""
    }
}

emit_declaration := proc(decl: Declaration, expr: Expr, output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) returns Str throws Str {
    mut result := output

    // Skip inline ext_func/ext_proc declarations - they're just declaring external functions exist
    if expr.params.len().gt(0) {
        mut first := Expr()
        expr.params.get(0, first)
        catch (err: IndexOutOfBoundsError) { throw err.msg }
        switch first.node_type {
        case NodeType.FuncDef(func_def):
            if func_def.is_ext() {
                return output
            }
        case:
            // Continue
        }
    }

    // Check if this is a function definition - skip or hoist depending on context
    if expr.params.len().gt(0) {
        mut first := Expr()
        expr.params.get(0, first)
        catch (err: IndexOutOfBoundsError) { throw err.msg }
        switch first.node_type {
        case NodeType.FuncDef(func_def):
            // Check if we're inside a function (nested function declaration)
            if ctx.current_function_name.len().gt(0) {
                // Nested function - hoist it to top level with mangled name
                mut mangled_name := ctx.current_function_name.concat("_").concat(decl.name)

                // Register the name mapping so function calls can find it
                ctx.nested_func_names.set(decl.name, mangled_name)

                // Register function definition so get_value_type can resolve return types
                context.scope_stack.declare_func(decl.name, func_def)

                // Generate the hoisted function prototype
                mut proto_output := ""
                proto_output = emit_func_signature(til_name(mangled_name), func_def, proto_output)
                proto_output = proto_output.concat(";\n")
                ctx.hoisted_prototypes.push(proto_output)

                // Generate the hoisted function definition
                mut func_output := ""

                // Save and set context for nested function
                mut saved_throw_types := ctx.current_throw_types
                mut saved_return_types := ctx.current_return_types
                mut saved_mut_params := ctx.current_ref_params
                mut saved_variadic_params := ctx.current_variadic_params
                mut saved_declared_vars := ctx.declared_vars
                mut saved_function_name := ctx.current_function_name
                mut saved_mangling_counter := ctx.mangling_counter

                ctx.current_throw_types = func_def.throw_types
                ctx.current_return_types = func_def.return_types
                ctx.current_function_name = mangled_name
                ctx.mangling_counter = 0  // Reset counter per-function for determinism
                ctx.current_ref_params = Vec.new(Str)
                ctx.current_variadic_params = Map.new(Str, Str)
                // Clear declared_vars and hoisted_struct_defaults for new function scope
                ctx.declared_vars = Set.new(Str)
                ctx.hoisted_struct_defaults = Map.new(Str, Str)

                // Track mut params and variadic params
                mut arg_idx := 0
                while arg_idx.lt(func_def.args.len()) {
                    mut arg := Declaration()
                    func_def.args.get(arg_idx, arg)
                    // Bug #60: All non-copy args are passed by pointer (mut, own, and const/default)
                    if not(arg.is_copy) {
                        ctx.current_ref_params.push(arg.name)
                    }
                    switch arg.value_type {
                    case ValueType.TMulti(elem_type_name):
                        ctx.current_variadic_params.set(arg.name, til_name(elem_type_name))
                    case:
                    }
                    arg_idx = arg_idx.add(1)
                }

                // Push scope frame for function
                context.scope_stack.push(ScopeType.Function)
                mut param_idx := 0
                while param_idx.lt(func_def.args.len()) {
                    mut param := Declaration()
                    func_def.args.get(param_idx, param)
                    mut value_type := param.value_type
                    switch param.value_type {
                    case ValueType.TMulti(elem_type):
                        value_type = ValueType.TCustom("Array")
                    case:
                    }
                    mut sym := SymbolInfo(value_type=value_type, is_mut=param.is_mut, is_copy=param.is_copy, is_own=param.is_own)
                    context.scope_stack.declare_symbol(param.name, sym)
                    param_idx = param_idx.add(1)
                }

                // Emit signature with mangled name
                func_output = emit_func_signature(mangled_name, func_def, func_output)
                func_output = func_output.concat(" {\n")

                // Emit body
                func_output = emit_stmts(func_def.body, func_output, 1, ctx, context)

                // For throwing void functions, add implicit return 0
                if func_def.throw_types.len().gt(0) {
                    if func_def.return_types.len().eq(0) {
                        func_output = func_output.concat("    return 0;\n")
                    }
                }

                func_output = func_output.concat("}\n\n")

                // Pop scope frame
                _ := context.scope_stack.pop()
                catch (err: IndexOutOfBoundsError) { throw err.msg }

                // Store in hoisted_functions
                ctx.hoisted_functions.push(func_output)

                // Restore context
                ctx.current_throw_types = saved_throw_types
                ctx.current_return_types = saved_return_types
                ctx.current_ref_params = saved_mut_params
                ctx.current_variadic_params = saved_variadic_params
                ctx.declared_vars = saved_declared_vars
                ctx.current_function_name = saved_function_name
                ctx.mangling_counter = saved_mangling_counter

                catch (err: AllocError) { throw err.msg }

                // Don't emit anything at declaration site - function is hoisted
                return output
            }
            // Top-level function - skip, already emitted before main
            return output
        case NodeType.StructDef(struct_def):
            // Skip - already emitted before functions
            return output
        case:
            // Continue with normal declaration
        }
    }

    mut indent_str := ""
    for _ in 0..indent {
        indent_str = indent_str.concat("    ")
    }

    // Hoist any throwing function calls in the RHS expression before emitting the declaration
    // This ensures throwing calls are properly handled with error checking
    if expr.params.len().gt(0) {
        mut first := Expr()
        expr.params.get(0, first)
        catch (err: IndexOutOfBoundsError) { throw err.msg }
        result = hoist_throwing_expr(first, result, indent, ctx, context)
    }

    // Bug #35: For underscore declarations, just emit the expression (discard result)
    // This avoids C redeclaration errors and matches the semantics of discarding
    if decl.name.eq("_") {
        if expr.params.len().gt(0) {
            result = result.concat(indent_str)
            mut first := Expr()
            expr.params.get(0, first)
            catch (err: IndexOutOfBoundsError) { throw err.msg }
            // FCall at statement level (indent > 0) adds ";\n" itself
            // Other expressions need semicolon added explicitly
            switch first.node_type {
            case NodeType.FCall:
                result = emit_expr(first, result, indent, ctx, context)
            case:
                result = emit_expr(first, result, 0, ctx, context)
                result = result.concat(";\n")
            }
        }
        return result
    }

    name := decl.name
    is_mut := decl.is_mut

    // Check if this is a struct construction (TypeName())
    mut struct_type := ""
    if expr.params.len().gt(0) {
        mut first := Expr()
        expr.params.get(0, first)
        catch (err: IndexOutOfBoundsError) { throw err.msg }
        struct_type = get_struct_construction_type(first, context)
    }

    // Check if this is an enum construction (Type.Variant or Type.Variant(value))
    mut enum_type := ""
    if expr.params.len().gt(0) {
        mut first := Expr()
        expr.params.get(0, first)
        catch (err: IndexOutOfBoundsError) { throw err.msg }
        enum_type = get_enum_construction_type(first, context)
    }

    // Track variable type for method mangling
    // Use the inferred type from struct/enum construction if available,
    // or use get_value_type to get return type of function calls like Vec.new(Str)
    mut var_type := decl.value_type
    if struct_type.len().gt(0) {
        var_type = ValueType.TCustom(struct_type)
    } else if enum_type.len().gt(0) {
        var_type = ValueType.TCustom(enum_type)
    } else {
        switch decl.value_type {
        case ValueType.TCustom(type_name):
            if type_name.eq(INFER_TYPE).and(expr.params.len().gt(0)) {
                // Try get_value_type (handles function calls like Vec.new)
                mut first := Expr()
                expr.params.get(0, first)
                var_type = get_value_type(context, first)
                catch (err: Str) {
                    // get_value_type failed, keep decl.value_type
                }
                catch (err: IndexOutOfBoundsError) { throw err.msg }
            }
        case:
            // Keep default decl.value_type
        }
    }
    // Add to scope_stack so get_value_type can find it
    context.scope_stack.declare_symbol(name, SymbolInfo(value_type=var_type, is_mut=decl.is_mut))

    // Check if variable already declared in this function (avoid C redefinition errors)
    // Use til_name() since that's what hoisting code uses when inserting into declared_vars
    already_declared := ctx.declared_vars.contains(til_name(name))

    if struct_type.len().gt(0) {
        // Extract type_name from struct_type (like Rust's if let Some(type_name) = struct_type)
        type_name := struct_type

        // Struct variable declaration with values (defaults or named args)

        // Build map of named arg values from struct literal
        mut named_values := Map.new(Str, Expr)
        if expr.params.len().gt(0) {
            mut first_param := Expr()
            expr.params.get(0, first_param)
            mut literal_arg_idx := 1
            while literal_arg_idx.lt(first_param.params.len()) {
                mut literal_arg := Expr()
                first_param.params.get(literal_arg_idx, literal_arg)
                switch literal_arg.node_type {
                case NodeType.NamedArg(field_name):
                    if literal_arg.params.len().gt(0) {
                        mut value_expr := Expr()
                        literal_arg.params.get(0, value_expr)
                        named_values.set(field_name, value_expr)
                    }
                case:
                }
                literal_arg_idx.inc()
            }
        }
        catch (err: IndexOutOfBoundsError) { throw err.msg }
        catch (err: AllocError) { throw err.msg }

        // Look up struct definition to get default values
        mut struct_def_found := false
        mut struct_def := context.scope_stack.lookup_struct(struct_type)
        struct_def_found = true
        catch (err: KeyNotFoundError) { }

        if struct_def_found {
            if struct_def.members.len().eq(0) {
                // Empty struct - use empty initializer
                if not(already_declared) {
                    if not(is_mut) {
                        result = result.concat("const ")
                    }
                    result = result.concat(til_name(struct_type))
                    result = result.concat(" ")
                    _ := ctx.declared_vars.insert(til_name(name))
                }
                result = result.concat(til_name(name))
                result = emit_struct_literal_assign(result, struct_type, already_declared, "{}")
            } else if struct_def.default_values.len().eq(0).and(named_values.len().eq(0)) {
                // No defaults and no named args - zero initialize
                if not(already_declared) {
                    if not(is_mut) {
                        result = result.concat("const ")
                    }
                    result = result.concat(til_name(struct_type))
                    result = result.concat(" ")
                    _ := ctx.declared_vars.insert(til_name(name))
                }
                result = result.concat(til_name(name))
                result = emit_struct_literal_assign(result, struct_type, already_declared, "{0}")
            } else {
                // Has default values or named args - emit designated initializer
                // First check if any default value is a throwing function call
                mut throwing_defaults := Vec.new(ThrowingDefault)
                for member: Declaration in struct_def.members {
                    if not(member.is_mut) {
                        continue
                    }
                    if named_values.contains_key(member.name) {
                        continue  // Named arg overrides default
                    }
                    mut default_expr := Expr()
                    mut has_default := false
                    struct_def.default_values.get(member.name, default_expr)
                    has_default = true
                    catch (err: KeyNotFoundError) { }
                    if has_default {
                        if is_throwing_fcall(default_expr, context) {
                            mut td := ThrowingDefault()
                            td.member_name = member.name
                            td.default_expr = default_expr
                            throwing_defaults.push(td)
                        }
                    }
                }
                catch (err: AllocError) { throw err.msg }

                // If we have throwing defaults, emit them as separate statements first
                // Only do inline hoisting at statement level (indent > 0)
                // At expression level, hoisting was already done by hoist_throwing_expr
                if indent.gt(0) {
                mut td_idx := 0
                while td_idx.lt(throwing_defaults.len()) {
                    mut td := ThrowingDefault()
                    throwing_defaults.get(td_idx, td)

                    // Get the function's throw types
                    mut throw_types := Vec.new(ValueType)
                    if td.default_expr.params.len().gt(0) {
                        mut first_param := Expr()
                        td.default_expr.params.get(0, first_param)
                        mut fcall_func_name := get_func_name_string(first_param)
                        mut lookup_name := fcall_func_name.replace("_", ".")
                        mut func_def := context.scope_stack.lookup_func(lookup_name)
                        throw_types = func_def.throw_types
                        catch (err: KeyNotFoundError) { /* REM None case in Rust */ }
                    }
                    catch (err: IndexOutOfBoundsError) { throw err.msg }
                    catch (err: AllocError) { throw err.msg }

                    mut temp_name := "_default_"
                    temp_name = temp_name.concat(td.member_name).concat("_").concat(next_mangled(ctx))
                    // Emit the function call with out-param using emit_throwing_call_propagate
                    result = emit_throwing_call_propagate(td.default_expr, throw_types, temp_name, "", result, indent, ctx, context)
                    // Record that this struct default was hoisted using "struct_name:member_name" key
                    key := type_name.concat(":").concat(td.member_name)
                    ctx.hoisted_struct_defaults.set(key, temp_name)
                    catch (err: AllocError) { throw err.msg }

                    td_idx.inc()
                }
                }  // end if indent.gt(0)

                if not(already_declared) {
                    if not(is_mut) {
                        result = result.concat("const ")
                    }
                    result = result.concat(til_name(struct_type))
                    result = result.concat(" ")
                    _ := ctx.declared_vars.insert(til_name(name))
                }
                result = result.concat(til_name(name))
                result = emit_struct_literal_start(result, struct_type, already_declared)
                result = result.concat("{")
                mut is_first := true
                for member: Declaration in struct_def.members {
                    // Only include mut fields (actual struct data members)
                    // Skip functions, constants, and non-mut fields
                    if not(member.is_mut) {
                        continue
                    }
                    if not(is_first) {
                        result = result.concat(", ")
                    }
                    is_first = false
                    result = result.concat(".").concat(member.name).concat(" = ")
                    // Use named arg value if provided, otherwise use default
                    mut value_expr := Expr()
                    mut has_named_value := false
                    named_values.get(member.name, value_expr)
                    has_named_value = true
                    catch (err: KeyNotFoundError) { }
                    if has_named_value.and(value_expr.params.len().gt(0).or(value_expr.line.gt(0))) {
                        result = emit_expr(value_expr, result, 0, ctx, context)
                    } else {
                        mut default_expr := Expr()
                        mut has_default := false
                        struct_def.default_values.get(member.name, default_expr)
                        has_default = true
                        catch (err: KeyNotFoundError) { }
                        if has_default {
                            // Check if this was a throwing default - use temp var instead
                            key := type_name.concat(":").concat(member.name)
                            mut temp_name := ""
                            ctx.hoisted_struct_defaults.get(key, temp_name)
                            result = result.concat(temp_name)
                            catch (err: KeyNotFoundError) {
                                if is_throwing_fcall(default_expr, context) {
                                    // Shouldn't happen - throwing defaults should be hoisted
                                    result = result.concat("/* ERROR: unhoisted throwing default */")
                                } else {
                                    result = emit_expr(default_expr, result, 0, ctx, context)
                                }
                            }
                        } else {
                            // No default - use zero
                            result = result.concat("0")
                        }
                    }
                }
                result = result.concat("};\n")
            }
        } else {
            // Struct not found - fall back to zero init
            if not(already_declared) {
                if not(is_mut) {
                    result = result.concat("const ")
                }
                result = result.concat(til_name(struct_type))
                result = result.concat(" ")
                _ := ctx.declared_vars.insert(til_name(name))
            }
            result = result.concat(til_name(name))
            result = emit_struct_literal_assign(result, struct_type, already_declared, "{0}")
        }
    } else if enum_type.len().gt(0) {
        // Enum variable declaration
        if not(already_declared) {
            if not(is_mut) {
                result = result.concat("const ")
            }
            result = result.concat(til_name(enum_type))
            result = result.concat(" ")
            _ := ctx.declared_vars.insert(til_name(name))
        }
        result = result.concat(til_name(name))
        result = result.concat(" = ")
        mut first := Expr()
        expr.params.get(0, first)
        catch (err: IndexOutOfBoundsError) { throw err.msg }
        result = emit_expr(first, result, 0, ctx, context)
        result = result.concat(";\n")
    } else if is_mut {
        // Hoist Dynamic params in RHS if it's an FCall (needed for methods like Set.contains)
        if expr.params.len().gt(0) {
            mut rhs_fcall := Expr()
            expr.params.get(0, rhs_fcall)
            switch rhs_fcall.node_type {
            case NodeType.FCall:
                if rhs_fcall.params.len().gt(1) {
                    mut got_fd := false
                    mut fd := SFuncDef()
                    fd = get_fcall_func_def(context, rhs_fcall)
                    got_fd = true
                    catch (err: KeyNotFoundError) { /* REM None case in Rust */ }
                    if got_fd {
                        mut param_types := Vec.new(ValueType)
                        mut param_by_ref := Vec.new(Bool)
                        for arg: Declaration in fd.args {
                            param_types.push(arg.value_type)
                            param_by_ref.push(param_needs_by_ref(arg))
                        }
                        args := rhs_fcall.params
                        empty_hoisted := Map.new(I64, Str)
                        dynamic_hoisted := hoist_for_dynamic_params(args, param_types, empty_hoisted, result, indent, ctx, context)
                        // Record hoisted Dynamic params in hoisted_exprs with & prefix
                        mut hoisted_map := Map.new(I64, Str)
                        for h: HoistedArg in dynamic_hoisted {
                            if lt(h.index, args.len()) {
                                mut arg_for_key := Expr()
                                args.get(h.index, arg_for_key)
                                expr_addr := to_ptr(arg_for_key)
                                hoisted_val := concat("&", h.temp_var)
                                ctx.hoisted_exprs.set(expr_addr, hoisted_val)
                            }
                            hoisted_map.set(h.index, h.temp_var)
                        }
                        // Hoist struct-returning FCall args when param is by-ref
                        ref_hoisted := hoist_for_ref_params(args, param_by_ref, hoisted_map, result, indent, ctx, context)
                        for rh: HoistedArg in ref_hoisted {
                            if lt(rh.index, args.len()) {
                                mut arg_for_key := Expr()
                                args.get(rh.index, arg_for_key)
                                expr_addr := to_ptr(arg_for_key)
                                hoisted_val := concat("&", rh.temp_var)
                                ctx.hoisted_exprs.set(expr_addr, hoisted_val)
                            }
                        }
                    }
                }
            case:
            }
            catch (err: IndexOutOfBoundsError) { throw err.msg }
            catch (err: AllocError) { throw err.msg }
        }
        // Determine C type - use explicit type annotation if present, otherwise infer
        if not(already_declared) {
            mut is_infer := false
            switch decl.value_type {
            case ValueType.TCustom(s):
                is_infer = s.eq(INFER_TYPE)
            case:
            }
            mut c_type := ""
            if is_infer {
                // INFER_TYPE - infer from RHS
                mut first := Expr()
                expr.params.get(0, first)
                catch (err: IndexOutOfBoundsError) { throw err.msg }
                vt := get_value_type(context, first)
                c_type = til_type_to_c(vt)
            } else {
                // Explicit type annotation - use it
                c_type = til_type_to_c(decl.value_type)
            }
            result = result.concat(c_type)
            result = result.concat(" ")
            _ := ctx.declared_vars.insert(til_name(name))
        }
        result = result.concat(til_name(name))
        if expr.params.len().gt(0) {
            result = result.concat(" = ")
            mut first := Expr()
            expr.params.get(0, first)
            catch (err: IndexOutOfBoundsError) { throw err.msg }
            result = emit_expr(first, result, 0, ctx, context)
        }
        result = result.concat(";\n")
    } else {
        // const declaration
        // Hoist Dynamic params in RHS if it's an FCall (needed for methods like Set.contains)
        if expr.params.len().gt(0) {
            mut rhs_fcall := Expr()
            expr.params.get(0, rhs_fcall)
            switch rhs_fcall.node_type {
            case NodeType.FCall:
                if rhs_fcall.params.len().gt(1) {
                    mut got_fd := false
                    mut fd := SFuncDef()
                    fd = get_fcall_func_def(context, rhs_fcall)
                    got_fd = true
                    catch (err: KeyNotFoundError) { /* REM None case in Rust */ }
                    if got_fd {
                        mut param_types := Vec.new(ValueType)
                        mut param_by_ref := Vec.new(Bool)
                        for arg: Declaration in fd.args {
                            param_types.push(arg.value_type)
                            param_by_ref.push(param_needs_by_ref(arg))
                        }
                        args := rhs_fcall.params
                        empty_hoisted := Map.new(I64, Str)
                        dynamic_hoisted := hoist_for_dynamic_params(args, param_types, empty_hoisted, result, indent, ctx, context)
                        // Record hoisted Dynamic params in hoisted_exprs with & prefix
                        mut hoisted_map := Map.new(I64, Str)
                        for h: HoistedArg in dynamic_hoisted {
                            if lt(h.index, args.len()) {
                                mut arg_for_key := Expr()
                                args.get(h.index, arg_for_key)
                                expr_addr := to_ptr(arg_for_key)
                                hoisted_val := concat("&", h.temp_var)
                                ctx.hoisted_exprs.set(expr_addr, hoisted_val)
                            }
                            hoisted_map.set(h.index, h.temp_var)
                        }
                        // Hoist struct-returning FCall args when param is by-ref
                        ref_hoisted := hoist_for_ref_params(args, param_by_ref, hoisted_map, result, indent, ctx, context)
                        for rh: HoistedArg in ref_hoisted {
                            if lt(rh.index, args.len()) {
                                mut arg_for_key := Expr()
                                args.get(rh.index, arg_for_key)
                                expr_addr := to_ptr(arg_for_key)
                                hoisted_val := concat("&", rh.temp_var)
                                ctx.hoisted_exprs.set(expr_addr, hoisted_val)
                            }
                        }
                    }
                }
            case:
            }
            catch (err: IndexOutOfBoundsError) { throw err.msg }
            catch (err: AllocError) { throw err.msg }
        }
        if not(already_declared) {
            // Determine C type - use explicit type annotation if present, otherwise infer
            mut is_infer := false
            switch decl.value_type {
            case ValueType.TCustom(s):
                is_infer = s.eq(INFER_TYPE)
            case:
            }
            mut c_type := ""
            if is_infer {
                // INFER_TYPE - infer from RHS
                mut first := Expr()
                expr.params.get(0, first)
                catch (err: IndexOutOfBoundsError) { throw err.msg }
                vt := get_value_type(context, first)
                c_type = til_type_to_c(vt)
            } else {
                // Explicit type annotation - use it
                c_type = til_type_to_c(decl.value_type)
            }
            result = result.concat("const ")
            result = result.concat(c_type)
            result = result.concat(" ")
            _ := ctx.declared_vars.insert(til_name(name))
        }
        result = result.concat(til_name(name))
        if expr.params.len().gt(0) {
            result = result.concat(" = ")
            mut first := Expr()
            expr.params.get(0, first)
            catch (err: IndexOutOfBoundsError) { throw err.msg }
            result = emit_expr(first, result, 0, ctx, context)
        }
        result = result.concat(";\n")
    }

    catch (err: AllocError) { throw err.msg }

    return result
}

emit_funcdef := proc(_func_def: SFuncDef, expr: Expr, output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) returns Str throws Str {
    // For now, just inline the function body (we're inside main anyway)
    // TODO: proper function generation with prototypes
    mut result := output
    mut indent_str := ""
    for _ in 0..indent {
        indent_str = indent_str.concat("    ")
    }
    result = result.concat(indent_str)
    result = result.concat("{\n")
    for stmt: Expr in expr.params {
        result = emit_expr(stmt, result, indent.add(1), ctx, context)
    }
    result = result.concat(indent_str)
    result = result.concat("}\n")
    return result
}

emit_assignment := proc(name: Str, expr: Expr, output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) returns Str throws Str {
    mut result := output
    mut indent_str := ""
    for _ in 0..indent {
        indent_str = indent_str.concat("    ")
    }

    if expr.params.len().gt(0) {
        mut rhs_expr := Expr()
        expr.params.get(0, rhs_expr)
        catch (err: IndexOutOfBoundsError) { throw err.msg }

        // Check if RHS is a call to a throwing function
        switch rhs_expr.node_type {
        case NodeType.FCall:
            mut got_func_def := false
            mut func_def := SFuncDef()
            func_def = get_fcall_func_def(context, rhs_expr)
            got_func_def = true
            catch (err: KeyNotFoundError) {
                // Not a known function - fall through
            }
            if got_func_def {
                throw_types := func_def.throw_types
                if throw_types.len().gt(0) {
                    // RHS is a throwing function call - emit with error propagation
                    // (typer should ensure non-throwing context doesn't call throwing functions without catch)
                    // Pass raw name so function can properly handle field access on mut params
                    result = emit_throwing_call_propagate(rhs_expr, throw_types, "", name, result, indent, ctx, context)
                    return result
                }
            }
        case:
        }

        // Hoist any throwing function calls nested in the RHS expression
        result = hoist_throwing_expr(rhs_expr, result, indent, ctx, context)
    }

    // Regular assignment
    result = result.concat(indent_str)
    // Check if assignment target is a field access on a mut param (self.field)
    // If so, emit with -> instead of .
    if name.contains(".") {
        // Find dot position manually (no index_of in TIL Str)
        mut dot_pos := 0
        mut name_idx := 0
        while name_idx.lt(name.len()) {
            mut ch := name.get_char(name_idx)
            if ch.eq(".") {
                dot_pos = name_idx
                name_idx = name.len()  // break
            } else {
                name_idx = name_idx.add(1)
            }
        }
        catch (err: IndexOutOfBoundsError) { throw err.msg }
        catch (err: AllocError) { throw err.msg }
        mut base := name.get_substr(0, dot_pos)
        mut rest := name.get_substr(dot_pos.add(1), name.len())
        catch (err: IndexOutOfBoundsError) { throw err.msg }
        catch (err: AllocError) { throw err.msg }
        if ctx.current_ref_params.contains(base) {
            // Mut param field access: til_self->field
            result = result.concat(til_name(base))
            result = result.concat("->")
            result = result.concat(rest)
        } else {
            result = result.concat(til_name(name))
        }
    } else if ctx.current_ref_params.contains(name) {
        // Direct assignment to mut param: *til_self = value
        result = result.concat("*")
        result = result.concat(til_name(name))
    } else {
        result = result.concat(til_name(name))
    }
    result = result.concat(" = ")
    if expr.params.len().gt(0) {
        mut first := Expr()
        expr.params.get(0, first)
        catch (err: IndexOutOfBoundsError) { throw err.msg }
        result = emit_expr(first, result, 0, ctx, context)
    }
    result = result.concat(";\n")
    return result
}

emit_return := proc(expr: Expr, output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) returns Str throws Str {
    mut result := output
    mut indent_str := ""
    for _ in 0..indent {
        indent_str = indent_str.concat("    ")
    }
    is_throwing := ctx.current_throw_types.len().gt(0)

    if is_throwing {
        // Throwing function: store value through _ret pointer and return 0 (success)
        if expr.params.len().gt(0) {
            mut return_expr := Expr()
            expr.params.get(0, return_expr)
            catch (err: IndexOutOfBoundsError) { throw err.msg }

            // Check if return expression is a call to a throwing function
            switch return_expr.node_type {
            case NodeType.FCall:
                mut got_func_def := false
                mut func_def := SFuncDef()
                func_def = get_fcall_func_def(context, return_expr)
                got_func_def = true
                catch (err: KeyNotFoundError) {
                    // Not a known function - fall through to regular handling
                }
                if got_func_def {
                    throw_types := func_def.throw_types
                    if throw_types.len().gt(0) {
                        // Return expression is a throwing function call - emit with error propagation
                        // The result will be stored via the assign_name "*_ret"
                        result = emit_throwing_call_propagate(return_expr, throw_types, "", "*_ret", result, indent, ctx, context)
                        result = result.concat(indent_str)
                        result = result.concat("return 0;\n")
                        return result
                    }
                }
            case:
            }

            // Hoist any throwing function calls nested in the return expression
            result = hoist_throwing_expr(return_expr, result, indent, ctx, context)

            // Regular return value - just emit it
            result = result.concat(indent_str)
            result = result.concat("*_ret = ")
            result = emit_expr(return_expr, result, 0, ctx, context)
            result = result.concat(";\n")
        }
        result = result.concat(indent_str)
        result = result.concat("return 0;\n")
    } else {
        // Non-throwing function: normal return
        // Check if return expression is a variadic function call
        if expr.params.len().gt(0) {
            mut return_expr := Expr()
            expr.params.get(0, return_expr)
            catch (err: IndexOutOfBoundsError) { throw err.msg }

            mut handled_variadic_return := false
            switch return_expr.node_type {
            case NodeType.FCall:
                mut variadic_info := detect_variadic_fcall(return_expr, ctx)
                // Variadic call in return - need to hoist it
                // First, hoist any nested throwing/variadic calls in the arguments
                mut hoisted_vec := hoist_throwing_args(return_expr.params, result, indent, ctx, context)
                // Convert Vec<HoistedArg> to Map<I64, Str>
                mut hoisted := Map.new(I64, Str)
                for entry: HoistedArg in hoisted_vec {
                    hoisted.set(entry.index, entry.temp_var)
                    catch (err: AllocError) { throw "emit_return: AllocError in hoist_throwing_args" }
                }

                mut variadic_args := Vec.new(Expr)
                mut vi := variadic_info.regular_count.add(1)
                while vi.lt(return_expr.params.len()) {
                    mut varg := Expr()
                    return_expr.params.get(vi, varg)
                    variadic_args.push(varg)
                    vi = vi.add(1)
                    catch (err: IndexOutOfBoundsError) { throw err.msg }
                    catch (err: AllocError) { throw err.msg }
                }
                mut arr_var := hoist_variadic_args(variadic_info.elem_type, variadic_args, hoisted, variadic_info.regular_count, result, indent, ctx, context)
                catch (err: AllocError) { throw err.msg }

                // Emit the function call storing result
                mut temp_var := next_mangled(ctx)
                mut ret_type := "int"
                mut ret_vt := get_value_type(context, return_expr)
                ret_type = til_type_to_c(ret_vt)
                result = result.concat(indent_str)
                result = result.concat(ret_type)
                result = result.concat(" ")
                result = result.concat(temp_var)
                result = result.concat(" = ")

                // Emit the function name
                mut func_name := get_fcall_func_name(return_expr)
                if func_name.len().gt(0) {
                    result = result.concat(til_func_name(func_name))
                }
                result = result.concat("(")

                // Emit regular args
                mut first := true
                mut ri := 0
                while ri.lt(variadic_info.regular_count) {
                    if not(first) {
                        result = result.concat(", ")
                    }
                    first = false
                    mut arg := Expr()
                    return_expr.params.get(ri.add(1), arg)
                    result = emit_expr(arg, result, 0, ctx, context)
                    ri = ri.add(1)
                    catch (err: IndexOutOfBoundsError) { throw err.msg }
                }

                // Emit variadic array pointer
                if not(first) {
                    result = result.concat(", ")
                }
                result = result.concat("&")
                result = result.concat(arr_var)
                result = result.concat(");\n")

                // Delete array
                result = result.concat(indent_str)
                result = result.concat(TIL_PREFIX)
                result = result.concat("Array_delete(&")
                result = result.concat(arr_var)
                result = result.concat(");\n")

                // Return the result
                result = result.concat(indent_str)
                result = result.concat("return ")
                result = result.concat(temp_var)
                result = result.concat(";\n")
                handled_variadic_return = true
                catch (err: KeyNotFoundError) {
                    // Not a variadic function - fall through to regular return
                }
            case:
            }
            if handled_variadic_return {
                return result
            }
        }

        // Hoist any throwing function calls nested in the return expression
        if expr.params.len().gt(0) {
            mut return_expr := Expr()
            expr.params.get(0, return_expr)
            catch (err: IndexOutOfBoundsError) { throw err.msg }
            result = hoist_throwing_expr(return_expr, result, indent, ctx, context)
        }

        // Regular non-variadic return
        result = result.concat(indent_str)
        result = result.concat("return")
        if expr.params.len().gt(0) {
            result = result.concat(" ")
            mut first_param := Expr()
            expr.params.get(0, first_param)
            catch (err: IndexOutOfBoundsError) { throw err.msg }
            result = emit_expr(first_param, result, 0, ctx, context)
        }
        result = result.concat(";\n")
    }

    return result
}

// Emit a throwing function call's name and arguments for hoisting
// If variadic_arr_var is non-empty, use that pre-constructed array instead of building one
emit_fcall_name_and_args_for_throwing := proc(expr: Expr, temp_var: Str, temp_suffix: Str, throw_types: Vec, nested_hoisted: Map, variadic_arr_var: Str, mut output: Str, mut ctx: CodegenContext, mut context: Context) returns Str throws Str {
    if lt(expr.params.len(), 1) {
        throw "emit_fcall_name_and_args_for_throwing: FCall with no params"
    }

    mut first := Expr()
    expr.params.get(0, first)

    mut func_name := ""
    mut type_receiver_name := ""
    mut has_type_receiver := false
    mut type_depth := 0  // Number of params (for chained access like self._len.eq)

    switch first.node_type {
    case NodeType.Identifier(name):
        if first.params.len().gt(0) {
            // Type-qualified: Type.method()
            // AST is FLAT: a.b.c is Identifier("a") { params: [Identifier("b"), Identifier("c")] }
            // Method is the LAST param, not the first
            type_depth = first.params.len()
            mut method_idx := type_depth.sub(1)
            mut method_expr := Expr()
            first.params.get(method_idx, method_expr)
            switch method_expr.node_type {
            case NodeType.Identifier(method_name):
                func_name = method_name
                type_receiver_name = name
                has_type_receiver = true
            case:
                throw "emit_fcall_name_and_args_for_throwing: type-qualified method not Identifier"
            }
            catch (err: IndexOutOfBoundsError) { throw err.msg }
        } else {
            func_name = name
        }
    case:
        throw "emit_fcall_name_and_args_for_throwing: first param not Identifier"
    }

    catch (err: IndexOutOfBoundsError) { throw err.msg }

    // Emit function name with potential type-qualified mangling (e.g., I64.to_str -> til_I64_to_str)
    // For struct methods, we already emitted til_Type_, so just add method name
    // For top-level functions, add til_ prefix via til_func_name
    mut is_struct_method := false
    if has_type_receiver {
        mut first_char := type_receiver_name.get_char(0)
        // TODO: Use proper type info from AST instead of uppercase hack (see interpreter.til)
        if first_char.is_uppercase() {
            // Type-qualified: Type.func -> til_Type_func
            output = output.concat(TIL_PREFIX)
            output = output.concat(type_receiver_name)
            output = output.concat("_")
            is_struct_method = true
        } else {
            // Instance call: instance.method -> til_Type_method (only if mangled name exists)
            mut receiver_type := ValueType.TCustom("")
            mut symbol := context.scope_stack.lookup_symbol(type_receiver_name)
            receiver_type = symbol.value_type
            switch receiver_type {
            case ValueType.TCustom(type_name):
                mut candidate := concat(type_name, ".")
                candidate = candidate.concat(func_name)
                _ := context.scope_stack.lookup_func(candidate)
                // If lookup succeeded, use mangled name
                output = output.concat(TIL_PREFIX)
                output = output.concat(type_name)
                output = output.concat("_")
                is_struct_method = true
            case:
                // Skip
            }
            catch (err: KeyNotFoundError) { }
        }
        catch (err: IndexOutOfBoundsError) { throw err.msg }
        catch (err: AllocError) { throw err.msg }
    }

    if is_struct_method {
        output = output.concat(func_name)
    } else {
        output = output.concat(til_func_name(func_name))
    }
    output = output.concat("(&")
    output = output.concat(temp_var)

    // Add error output pointers
    mut err_idx := 0
    while err_idx.lt(throw_types.len()) {
        output = output.concat(", &_err")
        output = output.concat(I64.to_str(err_idx))
        output = output.concat("_")
        output = output.concat(temp_suffix)
        err_idx = err_idx.add(1)
    }

    // Emit receiver for instance call
    if has_type_receiver {
        mut first_char := type_receiver_name.get_char(0)
        if not(first_char.is_uppercase()) {
            output = output.concat(", ")
            // Check if called function's self param is mut and if receiver is a mut param
            mut called_self_is_mut := false
            mut type_name := ""
            mut symbol := context.scope_stack.lookup_symbol(type_receiver_name)
            switch symbol.value_type {
            case ValueType.TCustom(tname):
                type_name = tname
                mut got_lookup := false
                mut lookup_fd := SFuncDef()
                lookup_fd = get_fcall_func_def(context, expr)
                got_lookup = true
                catch (err: KeyNotFoundError) { /* REM None case in Rust */ }
                if got_lookup {
                    if lookup_fd.args.len().gt(0) {
                        mut first_arg := Declaration()
                        lookup_fd.args.get(0, first_arg)
                        called_self_is_mut = first_arg.is_mut
                    }
                    catch (err: IndexOutOfBoundsError) { throw err.msg }
                }
            case:
                // Not a custom type
            }
            catch (err: KeyNotFoundError) { }
            mut receiver_is_ref_param := ctx.current_ref_params.contains(type_receiver_name)
            mut receiver_is_variadic_param := Map.contains_key(ctx.current_variadic_params, type_receiver_name)
            mut receiver_is_pointer := receiver_is_ref_param.or(receiver_is_variadic_param)
            mut needs_deref_close := false
            if called_self_is_mut.and(not(receiver_is_pointer)) {
                // Called function takes mut self, receiver is not a pointer - add &
                output = output.concat("&")
            } else if not(called_self_is_mut).and(receiver_is_pointer).and(eq(type_depth, 1)) {
                // Called function takes non-mut self, but receiver is a pointer - dereference
                output = output.concat("(*")
                needs_deref_close = true
            }
            output = output.concat(til_name(type_receiver_name))
            // Emit intermediate field accesses (all except the last which is the method)
            mut field_idx := 0
            while field_idx.lt(type_depth.sub(1)) {
                mut field_expr := Expr()
                first.params.get(field_idx, field_expr)
                switch field_expr.node_type {
                case NodeType.Identifier(field_name):
                    // Use -> for first field access if base is pointer (mut param or variadic param), . for rest
                    if field_idx.eq(0).and(receiver_is_pointer) {
                        output = output.concat("->")
                    } else {
                        output = output.concat(".")
                    }
                    output = output.concat(field_name)
                case:
                    // skip
                }
                field_idx = field_idx.add(1)
            }
            if needs_deref_close {
                output = output.concat(")")
            }
        }
        catch (err: IndexOutOfBoundsError) { throw err.msg }
        catch (err: AllocError) { throw err.msg }
    }

    // TODO: Check if this is a variadic function call and use variadic_arr_var
    // For now, emit remaining arguments (using hoisted temps where available)
    mut arg_idx := 0
    // Skip first param (function name), start from index 1
    mut i := 1
    while i.lt(expr.params.len()) {
        output = output.concat(", ")
        // Check if this arg was hoisted (Map lookup)
        mut hoisted_var := ""
        mut found_hoisted := false
        nested_hoisted.get(arg_idx, hoisted_var)
        found_hoisted = true
        catch (err: KeyNotFoundError) { }
        if found_hoisted {
            output = output.concat(hoisted_var)
        } else {
            mut arg := Expr()
            expr.params.get(i, arg)
            output = emit_expr(arg, output, 0, ctx, context)
            catch (err: IndexOutOfBoundsError) { throw err.msg }
        }
        arg_idx = arg_idx.add(1)
        i = i.add(1)
    }

    output = output.concat(")")
    return output
}

// Hoist throwing function calls from arguments (recursively)
// Returns a list of hoisted args with their temp var names
// Also modifies output to include hoisting code
hoist_throwing_args := proc(args: Vec, mut output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) returns Vec throws Str {
    mut hoisted := Vec.new(HoistedArg)
    mut indent_str := ""
    for _ in 0..indent {
        indent_str = indent_str.concat("    ")
    }

    mut idx := 0
    while idx.lt(args.len()) {
        mut arg := Expr()
        args.get(idx, arg)

        // Check if it's a throwing call
        mut throwing_fd_found := false
        mut throwing_fd := SFuncDef()
        switch arg.node_type {
        case NodeType.FCall:
            mut fd := get_fcall_func_def(context, arg)
            if fd.throw_types.len().gt(0) {
                throwing_fd = fd
                throwing_fd_found = true
            }
            catch (err: KeyNotFoundError) { /* REM None case in Rust */ }
        case:
        }
        // Check if it's a variadic call (even if not throwing)
        mut has_variadic := false
        mut variadic_regular_count := 0
        mut variadic_elem_type := ""
        mut variadic_info := detect_variadic_fcall(arg, ctx)
        has_variadic = true
        variadic_regular_count = variadic_info.regular_count
        variadic_elem_type = variadic_info.elem_type
        catch (err: KeyNotFoundError) {
            // Not a variadic function - defaults stay
        }

        // Handle throwing calls (may also be variadic)
        if throwing_fd_found {
            // RECURSIVELY hoist any throwing calls in this call's arguments first
            mut nested_hoisted := Map.new(I64, Str)
            if arg.params.len().gt(1) {
                mut nested_vec := hoist_throwing_args(arg.params, output, indent, ctx, context)
                // Convert Vec<HoistedArg> to Map<I64, Str>
                mut nhi := 0
                while nhi.lt(nested_vec.len()) {
                    mut entry := HoistedArg()
                    nested_vec.get(nhi, entry)
                    nested_hoisted.set(entry.index, entry.temp_var)
                    nhi = nhi.add(1)
                }
            }

            mut temp_var := next_mangled(ctx)

            // Determine C type for temp variable
            mut c_type := ""
            if throwing_fd.return_types.len().gt(0) {
                mut first_ret := ValueType.TCustom("")
                throwing_fd.return_types.get(0, first_ret)
                c_type = til_type_to_c(first_ret)
                catch (err: IndexOutOfBoundsError) { throw err.msg }
            } else {
                throw "Cannot hoist throwing call with no return type"
            }

            // Declare temp variable
            output = output.concat(indent_str)
            output = output.concat(c_type)
            output = output.concat(" ")
            output = output.concat(temp_var)
            output = output.concat(";\n")

            // Declare error variables for each throw type
            mut temp_suffix := next_mangled(ctx)
            mut err_idx := 0
            while err_idx.lt(throwing_fd.throw_types.len()) {
                mut throw_type := ValueType.TCustom("")
                throwing_fd.throw_types.get(err_idx, throw_type)
                switch throw_type {
                case ValueType.TCustom(type_name):
                    output = output.concat(indent_str)
                    output = output.concat(type_name)
                    output = output.concat(" _err")
                    output = output.concat(I64.to_str(err_idx))
                    output = output.concat("_")
                    output = output.concat(temp_suffix)
                    output = output.concat(";\n")
                case:
                    // Skip non-custom types
                }
                err_idx = err_idx.add(1)
                catch (err: IndexOutOfBoundsError) { throw err.msg }
            }

            // Detect and construct variadic array if needed
            mut variadic_arr_var := ""
            mut has_variadic_arr := false
            if has_variadic {
                mut variadic_args := Vec.new(Expr)
                mut vi := variadic_regular_count.add(1)
                while vi.lt(arg.params.len()) {
                    mut varg := Expr()
                    arg.params.get(vi, varg)
                    variadic_args.push(varg)
                    vi = vi.add(1)
                    catch (err: IndexOutOfBoundsError) { throw err.msg }
                }
                variadic_arr_var = hoist_variadic_args(variadic_elem_type, variadic_args, nested_hoisted, variadic_regular_count, output, indent, ctx, context)
                has_variadic_arr = true
            }

            // Emit the function call with output pointers
            output = output.concat(indent_str)
            output = output.concat("int _status_")
            output = output.concat(temp_suffix)
            output = output.concat(" = ")
            output = emit_fcall_name_and_args_for_throwing(arg, temp_var, temp_suffix, throwing_fd.throw_types, nested_hoisted, variadic_arr_var, output, ctx, context)
            output = output.concat(";\n")

            // Emit error checking - propagate if any error occurred
            output = output.concat(indent_str)
            output = output.concat("if (_status_")
            output = output.concat(temp_suffix)
            output = output.concat(" != 0) {\n")

            // Propagate error to current function's error pointers
            err_idx = 0
            while err_idx.lt(throwing_fd.throw_types.len()) {
                mut throw_type := ValueType.TCustom("")
                throwing_fd.throw_types.get(err_idx, throw_type)
                switch throw_type {
                case ValueType.TCustom(type_name):
                    // Find matching throw type in current function
                    mut curr_idx := 0
                    while curr_idx.lt(ctx.current_throw_types.len()) {
                        mut curr_throw := ValueType.TCustom("")
                        ctx.current_throw_types.get(curr_idx, curr_throw)
                        switch curr_throw {
                        case ValueType.TCustom(curr_type_name):
                            if curr_type_name.eq(type_name) {
                                output = output.concat(indent_str)
                                output = output.concat("    if (_status_")
                                output = output.concat(temp_suffix)
                                output = output.concat(" == ")
                                output = output.concat(err_idx.add(1).to_str())
                                output = output.concat(") { *_err")
                                output = output.concat(curr_idx.add(1).to_str())
                                output = output.concat(" = _err")
                                output = output.concat(err_idx.to_str())
                                output = output.concat("_")
                                output = output.concat(temp_suffix)
                                output = output.concat("; return ")
                                output = output.concat(curr_idx.add(1).to_str())
                                output = output.concat("; }\n")
                                break
                            }
                        case:
                        }
                        curr_idx = curr_idx.add(1)
                        catch (err: IndexOutOfBoundsError) { throw err.msg }
                    }
                case:
                    // Skip
                }
                err_idx = err_idx.add(1)
                catch (err: IndexOutOfBoundsError) { throw err.msg }
            }

            output = output.concat(indent_str)
            output = output.concat("}\n")

            // Emit Array.delete if variadic array was constructed
            if has_variadic_arr {
                output = output.concat(indent_str)
                output = output.concat(TIL_PREFIX)
                output = output.concat("Array_delete(&")
                output = output.concat(variadic_arr_var)
                output = output.concat(");\n")
            }

            // Record in hoisted_exprs map using expression address
            arg_addr := to_ptr(arg)
            ctx.hoisted_exprs.set(arg_addr, temp_var)

            mut hoisted_arg := HoistedArg()
            hoisted_arg.index = idx
            hoisted_arg.temp_var = temp_var
            hoisted.push(hoisted_arg)
        }
        // Handle non-throwing variadic calls
        else if has_variadic {
            // RECURSIVELY hoist any throwing/variadic calls in this call's arguments first
            mut nested_hoisted := Map.new(I64, Str)
            if arg.params.len().gt(1) {
                mut nested_vec := hoist_throwing_args(arg.params, output, indent, ctx, context)
                // Convert Vec<HoistedArg> to Map<I64, Str>
                mut nhi := 0
                while nhi.lt(nested_vec.len()) {
                    mut entry := HoistedArg()
                    nested_vec.get(nhi, entry)
                    nested_hoisted.set(entry.index, entry.temp_var)
                    nhi = nhi.add(1)
                }
            }

            mut temp_var := next_mangled(ctx)

            // Determine return type from function
            mut c_type := "int"
            mut got_fd := false
            mut fd := SFuncDef()
            fd = get_fcall_func_def(context, arg)
            got_fd = true
            catch (err: KeyNotFoundError) { /* REM None case in Rust */ }
            if got_fd {
                if fd.return_types.len().gt(0) {
                    mut ret_type := ValueType.TCustom("")
                    fd.return_types.get(0, ret_type)
                    c_type = til_type_to_c(ret_type)
                    catch (err: IndexOutOfBoundsError) { throw err.msg }
                }
            }

            // Declare temp variable
            output = output.concat(indent_str)
            output = output.concat(c_type)
            output = output.concat(" ")
            output = output.concat(temp_var)
            output = output.concat(";\n")

            // Construct variadic array
            mut variadic_args := Vec.new(Expr)
            mut vi := variadic_regular_count.add(1)
            while vi.lt(arg.params.len()) {
                mut varg := Expr()
                arg.params.get(vi, varg)
                variadic_args.push(varg)
                vi = vi.add(1)
            }
            mut variadic_arr_var := hoist_variadic_args(variadic_elem_type, variadic_args, nested_hoisted, variadic_regular_count, output, indent, ctx, context)

            // Emit the function call (non-throwing, so direct assignment)
            output = output.concat(indent_str)
            output = output.concat(temp_var)
            output = output.concat(" = ")

            // Emit function name
            mut func_name := get_fcall_func_name(arg)
            if func_name.len().gt(0) {
                output = output.concat(til_func_name(func_name))
            }
            output = output.concat("(")

            // Emit regular args (using nested hoisted temps)
            mut first := true
            mut ri := 0
            while ri.lt(variadic_regular_count) {
                if not(first) {
                    output = output.concat(", ")
                }
                first = false
                // Check if this arg was hoisted (Map lookup)
                mut found_hoisted := false
                mut hoisted_temp := ""
                nested_hoisted.get(ri, hoisted_temp)
                found_hoisted = true
                catch (err: KeyNotFoundError) { }
                if found_hoisted {
                    output = output.concat(hoisted_temp)
                } else {
                    mut param := Expr()
                    arg.params.get(ri.add(1), param)
                    output = emit_expr(param, output, 0, ctx, context)
                }
                ri = ri.add(1)
                catch (err: IndexOutOfBoundsError) { throw err.msg }
            }

            // Emit variadic array pointer
            if not(first) {
                output = output.concat(", ")
            }
            output = output.concat("&")
            output = output.concat(variadic_arr_var)

            output = output.concat(");\n")

            // Delete the variadic array
            output = output.concat(indent_str)
            output = output.concat(TIL_PREFIX)
            output = output.concat("Array_delete(&")
            output = output.concat(variadic_arr_var)
            output = output.concat(");\n")

            // Record in hoisted_exprs map using expression address
            arg_addr := to_ptr(arg)
            ctx.hoisted_exprs.set(arg_addr, temp_var)

            mut hoisted_arg := HoistedArg()
            hoisted_arg.index = idx
            hoisted_arg.temp_var = temp_var
            hoisted.push(hoisted_arg)
        }
        // Handle non-throwing, non-variadic FCalls - still need to recurse into their arguments
        // to find deeply nested variadic/throwing calls (e.g., not(or(false)))
        else {
            switch arg.node_type {
            case NodeType.FCall:
                if arg.params.len().gt(1) {
                    // Recurse to hoist any nested throwing/variadic calls
                    _ := hoist_throwing_args(arg.params, output, indent, ctx, context)
                } else {
                    // Handle struct literal arguments (no args, or only named args)
                    // For struct literals like Expr() passed as arguments, their throwing default field values
                    // (e.g., params = Vec.new(Expr)) need to be hoisted BEFORE the struct literal is created
                    func_name := get_fcall_func_name(arg)
                    if func_name.len().gt(0) {
                        mut is_struct := false
                        _ := context.scope_stack.lookup_struct(func_name)
                        is_struct = true
                        catch (err: KeyNotFoundError) { }
                        if is_struct {
                            // This is a struct literal - hoist its throwing defaults
                            _ := hoist_throwing_expr(arg, output, indent, ctx, context)
                        }
                    }
                }
            case:
                // Bug #55 fix: Handle non-FCall arguments (like NamedArg) by recursing via hoist_throwing_expr
                // This ensures throwing calls inside named args are hoisted properly
                _ := hoist_throwing_expr(arg, output, indent, ctx, context)
            }
        }
        idx = idx.add(1)

        catch (err: IndexOutOfBoundsError) { throw err.msg }
        catch (err: AllocError) { throw err.msg }
    }

    return hoisted
}

// Hoist non-lvalue args (string literals, function calls) when the param type is Dynamic
// Returns additional hoisted args to merge with throwing hoists
hoist_for_dynamic_params := proc(args: Vec, param_types: Vec, already_hoisted: Map, mut output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) returns Vec throws Str, AllocError {
    mut hoisted := Vec.new(HoistedArg)
    mut indent_str := ""
    for _ in 0..indent {
        indent_str = indent_str.concat("    ")
    }

    mut idx := 0
    while idx.lt(args.len()) {
        mut arg := Expr()
        args.get(idx, arg)

        // Skip if already hoisted by throwing args hoister or by a previous function call
        arg_addr := to_ptr(arg)
        if already_hoisted.contains_key(idx).or(ctx.hoisted_exprs.contains_key(arg_addr)) {
            idx = idx.add(1)
            continue
        }

        // Check if param type is Dynamic
        mut is_dynamic := false
        if idx.lt(param_types.len()) {
            mut pt := ValueType.TCustom("")
            param_types.get(idx, pt)
            switch pt {
            case ValueType.TCustom(type_name):
                if type_name.eq("Dynamic") {
                    is_dynamic = true
                }
            case:
            }
        }

        if not(is_dynamic) {
            idx = idx.add(1)
            continue
        }

        // Check if arg needs hoisting (not a simple identifier)
        mut needs_hoisting := true
        switch arg.node_type {
        case NodeType.Identifier(name):
            // Simple identifier with no params is an lvalue, doesn't need hoisting
            if arg.params.len().eq(0) {
                needs_hoisting = false
            }
        case NodeType.LLiteral(lit):
            // Literals need hoisting
            needs_hoisting = true
        case NodeType.FCall:
            // Function calls need hoisting
            needs_hoisting = true
        case:
            // Default to hoisting for safety
            needs_hoisting = true
        }

        if not(needs_hoisting) {
            idx = idx.add(1)
            continue
        }

        // Determine C type based on what the arg is
        mut c_type := "int64_t"
        switch arg.node_type {
        case NodeType.LLiteral(lit):
            switch lit {
            case Literal.Str(s):
                c_type = concat(TIL_PREFIX, "Str")
            case Literal.Number(n):
                c_type = "int64_t"
            case Literal.List(l):
                c_type = "int64_t"  // TODO: proper list type
            }
        case NodeType.Identifier(var_name):
            // Could be: enum constructor (Type.Variant), field access (var.field), or method call
            if arg.params.len().gt(0) {
                mut first_param := Expr()
                arg.params.get(0, first_param)
                switch first_param.node_type {
                case NodeType.Identifier(field_or_variant):
                    // Check if this is an enum constructor
                    mut is_enum := false
                    mut enum_def := context.scope_stack.lookup_enum(var_name)
                    is_enum = true
                    catch (err: KeyNotFoundError) { }
                    if is_enum {
                        // Bug #38 fix: use helper method
                        if enum_def.contains_key(field_or_variant) {
                            c_type = til_name(var_name)
                        }
                    } else {
                        // Field access - look up variable's type, then field's type
                        mut sym := context.scope_stack.lookup_symbol(var_name)
                        switch sym.value_type {
                        case ValueType.TCustom(struct_name):
                            mut struct_def := context.scope_stack.lookup_struct(struct_name)
                            // Find the field in the struct
                            mut found_field := false
                            for member: Declaration in struct_def.members {
                                if member.name.eq(field_or_variant) {
                                    c_type = til_type_to_c(member.value_type)
                                    found_field = true
                                }
                            }
                        case:
                        }
                        catch (err: KeyNotFoundError) { }
                    }
                case:
                }
                catch (err: IndexOutOfBoundsError) { throw err.msg }
            }
        case NodeType.FCall:
            // For function calls, try to determine return type
            mut got_fd := false
            mut fd := SFuncDef()
            fd = get_fcall_func_def(context, arg)
            got_fd = true
            catch (err: KeyNotFoundError) { /* REM None case in Rust */ }
            if got_fd {
                if fd.throw_types.len().gt(0) {
                    // Throwing call
                    if fd.return_types.len().gt(0) {
                        mut first_ret := ValueType.TCustom("")
                        fd.return_types.get(0, first_ret)
                        c_type = til_type_to_c(first_ret)
                        catch (err: IndexOutOfBoundsError) { throw err.msg }
                    } else {
                        throw arg.lang_error(context.path, "ccodegen", "Throwing call has no return type")
                    }
                } else {
                    // Non-throwing function call - use return type
                    if fd.return_types.len().gt(0) {
                        mut ret_type := ValueType.TCustom("")
                        fd.return_types.get(0, ret_type)
                        c_type = til_type_to_c(ret_type)
                        catch (err: IndexOutOfBoundsError) { throw err.msg }
                    }
                }
            } else {
                // Check if this is a struct constructor like Ptr()
                mut func_name := get_fcall_func_name(arg)
                if func_name.len().gt(0) {
                    mut struct_def := context.scope_stack.lookup_struct(func_name)
                    c_type = til_name(func_name)
                    catch (err: KeyNotFoundError) { }
                }
            }
        case:
            c_type = "int64_t"  // Default to int64_t for unknown types
        }

        mut temp_var := next_mangled(ctx)

        // Emit: til_Str _tmpXX = <expression>;
        output = output.concat(indent_str)
        output = output.concat(c_type)
        output = output.concat(" ")
        output = output.concat(temp_var)
        output = output.concat(" = ")
        output = emit_expr(arg, output, 0, ctx, context)
        output = output.concat(";\n")

        mut hoisted_arg := HoistedArg()
        hoisted_arg.index = idx
        hoisted_arg.temp_var = temp_var
        hoisted.push(hoisted_arg)

        idx = idx.add(1)

        catch (err: IndexOutOfBoundsError) { throw err.msg }
    }

    return hoisted
}

// Hoist variadic arguments into a til_Array
// Returns the array variable name
hoist_variadic_args := proc(elem_type: Str, variadic_args: Vec, already_hoisted: Map, regular_count: I64, mut output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) returns Str throws Str, AllocError {
    mut indent_str := ""
    for _ in 0..indent {
        indent_str = indent_str.concat("    ")
    }

    mut arr_var := next_mangled(ctx)
    mut err_suffix := next_mangled(ctx)
    mut variadic_count := variadic_args.len()

    // Declare the array variable
    output = output.concat(indent_str)
    output = output.concat(TIL_PREFIX)
    output = output.concat("Array ")
    output = output.concat(arr_var)
    output = output.concat(";\n")

    // Declare error vars for Array.new (AllocError) and Array.set (IndexOutOfBoundsError)
    output = output.concat(indent_str)
    output = output.concat(TIL_PREFIX)
    output = output.concat("AllocError _err_alloc_")
    output = output.concat(err_suffix)
    output = output.concat(";\n")

    output = output.concat(indent_str)
    output = output.concat(TIL_PREFIX)
    output = output.concat("IndexOutOfBoundsError _err_idx_")
    output = output.concat(err_suffix)
    output = output.concat(";\n")

    // Hoist variadic args into temp vars (needed to pass address to Array.set)
    mut arg_temps := Vec.new(Str)
    mut c_elem_type := concat(TIL_PREFIX, elem_type)
    mut i := 0
    while i.lt(variadic_count) {
        mut arg := Expr()
        variadic_args.get(i, arg)
        mut hoisted_idx := regular_count.add(i)

        // Check if already hoisted (Map lookup)
        mut found_hoisted := false
        mut temp := ""
        already_hoisted.get(hoisted_idx, temp)
        found_hoisted = true
        catch (err: KeyNotFoundError) { }

        if found_hoisted {
            arg_temps.push(temp)
        } else {
            // Need to hoist into a temp
            mut temp_var := next_mangled(ctx)
            output = output.concat(indent_str)
            output = output.concat(c_elem_type)
            output = output.concat(" ")
            output = output.concat(temp_var)
            output = output.concat(" = ")
            output = emit_expr(arg, output, 0, ctx, context)
            output = output.concat(";\n")
            arg_temps.push(temp_var)
        }

        i = i.add(1)
    }

    // Bug #60: Hoist Type and I64 literals for Array.new call
    // Type param needs hoisting because we can't take address of string literal for const char* const*
    mut type_temp := next_mangled(ctx)
    output = output.concat(indent_str)
    output = output.concat(TIL_PREFIX)
    output = output.concat("Type ")
    output = output.concat(type_temp)
    output = output.concat(" = \"")
    output = output.concat(elem_type)
    output = output.concat("\";\n")

    // Capacity param needs hoisting because we can't take address of integer literal
    mut count_temp := next_mangled(ctx)
    output = output.concat(indent_str)
    output = output.concat(TIL_PREFIX)
    output = output.concat("I64 ")
    output = output.concat(count_temp)
    output = output.concat(" = ")
    output = output.concat(I64.to_str(variadic_count))
    output = output.concat(";\n")

    // Emit Array.new call with error handling
    // int _status = til_Array_new(&arr, &_err_alloc, type_temp, &count_temp);
    output = output.concat(indent_str)
    output = output.concat("int _arr_status_")
    output = output.concat(err_suffix)
    output = output.concat(" = ")
    output = output.concat(TIL_PREFIX)
    output = output.concat("Array_new(&")
    output = output.concat(arr_var)
    output = output.concat(", &_err_alloc_")
    output = output.concat(err_suffix)
    // Bug #60: Type is already const char*, pass by value (no &)
    output = output.concat(", ")
    output = output.concat(type_temp)
    output = output.concat(", &")
    output = output.concat(count_temp)
    output = output.concat(");\n")

    // Emit error check for Array.new
    output = output.concat(indent_str)
    output = output.concat("if (_arr_status_")
    output = output.concat(err_suffix)
    output = output.concat(" != 0) {\n")
    // Propagate AllocError if current function throws it
    for curr_idx in 0..ctx.current_throw_types.len() {
        mut curr_throw := ValueType.TCustom("")
        ctx.current_throw_types.get(curr_idx, curr_throw)
        switch curr_throw {
        case ValueType.TCustom(curr_type_name):
            if curr_type_name.eq("AllocError") {
                output = output.concat(indent_str)
                output = output.concat("    *_err")
                output = output.concat(curr_idx.add(1).to_str())
                output = output.concat(" = _err_alloc_")
                output = output.concat(err_suffix)
                output = output.concat("; return ")
                output = output.concat(curr_idx.add(1).to_str())
                output = output.concat(";\n")
                break
            }
        case:
        }
    }
    catch (err: IndexOutOfBoundsError) { throw err.msg }
    output = output.concat(indent_str)
    output = output.concat("}\n")

    // Emit Array.set for each variadic arg
    i = 0
    while i.lt(arg_temps.len()) {
        mut temp := ""
        arg_temps.get(i, temp)

        output = output.concat(indent_str)
        output = output.concat("_arr_status_")
        output = output.concat(err_suffix)
        output = output.concat(" = ")
        output = output.concat(TIL_PREFIX)
        output = output.concat("Array_set(&_err_idx_")
        output = output.concat(err_suffix)
        output = output.concat(", &")
        output = output.concat(arr_var)
        output = output.concat(", ")
        output = output.concat(I64.to_str(i))
        output = output.concat(", (")
        output = output.concat(TIL_PREFIX)
        output = output.concat("Dynamic*)&")
        output = output.concat(temp)
        output = output.concat(");\n")

        // Error check for Array.set (IndexOutOfBoundsError - shouldn't happen but propagate if thrown)
        output = output.concat(indent_str)
        output = output.concat("if (_arr_status_")
        output = output.concat(err_suffix)
        output = output.concat(" != 0) {\n")
        for curr_idx in 0..ctx.current_throw_types.len() {
            mut curr_throw := ValueType.TCustom("")
            ctx.current_throw_types.get(curr_idx, curr_throw)
            switch curr_throw {
            case ValueType.TCustom(curr_type_name):
                if curr_type_name.eq("IndexOutOfBoundsError") {
                    output = output.concat(indent_str)
                    output = output.concat("    *_err")
                    output = output.concat(curr_idx.add(1).to_str())
                    output = output.concat(" = _err_idx_")
                    output = output.concat(err_suffix)
                    output = output.concat("; return ")
                    output = output.concat(curr_idx.add(1).to_str())
                    output = output.concat(";\n")
                    break
                }
            case:
            }
        }
        catch (err: IndexOutOfBoundsError) { throw err.msg }
        output = output.concat(indent_str)
        output = output.concat("}\n")

        i = i.add(1)
    }

    return arr_var
}

emit_fcall := proc(expr: Expr, output: Str, indent: I64, mut ctx: CodegenContext, mut context: Context) returns Str throws Str {
    // First param is the function name (or Type.method for type-qualified calls)
    mut params_len := expr.params.len()
    if params_len.lt(1) {
        throw "ccodegen: FCall with no params"
    }

    mut first := Expr()
    expr.params.get(0, first)
    catch (err: IndexOutOfBoundsError) { throw err.msg }

    // Get function name (handles both nested identifiers and precomp'd "Type.method" strings)
    mut func_name := get_func_name_string(first)
    catch (err: KeyNotFoundError) { throw first.lang_error(context.path, "ccodegen", "FCall first param not Identifier") }

    // For builtins, we need the original name with dots
    mut orig_func_name := get_til_func_name_string(first)
    catch (err: KeyNotFoundError) { orig_func_name = func_name }

    // Check if this is a call to a nested (hoisted) function - use mangled name
    mut mangled_name := ""
    ctx.nested_func_names.get(orig_func_name, mangled_name)
    if mangled_name.len().gt(0) {
        func_name = mangled_name
    }
    catch (err: KeyNotFoundError) { /* not a nested function, use original name */ }

    // NOTE: Old variadic param method handling (args.len(), args.get()) removed
    // Variadic params are now til_Array* so standard Array methods work

    mut result := output

    // Add indentation
    for _ in 0..indent {
        result = result.concat("    ")
    }

    // Statement level vs expression level:
    // - indent > 0: statement level (can emit hoisting statements, adds semicolon)
    // - indent == 0: expression level (nested in larger expression, no hoisting, no semicolons)
    is_stmt_level := indent.gt(0)

    // Check for type-qualified call on FCall result: func_name(fcall_result, args...)
    // This happens for chained method calls like delimiter.len().eq(0)
    // Parser produces: FCall { params: [Identifier("eq"), FCall { ... }, LLiteral(0)] }
    // Only check this for simple func names (no nested identifiers)
    if first.params.len().eq(0).and(params_len.gteq(2)) {
        mut second_param := Expr()
        expr.params.get(1, second_param)
        switch second_param.node_type {
        case NodeType.FCall:
            // The second param is an FCall result - use get_value_type to get its return type
            mut fcall_ret_type := get_value_type(context, second_param)
            switch fcall_ret_type {
            case ValueType.TCustom(type_name):
                mut candidate := format(type_name, ".", orig_func_name)
                // Try to lookup the function - if found, emit as Type_method
                mut func_info := context.scope_stack.lookup_func(candidate)
                // If we get here, the function was found (otherwise KeyNotFoundError)
                // Emit as Type_method(fcall_result, args...)
                result = result.concat(TIL_PREFIX)
                result = result.concat(type_name)
                result = result.concat("_")
                result = result.concat(orig_func_name)
                result = result.concat("(")
                // First arg is the fcall result
                result = emit_expr(second_param, result, 0, ctx, context)
                // Remaining args
                mut arg_i := 2
                while arg_i.lt(params_len) {
                    result = result.concat(", ")
                    mut arg := Expr()
                    expr.params.get(arg_i, arg)
                    result = emit_expr(arg, result, 0, ctx, context)
                    arg_i = arg_i.add(1)
                }
                result = result.concat(")")
                if is_stmt_level {
                    result = result.concat(";\n")
                }
                return result
            case:
                // Not a custom type, skip
            }
            catch (err: Str) { /* get_value_type failed, fall through */ }
            catch (err: KeyNotFoundError) { /* function not found, fall through */ }
        case:
            // Not an FCall, skip
        }
        catch (err: IndexOutOfBoundsError) { throw err.msg }
    }

    // Hardcoded builtins (compile-time intrinsics that can't be implemented in TIL)
    // loc() is now resolved in precomp phase, so it never reaches here

    // enum_to_str(e) - get enum variant name as string
    // Emits call to til_EnumType_to_str(e) which was generated during enum emission
    if orig_func_name.eq("enum_to_str") {
        if params_len.lt(2) {
            throw "ccodegen: enum_to_str requires 1 argument"
        }
        mut arg := Expr()
        expr.params.get(1, arg)
        // Get the enum type from the argument
        value_type := get_value_type(context, arg)
        switch value_type {
        case ValueType.TCustom(enum_type_name):
            // Verify it's an enum type
            _ := context.scope_stack.lookup_enum(enum_type_name)
            result = result.concat(til_name(enum_type_name)).concat("_to_str(")
            result = emit_expr(arg, result, 0, ctx, context)
            result = result.concat(")")
            if is_stmt_level {
                result = result.concat(";\n")
            }

            catch (err: KeyNotFoundError) {
                throw format("ccodegen: enum_to_str argument '", enum_type_name, "' is not an enum type")
            }
            return result
        case:
            throw format("ccodegen: enum_to_str argument has non-custom type")
        }
        catch (err: IndexOutOfBoundsError) { throw err.msg }
    } else if orig_func_name.eq("size_of") {
        // size_of(T) - runtime type size lookup via til_size_of function
        // Can be called with a literal type name (size_of(Str)) or a Type variable (size_of(T))
        if params_len.lt(2) {
            throw "ccodegen: size_of requires 1 argument"
        }
        mut arg := Expr()
        expr.params.get(1, arg)
        result = result.concat(TIL_PREFIX)
        result = result.concat("size_of(&")
        // If it's a literal type name, emit compound literal
        // If it's a variable, the variable already holds a Type (which is const char*)
        switch arg.node_type {
        case NodeType.Identifier(type_name):
            // Check if this is a Type variable or a literal type name
            // Type variables are declared with value_type TCustom("Type")
            mut is_type_var := false
            mut sym := context.scope_stack.lookup_symbol(type_name)
            switch sym.value_type {
            case ValueType.TCustom(t):
                if t.eq("Type") {
                    is_type_var = true
                }
            case:
            }
            catch (err: KeyNotFoundError) { }
            if is_type_var {
                // Type variable - already a const char*, wrap in Str struct literal
                // Note: Type is const char*, so we construct Str at runtime
                result = format(result, "((", TIL_PREFIX, "Str){(", TIL_PREFIX, "I64)", TIL_PREFIX, type_name, ", strlen(", TIL_PREFIX, type_name, ")})")
            } else {
                // Literal type name - create Str compound literal
                result = emit_str_literal(type_name, result)
            }
        case:
            // Not an identifier - emit as expression (should be Type/const char*)
            result = format(result, "((", TIL_PREFIX, "Str){(", TIL_PREFIX, "I64)")
            result = emit_expr(arg, result, 0, ctx, context)
            result = result.concat(", strlen(")
            result = emit_expr(arg, result, 0, ctx, context)
            result = result.concat(")})")
        }
        result = result.concat(")")
        if is_stmt_level {
            result = result.concat(";\n")
        }
        catch (err: IndexOutOfBoundsError) { throw err.msg }
        catch (err: AllocError) { throw err.msg }
        catch (err: I64_OverflowError) { throw err.msg }
        return result
    } else if orig_func_name.eq("type_as_str") {
        // type_as_str(T) - get type name as string
        // Can be called with a literal type name (type_as_str(Str)) or a Type variable (type_as_str(T))
        // Type variables are passed as const char* at runtime
        if params_len.lt(2) {
            throw "ccodegen: type_as_str requires 1 argument"
        }
        mut arg := Expr()
        expr.params.get(1, arg)
        switch arg.node_type {
        case NodeType.Identifier(type_name):
            // Check if this is a Type variable or a literal type name
            // Type variables are declared with value_type TCustom("Type")
            mut is_type_var := false
            mut sym := context.scope_stack.lookup_symbol(type_name)
            switch sym.value_type {
            case ValueType.TCustom(t):
                if t.eq("Type") {
                    is_type_var = true
                }
            case:
            }
            catch (err: KeyNotFoundError) { }
            if is_type_var {
                // Type variable - already a const char*, wrap in Str struct literal
                result = format(result, "((", TIL_PREFIX, "Str){(", TIL_PREFIX, "I64)", TIL_PREFIX, type_name, ", strlen(", TIL_PREFIX, type_name, ")})")
            } else {
                // Literal type name - create Str compound literal
                result = emit_str_literal(type_name, result)
            }
        case:
            result = emit_str_literal("unknown", result)
        }
        if is_stmt_level {
            result = result.concat(";\n")
        }
        catch (err: IndexOutOfBoundsError) { throw err.msg }
        catch (err: AllocError) { throw err.msg }
        catch (err: I64_OverflowError) { throw err.msg }
        return result
    } else if orig_func_name.eq("to_ptr") {
        // to_ptr(var) - get address of variable
        // For Dynamic params (already void*), just cast without taking address
        if params_len.lt(2) {
            throw "ccodegen: to_ptr requires 1 argument"
        }
        mut arg := Expr()
        expr.params.get(1, arg)
        // Check if arg is a Dynamic parameter (void*) - just use the pointer directly
        // Both mut Dynamic (void**) and non-mut Dynamic (void*) don't need &
        mut is_dynamic_param := false
        switch arg.node_type {
        case NodeType.Identifier(name):
            if arg.params.len().eq(0) {
                mut sym := context.scope_stack.lookup_symbol(name)
                switch sym.value_type {
                case ValueType.TCustom(t):
                    if t.eq("Dynamic") {
                        is_dynamic_param = true
                    }
                case:
                }
                catch (err: KeyNotFoundError) { }
            }
        case:
        }
        result = result.concat("(")
        result = result.concat(TIL_PREFIX)
        result = result.concat("I64)")
        if is_dynamic_param {
            // Dynamic param: just output the variable name (it's already a pointer)
            switch arg.node_type {
            case NodeType.Identifier(name):
                result = result.concat(til_name(name))
            case:
            }
        } else {
            // Check if arg was hoisted with & prefix already
            arg_addr := to_ptr(arg)
            mut found_hoisted := false
            mut temp_var := ""
            ctx.hoisted_exprs.get(arg_addr, temp_var)
            found_hoisted = true
            catch (err: KeyNotFoundError) { }
            if found_hoisted {
                if temp_var.starts_with("&") {
                    // Already has & prefix, just emit it
                    result = result.concat(temp_var)
                } else {
                    result = result.concat("&")
                    result = result.concat(temp_var)
                }
            } else {
                result = result.concat("&")
                result = emit_expr(arg, result, 0, ctx, context)
            }
        }
        if is_stmt_level {
            result = result.concat(";\n")
        }
        catch (err: IndexOutOfBoundsError) { throw err.msg }
        return result
    } else {
        // User-defined function call
        // Check for struct construction: TypeName() or TypeName(field=value) -> (til_TypeName){defaults...}
        // Struct names are PascalCase, no underscore in func_name (meaning not Type_method)
        // Check for named args to detect TypeName(field=value) pattern
        mut first_char := func_name.get_substr(0, 1)
        mut has_named_args := false
        mut named_arg_idx := 1
        while named_arg_idx.lt(params_len) {
            mut named_arg := Expr()
            expr.params.get(named_arg_idx, named_arg)
            switch named_arg.node_type {
            case NodeType.NamedArg(field_name):
                has_named_args = true
            case:
            }
            named_arg_idx = named_arg_idx.add(1)
        }
        catch (err: IndexOutOfBoundsError) { throw err.msg }
        if first_char.is_uppercase().and(not(func_name.contains("_"))).and(params_len.eq(1).or(has_named_args)) {
            // Look up struct definition to get default values
            mut struct_found := false
            mut struct_def := context.scope_stack.lookup_struct(func_name)
            struct_found = true
            catch (err: KeyNotFoundError) { }

            if struct_found {
                // Build map of named arg values
                mut named_values := Map.new(Str, Expr)
                mut nav_idx := 1
                while nav_idx.lt(params_len) {
                    mut nav_expr := Expr()
                    expr.params.get(nav_idx, nav_expr)
                    switch nav_expr.node_type {
                    case NodeType.NamedArg(field_name):
                        if nav_expr.params.len().gt(0) {
                            mut value_expr := Expr()
                            nav_expr.params.get(0, value_expr)
                            named_values.set(field_name, value_expr)
                            catch (err: IndexOutOfBoundsError) { throw err.msg }
                        }
                    case:
                    }
                    nav_idx = nav_idx.add(1)
                }
                catch (err: IndexOutOfBoundsError) { throw err.msg }

                if struct_def.members.len().eq(0).or(struct_def.default_values.len().eq(0).and(named_values.len().eq(0))) {
                    result = result.concat("(").concat(TIL_PREFIX).concat(func_name).concat("){}")
                } else {
                    result = result.concat("(").concat(TIL_PREFIX).concat(func_name).concat("){")
                    mut is_first := true
                    mut member_idx := 0
                    while member_idx.lt(struct_def.members.len()) {
                        mut member := Declaration()
                        struct_def.members.get(member_idx, member)
                        catch (err: IndexOutOfBoundsError) { throw err.msg }
                        if not(member.is_mut) {
                            member_idx = member_idx.add(1)
                        } else {
                            if not(is_first) {
                                result = result.concat(", ")
                            }
                            is_first = false
                            result = result.concat(".").concat(member.name).concat(" = ")
                            // Use named arg value if provided, otherwise use default
                            mut value_expr := Expr()
                            named_values.get(member.name, value_expr)
                            if value_expr.params.len().gt(0).or(value_expr.line.gt(0)) {
                                // Found named arg value
                                result = emit_expr(value_expr, result, 0, ctx, context)
                            } else {
                                // Check for default value
                                mut default_found := false
                                mut default_expr := Expr()
                                struct_def.default_values.get(member.name, default_expr)
                                default_found = true
                                catch (err: KeyNotFoundError) { }
                                if default_found {
                                    // Check if this was a throwing default - use temp var instead
                                    key := func_name.concat(":").concat(member.name)
                                    mut temp_name := ""
                                    ctx.hoisted_struct_defaults.get(key, temp_name)
                                    result = result.concat(temp_name)
                                    catch (err: KeyNotFoundError) {
                                        // Not hoisted - only error if it's a throwing fcall
                                        if is_throwing_fcall(default_expr, context) {
                                            // Shouldn't happen - throwing defaults should be hoisted
                                            result = result.concat("/* ERROR: unhoisted throwing default */")
                                        } else {
                                            result = emit_expr(default_expr, result, 0, ctx, context)
                                        }
                                    }
                                } else {
                                    result = result.concat("0")
                                }
                            }
                            catch (err: KeyNotFoundError) { }
                            member_idx = member_idx.add(1)
                        }
                    }
                    result = result.concat("}")
                }
            } else {
                result = result.concat("(").concat(TIL_PREFIX).concat(func_name).concat("){}")
            }

            if is_stmt_level {
                result = result.concat(";\n")
            }
            return result
        }

        // Regular function call
        // func_name already has underscores from get_func_name_string

        // Check if this is an enum variant constructor (Type_Variant)
        // Need to emit til_Type_make_Variant instead of til_Type_Variant
        mut is_enum_constructor := false
        mut enum_type_name := ""
        mut enum_variant_name := ""
        if func_name.contains("_") {
            // Check if Type_Variant where Type is an enum and Variant is a variant
            parts := func_name.split("_")
            if parts.len().gteq(2) {
                mut type_name := ""
                parts.get(0, type_name)
                mut variant_name := ""
                parts.get(1, variant_name)
                mut found_enum := false
                enum_def := context.scope_stack.lookup_enum(type_name)
                found_enum = true
                catch (err: KeyNotFoundError) { }
                if found_enum {
                    // Check if this variant exists in the enum
                    if enum_def.contains_key(variant_name) {
                        is_enum_constructor = true
                        enum_type_name = type_name
                        enum_variant_name = variant_name
                    }
                }
            }
        }

        result = result.concat(TIL_PREFIX)
        if is_enum_constructor {
            // Emit til_Type_make_Variant
            result = result.concat(enum_type_name)
            result = result.concat("_make_")
            result = result.concat(enum_variant_name)
        } else {
            result = result.concat(func_name)
        }
        result = result.concat("(")

        // Check if this is a variadic function
        mut variadic_info := VariadicParamInfo()
        mut is_variadic := false
        ctx.func_variadic_args.get(orig_func_name, variadic_info)
        is_variadic = true
        catch (err: KeyNotFoundError) {
            is_variadic = false
        }

        // Detect and construct variadic array if needed (only at statement level)
        mut variadic_arr_var := ""
        mut has_variadic_arr := false
        if is_variadic.and(is_stmt_level) {
            // Collect variadic args
            mut variadic_args := Vec.new(Expr)
            mut regular_end := add(variadic_info.regular_count, 1)  // +1 to skip func name
            mut vi := regular_end
            while vi.lt(params_len) {
                mut varg := Expr()
                expr.params.get(vi, varg)
                variadic_args.push(varg)
                vi = vi.add(1)
            }
            // Use empty hoisted map since we're not hoisting args here
            mut empty_hoisted := Map.new(I64, Str)
            variadic_arr_var = hoist_variadic_args(variadic_info.elem_type, variadic_args, empty_hoisted, variadic_info.regular_count, result, indent, ctx, context)
            has_variadic_arr = true
            catch (err: IndexOutOfBoundsError) { throw err.msg }
            catch (err: AllocError) { throw err.msg }
        }

        if is_variadic {
            // Emit regular args first
            mut i := 1
            mut regular_end := add(variadic_info.regular_count, 1)  // +1 to skip func name
            while i.lt(regular_end) {
                if i.gt(1) {
                    result = result.concat(", ")
                }
                mut arg := Expr()
                expr.params.get(i, arg)
                result = emit_expr(arg, result, 0, ctx, context)
                i = i.add(1)
            }
            catch (err: IndexOutOfBoundsError) { throw err.msg }

            // Emit variadic array pointer
            if has_variadic_arr {
                if variadic_info.regular_count.gt(0) {
                    result = result.concat(", ")
                }
                result = result.concat("&")
                result = result.concat(variadic_arr_var)
            }
        } else {
            // Non-variadic: emit args normally
            // Look up function to get parameter info (is_mut flags)
            // For ext_func, don't pass mut params by reference (mut is just documentation)
            mut param_info := Vec.new(ParamTypeInfo)
            mut is_ext_func := false
            mut got_lookup := false
            mut lookup_fd := SFuncDef()
            lookup_fd = get_fcall_func_def(context, expr)
            got_lookup = true
            catch (err: KeyNotFoundError) { /* REM None case in Rust */ }
            if got_lookup {
                // Build param_info from function args
                mut arg_i := 0
                while arg_i.lt(lookup_fd.args.len()) {
                    mut arg_decl := Declaration()
                    lookup_fd.args.get(arg_i, arg_decl)
                    param_info.push(ParamTypeInfo(by_ref = arg_decl.is_mut))
                    arg_i = arg_i.add(1)
                }
                is_ext_func = lookup_fd.is_ext()
            }
            catch (err: AllocError) { throw err.msg }
            catch (err: IndexOutOfBoundsError) { throw err.msg }

            mut i := 1
            while i.lt(params_len) {
                if i.gt(1) {
                    result = result.concat(", ")
                }
                mut arg := Expr()
                expr.params.get(i, arg)
                // Check if we should add & for mut param
                // For ext_func, don't treat mut as pass-by-reference
                mut arg_idx := i.sub(1)
                mut should_add_ampersand := false
                if param_info.len().gt(0).and(not(is_ext_func)) {
                    if arg_idx.lt(param_info.len()) {
                        mut info := ParamTypeInfo()
                        param_info.get(arg_idx, info)
                        if info.by_ref {
                            // Check if arg is a simple identifier
                            switch arg.node_type {
                            case NodeType.Identifier(arg_name):
                                if arg.params.len().eq(0) {
                                    should_add_ampersand = true
                                }
                            case:
                            }
                        }
                    }
                }
                if should_add_ampersand {
                    result = result.concat("&")
                }
                result = emit_expr(arg, result, 0, ctx, context)
                i = i.add(1)
            }
            catch (err: IndexOutOfBoundsError) { throw err.msg }
        }

        result = result.concat(")")
        // Only add statement terminator if this is a statement
        if is_stmt_level {
            result = result.concat(";\n")
        }

        // Emit Array.delete if variadic array was constructed
        if has_variadic_arr {
            for _ in 0..indent {
                result = result.concat("    ")
            }
            result = result.concat(TIL_PREFIX)
            result = result.concat("Array_delete(&")
            result = result.concat(variadic_arr_var)
            result = result.concat(");\n")
        }

        return result
    }

    catch (err: IndexOutOfBoundsError) { throw err.msg }
    catch (err: I64_OverflowError) { throw err.msg }
    catch (err: AllocError) { throw err.msg }
}

// Helper to emit a Str compound literal: ((til_Str){(til_I64)"...", len})
// This is valid in both constant initializers and expressions (unlike til_Str_from_literal)
emit_str_literal := proc(s: Str, output: Str) returns Str throws AllocError, IndexOutOfBoundsError, I64_OverflowError {
    mut result := output
    result = format(result, "((", TIL_PREFIX, "Str){(", TIL_PREFIX, "I64)\"")
    // Escape special characters for C string literals
    for i in 0..s.len() {
        c := get_char(s, i)
        if c.eq("\n") {
            result = result.concat("\\n")
        } else if c.eq("\r") {
            result = result.concat("\\r")
        } else if c.eq("\t") {
            result = result.concat("\\t")
        } else if c.eq("\\") {
            result = result.concat("\\\\")
        } else if c.eq("\"") {
            result = result.concat("\\\"")
        } else {
            result = result.concat(c)
        }
    }
    result = format(result, "\", ", s.len().to_str(), "})")
    return result
}

emit_literal := proc(lit: Literal, output: Str, context: Context) returns Str throws Str {
    mut result := ""
    switch lit {
    case Literal.Str(s):
        // Check if Str struct is defined (inline, like Rust)
        mut has_str := false
        _ := context.scope_stack.lookup_struct("Str")
        has_str = true
        catch (err: KeyNotFoundError) { }
        if has_str {
            result = emit_str_literal(s, output)
        } else {
            result = output
            result = result.concat("\"")
            // Escape special characters for C string literals
            for i in 0..s.len() {
                c := get_char(s, i)
                if c.eq("\n") {
                    result = result.concat("\\n")
                } else if c.eq("\r") {
                    result = result.concat("\\r")
                } else if c.eq("\t") {
                    result = result.concat("\\t")
                } else if c.eq("\\") {
                    result = result.concat("\\\\")
                } else if c.eq("\"") {
                    result = result.concat("\\\"")
                } else {
                    result = result.concat(c)
                }
            }
            result = result.concat("\"")
        }
    case Literal.Number(n):
        result = concat(output, n)
    case Literal.List(l):
        throw "ccodegen: List literals not yet supported"
    }
    catch (err: AllocError) { throw err.msg }
    catch (err: IndexOutOfBoundsError) { throw err.msg }
    catch (err: I64_OverflowError) { throw err.msg }
    return result
}
