mode cli

import("std.sys")
import("std.io")
import("modes.test")

// tests2.til - TIL test runner
// Runs compiled tests and compares output against .out files

// .out file format:
// args: <args>
// expected_status: <exit_code>
// output:
// <actual output>

// Path transform: src/examples/foo.til -> src/examples/foo.out
get_out_path := func(src_path: Str) returns Str throws AllocError {
    return src_path.replace(".til", ".out")
}

// Format output with args, exit code, and output
format_expected := func(args_str: Str, exit_code: I64, output: Str) returns Str {
    mut result := "args: "
    result = result.concat(args_str).concat("\n")
    result = result.concat("expected_status: ").concat(exit_code.to_str()).concat("\n")
    result = result.concat("output:\n")
    result = result.concat(output)
    return result
}

// Parse expected file: extract args, exit code, and output
parse_expected := proc(content: Str, mut args_str: Str, mut exit_code: I64, mut output: Str) throws AllocError, IndexOutOfBoundsError, Str {
    // Format:
    // args: <args>
    // expected_status: <exit_code>
    // output:
    // <actual output>

    lines := content.split("\n")
    if lt(lines.len(), 3) {
        throw "Invalid .out file format: need at least 3 lines. Use 'rstil interpret src/tests.til regen' to regenerate."
    }

    // Parse args line
    mut args_line := ""
    lines.get(0, args_line)
    if not(args_line.starts_with("args: ")) {
        throw "Invalid .out file format: first line must start with 'args: '. Use 'rstil interpret src/tests.til regen' to regenerate."
    }
    if args_line.len().gt(6) {
        args_str = get_substr(args_line, 6, args_line.len())
    } else {
        args_str = ""
    }

    // Parse expected_status line
    mut status_line := ""
    lines.get(1, status_line)
    if not(status_line.starts_with("expected_status: ")) {
        throw "Invalid .out file format: second line must start with 'expected_status: '. Use 'rstil interpret src/tests.til regen' to regenerate."
    }
    status_str := get_substr(status_line, 17, status_line.len())
    exit_code = status_str.to_i64()

    // Parse output line marker
    mut output_line := ""
    lines.get(2, output_line)
    if not(output_line.eq("output:")) {
        throw "Invalid .out file format: third line must be 'output:'. Use 'rstil interpret src/tests.til regen' to regenerate."
    }

    // Join remaining lines as output
    output = ""
    for i in 3..lines.len() {
        mut line := ""
        lines.get(i, line)
        if i.gt(3) {
            output = output.concat("\n")
        }
        output = output.concat(line)
    }
}

run_all_tests := proc(rs_common: Vec, rs_compiled: Vec, rs_interpreted: Vec) {
    mut errors_found := false
    for i in 0..rs_common.len() {
        mut path := ""
        rs_common.get(i, path)
        if test_rs_common(path) {
            errors_found = true
        }
    }
    for i in 0..rs_compiled.len() {
        mut path := ""
        rs_compiled.get(i, path)
        if test_rs_compiled(path) {
            errors_found = true
        }
    }
    for i in 0..rs_interpreted.len() {
        mut path := ""
        rs_interpreted.get(i, path)
        if test_rs_interpreted(path) {
            errors_found = true
        }
    }

    catch (err: IndexOutOfBoundsError) { panic(loc(), err.msg) }

    if errors_found {
        panic(loc(), "tests failed")
    }
}

regen := proc(files: Vec, rs_common: Vec, rs_compiled: Vec, rs_interpreted: Vec) {
    if files.len().eq(0) {
        // regen all
        for i in 0..rs_common.len() {
            mut path := ""
            rs_common.get(i, path)
            regen_test(path)
        }
        for i in 0..rs_compiled.len() {
            mut path := ""
            rs_compiled.get(i, path)
            regen_test_compiled(path)
        }
        for i in 0..rs_interpreted.len() {
            mut path := ""
            rs_interpreted.get(i, path)
            regen_test(path)
        }
    } else {
        // regen specific files - check which list they're in
        for i in 0..files.len() {
            mut path := ""
            files.get(i, path)
            if rs_compiled.contains(path) {
                regen_test_compiled(path)
            } else {
                regen_test(path)
            }
        }
    }

    catch (err: IndexOutOfBoundsError) { panic(loc(), err.msg) }
}

ensure_out_file := proc(path: Str, out_path: Str) {
    mut ls_result := ""
    _ := ls_result.run_cmd("ls", out_path)
    if not(ls_result.contains(".out")) {
        mut output := ""
        exit_code := output.run_cmd("./bin/rstil", "interpret", path)
        formatted := format_expected("", exit_code, output)
        _ := writefile(out_path, formatted)
        println("Created: ", out_path)
    }
}

test_rs_interpreted := proc(path: Str) returns Bool {
    out_path := get_out_path(path)
    ensure_out_file(path, out_path)

    // Read expected file and parse args, exit code, and output
    expected_content := readfile(out_path)
    mut args_str := ""
    mut expected_exit_code := 0
    mut expected_output := ""
    parse_expected(expected_content, args_str, expected_exit_code, expected_output)

    // Run interpreter and capture exit code + output
    mut interp_output := ""
    mut actual_exit_code := 0
    if args_str.eq("") {
        actual_exit_code = interp_output.run_cmd("./bin/rstil", "interpret", path)
    } else {
        // Use bash -c to pass arguments
        cmd := concat(concat(concat("./bin/rstil interpret ", path), " "), args_str)
        actual_exit_code = interp_output.run_cmd("bash", "-c", cmd)
    }

    // Check exit code
    if not(actual_exit_code.eq(expected_exit_code)) {
        println("FAIL (interpret exit code): ", path)
        println("  expected: ", expected_exit_code.to_str())
        println("  actual: ", actual_exit_code.to_str())
        return true
    }

    // Check output via diff
    _ := writefile(path.replace(".til", ".interpret.out"), interp_output)
    _ := writefile(path.replace(".til", ".expected.out"), expected_output)
    mut interp_diff := ""
    _ := interp_diff.run_cmd("diff", path.replace(".til", ".expected.out"), path.replace(".til", ".interpret.out"))
    if not(interp_diff.eq("")) {
        println("FAIL (interpret output): ", path)
        println(interp_diff)
        return true
    }

    catch (err: AllocError) { panic(loc(), err.msg) }
    catch (err: IndexOutOfBoundsError) { panic(loc(), err.msg) }
    catch (err: Str) { panic(loc(), err) }

    return false
}

test_rs_compiled := proc(path: Str) returns Bool {
    out_path := get_out_path(path)
    ensure_out_file(path, out_path)

    // Read expected file and parse args, exit code, and output
    expected_content := readfile(out_path)
    mut args_str := ""
    mut expected_exit_code := 0
    mut expected_output := ""
    parse_expected(expected_content, args_str, expected_exit_code, expected_output)

    // Run compiled and capture exit code + output
    mut run_output := ""
    mut actual_exit_code := 0
    if args_str.eq("") {
        actual_exit_code = run_output.run_cmd("./bin/rstil", "run", path)
    } else {
        // Use bash -c to pass arguments
        cmd := concat(concat(concat("./bin/rstil run ", path), " "), args_str)
        actual_exit_code = run_output.run_cmd("bash", "-c", cmd)
    }

    // Check exit code
    if not(actual_exit_code.eq(expected_exit_code)) {
        println("FAIL (run exit code): ", path)
        println("  expected: ", expected_exit_code.to_str())
        println("  actual: ", actual_exit_code.to_str())
        return true
    }

    // Check output via diff
    _ := writefile(path.replace(".til", ".run.out"), run_output)
    _ := writefile(path.replace(".til", ".expected.out"), expected_output)
    mut run_diff := ""
    _ := run_diff.run_cmd("diff", path.replace(".til", ".expected.out"), path.replace(".til", ".run.out"))
    if not(run_diff.eq("")) {
        println("FAIL (run output): ", path)
        println(run_diff)
        return true
    }

    catch (err: AllocError) { panic(loc(), err.msg) }
    catch (err: IndexOutOfBoundsError) { panic(loc(), err.msg) }
    catch (err: Str) { panic(loc(), err) }

    return false
}

test_rs_common := proc(path: Str) returns Bool {
    mut errors_found := test_rs_interpreted(path)
    if test_rs_compiled(path) {
        errors_found = true
    }

    return errors_found
}

regen_test := proc(path: Str) {
    out_path := get_out_path(path)
    // Read existing args if .out file exists, otherwise use empty args
    mut args_str := ""
    mut existing := readfile(out_path)
    if existing.len().gt(0) {
        mut dummy_exit := 0
        mut dummy_output := ""
        parse_expected(existing, args_str, dummy_exit, dummy_output)
    }

    // Catch errors from readfile or parse_expected - use empty args
    catch (err: Str) { args_str = "" }

    mut output := ""
    mut exit_code := 0
    if args_str.eq("") {
        exit_code = output.run_cmd("./bin/rstil", "interpret", path)
    } else {
        cmd := concat(concat(concat("./bin/rstil interpret ", path), " "), args_str)
        exit_code = output.run_cmd("bash", "-c", cmd)
    }
    formatted := format_expected(args_str, exit_code, output)
    _ := writefile(out_path, formatted)
    println("Regenerated: ", out_path)

    catch (err: AllocError) { panic(loc(), err.msg) }
    catch (err: IndexOutOfBoundsError) { panic(loc(), err.msg) }
}

regen_test_compiled := proc(path: Str) {
    out_path := get_out_path(path)
    // Read existing args if .out file exists, otherwise use empty args
    mut args_str := ""
    mut existing := readfile(out_path)
    if existing.len().gt(0) {
        mut dummy_exit := 0
        mut dummy_output := ""
        parse_expected(existing, args_str, dummy_exit, dummy_output)
    }

    // Catch errors from readfile or parse_expected - use empty args
    catch (err: Str) { args_str = "" }

    mut output := ""
    mut exit_code := 0
    if args_str.eq("") {
        exit_code = output.run_cmd("./bin/rstil", "run", path)
    } else {
        cmd := concat(concat(concat("./bin/rstil run ", path), " "), args_str)
        exit_code = output.run_cmd("bash", "-c", cmd)
    }
    formatted := format_expected(args_str, exit_code, output)
    _ := writefile(out_path, formatted)
    println("Regenerated (compiled): ", out_path)

    catch (err: AllocError) { panic(loc(), err.msg) }
    catch (err: IndexOutOfBoundsError) { panic(loc(), err.msg) }
}

usage := proc() {
    println("tests2 - TIL test runner")
    println("")
    println("Usage:")
    println("  tests2              Run all tests (default)")
    println("  tests2 test         Run all tests")
    println("  tests2 regen        Regenerate all .out files")
    println("  tests2 regen <path> Regenerate specific .out file")
    println("")
    println("Example:")
    println("  tests2 regen src/examples/hello_script.til")
}

main := proc(args: ..Str) {
    mut rs_common := Vec.new(Str)
    // test a part of posix compatibility, it doesn't require the mode to be script, it can be compiled too, like roc
    // Ideally we should benchmark against at least bash, python, roc, Rust, C and HolyC for this kind of mode, both interpreted and compiled
    rs_common.push("src/examples/hello_cli.til")
    rs_common.push("src/examples/hello_lib.til")
    rs_common.push("src/examples/hello_script.til")
    rs_common.push("src/examples/hello_test.til")
    rs_common.push("src/test/arithmetics.til")
    rs_common.push("src/test/arrays.til")
    rs_common.push("src/test/boolean.til")
    rs_common.push("src/test/branchless.til")
    rs_common.push("src/test/circular_test.til")
    rs_common.push("src/test/comparisons.til")
    rs_common.push("src/test/exit.til")
    rs_common.push("src/test/function_pointers.til")
    rs_common.push("src/test/loops.til")
    rs_common.push("src/test/pointers.til")
    rs_common.push("src/test/strings.til")
    rs_common.push("src/test/structs.til")
    rs_common.push("src/test/temp.til")
    rs_common.push("src/test/u8.til")
    rs_common.push("src/test/eval.til")
    rs_common.push("src/test/func_purity.til")
    rs_common.push("src/test/constfold.til")
    rs_common.push("src/test/c_mem.til")
    rs_common.push("src/test/flow.til")
    rs_common.push("src/test/fibonacci.til")
    rs_common.push("src/test/run_cmd.til")
    rs_common.push("src/test/variadic.til")
    rs_common.push("src/test/intro.til")
    rs_common.push("src/test/namespaces.til")

    mut rs_compiled := Vec.new(Str)
    rs_compiled.push("src/test/enums_simple.til")
    rs_compiled.push("src/test/throw_simple.til")
    rs_compiled.push("src/test/memcmp_str_compiled.til")
    // REM: precomp_throw_compiled.til is equivalent to precomp_throw.til (in rs_interpreted)
    // REM: Separate files needed because interpret/run produce different .out files
    rs_compiled.push("src/test/precomp_throw_compiled.til")
    // TODO FIX: enum constructors called as functions instead of struct init
    // rs_compiled.push("src/test/enums_payload.til")
    // TODO FIX: switch_simple.til - same issue plus:
    // - case labels missing til_ prefix (Color_Red vs til_Color_Red)
    // - payload extraction variable not declared (til_n)
    // - enum constructors called as functions instead of struct init
    // rs_compiled.push("src/test/switch_simple.til")

    mut rs_interpreted := Vec.new(Str)

    rs_interpreted.push("src/examples/hello_pure.til") // TODO FIX: mode pure forbids procs but core.til has procs
    rs_interpreted.push("src/test/args.til") // TODO FIX: format() variadic arg passed as Str instead of Array
    rs_interpreted.push("src/test/memcmp_str_interpreted.til")
    rs_interpreted.push("src/test/editor_mode_test.til") // TODO FIX: format() variadic arg passed as Str instead of Array
    rs_interpreted.push("src/test/enums.til") // TODO FIX: enum payload field has incomplete type
    rs_interpreted.push("src/test/errors.til") // TODO FIX: throwing func result used directly (needs temp var for error handling)
    rs_interpreted.push("src/test/forward_declarations.til") // TODO FIX: enum constructors emitted as function calls not struct init
    rs_interpreted.push("src/test/lists.til") // TODO FIX: error constructor AllocError() needs _new suffix or struct init
    rs_interpreted.push("src/test/maps.til") // TODO FIX: Dynamic param expects void* but Str passed
    rs_interpreted.push("src/test/mut_test.til") // TODO FIX: mut param not passed as pointer
    rs_interpreted.push("src/test/scope_isolation.til") // TODO FIX: nested func forward decl has const, definition has pointer
    rs_interpreted.push("src/test/sets.til") // TODO FIX: underscore _ redeclared in same scope
    rs_interpreted.push("src/test/test_lexer.til") // TODO FIX: Vec passed as Vec**, format() variadic
    rs_interpreted.push("src/test/test_parser.til") // TODO FIX: enum payload has incomplete type, Vec passed as Vec**
    rs_interpreted.push("src/test/ufcs.til") // TODO FIX: throwing functions in struct initializers (get_substr, Vec.new)
    rs_interpreted.push("src/test/vecs.til") // TODO FIX: compiled doesn't throw IndexOutOfBoundsError on bad index
    // REM: precomp_throw.til is equivalent to precomp_throw_compiled.til (in rs_compiled)
    // REM: Separate files needed because interpret/run produce different .out files
    rs_interpreted.push("src/test/precomp_throw.til")

    // rs_interpreted.push("src/examples/hello_script_safe.til") // TODO this is much harder than it sounds
    // rs_interpreted.push("src/test/undefined.til")
    // rs_interpreted.push("src/test/clone.til") // TODO Not sure why this one is commented, uncomment
    // rs_interpreted.push("src/test/cross_file_forward.til") // TODO Not sure why this one is commented, uncomment
    // rs_interpreted.push("src/test/enums_payload.til") // TODO Not sure why this one is commented, uncomment
    // rs_interpreted.push("src/test/return_value_usage.til") // TODO Not sure why this one is commented, uncomment
    // rs_interpreted.push("src/test/switch_simple.til") // TODO Not sure why this one is commented, uncomment

    // next level:
    // eval.til moved to rs_common
    // flow.til moved to rs_common (const folding fixed switch case expressions)
    rs_interpreted.push("src/test/introspections.til") // TODO FIX: error constructor AllocError() needs _new suffix or struct init
    rs_interpreted.push("src/test/modes.til") // TODO FIX: Vec passed as Vec**, format() variadic
    rs_interpreted.push("src/test/todo.til") // permanent TODO, design notes for future features

    // Self-hosting interpreter tests
    // run the self hosting libs just to see they pass typer checks
    rs_interpreted.push("src/self/interpreter.til") // TODO FIX: symbols already declared (double import?)
    rs_interpreted.push("src/til.til") // Type-check til.til by running with "help" argument (args in .out file)
    // rs_interpreted.push("src/test/builders.til") // TODO FIX: compilation times out (too large or infinite loop)

    // REPL test - requires stdin piping (e.g., echo "exit(0)" | ./bin/rstil repl)
    // run_cmd doesn't support stdin, would need bash -c wrapper or new ext_proc
    // Input must end with exit(0) to break out of the infinite while loop
    // rs_interpreted.push("src/modes/repl.til")

    // Parse args
    if args.len().eq(0) {
        // Default: run tests
        run_all_tests(rs_common, rs_compiled, rs_interpreted)
    } else {
        mut cmd := ""
        args.get(0, cmd)
        switch cmd {
        case "test":
            run_all_tests(rs_common, rs_compiled, rs_interpreted)
        case "regen":
            mut files := Vec.new(Str)
            for i in 1..args.len() {
                mut path := ""
                args.get(i, path)
                files.push(path)
            }
            regen(files, rs_common, rs_compiled, rs_interpreted)
        case:
            usage()
        }
    }

    catch (err: AllocError) { panic(loc(), err.msg) }
    catch (err: IndexOutOfBoundsError) { panic(loc(), err.msg) }
}
