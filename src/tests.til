mode cli

import("std.sys")
import("std.io")
import("modes.test")

// tests.til - TIL test runner
// Runs compiled tests and compares output against .out files

// .out file format:
// args: <args>
// expected_status: <exit_code>
// output:
// <actual output>

// Test entry struct - explicit about path, args, and expected status
TestEntry := struct {
    mut path: Str = ""              // Path to the .til file
    mut cmd_args: Str = ""          // Command line arguments (empty string if none)
    mut expected_status: I64 = 0    // Expected exit status (0 for success)
}

// Parse time output like "real\t0m0.042s" -> milliseconds
// Assumes LC_ALL=C format (dot as decimal separator)
// Returns -1 if parsing fails
parse_time_ms := func(time_output: Str) returns I64 throws AllocError, IndexOutOfBoundsError {
    // Find "real" line
    lines := time_output.split("\n")
    for i in 0..lines.len() {
        mut line := ""
        lines.get(i, line)
        if line.starts_with("real") {
            // Format: "real\t0m0.042s"
            // Find position of 'm' using index_of
            m_pos := line.find("m")
            s_pos := line.find("s")
            if m_pos.gt(0) {
                if s_pos.gt(m_pos) {
                    // Extract seconds part between 'm' and 's'
                    sec_str := get_substr(line, m_pos.add(1), s_pos)
                    // Convert to milliseconds (multiply by 1000)
                    // Parse as float by splitting on dot
                    parts := sec_str.split(".")
                    if parts.len().eq(2) {
                        mut whole := ""
                        mut frac := ""
                        parts.get(0, whole)
                        parts.get(1, frac)
                        // Pad or truncate frac to 3 digits for ms
                        while frac.len().lt(3) {
                            frac = frac.concat("0")
                        }
                        if frac.len().gt(3) {
                            frac = get_substr(frac, 0, 3)
                        }
                        return whole.to_i64().mul(1000).add(frac.to_i64())
                    }
                }
            }
        }
    }
    return sub(0, 1)
}

// Run a command with timing, returns exit code and stores time in ms
// Output goes to mut output param, time goes to mut time_ms param
// Uses LC_ALL=C for consistent time format
run_timed := proc(mut output: Str, mut time_ms: I64, cmd: Str) returns I64 {
    // Wrap command to capture both output and timing
    // Uses LC_ALL=C for consistent time format (dot as decimal separator)
    mut wrapped := "LC_ALL=C bash -c 'time "
    wrapped = wrapped.concat(cmd).concat("' 2>&1")
    mut full_output := ""
    exit_code := full_output.run_cmd("bash", "-c", wrapped)

    // Split output at "\nreal" - everything before is command output, after is time
    // Also handle case where output starts with "real" (no program output)
    split_pos := full_output.find("\nreal")
    if split_pos.gt(0) {
        // Normal case: program output before time
        output = get_substr(full_output, 0, split_pos)
        time_output := get_substr(full_output, split_pos, full_output.len())
        time_ms = parse_time_ms(time_output)
    } else {
        if split_pos.eq(0) {
            // Time at very start (only newline before real)
            output = ""
            time_ms = parse_time_ms(full_output)
        } else {
            // Try finding "real" at start (no newline before)
            if full_output.starts_with("real") {
                output = ""
                time_ms = parse_time_ms(full_output)
            } else {
                output = full_output
                time_ms = sub(0, 1)
            }
        }
    }

    catch (err: IndexOutOfBoundsError) {
        output = full_output
        time_ms = sub(0, 1)
    }
    catch (err: AllocError) {
        output = full_output
        time_ms = sub(0, 1)
    }

    return exit_code
}

// Format milliseconds as "0.042s"
format_time := func(ms: I64) returns Str {
    if ms.lt(0) {
        return "N/A"
    }
    secs := ms.div(1000)
    millis := ms.mod(1000)
    mut millis_str := millis.to_str()
    while millis_str.len().lt(3) {
        millis_str = concat("0", millis_str)
    }
    return secs.to_str().concat(".").concat(millis_str).concat("s")
}

// Path transform: src/examples/foo.til -> out/examples/foo.interpreted.out or foo.compiled.out
// test_mode: "compiled" -> .compiled.out, anything else -> .interpreted.out
// cmd_args: "" for default, "version" for .version.out, etc.
get_out_path := func(src_path: Str, test_mode: Str, cmd_args: Str) returns Str throws AllocError {
    base := src_path.replace(".til", "").replace("src/", "out/")
    mut suffix := ""
    if test_mode.eq("compiled") {
        suffix = ".compiled"
    } else {
        suffix = ".interpreted"
    }
    if not(cmd_args.eq("")) {
        // Replace spaces with dots for filename: "help foo" -> ".help.foo"
        suffix = suffix.concat(".").concat(cmd_args.replace(" ", "."))
    }
    return base.concat(suffix).concat(".out")
}

// Format output file content
format_expected := func(cmd_args: Str, exit_code: I64, output: Str) returns Str {
    mut result := "args: "
    result = result.concat(cmd_args).concat("\n")
    result = result.concat("expected_status: ").concat(exit_code.to_str()).concat("\n")
    result = result.concat("output:\n")
    result = result.concat(output)
    return result
}

run_all_tests := proc(rs_common: Vec, rs_compiled: Vec, rs_interpreted: Vec) {
    mut errors_found := false
    mut total_interpret_ms := 0
    mut total_compile_ms := 0
    mut benchmark_content := ""

    println("=== rs_common (", rs_common.len().to_str(), " tests) ===")
    benchmark_content = benchmark_content.concat("* rs_common\n")
    for i in 0..rs_common.len() {
        mut entry := TestEntry()
        rs_common.get(i, entry)
        mut interp_ms := 0
        mut compile_ms := 0
        if test_rs_common(entry, interp_ms, compile_ms) {
            errors_found = true
        }
        total_interpret_ms = total_interpret_ms.add(interp_ms)
        total_compile_ms = total_compile_ms.add(compile_ms)
        // Compare times and add "faster" label
        mut faster := ""
        if interp_ms.lt(compile_ms) {
            faster = " (faster interpreted)"
        } else {
            if compile_ms.lt(interp_ms) {
                faster = " (faster compiled)"
            }
        }
        // Show args if present
        mut args_display := ""
        if not(entry.cmd_args.eq("")) {
            args_display = concat(" ", entry.cmd_args)
        }
        println("[", i.to_str(), "/", rs_common.len().to_str(), "] ", entry.path, args_display, " (interpreted: ", format_time(interp_ms), ", compiled: ", format_time(compile_ms), ")", faster)
        benchmark_content = benchmark_content.concat("- ").concat(entry.path).concat(" :: interpreted: ").concat(format_time(interp_ms)).concat(", compiled: ").concat(format_time(compile_ms)).concat(faster).concat("\n")
    }

    println("=== rs_compiled (", rs_compiled.len().to_str(), " tests) ===")
    benchmark_content = benchmark_content.concat("\n* rs_compiled\n")
    for i in 0..rs_compiled.len() {
        mut entry := TestEntry()
        rs_compiled.get(i, entry)
        mut time_ms := 0
        if test_rs_compiled(entry, time_ms) {
            errors_found = true
        }
        total_compile_ms = total_compile_ms.add(time_ms)
        // Show args if present
        mut args_display := ""
        if not(entry.cmd_args.eq("")) {
            args_display = concat(" ", entry.cmd_args)
        }
        println("[", i.to_str(), "/", rs_compiled.len().to_str(), "] ", entry.path, args_display, " (compiled: ", format_time(time_ms), ")")
        benchmark_content = benchmark_content.concat("- ").concat(entry.path).concat(" :: compiled: ").concat(format_time(time_ms)).concat("\n")
    }

    println("=== rs_interpreted (", rs_interpreted.len().to_str(), " tests) ===")
    benchmark_content = benchmark_content.concat("\n* rs_interpreted\n")
    for i in 0..rs_interpreted.len() {
        mut entry := TestEntry()
        rs_interpreted.get(i, entry)
        mut time_ms := 0
        if test_rs_interpreted(entry, time_ms) {
            errors_found = true
        }
        total_interpret_ms = total_interpret_ms.add(time_ms)
        // Show args if present
        mut args_display := ""
        if not(entry.cmd_args.eq("")) {
            args_display = concat(" ", entry.cmd_args)
        }
        println("[", i.to_str(), "/", rs_interpreted.len().to_str(), "] ", entry.path, args_display, " (interpreted: ", format_time(time_ms), ")")
        benchmark_content = benchmark_content.concat("- ").concat(entry.path).concat(" :: interpreted: ").concat(format_time(time_ms)).concat("\n")
    }

    // Print and write totals
    println("")
    println("=== Totals ===")
    println("Total interpreted: ", format_time(total_interpret_ms))
    println("Total compiled:    ", format_time(total_compile_ms))
    println("Total:             ", format_time(total_interpret_ms.add(total_compile_ms)))

    // Build totals section and prepend it
    mut totals := "* Totals\n"
    totals = totals.concat("- Total interpreted :: ").concat(format_time(total_interpret_ms)).concat("\n")
    totals = totals.concat("- Total compiled :: ").concat(format_time(total_compile_ms)).concat("\n")
    totals = totals.concat("- Total :: ").concat(format_time(total_interpret_ms.add(total_compile_ms))).concat("\n\n")

    _ := writefile("benchmark.org", totals.concat(benchmark_content))

    catch (err: IndexOutOfBoundsError) { panic(loc(), err.msg) }

    if errors_found {
        panic(loc(), "tests failed")
    }
}

regen := proc(files: Vec, rs_common: Vec, rs_compiled: Vec, rs_interpreted: Vec) {
    if files.len().eq(0) {
        // regen all
        for i in 0..rs_common.len() {
            mut entry := TestEntry()
            rs_common.get(i, entry)
            regen_test(entry)
        }
        for i in 0..rs_compiled.len() {
            mut entry := TestEntry()
            rs_compiled.get(i, entry)
            regen_test_compiled(entry)
        }
        for i in 0..rs_interpreted.len() {
            mut entry := TestEntry()
            rs_interpreted.get(i, entry)
            regen_test(entry)
        }
    } else {
        // regen specific files - find matching entries in lists
        for i in 0..files.len() {
            mut file := ""
            files.get(i, file)
            // Search in each list for entries with matching path
            for j in 0..rs_common.len() {
                mut entry := TestEntry()
                rs_common.get(j, entry)
                if entry.path.eq(file) {
                    regen_test(entry)
                }
            }
            for j in 0..rs_compiled.len() {
                mut entry := TestEntry()
                rs_compiled.get(j, entry)
                if entry.path.eq(file) {
                    regen_test_compiled(entry)
                }
            }
            for j in 0..rs_interpreted.len() {
                mut entry := TestEntry()
                rs_interpreted.get(j, entry)
                if entry.path.eq(file) {
                    regen_test(entry)
                }
            }
        }
    }

    catch (err: IndexOutOfBoundsError) { panic(loc(), err.msg) }
    catch (err: AllocError) { panic(loc(), err.msg) }
}

// Ensure parent directory exists for a file path
ensure_parent_dir := proc(file_path: Str) throws AllocError {
    // Extract directory by finding last /
    mut last_slash := 0
    for i in 0..file_path.len() {
        if file_path.get_char(i).eq("/") {
            last_slash = i
        }
    }
    if last_slash.gt(0) {
        dir := get_substr(file_path, 0, last_slash)
        mut result := ""
        _ := result.run_cmd("mkdir", "-p", dir)
    }

    catch (err: IndexOutOfBoundsError) { /* ignore */ }
}

ensure_out_file := proc(path: Str, cmd_args: Str, out_path: Str, test_mode: Str) {
    mut ls_result := ""
    _ := ls_result.run_cmd("ls", out_path)
    if not(ls_result.contains(".out")) {
        mut output := ""
        mut exit_code := 0
        if test_mode.eq("compiled") {
            if cmd_args.eq("") {
                exit_code = output.run_cmd("./bin/rstil", "run", path)
            } else {
                cmd := concat(concat("./bin/rstil run ", path), concat(" ", cmd_args))
                exit_code = output.run_cmd("bash", "-c", cmd)
            }
        } else {
            if cmd_args.eq("") {
                exit_code = output.run_cmd("./bin/rstil", "interpret", path)
            } else {
                cmd := concat(concat("./bin/rstil interpret ", path), concat(" ", cmd_args))
                exit_code = output.run_cmd("bash", "-c", cmd)
            }
        }
        formatted := format_expected(cmd_args, exit_code, output)
        ensure_parent_dir(out_path)
        _ := writefile(out_path, formatted)
        println("Created: ", out_path)
    }

    catch (err: AllocError) { panic(loc(), err.msg) }
}

test_rs_interpreted := proc(entry: TestEntry, mut time_ms: I64) returns Bool {
    out_path := get_out_path(entry.path, "interpreted", entry.cmd_args)
    ensure_out_file(entry.path, entry.cmd_args, out_path, "interpreted")

    // Run interpreter with timing
    mut interp_output := ""
    mut interp_time := 0
    mut cmd := "./bin/rstil interpret "
    cmd = cmd.concat(entry.path)
    if not(entry.cmd_args.eq("")) {
        cmd = cmd.concat(" ").concat(entry.cmd_args)
    }
    actual_exit_code := run_timed(interp_output, interp_time, cmd)
    time_ms = interp_time

    // Check exit code (use entry.expected_status)
    if not(actual_exit_code.eq(entry.expected_status)) {
        println("FAIL (interpret exit code): ", entry.path)
        println("  expected: ", entry.expected_status.to_str())
        println("  actual: ", actual_exit_code.to_str())
        return true
    }

    // Check output via diff
    tmp_actual := "src/test/tmp/actual.out"
    _ := writefile(tmp_actual, format_expected(entry.cmd_args, actual_exit_code, interp_output))
    mut diff_result := ""
    _ := diff_result.run_cmd("diff", out_path, tmp_actual)
    mut rm_result := ""
    _ := rm_result.run_cmd("rm", tmp_actual)
    if not(diff_result.eq("")) {
        println("FAIL (interpret output): ", entry.path)
        println(diff_result)
        return true
    }

    catch (err: AllocError) { panic(loc(), err.msg) }

    return false
}

test_rs_compiled := proc(entry: TestEntry, mut time_ms: I64) returns Bool {
    out_path := get_out_path(entry.path, "compiled", entry.cmd_args)
    ensure_out_file(entry.path, entry.cmd_args, out_path, "compiled")

    // Run compiled with timing
    mut run_output := ""
    mut run_time := 0
    mut cmd := "./bin/rstil run "
    cmd = cmd.concat(entry.path)
    if not(entry.cmd_args.eq("")) {
        cmd = cmd.concat(" ").concat(entry.cmd_args)
    }
    actual_exit_code := run_timed(run_output, run_time, cmd)
    time_ms = run_time

    // Check exit code (use entry.expected_status)
    if not(actual_exit_code.eq(entry.expected_status)) {
        println("FAIL (run exit code): ", entry.path)
        println("  expected: ", entry.expected_status.to_str())
        println("  actual: ", actual_exit_code.to_str())
        return true
    }

    // Check output via diff
    tmp_actual := "src/test/tmp/actual.out"
    _ := writefile(tmp_actual, format_expected(entry.cmd_args, actual_exit_code, run_output))
    mut diff_result := ""
    _ := diff_result.run_cmd("diff", out_path, tmp_actual)
    mut rm_result := ""
    _ := rm_result.run_cmd("rm", tmp_actual)
    if not(diff_result.eq("")) {
        println("FAIL (run output): ", entry.path)
        println(diff_result)
        return true
    }

    catch (err: AllocError) { panic(loc(), err.msg) }

    return false
}

test_rs_common := proc(entry: TestEntry, mut interpret_ms: I64, mut compile_ms: I64) returns Bool {
    // rs_common tests use .interpreted.out for BOTH modes (same expected output)
    out_path := get_out_path(entry.path, "interpreted", entry.cmd_args)
    ensure_out_file(entry.path, entry.cmd_args, out_path, "interpreted")

    mut errors_found := false

    // Test interpreted mode with timing
    mut interp_output := ""
    mut interp_time := 0
    mut cmd := "./bin/rstil interpret "
    cmd = cmd.concat(entry.path)
    if not(entry.cmd_args.eq("")) {
        cmd = cmd.concat(" ").concat(entry.cmd_args)
    }
    interp_exit_code := run_timed(interp_output, interp_time, cmd)
    interpret_ms = interp_time

    if not(interp_exit_code.eq(entry.expected_status)) {
        println("FAIL (interpret exit code): ", entry.path)
        println("  expected: ", entry.expected_status.to_str())
        println("  actual: ", interp_exit_code.to_str())
        errors_found = true
    } else {
        tmp_actual := "src/test/tmp/actual.out"
        _ := writefile(tmp_actual, format_expected(entry.cmd_args, interp_exit_code, interp_output))
        mut interp_diff := ""
        _ := interp_diff.run_cmd("diff", out_path, tmp_actual)
        mut rm_result := ""
        _ := rm_result.run_cmd("rm", tmp_actual)
        if not(interp_diff.eq("")) {
            println("FAIL (interpret output): ", entry.path)
            println(interp_diff)
            errors_found = true
        }
    }

    // Test compiled mode with timing
    mut run_output := ""
    mut run_time := 0
    cmd = "./bin/rstil run "
    cmd = cmd.concat(entry.path)
    if not(entry.cmd_args.eq("")) {
        cmd = cmd.concat(" ").concat(entry.cmd_args)
    }
    run_exit_code := run_timed(run_output, run_time, cmd)
    compile_ms = run_time

    if not(run_exit_code.eq(entry.expected_status)) {
        println("FAIL (run exit code): ", entry.path)
        println("  expected: ", entry.expected_status.to_str())
        println("  actual: ", run_exit_code.to_str())
        errors_found = true
    } else {
        tmp_actual := "src/test/tmp/actual.out"
        _ := writefile(tmp_actual, format_expected(entry.cmd_args, run_exit_code, run_output))
        mut run_diff := ""
        _ := run_diff.run_cmd("diff", out_path, tmp_actual)
        mut rm_result := ""
        _ := rm_result.run_cmd("rm", tmp_actual)
        if not(run_diff.eq("")) {
            println("FAIL (run output): ", entry.path)
            println(run_diff)
            errors_found = true
        }
    }

    catch (err: AllocError) { panic(loc(), err.msg) }

    return errors_found
}

regen_test := proc(entry: TestEntry) throws AllocError {
    out_path := get_out_path(entry.path, "interpreted", entry.cmd_args)

    mut output := ""
    mut exit_code := 0
    if entry.cmd_args.eq("") {
        exit_code = output.run_cmd("./bin/rstil", "interpret", entry.path)
    } else {
        cmd := concat(concat("./bin/rstil interpret ", entry.path), concat(" ", entry.cmd_args))
        exit_code = output.run_cmd("bash", "-c", cmd)
    }
    formatted := format_expected(entry.cmd_args, exit_code, output)
    ensure_parent_dir(out_path)
    _ := writefile(out_path, formatted)
    println("Regenerated: ", out_path)
}

regen_test_compiled := proc(entry: TestEntry) throws AllocError {
    out_path := get_out_path(entry.path, "compiled", entry.cmd_args)

    mut output := ""
    mut exit_code := 0
    if entry.cmd_args.eq("") {
        exit_code = output.run_cmd("./bin/rstil", "run", entry.path)
    } else {
        cmd := concat(concat("./bin/rstil run ", entry.path), concat(" ", entry.cmd_args))
        exit_code = output.run_cmd("bash", "-c", cmd)
    }
    formatted := format_expected(entry.cmd_args, exit_code, output)
    ensure_parent_dir(out_path)
    _ := writefile(out_path, formatted)
    println("Regenerated (compiled): ", out_path)
}

usage := proc() {
    println("tests - TIL test runner")
    println("")
    println("Usage:")
    println("  tests                      Run all tests (default)")
    println("  tests test                 Run all tests")
    println("  tests regen                Regenerate all .out files")
    println("  tests regen <path>         Regenerate specific .out file")
    println("  tests regen <path> <args>  Regenerate with specific args")
    println("")
    println("Examples:")
    println("  tests regen src/examples/hello_script.til")
    println("  tests regen src/til.til version")
}

main := proc(args: ..Str) {
    mut rs_common := Vec.new(TestEntry)
    // test a part of posix compatibility, it doesn't require the mode to be script, it can be compiled too, like roc
    // Ideally we should benchmark against at least bash, python, roc, Rust, C and HolyC for this kind of mode, both interpreted and compiled
    rs_common.push(TestEntry(path="src/examples/hello_cli.til"))
    rs_common.push(TestEntry(path="src/examples/hello_lib.til"))
    rs_common.push(TestEntry(path="src/examples/hello_pure.til"))
    rs_common.push(TestEntry(path="src/examples/hello_script.til"))
    rs_common.push(TestEntry(path="src/examples/hello_test.til"))
    rs_common.push(TestEntry(path="src/test/arithmetics.til"))
    rs_common.push(TestEntry(path="src/test/arrays.til"))
    rs_common.push(TestEntry(path="src/test/boolean.til"))
    rs_common.push(TestEntry(path="src/test/branchless.til"))
    rs_common.push(TestEntry(path="src/test/c_mem.til"))
    rs_common.push(TestEntry(path="src/test/circular_test.til"))
    rs_common.push(TestEntry(path="src/test/comparisons.til"))
    rs_common.push(TestEntry(path="src/test/constfold.til"))
    rs_common.push(TestEntry(path="src/test/editor_mode_test.til"))
    rs_common.push(TestEntry(path="src/test/enums.til"))
    rs_common.push(TestEntry(path="src/test/errors.til"))
    rs_common.push(TestEntry(path="src/test/eval.til"))
    rs_common.push(TestEntry(path="src/test/exit.til", expected_status=69))
    rs_common.push(TestEntry(path="src/test/fibonacci.til"))
    rs_common.push(TestEntry(path="src/test/flow.til"))
    rs_common.push(TestEntry(path="src/test/forward_declarations.til"))
    rs_common.push(TestEntry(path="src/test/func_purity.til"))
    rs_common.push(TestEntry(path="src/test/function_pointers.til"))
    rs_common.push(TestEntry(path="src/test/intro.til"))
    rs_common.push(TestEntry(path="src/test/introspections.til"))
    rs_common.push(TestEntry(path="src/test/lists.til"))
    rs_common.push(TestEntry(path="src/test/loops.til"))
    rs_common.push(TestEntry(path="src/test/maps.til"))
    rs_common.push(TestEntry(path="src/test/modes.til"))
    rs_common.push(TestEntry(path="src/test/mut_test.til"))
    rs_common.push(TestEntry(path="src/test/namespaces.til"))
    rs_common.push(TestEntry(path="src/test/optional_args.til"))
    rs_common.push(TestEntry(path="src/test/pointers.til"))
    rs_common.push(TestEntry(path="src/test/return_value_usage.til"))
    rs_common.push(TestEntry(path="src/test/run_cmd.til"))
    rs_common.push(TestEntry(path="src/test/scope_isolation.til"))
    rs_common.push(TestEntry(path="src/test/sets.til"))
    rs_common.push(TestEntry(path="src/test/strings.til"))
    rs_common.push(TestEntry(path="src/test/structs.til"))
    rs_common.push(TestEntry(path="src/test/temp.til"))
    rs_common.push(TestEntry(path="src/test/todo.til")) // permanent TODO, design notes for future features
    rs_common.push(TestEntry(path="src/test/args.til"))
    rs_common.push(TestEntry(path="src/test/u8.til"))
    rs_common.push(TestEntry(path="src/test/variadic.til"))
    rs_common.push(TestEntry(path="src/test/vecs.til"))
    rs_common.push(TestEntry(path="src/test/ufcs.til"))

    mut rs_compiled := Vec.new(TestEntry)
    rs_compiled.push(TestEntry(path="src/test/memcmp_str_compiled.til"))
    rs_compiled.push(TestEntry(path="src/test/precomp_throw.til")) // REM precomp_throw.til is in both rs_interpreted and rs_compiled because interpret/run produce different outputs
    rs_compiled.push(TestEntry(path="src/test/throw_simple.til"))
    // rs_compiled.push(TestEntry(path="src/til.til")) // TODO: til.til can be unified to rs_common once til.til compiles without C errors
    // TODO FIX: enum constructors called as functions instead of struct init
    // rs_compiled.push(TestEntry(path="src/test/enums_payload.til"))
    // TODO FIX: switch_simple.til - same issue plus:
    // - case labels missing til_ prefix (Color_Red vs til_Color_Red)
    // - payload extraction variable not declared (til_n)
    // - enum constructors called as functions instead of struct init
    // rs_compiled.push(TestEntry(path="src/test/switch_simple.til"))

    mut rs_interpreted := Vec.new(TestEntry)

    rs_interpreted.push(TestEntry(path="src/test/memcmp_str_interpreted.til")) // compiles but runtime: memcmp on Str structs compares pointers not content
    rs_interpreted.push(TestEntry(path="src/test/precomp_throw.til")) // REM precomp_throw.til is in both rs_interpreted and rs_compiled because interpret/run produce different outputs
    rs_interpreted.push(TestEntry(path="src/test/test_lexer.til")) // TODO FIX: gcc: Str <= comparison, Bool.and missing, get_substr wrong args in while condition
    rs_interpreted.push(TestEntry(path="src/test/test_parser.til")) // TODO FIX: gcc: Str <= comparison, Bool.and missing, get_substr wrong args in while condition

    // rs_interpreted.push(TestEntry(path="src/examples/hello_script_safe.til")) // TODO this is much harder than it sounds
    // rs_interpreted.push(TestEntry(path="src/test/undefined.til"))
    // rs_interpreted.push(TestEntry(path="src/test/clone.til")) // TODO FIX: I64_OverflowError has no method 'clone'
    // rs_interpreted.push(TestEntry(path="src/test/cross_file_forward.til")) // TODO FIX: Vec field access needs pointer, not value
    // rs_interpreted.push(TestEntry(path="src/test/enums_payload.til")) // TODO FIX: enum constructors emitted as function calls not struct init
    // rs_interpreted.push(TestEntry(path="src/test/switch_simple.til")) // TODO FIX: enum constructors as func calls, case payload var undeclared

    // Self-hosting interpreter tests
    // run the self hosting libs just to see they pass typer checks
    rs_interpreted.push(TestEntry(path="src/self/interpreter.til")) // TODO FIX: precomp: Import not found in stored ASTs - init should have stored it
    rs_interpreted.push(TestEntry(path="src/til.til", cmd_args="help")) // TODO FIX: gcc: Str <= comparison, Bool.and missing, get_substr wrong args
    rs_interpreted.push(TestEntry(path="src/til.til", cmd_args="version")) // TODO FIX: gcc: Str <= comparison, Bool.and missing, get_substr wrong args
    // rs_interpreted.push(TestEntry(path="src/test/builders.til")) // TODO FIX: compilation times out (too large or infinite loop)

    // REPL test - requires stdin piping (e.g., echo "exit(0)" | ./bin/rstil repl)
    // run_cmd doesn't support stdin, would need bash -c wrapper or new ext_proc
    // Input must end with exit(0) to break out of the infinite while loop
    // rs_interpreted.push(TestEntry(path="src/modes/repl.til"))

    // Parse args
    if args.len().eq(0) {
        // Default: run tests
        run_all_tests(rs_common, rs_compiled, rs_interpreted)
    } else {
        mut cmd := ""
        args.get(0, cmd)
        switch cmd {
        case "test":
            run_all_tests(rs_common, rs_compiled, rs_interpreted)
        case "regen":
            mut files := Vec.new(Str)
            for i in 1..args.len() {
                mut path := ""
                args.get(i, path)
                files.push(path)
            }
            regen(files, rs_common, rs_compiled, rs_interpreted)
        case:
            usage()
        }
    }

    catch (err: AllocError) { panic(loc(), err.msg) }
    catch (err: IndexOutOfBoundsError) { panic(loc(), err.msg) }
}
