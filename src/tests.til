#!/usr/bin/env -S ./bin/rstil run
mode cli

import("std.sys")
import("std.io")
import("modes.test")

// Global flag: force rebuild for compiled tests (set by --force-rebuild arg)
mut g_force_rebuild := false

// tests.til - TIL test runner
// Runs compiled tests and compares output against .out files

// .out file format:
// args: <args>
// expected_status: <exit_code>
// output:
// <actual output>

// Test entry struct - explicit about path, args, and expected status
TestEntry := struct {
    mut path: Str = ""              // Path to the .til file
    mut cmd_args: Str = ""          // Command line arguments (empty string if none)
    mut expected_status: I64 = 0    // Expected exit status (0 for success)
}

// A phase describes one execution mode for test.til
TestPhase := struct {
    mut test_mode: Str = ""         // e.g. "rs_interpreted", "til_compiled"
    mut out_mode: Str = ""          // "interpreted" or "compiled" - which .out file to use
    mut label: Str = ""             // for display, e.g. "interpreted", "til compiled"
}

// Running test - tracks a test in progress for parallel execution
RunningTest := struct {
    mut pid: I64 = 0                // Current process ID
    mut entry: TestEntry = TestEntry()
    mut actual_file: Str = ""       // Unique actual output file path
    mut current_phase: I64 = 0      // Index into phases vec (0, 1, ...)
    mut phase_times: Vec = Vec.new(I64)  // Times for completed phases
}

// Parse time output like "real\t0m0.042s" -> milliseconds
// Assumes LC_ALL=C format (dot as decimal separator)
// Returns -1 if parsing fails
parse_time_ms := func(time_output: Str) returns I64 throws IndexOutOfBoundsError {
    // Find "real" line
    lines := time_output.split("\n")
    for line: Str in lines {
        if line.starts_with("real") {
            // Format: "real\t0m0.042s"
            // Find position of 'm' using index_of
            m_pos := line.find("m")
            s_pos := line.find("s")
            if m_pos.gt(0) {
                if s_pos.gt(m_pos) {
                    // Extract seconds part between 'm' and 's'
                    sec_str := get_substr(line, m_pos.add(1), s_pos)
                    // Convert to milliseconds (multiply by 1000)
                    // Parse as float by splitting on dot
                    parts := sec_str.split(".")
                    if parts.len().eq(2) {
                        mut whole := ""
                        mut frac := ""
                        parts.get(0, whole)
                        parts.get(1, frac)
                        // Pad or truncate frac to 3 digits for ms
                        while frac.len().lt(3) {
                            frac = frac.concat("0")
                        }
                        if frac.len().gt(3) {
                            frac = get_substr(frac, 0, 3)
                        }
                        return whole.to_i64().mul(1000).add(frac.to_i64())
                    }
                }
            }
        }
    }
    return sub(0, 1)
}

// Run a command with timing, returns exit code and stores time in ms
// Output goes to mut output param, time goes to mut time_ms param
// Uses LC_ALL=C for consistent time format
run_timed := proc(mut output: Str, mut time_ms: I64, cmd: Str) returns I64 {
    // Wrap command to capture both output and timing
    // Uses LC_ALL=C for consistent time format (dot as decimal separator)
    mut wrapped := "LC_ALL=C bash -c 'time "
    wrapped = wrapped.concat(cmd).concat("' 2>&1")
    mut full_output := ""
    exit_code := full_output.run_cmd("bash", "-c", wrapped)

    // Truncate huge outputs (e.g., gcc errors) to prevent hangs
    // Str.find() is O(n*m) and slow in interpreter for large strings
    // Keep the END of output since timing info is at the end
    max_output := 1000
    if full_output.len().gt(max_output) {
        start_pos := full_output.len().sub(max_output)
        full_output = get_substr(full_output, start_pos, full_output.len())
    }

    // Split output at "\nreal" - everything before is command output, after is time
    // Also handle case where output starts with "real" (no program output)
    split_pos := full_output.find("\nreal")
    if split_pos.gt(0) {
        // Normal case: program output before time
        output = get_substr(full_output, 0, split_pos)
        time_output := get_substr(full_output, split_pos, full_output.len())
        time_ms = parse_time_ms(time_output)
    } else {
        if split_pos.eq(0) {
            // Time at very start (only newline before real)
            output = ""
            time_ms = parse_time_ms(full_output)
        } else {
            // Try finding "real" at start (no newline before)
            if full_output.starts_with("real") {
                output = ""
                time_ms = parse_time_ms(full_output)
            } else {
                output = full_output
                time_ms = sub(0, 1)
            }
        }
    }

    catch (err: IndexOutOfBoundsError) {
        output = full_output
        time_ms = sub(0, 1)
    }

    return exit_code
}

// Format milliseconds as "0.042s"
format_time := func(ms: I64) returns Str {
    if ms.lt(0) {
        return "N/A"
    }
    secs := ms.div(1000)
    millis := ms.mod(1000)
    mut millis_str := millis.to_str()
    while millis_str.len().lt(3) {
        millis_str = concat("0", millis_str)
    }
    return secs.to_str().concat(".").concat(millis_str).concat("s")
}

// Path transform: src/examples/foo.til -> gen/out/examples/foo.interpreted.out
// test_mode: "interpreted" or "compiled"
// cmd_args: "" for default, "help" for .help.out, etc.
get_out_path := func(src_path: Str, test_mode: Str, cmd_args: Str) returns Str {
    base := src_path.replace(".til", "").replace("src/", "gen/out/")
    mut suffix := ""
    if not(cmd_args.eq("")) {
        // Replace spaces with dots for filename: "help foo" -> ".help.foo"
        suffix = concat(".", cmd_args.replace(" ", "."))
    }
    return base.concat(suffix).concat(".").concat(test_mode).concat(".out")
}

// Format output file content
format_expected := func(cmd_args: Str, exit_code: I64, output: Str) returns Str {
    mut result := "args: "
    result = result.concat(cmd_args).concat("\n")
    result = result.concat("expected_status: ").concat(exit_code.to_str()).concat("\n")
    result = result.concat("output:\n")
    result = result.concat(output)
    return result
}

// Unified test function - runs a test with given command prefix
// cmd_prefix: "./bin/rstil interpret ", "./bin/rstil run ", etc.
// fail_label: label for error messages (e.g., "interpret", "run", "til interpret", "til run")
// out_mode: "interpreted" or "compiled" - determines which .out file to compare against
test_with_cmd := proc(entry: TestEntry, cmd_prefix: Str, fail_label: Str, out_mode: Str, mut time_ms: I64) returns Bool {
    out_path := get_out_path(entry.path, out_mode, entry.cmd_args)
    ensure_out_file(entry.path, entry.cmd_args, out_path, out_mode)

    mut output := ""
    mut run_time := 0
    mut cmd := cmd_prefix
    cmd = cmd.concat(entry.path)
    if not(entry.cmd_args.eq("")) {
        cmd = cmd.concat(" ").concat(entry.cmd_args)
    }
    actual_exit_code := run_timed(output, run_time, cmd)
    time_ms = run_time

    // Check exit code
    if not(actual_exit_code.eq(entry.expected_status)) {
        println("FAIL (", fail_label, " exit code): ", entry.path)
        println("  expected: ", entry.expected_status.to_str())
        println("  actual: ", actual_exit_code.to_str())
        if not(output.eq("")) {
            println("  output: ", output)
        }
        return true
    }

    // Check output via diff
    tmp_actual := "gen/out/actual.out"
    actual_content := format_expected(entry.cmd_args, actual_exit_code, output)
    _ := writefile(tmp_actual, actual_content)
    mut diff_u_result := ""
    _ := diff_u_result.run_cmd("diff", "-u", out_path, tmp_actual)
    mut rm_result := ""
    _ := rm_result.run_cmd("rm", tmp_actual)
    if not(diff_u_result.eq("")) {
        println("FAIL (", fail_label, " output): ", entry.path)
        println(diff_u_result)
        return true
    }

    return false
}

// Wrapper functions
test_rs_interpreted := proc(entry: TestEntry, mut time_ms: I64) returns Bool {
    return test_with_cmd(entry, "./bin/rstil interpret ", "rstil interpret", "interpreted", time_ms)
}

test_rs_compiled := proc(entry: TestEntry, mut time_ms: I64) returns Bool {
    return test_with_cmd(entry, "./bin/rstil run ", "rstil run", "compiled", time_ms)
}

test_til_interpreted := proc(entry: TestEntry, mut time_ms: I64) returns Bool {
    return test_with_cmd(entry, "./bin/til interpret ", "til interpret", "interpreted", time_ms)
}

test_til_compiled := proc(entry: TestEntry, mut time_ms: I64) returns Bool {
    return test_with_cmd(entry, "./bin/til run ", "til run", "compiled", time_ms)
}

// ============ Parallel execution ============

// Kill all running test processes and panic
kill_running_and_panic := proc(running: Vec, msg: Str) {
    for rt: RunningTest in running {
        if rt.pid.gt(0) {
            mut output := ""
            _ := output.run_cmd("kill", "-9", rt.pid.to_str())
        }
    }
    panic(loc(), msg)
}

// Generate unique actual output file path for test.til
// path: src/test/foo.til -> gen/out/test/foo
// mode: rs_interpreted, til_compiled, etc.
// args: "help" -> ".help", "help foo" -> ".help_foo", "" -> ""
get_actual_path := func(path: Str, test_mode: Str, cmd_args: Str) returns Str {
    base := path.replace(".til", "").replace("src/", "gen/out/")
    mut args_part := ""
    if cmd_args.len().gt(0) {
        args_part = concat(".", cmd_args.replace(" ", "_"))
    }
    return format(base, ".", test_mode, args_part, ".actual.out")
}

// Spawn a test using test.til
// test.til handles: run command, compare output, report result
// Output is redirected to a result file for later reading
spawn_test := proc(entry: TestEntry, phase: TestPhase, test_id: I64) returns RunningTest throws SpawnError {
    // Build command: ./bin/test_runner [--force-rebuild] <mode> <out_mode> <expected_status> <file> [args...]
    mut force_flag := ""
    if g_force_rebuild {
        force_flag = "--force-rebuild "
    }
    mut cmd := format("./bin/test_runner ", force_flag, phase.test_mode, " ", phase.out_mode, " ", entry.expected_status.to_str(), " ", entry.path)
    if entry.cmd_args.len().gt(0) {
        cmd = cmd.concat(" ").concat(entry.cmd_args)
    }

    // Redirect output to a unique result file
    result_file := format("tmp/test_result_", test_id.to_str(), ".txt")
    cmd = format(cmd, " > ", result_file, " 2>&1")

    pid := spawn_cmd(cmd)

    mut rt := RunningTest(pid=pid, entry=entry, actual_file=result_file, current_phase=0)
    rt.phase_times = Vec.new(I64)
    return rt
}

// Parse test.til result file
// On success: "PASS <time_ms>"
// On failure: error output (and exit code was non-zero)
// Returns true if passed, false if failed
parse_test_result := proc(result_file: Str, exit_status: I64, mut time_ms: I64, mut error_output: Str) returns Bool throws IndexOutOfBoundsError {
    output := readfile(result_file)

    if exit_status.eq(0) {
        // Success - parse "PASS <time_ms>"
        if output.starts_with("PASS ") {
            time_str := get_substr(output, 5, output.len()).replace("\n", "")
            time_ms = time_str.to_i64()
        } else {
            time_ms = 0
        }
        return true
    } else {
        // Failure - output contains error message
        error_output = output
        time_ms = 0
        return false
    }
}

// Unified parallel test runner
// phases: Vec of TestPhase (1 for single-mode, 2 for common tests)
// phase_totals: mut Vec of I64, one per phase for accumulating times
run_parallel := proc(tests: Vec, phases: Vec, parallelism: I64, mut phase_totals: Vec, mut benchmark_content: Str) returns I64 {
    // Ensure tmp directory exists for result files
    mut mkdir_out := ""
    _ := mkdir_out.run_cmd("mkdir", "-p", "tmp")

    // Ensure expected output files exist before spawning tests
    for entry: TestEntry in tests {
        for phase: TestPhase in phases {
            out_path := get_out_path(entry.path, phase.out_mode, entry.cmd_args)
            ensure_out_file(entry.path, entry.cmd_args, out_path, phase.out_mode)
        }
    }

    mut errors := 0
    mut pending_idx := 0
    mut running := Vec.new(RunningTest)
    mut next_id := 0
    mut completed_count := 0
    num_phases := phases.len()

    // Start initial batch
    while running.len().lt(parallelism) {
        if pending_idx.lt(tests.len()) {
            mut entry := TestEntry()
            tests.get(pending_idx, entry)
            mut phase := TestPhase()
            phases.get(0, phase)
            rt := spawn_test(entry, phase, next_id)
            running.push(rt)
            pending_idx.inc()
            next_id.inc()
        } else {
            break
        }
    }

    // Poll loop
    while running.len().gt(0) {
        mut i := 0
        while i.lt(running.len()) {
            mut current := RunningTest()
            running.get(i, current)

            status := check_cmd_status(current.pid)
            neg_one := sub(0, 1)
            if not(status.eq(neg_one)) {
                // Phase finished - parse result from test.til
                mut current_phase := TestPhase()
                phases.get(current.current_phase, current_phase)

                mut time_ms := 0
                mut error_output := ""
                passed := parse_test_result(current.actual_file, status, time_ms, error_output)

                // Record time for this phase
                current.phase_times.push(time_ms)

                // Accumulate to phase total
                mut old_total := 0
                phase_totals.get(current.current_phase, old_total)
                phase_totals.set(current.current_phase, old_total.add(time_ms))

                if not(passed) {
                    // Phase failed - test.til already printed details
                    errors.inc()
                    println(error_output)
                    completed_count.inc()
                    println("[", completed_count.to_str(), "/", tests.len().to_str(), "] ", current.entry.path, " - FAILED (", current_phase.label, ")")

                    // Remove from running
                    last_idx := running.len().sub(1)
                    if i.lt(last_idx) {
                        mut last := RunningTest()
                        running.get(last_idx, last)
                        running.set(i, last)
                    }
                    mut popped := RunningTest()
                    running.pop(popped)

                    // Start next test
                    if pending_idx.lt(tests.len()) {
                        mut fail_next_entry := TestEntry()
                        tests.get(pending_idx, fail_next_entry)
                        mut fail_first_phase := TestPhase()
                        phases.get(0, fail_first_phase)
                        fail_rt := spawn_test(fail_next_entry, fail_first_phase, next_id)
                        running.push(fail_rt)
                        pending_idx.inc()
                        next_id.inc()
                    }
                } else {
                    // Phase passed
                    next_phase_idx := current.current_phase.add(1)
                    if next_phase_idx.lt(num_phases) {
                        // More phases to run - spawn next with test.til
                        mut next_phase := TestPhase()
                        phases.get(next_phase_idx, next_phase)

                        mut next_force_flag := ""
                        if g_force_rebuild {
                            next_force_flag = "--force-rebuild "
                        }
                        mut cmd := format("./bin/test_runner ", next_force_flag, next_phase.test_mode, " ", next_phase.out_mode, " ", current.entry.expected_status.to_str(), " ", current.entry.path)
                        if current.entry.cmd_args.len().gt(0) {
                            cmd = cmd.concat(" ").concat(current.entry.cmd_args)
                        }
                        new_result := format("tmp/test_result_", next_id.to_str(), ".txt")
                        cmd = format(cmd, " > ", new_result, " 2>&1")
                        new_pid := spawn_cmd(cmd)
                        next_id.inc()

                        current.pid = new_pid
                        current.actual_file = new_result
                        current.current_phase = next_phase_idx
                        running.set(i, current)
                        i.inc()
                    } else {
                        // All phases complete - print result
                        completed_count.inc()

                        mut args_display := ""
                        if not(current.entry.cmd_args.eq("")) {
                            args_display = concat(" ", current.entry.cmd_args)
                        }

                        // Build timing string
                        mut timing_str := ""
                        mut bench_timing := ""
                        for p_idx in 0..num_phases {
                            mut p := TestPhase()
                            phases.get(p_idx, p)
                            mut t := 0
                            current.phase_times.get(p_idx, t)
                            if p_idx.gt(0) {
                                timing_str = timing_str.concat(", ")
                                bench_timing = bench_timing.concat(", ")
                            }
                            timing_str = timing_str.concat(p.label).concat(": ").concat(format_time(t))
                            bench_timing = bench_timing.concat(p.label).concat(": ").concat(format_time(t))
                        }

                        // For 2-phase tests, add "faster" annotation
                        mut faster := ""
                        if num_phases.eq(2) {
                            mut t0 := 0
                            mut t1 := 0
                            current.phase_times.get(0, t0)
                            current.phase_times.get(1, t1)
                            if t0.lt(t1) {
                                mut p0 := TestPhase()
                                phases.get(0, p0)
                                faster = format(" (faster ", p0.label, ")")
                            } else {
                                if t1.lt(t0) {
                                    mut p1 := TestPhase()
                                    phases.get(1, p1)
                                    faster = format(" (faster ", p1.label, ")")
                                }
                            }
                        }
                        // For 4-phase tests (all_common: rs_interpreted, rs_compiled, til_interpreted, til_compiled)
                        if num_phases.eq(4) {
                            mut rs_interp := 0  // rs_interpreted
                            mut rs_comp := 0    // rs_compiled
                            mut til_interp := 0 // til_interpreted
                            mut til_comp := 0   // til_compiled
                            current.phase_times.get(0, rs_interp)
                            current.phase_times.get(1, rs_comp)
                            current.phase_times.get(2, til_interp)
                            current.phase_times.get(3, til_comp)
                            // Compare rstil modes: interpreted vs compiled
                            mut rstil_faster := ""
                            if rs_interp.lt(rs_comp) {
                                rstil_faster = "interpreted"
                            } else if rs_comp.lt(rs_interp) {
                                rstil_faster = "compiled"
                            }
                            // Compare interpreted: rstil vs til
                            mut interp_faster := ""
                            if rs_interp.lt(til_interp) {
                                interp_faster = "rstil"
                            } else if til_interp.lt(rs_interp) {
                                interp_faster = "til"
                            }
                            // Compare compiled: rstil vs til
                            mut compiled_faster := ""
                            if rs_comp.lt(til_comp) {
                                compiled_faster = "rstil"
                            } else if til_comp.lt(rs_comp) {
                                compiled_faster = "til"
                            }
                            // Build annotation
                            mut parts := Vec.new(Str)
                            if rstil_faster.len().gt(0) {
                                parts.push(format("rstil: faster ", rstil_faster))
                            }
                            if interp_faster.len().gt(0) {
                                parts.push(format("interpreted: faster ", interp_faster))
                            }
                            if compiled_faster.len().gt(0) {
                                parts.push(format("compiled: faster ", compiled_faster))
                            }
                            if parts.len().gt(0) {
                                faster = " ("
                                mut pi := 0
                                while pi.lt(parts.len()) {
                                    if pi.gt(0) {
                                        faster = faster.concat(", ")
                                    }
                                    mut part := ""
                                    parts.get(pi, part)
                                    faster = faster.concat(part)
                                    pi.inc()
                                }
                                faster = faster.concat(")")
                            }
                        }

                        println("[", completed_count.to_str(), "/", tests.len().to_str(), "] ", current.entry.path, args_display, " (", timing_str, ")", faster)
                        benchmark_content = benchmark_content.concat("- ").concat(current.entry.path).concat(" :: ").concat(bench_timing).concat(faster).concat("\n")

                        // Remove from running
                        success_last_idx := running.len().sub(1)
                        if i.lt(success_last_idx) {
                            mut success_last := RunningTest()
                            running.get(success_last_idx, success_last)
                            running.set(i, success_last)
                        }
                        mut success_popped := RunningTest()
                        running.pop(success_popped)

                        // Start next test
                        if pending_idx.lt(tests.len()) {
                            mut next_entry := TestEntry()
                            tests.get(pending_idx, next_entry)
                            mut next_first_phase := TestPhase()
                            phases.get(0, next_first_phase)
                            next_rt := spawn_test(next_entry, next_first_phase, next_id)
                            running.push(next_rt)
                            pending_idx.inc()
                            next_id.inc()
                        }
                    }
                }
            } else {
                i.inc()
            }
        }

        if running.len().gt(0) {
            sleep(10)
        }
    }

    catch (err: SpawnError) { kill_running_and_panic(running, err.msg) }
    catch (err: SleepError) { kill_running_and_panic(running, err.msg) }
    catch (err: IndexOutOfBoundsError) { kill_running_and_panic(running, err.msg) }

    return errors
}

// ============ End parallel execution ============

run_all_tests := proc(til_interpreted: Vec, til_compiled: Vec, til_common: Vec, rs_interpreted: Vec, rs_compiled: Vec, rs_common: Vec, all_common: Vec, parallelism: I64) {
    mut errors_found := 0
    total_tests := til_interpreted.len().add(til_compiled.len()).add(til_common.len()).add(rs_interpreted.len()).add(rs_compiled.len()).add(rs_common.len()).add(all_common.len())
    mut benchmark_content := ""

    // til_interpreted
    mut til_interp_totals := Vec.new(I64)
    til_interp_totals.push(0)
    if til_interpreted.len().gt(0) {
        println("\n=== til_interpreted (", til_interpreted.len().to_str(), " tests) ===\n")
        benchmark_content = benchmark_content.concat("* til_interpreted\n")
        mut til_interp_phases := Vec.new(TestPhase)
        til_interp_phases.push(TestPhase(test_mode="til_interpreted", out_mode="til_interpreted", label="til interpreted"))
        errors_found = errors_found.add(run_parallel(til_interpreted, til_interp_phases, parallelism, til_interp_totals, benchmark_content))
    }

    // til_compiled
    mut til_compile_totals := Vec.new(I64)
    til_compile_totals.push(0)
    if til_compiled.len().gt(0) {
        println("\n=== til_compiled (", til_compiled.len().to_str(), " tests) ===\n")
        benchmark_content = benchmark_content.concat("\n* til_compiled\n")
        mut til_compile_phases := Vec.new(TestPhase)
        til_compile_phases.push(TestPhase(test_mode="til_compiled", out_mode="til_compiled", label="til compiled"))
        errors_found = errors_found.add(run_parallel(til_compiled, til_compile_phases, parallelism, til_compile_totals, benchmark_content))
    }

    // til_common (both modes per test)
    mut til_common_totals := Vec.new(I64)
    til_common_totals.push(0)
    til_common_totals.push(0)
    if til_common.len().gt(0) {
        println("\n=== til_common (", til_common.len().to_str(), " tests) ===\n")
        benchmark_content = benchmark_content.concat("\n* til_common\n")
        mut til_common_phases := Vec.new(TestPhase)
        til_common_phases.push(TestPhase(test_mode="til_interpreted", out_mode="til_interpreted", label="til interpreted"))
        til_common_phases.push(TestPhase(test_mode="til_compiled", out_mode="til_interpreted", label="til compiled"))
        errors_found = errors_found.add(run_parallel(til_common, til_common_phases, parallelism, til_common_totals, benchmark_content))
    }

    // rs_interpreted
    mut rs_interp_totals := Vec.new(I64)
    rs_interp_totals.push(0)
    if rs_interpreted.len().gt(0) {
        println("\n=== rs_interpreted (", rs_interpreted.len().to_str(), " tests) ===\n")
        benchmark_content = benchmark_content.concat("\n* rs_interpreted\n")
        mut rs_interp_phases := Vec.new(TestPhase)
        rs_interp_phases.push(TestPhase(test_mode="rs_interpreted", out_mode="rs_interpreted", label="interpreted"))
        errors_found = errors_found.add(run_parallel(rs_interpreted, rs_interp_phases, parallelism, rs_interp_totals, benchmark_content))
    }

    // rs_compiled
    mut rs_compile_totals := Vec.new(I64)
    rs_compile_totals.push(0)
    if rs_compiled.len().gt(0) {
        println("\n=== rs_compiled (", rs_compiled.len().to_str(), " tests) ===\n")
        benchmark_content = benchmark_content.concat("\n* rs_compiled\n")
        mut rs_compile_phases := Vec.new(TestPhase)
        rs_compile_phases.push(TestPhase(test_mode="rs_compiled", out_mode="rs_compiled", label="compiled"))
        errors_found = errors_found.add(run_parallel(rs_compiled, rs_compile_phases, parallelism, rs_compile_totals, benchmark_content))
    }

    // rs_common (both modes per test)
    mut rs_common_totals := Vec.new(I64)
    rs_common_totals.push(0)
    rs_common_totals.push(0)
    if rs_common.len().gt(0) {
        println("\n=== rs_common (", rs_common.len().to_str(), " tests) ===\n")
        benchmark_content = benchmark_content.concat("\n* rs_common\n")
        mut rs_common_phases := Vec.new(TestPhase)
        rs_common_phases.push(TestPhase(test_mode="rs_interpreted", out_mode="rs_interpreted", label="interpreted"))
        rs_common_phases.push(TestPhase(test_mode="rs_compiled", out_mode="rs_interpreted", label="compiled"))
        errors_found = errors_found.add(run_parallel(rs_common, rs_common_phases, parallelism, rs_common_totals, benchmark_content))
    }

    // all_common (4 modes: rs_interpreted, rs_compiled, til_interpreted, til_compiled)
    mut all_common_totals := Vec.new(I64)
    all_common_totals.push(0)
    all_common_totals.push(0)
    all_common_totals.push(0)
    all_common_totals.push(0)
    if all_common.len().gt(0) {
        println("\n=== all_common (", all_common.len().to_str(), " tests) ===\n")
        benchmark_content = benchmark_content.concat("\n* all_common\n")
        mut all_common_phases := Vec.new(TestPhase)
        all_common_phases.push(TestPhase(test_mode="rs_interpreted", out_mode="rs_interpreted", label="rs interpreted"))
        all_common_phases.push(TestPhase(test_mode="rs_compiled", out_mode="rs_interpreted", label="rs compiled"))
        all_common_phases.push(TestPhase(test_mode="til_interpreted", out_mode="rs_interpreted", label="til interpreted"))
        all_common_phases.push(TestPhase(test_mode="til_compiled", out_mode="rs_interpreted", label="til compiled"))
        errors_found = errors_found.add(run_parallel(all_common, all_common_phases, parallelism, all_common_totals, benchmark_content))
    }

    // Compute totals (only for non-empty categories)
    mut total_til_interp := 0
    mut total_til_compile := 0
    mut total_rs_interp := 0
    mut total_rs_compile := 0
    if til_interpreted.len().gt(0) {
        til_interp_totals.get(0, total_til_interp)
    }
    if til_compiled.len().gt(0) {
        til_compile_totals.get(0, total_til_compile)
    }
    if rs_interpreted.len().gt(0) {
        rs_interp_totals.get(0, total_rs_interp)
    }
    if rs_compiled.len().gt(0) {
        rs_compile_totals.get(0, total_rs_compile)
    }
    if til_common.len().gt(0) {
        mut til_common_interp := 0
        mut til_common_compile := 0
        til_common_totals.get(0, til_common_interp)
        til_common_totals.get(1, til_common_compile)
        total_til_interp = total_til_interp.add(til_common_interp)
        total_til_compile = total_til_compile.add(til_common_compile)
    }
    if rs_common.len().gt(0) {
        mut rs_common_interp := 0
        mut rs_common_compile := 0
        rs_common_totals.get(0, rs_common_interp)
        rs_common_totals.get(1, rs_common_compile)
        total_rs_interp = total_rs_interp.add(rs_common_interp)
        total_rs_compile = total_rs_compile.add(rs_common_compile)
    }
    if all_common.len().gt(0) {
        mut all_common_rs_interp := 0
        mut all_common_rs_compile := 0
        mut all_common_til_interp := 0
        mut all_common_til_compile := 0
        all_common_totals.get(0, all_common_rs_interp)
        all_common_totals.get(1, all_common_rs_compile)
        all_common_totals.get(2, all_common_til_interp)
        all_common_totals.get(3, all_common_til_compile)
        total_rs_interp = total_rs_interp.add(all_common_rs_interp)
        total_rs_compile = total_rs_compile.add(all_common_rs_compile)
        total_til_interp = total_til_interp.add(all_common_til_interp)
        total_til_compile = total_til_compile.add(all_common_til_compile)
    }

    // Print and write totals
    println("")
    println("=== Totals ===")
    println("Total rust interpreted:  ", format_time(total_rs_interp))
    println("Total rust compiled:     ", format_time(total_rs_compile))
    println("Total til interpreted:   ", format_time(total_til_interp))
    println("Total til compiled:      ", format_time(total_til_compile))
    total_all := total_rs_interp.add(total_rs_compile).add(total_til_interp).add(total_til_compile)
    println("Total:                   ", format_time(total_all))
    println("Failed:                  ", errors_found.to_str(), "/", total_tests.to_str())

    // Build totals section and prepend it
    mut totals := "* Totals\n"
    totals = totals.concat("- Total rust interpreted :: ").concat(format_time(total_rs_interp)).concat("\n")
    totals = totals.concat("- Total rust compiled :: ").concat(format_time(total_rs_compile)).concat("\n")
    totals = totals.concat("- Total til interpreted :: ").concat(format_time(total_til_interp)).concat("\n")
    totals = totals.concat("- Total til compiled :: ").concat(format_time(total_til_compile)).concat("\n")
    totals = totals.concat("- Total :: ").concat(format_time(total_all)).concat("\n\n")

    _ := writefile("gen/benchmark.org", totals.concat(benchmark_content))

    catch (err: IndexOutOfBoundsError) { panic(loc(), err.msg) }

    if errors_found.gt(0) {
        panic(loc(), errors_found.to_str().concat("/").concat(total_tests.to_str()).concat(" tests failed."))
    }
}

regen := proc(files: Vec, rs_common: Vec, rs_compiled: Vec, rs_interpreted: Vec, til_common: Vec, til_compiled: Vec, til_interpreted: Vec, all_common: Vec) {
    // rs_common, rs_interpreted, all_common -> .rs_interpreted.out (via regen_test)
    // rs_compiled -> .rs_compiled.out (via regen_test_compiled)
    // til_common, til_interpreted -> .til_interpreted.out (via regen_test_til)
    // til_compiled -> .til_compiled.out (via regen_test_til_compiled)
    if files.len().eq(0) {
        // regen all
        for entry: TestEntry in rs_common {
            regen_test(entry)
        }
        for entry: TestEntry in rs_compiled {
            regen_test_compiled(entry)
        }
        for entry: TestEntry in rs_interpreted {
            regen_test(entry)
        }
        for entry: TestEntry in til_common {
            regen_test_til(entry)
        }
        for entry: TestEntry in til_compiled {
            regen_test_til_compiled(entry)
        }
        for entry: TestEntry in til_interpreted {
            regen_test_til(entry)
        }
        for entry: TestEntry in all_common {
            regen_test(entry)
        }
    } else {
        // regen specific files - find matching entries in lists
        // Match by full path or by suffix (e.g., "loops.til" matches "src/test/loops.til")
        for file: Str in files {
            // Search in each list for entries with matching path
            for entry: TestEntry in rs_common {
                if entry.path.eq(file).or(entry.path.ends_with(file)) {
                    regen_test(entry)
                }
            }
            for entry: TestEntry in rs_compiled {
                if entry.path.eq(file).or(entry.path.ends_with(file)) {
                    regen_test_compiled(entry)
                }
            }
            for entry: TestEntry in rs_interpreted {
                if entry.path.eq(file).or(entry.path.ends_with(file)) {
                    regen_test(entry)
                }
            }
            for entry: TestEntry in til_common {
                if entry.path.eq(file).or(entry.path.ends_with(file)) {
                    regen_test_til(entry)
                }
            }
            for entry: TestEntry in til_compiled {
                if entry.path.eq(file).or(entry.path.ends_with(file)) {
                    regen_test_til_compiled(entry)
                }
            }
            for entry: TestEntry in til_interpreted {
                if entry.path.eq(file).or(entry.path.ends_with(file)) {
                    regen_test_til(entry)
                }
            }
            for entry: TestEntry in all_common {
                if entry.path.eq(file).or(entry.path.ends_with(file)) {
                    regen_test(entry)
                }
            }
        }
    }
}

// Ensure parent directory exists for a file path
ensure_parent_dir := proc(file_path: Str) {
    // Extract directory by finding last /
    mut last_slash := 0
    for i in 0..file_path.len() {
        if file_path.get_char(i).eq("/") {
            last_slash = i
        }
    }
    if last_slash.gt(0) {
        dir := get_substr(file_path, 0, last_slash)
        mut result := ""
        _ := result.run_cmd("mkdir", "-p", dir)
    }

    catch (err: IndexOutOfBoundsError) { /* ignore */ }
}

// Auto-generate missing .out files
// test_mode determines which binary and command to use
ensure_out_file := proc(path: Str, cmd_args: Str, out_path: Str, test_mode: Str) {
    // file_mtime returns -1 if file doesn't exist
    if file_mtime(out_path).lt(0) {
        mut output := ""
        mut cmd := ""
        if test_mode.eq("rs_compiled") {
            cmd = concat("./bin/rstil run ", path)
        } else if test_mode.eq("til_interpreted") {
            cmd = concat("./bin/til interpret ", path)
        } else if test_mode.eq("til_compiled") {
            cmd = concat("./bin/til run ", path)
        } else {
            // rs_interpreted is the default
            cmd = concat("./bin/rstil interpret ", path)
        }
        if not(cmd_args.eq("")) {
            cmd = cmd.concat(" ").concat(cmd_args)
        }
        exit_code := output.run_cmd("bash", "-c", cmd)
        formatted := format_expected(cmd_args, exit_code, output)
        ensure_parent_dir(out_path)
        _ := writefile(out_path, formatted)
        println("Created: ", out_path)
    }
}

// _common tests compare both modes against same .interpreted.out file
test_rs_common := proc(entry: TestEntry, mut interpret_ms: I64, mut compile_ms: I64) returns I64 {
    mut errors_found := 0
    if test_with_cmd(entry, "./bin/rstil interpret ", "interpret", "interpreted", interpret_ms) {
        errors_found.inc()
    }
    if test_with_cmd(entry, "./bin/rstil run ", "run", "interpreted", compile_ms) {
        errors_found.inc()
    }
    return errors_found
}

test_til_common := proc(entry: TestEntry, mut interpret_ms: I64, mut compile_ms: I64) returns I64 {
    mut errors_found := 0
    if test_with_cmd(entry, "./bin/til interpret ", "til interpret", "interpreted", interpret_ms) {
        errors_found.inc()
    }
    if test_with_cmd(entry, "./bin/til run ", "til run", "interpreted", compile_ms) {
        errors_found.inc()
    }
    return errors_found
}

regen_test := proc(entry: TestEntry) {
    out_path := get_out_path(entry.path, "rs_interpreted", entry.cmd_args)

    mut output := ""
    mut exit_code := 0
    if entry.cmd_args.eq("") {
        exit_code = output.run_cmd("./bin/rstil", "interpret", entry.path)
    } else {
        cmd := concat(concat("./bin/rstil interpret ", entry.path), concat(" ", entry.cmd_args))
        exit_code = output.run_cmd("bash", "-c", cmd)
    }
    formatted := format_expected(entry.cmd_args, exit_code, output)
    ensure_parent_dir(out_path)
    _ := writefile(out_path, formatted)
    println("Regenerated: ", out_path)
}

regen_test_compiled := proc(entry: TestEntry) {
    out_path := get_out_path(entry.path, "rs_compiled", entry.cmd_args)

    mut output := ""
    mut exit_code := 0
    if entry.cmd_args.eq("") {
        exit_code = output.run_cmd("./bin/rstil", "run", entry.path)
    } else {
        cmd := concat(concat("./bin/rstil run ", entry.path), concat(" ", entry.cmd_args))
        exit_code = output.run_cmd("bash", "-c", cmd)
    }
    formatted := format_expected(entry.cmd_args, exit_code, output)
    ensure_parent_dir(out_path)
    _ := writefile(out_path, formatted)
    println("Regenerated (rs_compiled): ", out_path)
}

regen_test_til := proc(entry: TestEntry) {
    out_path := get_out_path(entry.path, "til_interpreted", entry.cmd_args)

    mut output := ""
    mut exit_code := 0
    if entry.cmd_args.eq("") {
        exit_code = output.run_cmd("./bin/til", "interpret", entry.path)
    } else {
        cmd := concat(concat("./bin/til interpret ", entry.path), concat(" ", entry.cmd_args))
        exit_code = output.run_cmd("bash", "-c", cmd)
    }
    formatted := format_expected(entry.cmd_args, exit_code, output)
    ensure_parent_dir(out_path)
    _ := writefile(out_path, formatted)
    println("Regenerated (til_interpreted): ", out_path)
}

regen_test_til_compiled := proc(entry: TestEntry) {
    out_path := get_out_path(entry.path, "til_compiled", entry.cmd_args)

    mut output := ""
    mut exit_code := 0
    if entry.cmd_args.eq("") {
        exit_code = output.run_cmd("./bin/til", "run", entry.path)
    } else {
        cmd := concat(concat("./bin/til run ", entry.path), concat(" ", entry.cmd_args))
        exit_code = output.run_cmd("bash", "-c", cmd)
    }
    formatted := format_expected(entry.cmd_args, exit_code, output)
    ensure_parent_dir(out_path)
    _ := writefile(out_path, formatted)
    println("Regenerated (til_compiled): ", out_path)
}

usage := proc() {
    println("Usage: tests [command] [options]")
    println("")
    println("Entering no arguments is equivalent to: 'tests test'")
    println("")
    println("Options:")
    println("  -jN     Run up to N tests in parallel (default: CPU thread count)")
    println("")
    println("Commands:")
    println("")
    println("test: runs all tests.")
    println("test <mode> <file> [args]: runs single file with mode's behavior.")
    println("regen: regenerates all .out files.")
    println("regen <file> [file2...]: regenerates .out files for specific test files.")
    println("help: prints this.")
    println("")
    println("Modes: rs_interpreted, rs_compiled, rs_common, til_interpreted, til_compiled, til_common, all_common")
}

main := proc(args: ..Str) {
    // Parse options first (before other args processing)
    // -jN: parallelism (e.g., -j8, -j32) - like make
    // --force-rebuild: force recompilation for compiled tests
    mut parallelism := get_thread_count()
    mut remaining_args := Vec.new(Str)
    for arg: Str in args {
        if arg.starts_with("-j") {
            // Extract number after -j
            j_str := get_substr(arg, 2, arg.len())
            if j_str.len().gt(0) {
                parallelism = j_str.to_i64()
                if parallelism.lt(1) {
                    parallelism = 1
                }
            }
        } else if arg.eq("--force-rebuild") {
            g_force_rebuild = true
        } else {
            remaining_args.push(arg)
        }
    }

    // rs_common: tests that pass with rstil but NOT yet with til (interpreted or compiled)
    // (tests that pass all 4 modes are in all_common)
    mut rs_common := Vec.new(TestEntry)
    rs_common.push(TestEntry(path="src/test/args.til"))  // TODO: til compiled segfault
    rs_common.push(TestEntry(path="src/test/bug43.til"))  // TODO: til compiled segfault
    rs_common.push(TestEntry(path="src/test/bug47.til"))  // TODO: rstil slow (~41s), til interpret works, til compiled times out at 120s
    rs_common.push(TestEntry(path="src/test/bug50.til", expected_status=1))  // REM: must stay in rs_common - "rstil" vs "til" in error msg (Issue #69)
    rs_common.push(TestEntry(path="src/test/panic.til", expected_status=1))  // TODO: error msg format differs from til
    rs_common.push(TestEntry(path="src/test/sugar.til", expected_status=1))  // REM: error output differs from til (Issue #69)
    rs_common.push(TestEntry(path="src/test/test_parser.til"))  // TODO: slow (~28s) but works
    rs_common.push(TestEntry(path="src/til.til", cmd_args="help"))  // TODO: slow (~4min) but works

    mut rs_compiled := Vec.new(TestEntry)

    rs_compiled.push(TestEntry(path="src/test/memcmp_str_compiled.til")) // TODO decide: compiles but runtime: memcmp on Str structs compares pointers not content
    rs_compiled.push(TestEntry(path="src/test/precomp_throw.til", expected_status=1)) // Bug #54: should fail at precomp, currently fails at runtime
    rs_compiled.push(TestEntry(path="src/test/extra_syscalls.til"))  // TODO: list_dir/glob_files wrappers timeout in interpreted modes

    mut rs_interpreted := Vec.new(TestEntry)

    rs_interpreted.push(TestEntry(path="src/examples/hello_safe_script.til", expected_status=1)) // TODO: compiled version doesn't enforce safe_script whitelist
    rs_interpreted.push(TestEntry(path="src/test/memcmp_str_interpreted.til")) // TODO decide: compiles but runtime: memcmp on Str structs compares pointers not content
    rs_interpreted.push(TestEntry(path="src/test/precomp_throw.til", expected_status=1)) // Bug #54: should fail at precomp, currently fails at runtime
    rs_interpreted.push(TestEntry(path="src/test/introspections.til")) // TODO: Add C impl for Introspection.has_const/has_field in src/ext.c

    // REPL test - requires stdin piping (e.g., echo "exit(0)" | ./bin/rstil repl)
    // run_cmd doesn't support stdin, would need bash -c wrapper or new ext_proc
    // Input must end with exit(0) to break out of the infinite while loop
    // rs_interpreted.push(TestEntry(path="src/modes/repl.til"))

    // til_interpreted: tests that pass with til interpret but NOT yet with til_compiled
    // (tests that pass all 4 modes are in all_common)
    mut til_interpreted := Vec.new(TestEntry)

    // til_compiled: tests run with the self-hosted TIL compiler only
    mut til_compiled := Vec.new(TestEntry)
    // til_common: tests run with BOTH self-hosted TIL interpreter and compiler
    // (for tests where output differs between rstil and til, e.g. error messages)
    mut til_common := Vec.new(TestEntry)
    til_common.push(TestEntry(path="src/test/panic.til", expected_status=1))  // error msg format differs: "til" vs "rstil"
    til_common.push(TestEntry(path="src/test/sugar.til", expected_status=1))  // REM: error output differs from rstil (Issue #69)

    // all_common: tests run with ALL 4 modes (rs_interpreted, rs_compiled, til_interpreted, til_compiled)
    mut all_common := Vec.new(TestEntry)
    all_common.push(TestEntry(path="src/test/arenas.til"))
    all_common.push(TestEntry(path="src/test/args.til"))
    all_common.push(TestEntry(path="src/test/arrays.til"))
    all_common.push(TestEntry(path="src/test/bug43.til"))
    all_common.push(TestEntry(path="src/test/bug52.til"))
    all_common.push(TestEntry(path="src/test/bug55.til"))
    all_common.push(TestEntry(path="src/test/bug56.til"))
    all_common.push(TestEntry(path="src/test/bug57.til"))
    all_common.push(TestEntry(path="src/test/c_mem.til"))
    all_common.push(TestEntry(path="src/test/cross_file_forward.til"))
    all_common.push(TestEntry(path="src/test/deterministic.til"))
    all_common.push(TestEntry(path="src/examples/empty.til"))
    all_common.push(TestEntry(path="src/examples/hello_cli.til"))
    all_common.push(TestEntry(path="src/examples/hello_lib.til"))
    all_common.push(TestEntry(path="src/examples/hello_liba.til"))
    all_common.push(TestEntry(path="src/examples/hello_pure.til"))
    all_common.push(TestEntry(path="src/examples/hello_pura.til"))
    all_common.push(TestEntry(path="src/examples/hello_script.til"))
    all_common.push(TestEntry(path="src/examples/hello_test.til"))
    all_common.push(TestEntry(path="src/examples/lolalalo.til"))
    all_common.push(TestEntry(path="src/test/arithmetics.til"))
    all_common.push(TestEntry(path="src/test/boolean.til"))
    all_common.push(TestEntry(path="src/test/branchless.til"))
    all_common.push(TestEntry(path="src/test/bug41.til"))
    all_common.push(TestEntry(path="src/test/bug46.til"))
    all_common.push(TestEntry(path="src/test/bug48.til", expected_status=1))
    all_common.push(TestEntry(path="src/test/bug49.til", expected_status=1))
    all_common.push(TestEntry(path="src/test/circular_test.til"))
    all_common.push(TestEntry(path="src/test/clone.til"))
    all_common.push(TestEntry(path="src/test/comparisons.til"))
    all_common.push(TestEntry(path="src/test/constfold.til"))
    all_common.push(TestEntry(path="src/test/editor_mode_test.til"))
    all_common.push(TestEntry(path="src/test/enums.til"))
    all_common.push(TestEntry(path="src/test/errors.til"))
    all_common.push(TestEntry(path="src/test/eval.til"))
    all_common.push(TestEntry(path="src/test/exit.til", expected_status=69))
    all_common.push(TestEntry(path="src/test/fibonacci.til"))
    all_common.push(TestEntry(path="src/test/flow.til"))
    all_common.push(TestEntry(path="src/test/forward_declarations.til"))
    all_common.push(TestEntry(path="src/test/function_pointers.til"))
    all_common.push(TestEntry(path="src/test/func_purity.til"))
    all_common.push(TestEntry(path="src/test/intro.til"))
    all_common.push(TestEntry(path="src/test/lists.til"))
    all_common.push(TestEntry(path="src/test/literals.til"))
    all_common.push(TestEntry(path="src/test/loops.til"))
    all_common.push(TestEntry(path="src/test/maps.til"))
    all_common.push(TestEntry(path="src/test/modes.til"))
    all_common.push(TestEntry(path="src/test/mut_test.til"))
    all_common.push(TestEntry(path="src/test/namespaces.til"))
    all_common.push(TestEntry(path="src/test/optional_args.til"))
    all_common.push(TestEntry(path="src/test/parallel_cmd.til"))
    all_common.push(TestEntry(path="src/test/pointers.til"))
    all_common.push(TestEntry(path="src/test/reg_expr.til"))
    all_common.push(TestEntry(path="src/test/return_value_usage.til"))
    all_common.push(TestEntry(path="src/test/scope_isolation.til"))
    all_common.push(TestEntry(path="src/test/sets.til"))
    all_common.push(TestEntry(path="src/test/strings.til"))
    all_common.push(TestEntry(path="src/test/structs.til"))
    all_common.push(TestEntry(path="src/test/syscalls.til"))
    all_common.push(TestEntry(path="src/test/test_lexer.til"))
    all_common.push(TestEntry(path="src/test/u8.til"))
    all_common.push(TestEntry(path="src/test/underscore.til"))
    all_common.push(TestEntry(path="src/test/ufcs.til"))
    all_common.push(TestEntry(path="src/test/undefined.til", expected_status=1))
    all_common.push(TestEntry(path="src/test/variadic.til"))
    all_common.push(TestEntry(path="src/test/vecs.til"))

    // Parse remaining args (after -j was extracted)
    if remaining_args.len().eq(0) {
        // Default: run tests
        run_all_tests(til_interpreted, til_compiled, til_common, rs_interpreted, rs_compiled, rs_common, all_common, parallelism)
    } else {
        mut cmd := ""
        remaining_args.get(0, cmd)
        switch cmd {
        case "test":
            if remaining_args.len().eq(1) {
                run_all_tests(til_interpreted, til_compiled, til_common, rs_interpreted, rs_compiled, rs_common, all_common, parallelism)
            } else if remaining_args.len().lt(3) {
                println("Error: test requires <mode> and <file>")
                usage()
                exit(1)
            } else {
                mut test_mode := ""
                mut file := ""
                remaining_args.get(1, test_mode)
                remaining_args.get(2, file)
                // Collect optional args
                mut cmd_args := ""
                for i in 3..remaining_args.len() {
                    mut arg := ""
                    remaining_args.get(i, arg)
                    if cmd_args.len().gt(0) {
                        cmd_args = cmd_args.concat(" ")
                    }
                    cmd_args = cmd_args.concat(arg)
                }
                entry := TestEntry(path=file, cmd_args=cmd_args)
                mut time_ms := 0
                mut time_ms2 := 0
                switch test_mode {
                case "rs_interpreted":
                    _ := test_rs_interpreted(entry, time_ms)
                case "rs_compiled":
                    _ := test_rs_compiled(entry, time_ms)
                case "rs_common":
                    _ := test_rs_common(entry, time_ms, time_ms2)
                case "til_interpreted":
                    _ := test_til_interpreted(entry, time_ms)
                case "til_compiled":
                    _ := test_til_compiled(entry, time_ms)
                case "til_common":
                    _ := test_til_common(entry, time_ms, time_ms2)
                case:
                    println("Error: unknown mode '", test_mode, "'")
                    usage()
                    exit(1)
                }
            }
        case "regen":
            mut files := Vec.new(Str)
            for regen_idx in 1..remaining_args.len() {
                mut path := ""
                remaining_args.get(regen_idx, path)
                files.push(path)
            }
            regen(files, rs_common, rs_compiled, rs_interpreted, til_common, til_compiled, til_interpreted, all_common)
        case:
            usage()
        }
    }

    catch (err: IndexOutOfBoundsError) { panic(loc(), err.msg) }
}
