mode script
// TODO use mode test and asserts instead of prints
import("src/core/modes/test")

// Basic control flow (execution):

// regular comment
/* block comment */
/** doc comment */
# another comment
/*

multi
line
comments

/*
nested too
*/

doesn't break with nesting like C // REM nested comments don't work properly in til-mode, but that's an emacs limitation
                                  // TODO rewrite emacs in til and fix this

*/ // TODO should it be unterminated comment when we remove this? or give errors in "doesn't break"

// 'if' statements:

if not(gt(4, 2)) {
    panic(loc(), "'gt(4, 2)' (aka '4 > 2') is not true") // Obviously this should never happen
}

// 'if/elif/else/' statements:

// if 42 { // fails because ints are not allowed for conditions, only bool
if gt(2, 4) {
    println("'if' within if/else not acepting 2 is lesser than 4")
} else if gt(3, 4) {
    println("'if' within elif not acepting 3 is lesser than 4")
} else {
    println("else: 'else' is allowed and functional")
}

// while loops:
{
    mut it := 0 // one has to use the 'mut' keyword for actual variables, since all declarations are const by default
    print("it: ")
    // while 42 { // fails, because while expects a bool first, not an int
    while lt(it, 5) {
        print(it.to_str(), " ")
        it = add(it, 1)
    }
    print("\n")
}

// for loops:
{
    print("i: ")
    for i in 0..5 {
        print(i.to_str(), " ")
    }
    print("\n")
}

// switch statements:

test_int_switch := proc (to_switch: I64) {
    switch to_switch {
    case add(40, 2):
        println("in test_int_switch(): ", to_switch.to_str(), " is the answer."); // semicolons are optional here too
    case:
        println("in test_int_switch(): ", to_switch.to_str(), " is a wrong answer.");
    }
}
test_int_switch(00);
test_int_switch(42); // note the semicolon between statements is being ignored
test_int_switch(69); // note the semicolon between statements is being ignored at the end of the file too
// test_int_switch(69);; // fails because there's no need for ';;' (aka empty statements), try 'if true {}' instead, whatever you want that for.

test_int_range := proc(x: I64) {
    switch x {
    case 1..5:
        println(x.to_str(), " is between 1 and 5")
    case 10..20:
        println(x.to_str(), " is between 10 and 20")
    case:
        println(x.to_str(), " is out of ranges")
    }
}
test_int_range(3)
test_int_range(10)
test_int_range(19)
test_int_range(20)
test_int_range(42)

test_for_loop_range := proc(min_range : I64, max_range : I64) {

    mut sum_for := 0
    for i in min_range..max_range {
        sum_for = sum_for.add(i)
    }

    mut sum_while := 0
    mut j := min_range
    while j.lt(max_range) {
        sum_while = sum_while.add(j)
        j.inc()
    }

    assert_eq(loc(), sum_for, sum_while)
}
test_for_loop_range(0, 12)
test_for_loop_range(3, 5)
test_for_loop_range(4, 2) // TODO this should probably fail, but it hasn't been tested yet

// Test: Nested function returns and stack frame handling
test_nested_function_returns := proc() {
    // Simple return (baseline)
    test_simple := proc() returns I64 {
        return 42
    }
    assert_eq(loc(), 42, test_simple())

    // Nested function calls with returns
    inner_function := proc() returns I64 {
        return 100
    }

    middle_function := proc() returns I64 {
        mut val := inner_function()
        return add(val, 10)  // Should return 110
    }

    outer_function := proc() returns I64 {
        mut val := middle_function()
        return add(val, 1)  // Should return 111
    }

    assert_eq(loc(), 111, outer_function())

    // Deep nesting (4+ levels)
    level_4 := proc() returns I64 {
        return 4
    }

    level_3 := proc() returns I64 {
        mut v := level_4()
        return add(v, 3)  // Should be 7
    }

    level_2 := proc() returns I64 {
        mut v := level_3()
        return add(v, 2)  // Should be 9
    }

    level_1 := proc() returns I64 {
        mut v := level_2()
        return add(v, 1)  // Should be 10
    }

    assert_eq(loc(), 10, level_1())

    // Test with simple helper function
    add_ten_helper := func(x: I64) returns I64 {
        return add(x, 10)
    }

    assert_eq(loc(), 15, add_ten_helper(5))
}
test_nested_function_returns()
